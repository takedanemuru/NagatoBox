' Gambas class file

Property Read Titles As String[]
Property Read ActiveIndex As Integer

Private $headerContainer As NagatoHeaderContainer
Private $bodyContainer As NagatoBodyContainer
Private $footerContainer As NagatoFooterContainer
Private $leftPane As NagatoLeftPane

Private Sub refreshTitle()
    
    $headerContainer.Refresh()
    Me.Title = $bodyContainer.Titles[$bodyContainer.ActiveIndex]
    
End

Public Sub AddUrl(argUrl As String)
    
    $bodyContainer(MikuruSignal.WebNew, [argUrl])
    
End

Public Sub SetUrl(argUrl As String)
    
    $bodyContainer(MikuruSignal.UrlNew, [argUrl])
    
End

Public Sub Form_Open()
    
    Me.FullScreen = True
    PersonaDrawingArea.Visible = False
    
    $headerContainer = New NagatoHeaderContainer(PersonaDrawingArea, SearchBoxContainer) As "UX"
    $bodyContainer = New NagatoBodyContainer(MenuHBox, WebsVBox) As "Body"
    $footerContainer = New NagatoFooterContainer(MainVBox, InPageSearchHBox) As "UX"
    $leftPane = New NagatoLeftPane(LeftTabStrip) As "UX"
    $bodyContainer(MikuruSignal.WebNew, [NagatoSettings.HomePage])

    NagatoSettingsForm.Load(Me)

End

Public Sub Form_Close()
    
    NagatoSettingsForm.Save(Me)
    NagatoDBus.Unregister()
    
End

Public Sub Body_Signal(argEvent As Integer, argArguments As Variant[])
    
    Select Case argEvent
        Case MikuruSignal.WebRequestRefresh, MikuruSignal.WebNewTitle
            refreshTitle()
        Case MikuruSignal.ViewEditBookmark
            LeftPanel.Hidden = False
        Case MikuruSignal.WebDownload, MikuruSignal.InPageSearchToggle
            $footerContainer(argEvent, argArguments)
        Case MikuruSignal.PersonaSetText
            $headerContainer.SetText(argArguments[0])
        Case MikuruSignal.WebProgress
            If Me.ActiveIndex = argArguments[0] Then Me.Title = argArguments[4]
            $headerContainer.SetText(Subst$("&1:&2%", argArguments[1], argArguments[2] * 100))
            $bodyContainer(argEvent, argArguments)
    End Select
    
End

Public Sub UX_Signal(argSignal As Integer, argValues As Variant[])
    
    $bodyContainer(argSignal, argValues)
    refreshTitle()
    
End

Private Function Titles_Read() As String[]

    Return $bodyContainer.Titles

End

Private Function ActiveIndex_Read() As Integer

    Return $bodyContainer.ActiveIndex

End
