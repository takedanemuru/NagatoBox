' Gambas class file

Property Read Titles As String[]
Property Read ActiveIndex As Integer

Private $headerContainer As NagatoHeaderContainer
Private $bodyCotainer As NagatoBodyContainer
Private $downloadBar As NagatoWebdownloadsBar
Private $bookmarkTreeView As NagatoBookmarkTreeView

Private Sub refreshTitle()
    
    $headerContainer.Refresh()
    Me.Title = $bodyCotainer.Titles[$bodyCotainer.ActiveIndex]
    
End

Public Sub AddUrl(argUrl As String)
    
    $bodyCotainer.AddUrl(argUrl)
    
End

Public Sub SetUrl(argUrl As String)
    
    $bodyCotainer.SetUrl(argUrl)
    
End

Public Sub _new()

    MikuruWebSettings.Initialize()

End

Public Sub Form_Open()
    
    $headerContainer = New NagatoHeaderContainer(PersonaDrawingArea, SearchBoxContainer) As "Header"
    $bodyCotainer = New NagatoBodyContainer(MenuHBox, WebsVBox, InPageSearchHBox) As "Body"
    $bookmarkTreeView = New NagatoBookmarkTreeView(LeftPanel) As "BookmarkTreeView"
    $downloadBar = New NagatoWebdownloadsBar(MainVBox) As "DownloadBar"
    
    $bodyCotainer.Start()
    
End

Public Sub Form_Close()
    
    NagatoSettingsForm.Save(Me)
    NagatoDBus.Unregister()
    
End

Public Sub BookmarkTreeView_MenuEventWithArg(argEvent As Integer, argArgument As Variant)
    
    Select Case argEvent
        Case NagatoMenuPipe.WebNew
            $bodyCotainer.AddUrl(argArgument)
    End Select
    
End

Public Sub Body_MenuEvent(argEvent As Integer)
    
    Select Case argEvent
        Case NagatoMenuPipe.WebRequestRefresh
            $headerContainer.Refresh()
        Case NagatoMenuPipe.ViewEditBookmark
            LeftPanel.Hidden = False
    End Select
    
End

Public Sub Body_MenuEventWithArg(argEvent As Integer, argArgument As Variant)
    
    Select Case argEvent
        Case NagatoMenuPipe.WebDownload
            $downloadBar.SetWebDownload(argArgument)
    End Select
    
End

Public Sub Body_MenuEventWithArgs(argEvent As Integer, argArguments As Variant[])
    
    Select Case argEvent
        Case NagatoMenuPipe.WebProgress
            $headerContainer.SetText(Subst$("&1:&2%", argArguments[1], argArguments[2] * 100))
    End Select
    
End

Public Sub Header_MenuEvent(argEvent As Integer)
    
    $bodyCotainer.HeaderCall(argEvent, Null)
    
End

Public Sub Header_MenuEventWithArg(argEvent As Integer, argArgument As Variant)
    
    $bodyCotainer.HeaderCall(argEvent, [argArgument])
    refreshTitle()
    
End

Public Sub Header_MenuEventWithArgs(argEvent As Integer, argArguments As Variant[])
    
    $bodyCotainer.HeaderCall(argEvent, argArguments)
    refreshTitle()
    
End

Private Function Titles_Read() As String[]

    Return $bodyCotainer.Titles

End

Private Function ActiveIndex_Read() As Integer

    Return $bodyCotainer.ActiveIndex

End
