' Gambas class file

Inherits NagatoTable

Public Sub _EnsureTable()
    
    Dim yukiTable As Table
    
    Try Me._$connection.Open()
    
    With NagatoDB
        If Not Me._$connection.Tables.Exist(.Table.Category) Then
            yukiTable = Me._$connection.Tables.Add(.Table.Category)
            yukiTable.Fields.Add(.Column.Id, db.Serial)
            yukiTable.Fields.Add(.Column.Category, db.String)
            yukiTable.PrimaryKey = [.Column.Id]
            yukiTable.Update()
        Endif
    End With

End

Public Function GetCategoryNames() As String[]

    Dim yukiNames As New String[]
    Dim yukiResult As Result = Me._$connection.Find(NagatoDB.Table.Category)
    
    For Each yukiResult
        yukiNames.Add(yukiResult[NagatoDB.Column.Category])
    Next
    
    Return yukiNames.Sort()

End

Public Sub AddNewCategory(argCategoryName As String)
    
    Dim yukiResult As Result 
    
    If argCategoryName = "" Then Return
    
    yukiResult = Me._$connection.Find(NagatoDB.Table.Category, "category = &1", argCategoryName)
    
    If yukiResult.Count = 0 Then 
        yukiResult = Me._$connection.Create(NagatoDB.Table.Category)
        yukiResult[NagatoDB.Column.Category] = argCategoryName
        yukiResult.Update()
    Endif
    
End

Public Function GetCategoryId(argCategoryName As String) As Integer
    
    Dim yukiResult As Result 
    
    If argCategoryName = "" Then argCategoryName = ("uncategorized")
    
    yukiResult = Me._$connection.Find(NagatoDB.Table.Category, "category = &1", argCategoryName)
    
    If yukiResult.Count = 0 Then
        yukiResult = Me._$connection.Create(NagatoDB.Table.Category)
        yukiResult[NagatoDB.Column.Category] = argCategoryName
        yukiResult.Update()
    Endif

    yukiResult = Me._$connection.Find(NagatoDB.Table.Category, "category = &1", argCategoryName)

    Return yukiResult[NagatoDB.Column.Id]
    
End
