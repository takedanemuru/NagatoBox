' Gambas class file

Inherits NagatoTable

Public Sub _EnsureTable() ' override
    
    Dim yukiTable As Table
    
    With NagatoDB
        If Not Me._$connection.Tables.Exist(.Table.Bookmark) Then
            yukiTable = Me._$connection.Tables.Add(.Table.Bookmark)
            yukiTable.Fields.Add(.Column.Id, db.Serial)
            yukiTable.Fields.Add(.Column.Url, db.String)
            yukiTable.Fields.Add(.Column.PageName, db.String)
            yukiTable.Fields.Add(.Column.UserDefinedPageName, db.String)
            yukiTable.Fields.Add(.Column.CategoryId, db.Integer)
            yukiTable.PrimaryKey = [.Column.Id]
            yukiTable.Update()
        Endif
    End With

End

Public Function GetBookmarkResultByCategoryName(argCategoryId As Integer) As Result
    
    Dim yukiQuery As String = "select * from &1 where category_id = &2 order by &3 asc"
    
    With NagatoDB
        yukiQuery = Subst$(yukiQuery, .Table.Bookmark, argCategoryId, .Column.UserDefinedPageName)
    End With
    
    Return Me._$connection.Exec(yukiQuery)
    
End

Public Function GetBookmarkResultFoundByUrl(argUrl As String) As Result
    
    Return Me._$connection.Find(NagatoDB.Table.Bookmark, "url = &1", argUrl)
    
End

Public Function HasBookmarkInCategory(argCategoryId As Integer) As Boolean
    
    With NagatoDB
        If Me._$connection.Find(.Table.Bookmark, "category_id = &1", argCategoryId).Count <> 0 Then Return True
    End With
    
Finally
    Return False
    
End

Public Function HasBookmark(argUrl As String, argCategoryId As Integer) As Boolean
    
    With NagatoDB
        If Me._$connection.Find(.Table.Bookmark, "url = &1 and category_id = &2", argUrl, argCategoryId).Count = 0 Then
            Return False
        Else
            Return True
        Endif
    End With
    
End

Public Sub AddBookMark(argUrl As String, argTitle As String, argUserTitle As String, argCategoryId As Integer)
    
    Dim yukiResult As Result
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Create(NagatoDB.Table.Bookmark)
        yukiResult[NagatoDB.Column.Url] = argUrl
        yukiResult[NagatoDB.Column.PageName] = argTitle
        yukiResult[NagatoDB.Column.UserDefinedPageName] = argUserTitle
        yukiResult[NagatoDB.Column.CategoryId] = argCategoryId
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Me._$connection.Rollback()
    
End
