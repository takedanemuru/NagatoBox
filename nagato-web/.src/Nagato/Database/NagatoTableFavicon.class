' Gambas class file

Inherits NagatoTable

Private Function getBaseUrl(argUrl As String) As String
    
    Return Split(argUrl, "/", "", True)[1]
    
Catch
    Debug argUrl
    
End

Public Sub _EnsureTable() ' override
    
    Dim yukiTable As Table
    
    If Not Me._$connection.Opened Then Me._$connection.Open()
    
    With NagatoDB
        If Not Me._$connection.Tables.Exist(.Table.Favicon) Then
            yukiTable = Me._$connection.Tables.Add(.Table.Favicon)
            yukiTable.Fields.Add(.Column.Id, db.Serial)
            yukiTable.Fields.Add(.Column.Url, db.String)
            yukiTable.Fields.Add(.Column.Favicon, db.Blob)
            yukiTable.PrimaryKey = [.Column.Id]
            yukiTable.Update()
        Endif
    End With

End

Public Sub SetFavicon(argUrl As String, argFavicon As Picture)
    
    Dim yukiResult As Result
    
    If argUrl Not Begins "http" Then Return
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Create(NagatoDB.Table.Favicon)
        yukiResult[NagatoDB.Column.Url] = getBaseUrl(argUrl)
        yukiResult[NagatoDB.Column.Favicon] = MikuruBlob.GetBlobFromPicture(argFavicon)
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Me._$connection.Rollback()
    
End

Public Function HasFavicon(argUrl As String) As Boolean
    
    Return Me._$connection.Find(NagatoDB.Table.Favicon, "url = &1", getBaseUrl(argUrl)).Count > 0
    
End

Public Function GetFavicon(argUrl As String) As Picture
    
    Dim yukiResult As Result = Me._$connection.Find(NagatoDB.Table.Favicon, "url = &1", getBaseUrl(argUrl))
    
    Return MikuruBlob.GetPictureFromBlob(yukiResult[NagatoDB.Column.Favicon].Data)
    
End
