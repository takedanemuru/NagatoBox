' Gambas class file

Inherits NagatoDB

Property Read CategoryNames As String[]

Public _$uniqueName As String = "Web"

Private $tableBookmark As NagatoTableBookmark
Private $tableCategory As NagatoTableCategory

Event Changed

Private Function getCategoryId(argCategoryName As String) As Integer
    
    Return $tableCategory.GetCategoryId(argCategoryName)
    
End

Public Sub DeleteBookmark(argUrl As String)
    
    Try Me._$connection.Delete(Me.Table.Bookmark, "url = &1", argUrl)
    
    If Not Error Then Raise Changed
    
End

Public Sub RenameBookmark(argUrl As String, argNewName As String)
    
    Dim yukiResult As Result = Me._$connection.Edit(Me.Table.Bookmark, "url = &1", argUrl)
    
    yukiResult[Me.Column.UserDefinedPageName] = argNewName
    yukiResult.Update()
    
    Raise Changed

End

Public Sub ChangeBookmarkCategory(argUrl As String, argCategory As String)
    
    Dim yukiResult As Result = Me._$connection.Edit(Me.Table.Bookmark, "url = &1", argUrl)
    
    yukiResult[Me.Column.CategoryId] = getCategoryId(argCategory)
    yukiResult.Update()
    
    Raise Changed

End

Public Sub DeleteCategory(argCategoryName As String)
    
    Try Me._$connection.Delete(Me.Table.Category, "category = &1", argCategoryName)
    
    Raise Changed

End

Public Function GetBookmarkResultByCategoryName(argCategoryName As String) As Result
    
    Return $tableBookmark.GetBookmarkResultByCategoryName(getCategoryId(argCategoryName))
    
End

Public Function GetBookmarkResultByUrl(argUrl As String) As Result
    
    Return $tableBookmark.GetBookmarkResultFoundByUrl(argUrl)
    
End

Public Sub PushBookmark(argUrl As String, argTitle As String, argUserTitle As String, argCategory As String)
    
    Dim yukiCategoryId As Integer = getCategoryId(argCategory)
    
    If $tableBookmark.HasBookmark(argUrl, yukiCategoryId) Then Return
    $tableBookmark.AddBookMark(argUrl, argTitle, argUserTitle, yukiCategoryId)
    
    Raise Changed

End

Public Sub AddNewCategory(argCategoryName As String)
    
    $tableCategory.AddNewCategory(argCategoryName)
    
    Raise Changed

End

Public Sub _new()
    
    Me._SetConnection()
    
    $tableCategory = New NagatoTableCategory(Me._$connection)
    $tableBookmark = New NagatoTableBookmark(Me._$connection)
    
End

Private Function CategoryNames_Read() As String[]

    Return $tableCategory.GetCategoryNames()

End
