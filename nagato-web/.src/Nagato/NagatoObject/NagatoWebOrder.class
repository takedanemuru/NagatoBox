' Gambas class file

Inherits NagatoObject

Property Read ActiveIndex As Integer
Property Read ActiveOrder As Integer
Property Read Max As Integer

Private $webOrder As New Integer[]
Private $webActiveIndex As Integer = 0

Public Function GetNextTabOrder(argTabIndex As Integer) As Integer
    
    If $webOrder.Count = 1 Then
        Return -1
    Else If $webOrder.Count = 2 Then
        Return 0
    Else If argTabIndex = $webActiveIndex Then
        Return IIf(argTabIndex = $webOrder.Max, argTabIndex - 1, argTabIndex)
    Else If argTabIndex > $webActiveIndex Then
        Return $webActiveIndex
    Else
        Return $webActiveIndex - 1
    Endif
    
End

Public Sub Set(argUniqueIndex As Integer, Optional argIndex As Integer)
    
    If Not argIndex Then
        $webOrder.Add(argUniqueIndex)
    Else
        $webOrder.Add(argUniqueIndex, argIndex)
    End If
    
End

Public Sub Remove(argIndexToRemove As Integer, argIndexNextActive As Integer)
    
    $webOrder.Remove(argIndexToRemove)
    $webActiveIndex = argIndexNextActive
    
End

Public Sub Reorder(argOrigin As Integer, argTarget As Integer)
    
    Dim yukiActiveTab As Integer = -1
    
    If argOrigin = $webActiveIndex Then yukiActiveTab = argTarget
    
    If argOrigin > argTarget Then
        $webOrder.Add($webOrder[argOrigin], argTarget)
        $webOrder.Remove(argOrigin + 1, 1)
    Else
        Try $webOrder.Add($webOrder[argOrigin], argTarget + 1)
        If Error Then $webOrder.Add($webOrder[argOrigin])
        $webOrder.Remove(argOrigin, 1)
    Endif
    
    If yukiActiveTab <> -1 Then $webActiveIndex = yukiActiveTab
    If $webActiveIndex > $webOrder.Max Then $webActiveIndex = $webOrder.Max
    
End

Public Sub MoveActiveIndex(argIndex As Integer)
    
    $webActiveIndex = argIndex
    
End

Public Sub _get(argIndex As Integer) As Integer
    
    Return $webOrder[argIndex]
    
End

Private Function ActiveIndex_Read() As Integer

    Return $webActiveIndex

End

Private Function ActiveOrder_Read() As Integer

    Return $webOrder[$webActiveIndex]

End

Private Function Max_Read() As Integer

    Return $webOrder.Max

End
