' Gambas class file

Inherits NagatoObject

Private $drawingArea As DrawingArea
Private $tabs As NagatoTabs
Private $observer As Observer

Private Sub pressOnTab()
    
    Dim yukiTabIndex As Integer = $tabs.GetTabIndex(Mouse.X, Mouse.Y)
    
    If Mouse.Left Then 
        Raise Signal(MikuruSignal.PersonaSelect, [yukiTabIndex])
    Else If Not Mouse.Control And If NagatoSettings.RecordHistory Then 
        Raise Signal(MikuruSignal.PersonaDelete, [yukiTabIndex])
    Else
        Raise Signal(MikuruSignal.PersonaDeleteWithoutRecord, [yukiTabIndex])
    End If
    
    
End

Private Function getDropIcon() As Picture
    
    Dim yukiImage As New Image(192, 64, Color.Pink)
    
    Paint.Begin(yukiImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.DrawImage(Image.Load("pictures/icons/navigation-down.png"), 8, 8)
        Paint.RichText("Drop to move", 64, 0, 128, 64, Align.Center)
        Paint.Fill()
    Paint.End()
    
    Return yukiImage.Picture
    
End

Public Sub _new(argDrawingArea As DrawingArea, argTabs As NagatoTabs)
    
    $drawingArea = argDrawingArea
    $tabs = argTabs
    $observer = New Observer(argDrawingArea) As "Asakura"
    
End

Public Sub Asakura_MouseDown()
    
    Select Case $tabs.GetTabType(Mouse.X, Mouse.Y)
        Case NagatoTabs.TypeWebView
            pressOnTab()
        Case NagatoTabs.TypePlusButton
            If Mouse.Left Then Raise Signal(MikuruSignal.WebNew, [NagatoSettings.HomePage])
    End Select
    
End

Public Sub Asakura_Enter()
    
    $drawingArea.Focus = True
    
End

Public Sub Asakura_MouseMove()
    
    $tabs.SetOnMouseIndex(Mouse.X, Mouse.Y)
    $drawingArea.Refresh()
    
End

Public Sub Asakura_Leave()
    
    $tabs.ClearOnMouseIndex()
    $drawingArea.Refresh()
    $drawingArea.Focus = False
    
End

Public Sub Asakura_MouseDrag()
    
    If $tabs.GetTabOrder(Mouse.StartX) = ($tabs.Max + 1) Then Return
    
   If $tabs.GetTabType(Mouse.StartX, Mouse.StartY) = NagatoTabs.TypeWebView Then
        Drag.Icon = getDropIcon()
        $drawingArea.Drag(CStr(Mouse.StartX), "text/nagato-web")
    Endif
    
End

Public Sub Asakura_Drop()
    
    Dim yukiOriginX As Integer
    Dim yukiDropX As Integer
    
    If Drag.Format <> "text/nagato-web" Then Return
    If $tabs.Max = 1 Then Return
    
    yukiOriginX = $tabs.GetTabOrder(Mouse.StartX)
    yukiDropX = $tabs.GetTabOrder(Drag.X)
    Drag.Hide()
    
    Raise Signal(MikuruSignal.PersonaReorder, [yukiOriginX, yukiDropX])
    
End
