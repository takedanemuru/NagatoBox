' Gambas class file

Inherits NagatoObject

Property Read Titles As String[]
Property Read ActiveIndex As Integer

Private $menubar As NagatoMenuBar
Private $webs As NagatoWebs

Private Sub insertWeb(argIndex As Integer, argUrl As String)
    
    $webs.Tabs.Insert(argIndex, argUrl)
    $menubar.SetUrl(argUrl)
    $webs.Active.Show()

End

Private Sub addWeb(Optional argUrl As String = NagatoSettings.HomePage)
    
    $webs.Tabs.Add(argUrl)
    $menubar.SetUrl(argUrl)
    $webs.Tabs.ShowLast()

End

Public Sub _call(argEvent As Integer, argArguments As Variant[])
    
    Select Case argEvent
        Case MikuruSignal.WebNew
            addWeb(argArguments[0])
        Case MikuruSignal.UrlNew
            $webs.Active.SetUrl(argArguments[0])
        Case MikuruSignal.PersonaSelect
            $webs.Tabs.ShowByIndex(argArguments[0])
            $menubar.SetNavigationButton($webs.Active.History)
        Case MikuruSignal.PersonaDelete
            $webs.Tabs.Remove(argArguments[0], True)
        Case MikuruSignal.PersonaDeleteWithoutRecord
            $webs.Tabs.Remove(argArguments[0])
        Case MikuruSignal.PersonaReorder
            $webs.Tabs.Reorder(argArguments[0], argArguments[1])
        Case MikuruSignal.WebProgress
            If argArguments[0] <> $webs.Active.UniqueIndex Then Return
            $menubar.SetUrl(argArguments[1])
            $menubar.SetNavigationButton($webs.Active.History)
        Case MikuruSignal.InPageSearchQuery
            $webs.Active.InPageSearch(argArguments[0])
    End Select
    
    $menubar.SetUrl($webs.Active.TabUrl)
    
End

Public Sub _new(argMenubarContainer As Container, argWebsContainer As Container)
    
    $menubar = New NagatoMenuBar(argMenubarContainer) As "UserControl"
    $webs = New NagatoWebs(argWebsContainer) As "UserControl"
    
End

Public Sub UserControl_Signal(argEvent As Integer, argValues As Variant[])
    
    Select Case argEvent
        Case MikuruSignal.WebBack
            $webs.Active.Back()
        Case MikuruSignal.WebForward
            $webs.Active.Forward()
        Case MikuruSignal.UrlAddBookmark
            MikuruDialogPortalAddBookmark($webs.Active.TabTitle, $webs.Active.TabUrl)
        Case MikuruSignal.UrlNew
            $webs.Active.SetUrl(argValues[0])
        Case MikuruSignal.WebNew
            addWeb(argValues[0])
        Case MikuruSignal.WebNewInsert
            insertWeb($webs.Active.Index + 1, argValues[0]) ' insert new web tab next to active tab
        Default
            Raise Signal(argEvent, argValues)
    End Select
    
End

Private Function Titles_Read() As String[]

    Return $webs.Tabs.Titles

End

Private Function ActiveIndex_Read() As Integer

    Return $webs.Active.Index

End
