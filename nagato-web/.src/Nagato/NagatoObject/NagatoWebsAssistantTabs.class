' Gambas class file

Inherits NagatoObject

Property Read Titles As String[]

Private $webs As Collection
Private $webOrder2 As NagatoWebOrder
Private $container As Container

Event RequestRefresh

Public Sub Add(argUrl As String)
    
    Dim yukiUniqueIndex As Integer = MikuruUniqueIndex.Get()
    Dim yukiWeb As New NagatoWeb($container, argUrl, yukiUniqueIndex) As "TFEI"

    $webs.Add(yukiWeb, yukiUniqueIndex)
    $webOrder2.Set(yukiUniqueIndex)
     
End

Public Sub Insert(argIndex As Integer, argUrl As String)
    
    Dim yukiUniqueIndex As Integer = MikuruUniqueIndex.Get()
    Dim yukiWeb As New NagatoWeb($container, argUrl, yukiUniqueIndex) As "TFEI"

    $webs.Add(yukiWeb, yukiUniqueIndex)
    $webOrder2.Set(yukiUniqueIndex, argIndex)
    
End

Public Sub ShowLast()
    
    $webOrder2.MoveActiveIndex($webOrder2.Max)
    Raise RequestRefresh
    
End

Public Sub ShowByIndex(argIndex As Integer)
    
    $webOrder2.MoveActiveIndex(argIndex)
    Raise RequestRefresh
    
End

Public Sub Remove(argTabIndex As Integer)
    
    Dim yukiNextActiveTabIndex As Integer = $webOrder2.GetNextTabOrder(argTabIndex)
    
    If $webOrder2.Max = 0 Then Return
    
    $webs[$webOrder2[argTabIndex]].Delete()
    $webs.Remove($webOrder2[argTabIndex])
    $webOrder2.Remove(argTabIndex, yukiNextActiveTabIndex)
    
    Raise RequestRefresh
    
End

Public Sub Reorder(argOrigin As Integer, argTarget As Integer)
    
    $webOrder2.Reorder(argOrigin, argTarget)
    
End

Public Sub _new(argWebs As Collection, argWebOrder As NagatoWebOrder, argContainer As Container)
    
    $webs = argWebs
    $webOrder2 = argWebOrder
    $container = argContainer
    
End

Private Function Titles_Read() As String[]

    Dim yukiCount As Integer
    Dim yukiReturn As New String[]

    For yukiCount = 0 To $webOrder2.Max
        yukiReturn.Add($webs[$webOrder2[yukiCount]].Title)
    Next

    Return yukiReturn

End
