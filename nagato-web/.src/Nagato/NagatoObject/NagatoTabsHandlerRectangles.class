' Gambas class file

Inherits NagatoObject

Property Read Max As Integer

Private $drawingArea As DrawingArea
Private $tabs As New Rect[]
Private $rectangles As NagatoRectangles

Private Function addRectangle(argIndex As Integer, argLeft As Integer) As Integer
    
    Dim yukiRect As Rect = $rectangles[argLeft, HaruhiMain.Titles[argIndex]]
    
    $tabs.Add(yukiRect)
    
    Return argLeft + yukiRect.W + MikuruUxTab.Margin
    
End

Public Function GetTabOrder(argMouseX As Integer) As Integer
    
    Dim yukiCount As Integer
    
    For yukiCount = 0 To $tabs.Max - 1
        If $tabs[yukiCount].X + $tabs[yukiCount].W > argMouseX Then Return yukiCount
    Next
    
    Return $tabs.Max + 1
    
End

Public Function GetTabIndex(argMouseX As Integer, argMouseY As Integer) As Integer
    
    Dim yukiCount As Integer
    
    For yukiCount = 0 To $tabs.Max
        If $tabs[yukiCount].Contains(argMouseX, argMouseY) Then Return yukiCount
    Next
    
    Return -1
    
End

Public Sub Refresh()
    
    Dim yukiCount As Integer
    Dim yukiLeft As Integer = MikuruUxTab.Padding
    
    $tabs.Clear()
    
    For yukiCount = 0 To HaruhiMain.Titles.Max
        yukiLeft = addRectangle(yukiCount, yukiLeft)
    Next
    
    $tabs.Add($rectangles.GetPlusButtonRectangle(yukiLeft))
    
End

Public Sub _new(argDrawingArea As DrawingArea)
    
    $drawingArea = argDrawingArea
    $rectangles = New NagatoRectangles(argDrawingArea)
    
End

Public Sub _get(argIndex As Integer) As Rect
    
    Return $tabs[argIndex]
    
End

Private Function Max_Read() As Integer

    Return $tabs.Max

End
