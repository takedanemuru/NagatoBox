' Gambas class file

Inherits NagatoObject

Private $container As Container
Private $searchBox As ButtonBox

Private Sub initializeSpacer()
    
    Dim yukiWidget As New Panel($container)
    
    With yukiWidget
        .Expand = True
    End With
    
End

Private Sub initializeSearchBox()
    
    $searchBox = New ButtonBox($container) As "SearchBox"
    
    With $searchBox
        .Width = TFEI.UX.SearchBoxWidth
        .Picture = MikuruIcon.Get("search")
        .Expand = False
    End With
    
End

Private Sub initializeCloseButton()
    
    Dim yukiWidget As New MenuButton($container) As "CloseButton"
    
    With yukiWidget
        .Width = $container.Height
        .Expand = False
        .Picture = MikuruIcon.Get("button-cross")
        .Border = False
    End With
    
End

Public Sub SetFocus()
    
    $searchBox.Text = ""
    $searchBox.SetFocus()
    $container.Visible = Not $container.Visible
    
End

Public Sub Hide()
    
    $container.Visible = False
    
End

Public Sub _new(argContainer As Container)
    
    $container = argContainer
    initializeSpacer()
    initializeSearchBox()
    initializeCloseButton()
    
End

Public Sub SearchBox_Activate()
    
    If $searchBox.Text = "" Then Return
    
    Raise MenuEventWithArg(MikuruSignal.InPageSearchQuery, $searchBox.Text)
    
End

Public Sub SearchBox_Click()
    
    SearchBox_Activate()
    
End

Public Sub CloseButton_Click()
    
    $container.Visible = False
    
End
