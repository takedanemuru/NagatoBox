' Gambas class file

Inherits NagatoObject

Private $categoryMenu As Menu

Private Sub addSubCategories(argCategory As String)
    
    Dim yukiCategory As String
    Dim yukiMenu As NagatoSubMenuBookmarkCategory
    
    For Each yukiCategory In NagatoDBBookmark.CategoryNames.Sort(gb.Natural + gb.IgnoreCase)
        If argCategory = yukiCategory Then Continue
        If yukiCategory Not Begins argCategory Then Continue
        yukiMenu = New NagatoSubMenuBookmarkCategory($categoryMenu, yukiCategory) As "TFEI"
    Next
    
End

Private Sub addPage(argResult As Result, argCategory As String)
    
    Dim yukiMenu As New Menu($categoryMenu) As "BookmarkItemMenu"
    Dim yukiTitle As String = argResult[NagatoDB.Column.UserDefinedPageName]
    
    With yukiMenu
        .Text = MikuruCollapsedText[yukiTitle, Int(Screen.W / 4)]
        .Tag = argResult[NagatoDB.Column.Url]
        If Not NagatoSettings.ShowFavicon Then Return
        .Picture = NagatoDBFavicon.Get(argResult[NagatoDB.Column.Url])
    End With
    
End

Private Sub addPages(argCategory As String)
    
    Dim yukiResult As Result = NagatoDBBookmark.GetBookmarkResultByCategoryName(argCategory)

    For Each yukiResult
        If yukiResult[NagatoDB.Column.UserDefinedPageName] = "" Then Continue
        addPage(yukiResult, argCategory)
    Next    
End

Private Sub initializeCategoryMenu(argParentMenu As Menu, argCategory As String)
    
    Dim yukiCategoryName As String[] = Split(argCategory, "/", "", True) 
    
    $categoryMenu = New Menu(argParentMenu) 
    
    With $categoryMenu
        .Text = yukiCategoryName[yukiCategoryName.Max]
        .Picture = MikuruIcon["folder"]
    End With
    
End

Public Sub _new(argParentMenu As Menu, argCategory As String)
    
    initializeCategoryMenu(argParentMenu, argCategory)
    addSubCategories(argCategory)
    addPages(argCategory)
    
End

Public Sub BookmarkItemMenu_Click()
    
    Raise Signal(MikuruSignal.WebNew, [Last.Tag])
    
End
