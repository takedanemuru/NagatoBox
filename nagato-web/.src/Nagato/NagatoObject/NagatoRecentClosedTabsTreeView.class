' Gambas class file

Inherits NagatoObject

Private $treeView As TreeView
Private $observer As Observer

Private Sub addItem(argResult As Result)
    
    Dim yukiUrl As String = argResult[NagatoDB.Column.Url]
    Dim yukiPageName As String = argResult[NagatoDB.Column.PageName]
    Dim yukiFavicon As Picture = NagatoDBFavicon.Get(yukiUrl)
    
    $treeView.Add(yukiUrl, yukiPageName, yukiFavicon)
    
End

Private Sub refresh()
    
    Dim yukiResult As Result = NagatoDBHistory.GetAll()
    
    $treeView.Clear()
    
    For Each yukiResult
        If Not $treeView.Exist(yukiResult[NagatoDB.Column.Url]) Then addItem(yukiResult)
    Next
    
End

Public Sub _new(argContainer As Container)
    
    $treeView = New TreeView(argContainer) As "TreeView"
    $treeView.Foreground = Color.DarkCyan
    $observer = New Observer(NagatoDBHistory) As "Asakura"
    
    refresh()
    
End

Public Sub Asakura_Update()
    
    refresh()
    
End

Public Sub TreeView_Activate()
    
    If $treeView.Key Begins "http" Then Raise Signal(MikuruSignal.WebNew, [$treeView.Key]) 
    
End

Public Sub TreeView_Menu()
    
    Dim yukiMenu As Object
    
    If $treeView.Key <> "" Then
        yukiMenu = New NagatoContextMenuRecentClosedTabs(HaruhiMain, "") As "ContextMenu"
    Else
        yukiMenu = New NagatoContextMenuRecentClosedTabsOnRoot(HaruhiMain, "") As "ContextMenu"
    End If
    
    yukiMenu.Popup()
    
End

Public Sub ContextMenu_Signal(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.ContextOpen
            Me.TreeView_Activate()
        Case MikuruSignal.ContextDelete
            NagatoDBHistory.Delete($treeView.Key)
        Case MikuruSignal.ContextDeleteAll
            NagatoDBHistory.DeleteRelated($treeView.Key)
        Case MikuruSignal.ContextReload
            refresh()
    End Select
    
End
