' Gambas class file

Inherits NagatoObject

Private $webView As WebView
Private $uniqueIndex As Integer
Private $observer As Observer

Private Function getProgressValues() As Variant[]
    
    Dim yukiValues As New Variant[]
    Dim yukiHistory As New NagatoWebPageHistory($webView.History.Index, $webView.History.Max)
    
    yukiValues.Add($uniqueIndex)
    yukiValues.Add($webView.Url)
    yukiValues.Add($webView.Progress)
    yukiValues.Add(yukiHistory)
    yukiValues.Add($webView.Title)
    
    Return yukiValues
    
End

Public Sub ForceRaiseProgress()
    
    Raise Signal(MikuruSignal.WebProgress, getProgressValues())
    
End

Public Sub _new(argWebView As WebView, argUniqueIndex As Integer)
    
    $webView = argWebView
    $uniqueIndex = argUniqueIndex
    $observer = New Observer($webView) As "Asakura"
    
End

Public Sub Asakura_Download(argWebDownload As WebDownload)
    
    Dim yukiDownload As NagatoWebDownload2
    
    argWebDownload.Path = Temp$("download_" & File.Name(argWebDownload.Url))
    
    Dialog.Title = ("Download")
    Dialog.Path = MikuruSafePath.Get(NagatoSettings.DownloadDirectory &/ File.Name(argWebDownload.Url))
    
    If Not Dialog.SaveFile() Then
        yukiDownload = New NagatoWebDownload2(argWebDownload, Dialog.Path) As "TFEI"
    Else
        argWebDownload.Cancel()
    Endif
    
End

Public Sub Asakura_Progress()
    
    Raise Signal(MikuruSignal.WebProgress, getProgressValues())
    
End

Public Sub Asakura_Title()
    
    If $webView.Title = "" Then Return
    
    Raise Signal(MikuruSignal.WebNewTitle, Null)
    
End

Public Sub Asakura_Icon()
    
    If $webView.Icon <> Null Then NagatoDBFavicon.Push($webView.Url, $webView.Icon)
    
End
