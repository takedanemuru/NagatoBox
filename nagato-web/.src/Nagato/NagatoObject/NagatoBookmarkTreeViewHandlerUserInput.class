' Gambas class file

Inherits NagatoObject

Private $treeView As TreeView
Private $observer As Observer
Private $title As String

Event RequestRemove(argData As String)

Public Sub _new(argTreeView As TreeView)
    
    $treeView = argTreeView
    $observer = New Observer($treeView) As "Asakura"
    
End

Public Sub Asakura_Activate()
    
    If $treeView.Key Begins "http" Then Raise MenuEventWithArg(MikuruSignal.WebNew, $treeView.Key) 
    
End

Public Sub Asakura_MouseDrag()
    
    If MikuruUrl.IsWebPage($treeView.Key) Then
        $title = $treeView.Current.Text
        Drag.Icon = Stock["medium/file"]
        $treeView.Drag($treeView.Key, "text/plain")
    End If
    
End

Public Sub Asakura_DragMove()
    
    If Not $treeView.FindAt(Drag.X, Drag.Y) Then
        $treeView[$treeView.Item.Key].Selected = True
    Else
        Return
    End If
    
End

Public Sub Asakura_Drop()
    
    Dim yukiCategory As String
    
    If $title = "" Then Return
    If Not MikuruUrl.IsWebPage(Drag.Data) Then Return
    
    If $treeView.Item.Key = "NagatoTrashBin" Then
        Raise RequestRemove(Drag.Data)
    Else
        yukiCategory = IIf(MikuruUrl.IsWebPage($treeView.Item.Key), $treeView.Item.ParentKey, $treeView.Item.Key)
        Try $treeView.Remove(Drag.Data)
        Try $treeView.Add(Drag.Data, $title,, yukiCategory)
        If Not Error Then NagatoDBWeb.ChangeBookmarkCategory(Drag.Data, yukiCategory)
        $title = ""
    End If
    
End
