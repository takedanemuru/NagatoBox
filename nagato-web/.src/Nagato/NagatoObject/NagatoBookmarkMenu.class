' Gambas class file

Inherits NagatoObject

Private Const MenuName As String = "WebBookmarkMenu"

Private $rootMenu As Menu
Private $categories As NagatoSubMenuBookmark

Private Sub initializeRootMenu(argButton As MenuButton)
    
    $rootMenu = New Menu(HaruhiMain) As "RootMenu"
    $rootMenu.Name = MenuName
    
    argButton.Menu = MenuName
    
End

Private Sub addBookmarkCurrentPageMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "BookmarkCurrentPageMenu"
    
    With yukiMenu
        .Text = ("Bookmark current page")
        .Picture = MikuruIcon.Get("toggle-expand-alt")
    End With
    
End

Private Sub addEditBookmarkMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "BookmarkEditMenu"
    
    With yukiMenu
        .Text = ("Edit bookmark")
        .Picture = MikuruIcon.Get("document-edit")
    End With
    
End

Private Sub refreshMenus()
    
    $rootMenu.Children.Clear()
    addBookmarkCurrentPageMenu()
    addEditBookmarkMenu()
        MikuruMenuSeparator.Set($rootMenu)
    $categories.Refresh()
    
End

Public Sub _new(argButton As MenuButton)
    
    initializeRootMenu(argButton)
    MikuruMenuSeparator.Set($rootMenu) ' use as dummy menu
    $categories = New NagatoSubMenuBookmark($rootMenu) As "TFEI"
    
End

Public Sub BookmarkCurrentPageMenu_Click()
    
    Raise MenuEvent(MikuruSignal.UrlAddBookmark)
    
End

Public Sub BookmarkEditMenu_Click()
    
    Raise MenuEvent(MikuruSignal.ViewEditBookmark)
    
End

Public Sub RootMenu_Show()
    
    refreshMenus()
    
End
