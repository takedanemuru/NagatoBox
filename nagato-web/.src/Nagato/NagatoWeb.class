' Gambas class file

Inherits NagatoMenuPipe

Property Read UniqueIndex As Integer
Property Read Title As String
Property Read Url As String

Private $container As Container
Private $webView As WebView
Private $uniqueIndex As Integer
Private $lastLink As String

Private Sub addWebView(argUrl As String)
    
    $webView = New WebView($container) As "WebView"
     
    With $webView
        .Expand = True
        .Url = argUrl
        .UserAgent = "NagatoWeb"
        .Tracking = True
        .Font = Application.Font
    End With
    
End

Public Sub Forward()
    
    Try $webView.Forward()
    
End

Public Sub Back()
    
    Try $webView.Back()
    
End

Public Sub SetUrl(argUrl As String)
    
    $webView.Url = argUrl
    
End

Public Sub Delete()
    
    $webView.Delete()
    
End

Public Sub Toggle(argActiveTabIndex As Integer)
    
    If argActiveTabIndex = Me.UniqueIndex Then
        $webView.Visible = True
    Else
        $webView.Visible = False
    Endif
    
End

Public Sub InPageSearch(argQuery As String)
    
    $webView.FindText(argQuery, False, False, True)
    
End

Public Sub _new(argContainer As Container, argStartupUrl As String, argUniqueIndex As Integer)
    
    $uniqueIndex = argUniqueIndex
    $container = argContainer
    
    addWebView(argStartupUrl)
    
End

Public Sub WebView_Title()
    
    Raise MenuEvent(NagatoMenuPipe.WebRequestRefresh)
    
End

Public Sub WebView_Load()
    
    If $webView.Progress = 1 Then
        Raise MenuEventWithArgs(NagatoMenuPipe.WebLoaded, [$webView.Url, $webView.Title])
    End If
    
End

Public Sub WebView_MouseMove()
    
    Dim yukiHitTest As WebHitTest = $webView.HitTest(Mouse.X, Mouse.Y)
    Dim yukiUrl As String = yukiHitTest.Url
    
    If yukiUrl <> "" Then $lastLink = yukiUrl
    
End

Public Sub WebView_NewWindow(argModal As Boolean)
    
    Raise MenuEventWithArg(NagatoMenuPipe.WebNewInsert, $lastLink)
    
End

Public Sub WebView_KeyPress()
    
    If Key.Alt Then
        Select Case Key.Code
            Case Key.Left
                $webView.Back()
            Case Key.Right
                $webView.Forward()
        End Select
    Else If Key.Control Then
        Select Case Key.Code
            Case Key["f"]
                Raise MenuEvent(NagatoMenuPipe.InPageSearchToggle)
        End Select
    Endif
    
End

Public Sub WebView_Download(argDownload As WebDownload)
    
    Raise MenuEventWithArg(NagatoMenuPipe.WebDownload, argDownload)
    
End

Public Sub WebView_Progress()
    
    Raise MenuEventWithArgs(NagatoMenuPipe.WebProgress, [Me.UniqueIndex, $webView.Url, $webView.Progress])
    
End

Private Function UniqueIndex_Read() As Integer

    Return $uniqueIndex

End

Private Function Title_Read() As String

    Return $webView.Title

End

Private Function Url_Read() As String

    Return $webView.Url

End
