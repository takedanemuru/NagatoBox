' Gambas class file

Inherits NagatoMenuPipe

Property Read UniqueIndex As Integer
Property Read Title As String
Property Read Url As String

Private $container As Container
Private $webView As WebView
Private $uniqueIndex As Integer
Private $lastLink As String

Private Sub addWebView(argUrl As String)
    
    $webView = New WebView($container) As "WebView"
     
    With $webView
        .Expand = True
        .Url = argUrl
        .UserAgent = "NagatoWeb"
    End With
    
End

Public Sub Forward()
    
    Try $webView.Forward()
    
End

Public Sub Back()
    
    Try $webView.Back()
    
End

Public Sub SetUrl(argUrl As String)
    
    $webView.Url = argUrl
    
End

Public Sub Delete()
    
    $webView.Delete()
    
End

Public Sub Toggle(argActiveTabIndex As Integer)
    
    If argActiveTabIndex = Me.UniqueIndex Then
        $webView.Visible = True
    Else
        $webView.Visible = False
    Endif
    
End

Public Sub InPageSearch(argQuery As String)
    
    $webView.FindText(argQuery, False, False, True)
    
End

Public Sub _new(argContainer As Container, argStartupUrl As String, argUniqueIndex As Integer)
    
    $uniqueIndex = argUniqueIndex
    $container = argContainer
    
    addWebView(argStartupUrl)
    
End

Public Sub WebView_Title()
    
    Raise MenuEvent(NagatoMenuPipe.WebRequestRefresh)
    
End

Public Sub WebView_Load()
    
    Raise MenuEventWithArg(NagatoMenuPipe.WebLoaded, $webView.Url)
    
End

Public Sub WebView_Link(argLink As String)
    
    If Not argLink = "" Then $lastLink = argLink
    
End

Public Sub WebView_NewWindow(argModal As Boolean)
    
    Debug argModal
    
    Raise MenuEventWithArg(NagatoMenuPipe.WebNewInsert, $lastLink)
    
End

Public Sub WebView_KeyPress()
    
    If Key.Alt Then
        Select Case Key.Code
            Case Key.Left
                $webView.Back()
            Case Key.Right
                $webView.Forward()
        End Select
    Else If Key.Control Then
        Select Case Key.Code
            Case Key["f"]
                Raise MenuEvent(NagatoMenuPipe.InPageSearchToggle)
        End Select
    Endif
    
End

Public Sub WebView_Download(argDownload As WebDownload)
    
    Dialog.Title = ("Download")
    Dialog.Path = MikuruSafePath.Get(NagatoSettings.DownloadDirectory &/ File.Name(argDownload.Url))
    
    If Not Dialog.SaveFile() Then
        argDownload.Path = Dialog.Path
    End If
    
End

Public Sub WebView_Progress()
    
    Raise MenuEventWithArgs(NagatoMenuPipe.WebProgress, [Me.UniqueIndex, $webView.Url, $webView.Progress])
    
End

Private Function UniqueIndex_Read() As Integer

    Return $uniqueIndex

End

Private Function Title_Read() As String

    Return $webView.Title

End

Private Function Url_Read() As String

    Return $webView.Url

End
