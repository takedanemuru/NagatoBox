' Gambas class file

Create Static

Property HomePage As String
Property DownloadDirectory As String
Property WebViewFont As Font

Private $settings As New Settings

Public Sub LoadFormSettings(argForm As Form)
    
    argForm.X = $settings["form/x", argForm.X]
    argForm.Y = $settings["form/y", argForm.Y]
    argForm.Height = $settings["form/height", argForm.Height]
    argForm.Width = $settings["form/width", argForm.Width]
    
End

Public Sub SaveFormSettings(argForm As Form)
    
    $settings["form/x"] = argForm.X
    $settings["form/y"] = argForm.Y
    $settings["form/height"] = argForm.Height
    $settings["form/width"] = argForm.Width
    
End

Private Function HomePage_Read() As String

    Return $settings["web/homepage", "http://duckduckgo.com"]

End

Private Sub HomePage_Write(Value As String)

    $settings["web/homepage"] = Value
    $settings.Save()

End

Private Function DownloadDirectory_Read() As String

    Return $settings["web/download_directory", NagatoXdgUserDirs.Download]

End

Private Sub DownloadDirectory_Write(Value As String)

    $settings["web/download_directory"] = Value
    $settings.Save()

End

Private Function WebViewFont_Read() As Font

    Return $settings["web/font", HaruhiMain.Font]

End

Private Sub WebViewFont_Write(Value As Font)

    $settings["web/font"] = Value
    $settings.Save()

    Debug Me.WebViewFont.Name

End
