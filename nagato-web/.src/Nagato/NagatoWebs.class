' Gambas class file

Inherits NagatoMenuPipe

Property Read ActiveTabTitle As String
Property Read ActiveTabUrl As String
Property Read ActiveIndex As Integer
Property Read Titles As String[]
Property Read ActiveWeb As NagatoWeb

Private $container As Container
Private $webs As New Collection
Private $webOrder2 As New NagatoWebOrder

Public Sub AddWeb(argUrl As String)
    
    Dim yukiUniqueIndex As Integer = MikuruUniqueIndex.Get()
    Dim yukiWeb As New NagatoWeb($container, argUrl, yukiUniqueIndex) As "Menu"

    $webs.Add(yukiWeb, yukiUniqueIndex)
    $webOrder2.Set(yukiUniqueIndex)
     
End

Public Sub InsertWeb(argIndex As Integer, argUrl As String)
    
    Dim yukiUniqueIndex As Integer = MikuruUniqueIndex.Get()
    Dim yukiWeb As New NagatoWeb($container, argUrl, yukiUniqueIndex) As "Menu"

    $webs.Add(yukiWeb, yukiUniqueIndex)
    $webOrder2.Set(yukiUniqueIndex, argIndex)
    
End

Public Sub ShowActiveTab()
    
    Dim yukiCount As Integer
    
    For yukiCount = 0 To $webOrder2.Max
        $webs[$webOrder2[yukiCount]].Toggle($webOrder2.ActiveOrder)
    Next
    
End

Public Sub ShowLastTab()
    
    $webOrder2.MoveActiveIndex($webOrder2.Max)
    Me.ShowActiveTab()
    
End

Public Sub ShowTab(argIndex As Integer)
    
    $webOrder2.MoveActiveIndex(argIndex)
    Me.ShowActiveTab()
    
End

Public Sub SetActiveTabUrl(argUrl As String)
    
    Me.ActiveWeb.SetUrl(argUrl)
    
End

Public Sub RemoveTab(argTabIndex As Integer)
    
    Dim yukiNextActiveTabIndex As Integer = $webOrder2.GetNextTabOrder(argTabIndex)
    
    If $webOrder2.Max = 0 Then Return
    
    $webs[$webOrder2[argTabIndex]].Delete()
    $webs.Remove($webOrder2[argTabIndex])
    $webOrder2.Remove(argTabIndex, yukiNextActiveTabIndex)
    Me.ShowActiveTab()
    
End

Public Sub InPageSearch(argQuery As String)
    
    Me.ActiveWeb.InPageSearch(argQuery)
    
End

Public Sub Forward()
    
    Me.ActiveWeb.Forward()
    
End

Public Sub Back()
    
    Me.ActiveWeb.Back()
    
End

Public Sub Reorder(argOrigin As Integer, argTarget As Integer)
    
    $webOrder2.Reorder(argOrigin, argTarget)
    
End

Public Sub _new(argContainer As Container)
    
    $container = argContainer
    
End

Private Function ActiveTabTitle_Read() As String

    Return $webs[$webOrder2.ActiveOrder].Title

End

Private Function ActiveIndex_Read() As Integer

    Return $webOrder2.ActiveIndex

End

Private Function ActiveTabUrl_Read() As String

    Return Me.ActiveWeb.Url

End

Private Function Titles_Read() As String[]

    Dim yukiCount As Integer
    Dim yukiReturn As New String[]

    For yukiCount = 0 To $webOrder2.Max
        yukiReturn.Add($webs[$webOrder2[yukiCount]].Title)
    Next

    Return yukiReturn

End

Private Function ActiveWeb_Read() As NagatoWeb

    Return $webs[$webOrder2.ActiveOrder]

End
