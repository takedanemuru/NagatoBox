' Gambas class file

Inherits NagatoMenuPipe

Private $treeView As TreeView
Private $title As String

Private Sub setBookmark(argCategory As String)
    
    Dim yukiResult As Result = NagatoDBWeb.GetBookmarkResultFoundByCategoryName(argCategory)
    
    For Each yukiResult
        With NagatoDBWeb
            $treeView.Add(yukiResult[.ColumnUrl], yukiResult[.ColumnUserDefinedPageName],, argCategory)
        End With
    Next
    
End

Private Sub setCategory()
    
    Dim yukiCategory As String
    
    For Each yukiCategory In NagatoDBWeb.CategoryNames
        'If Not NagatoDBWeb.HasBookmarkInCategory(yukiCategory) Then Continue
        $treeView.Add(yukiCategory, yukiCategory, MikuruIcon.Get("folder"))
        setBookmark(yukiCategory)
    Next
    
End

Public Sub _new(argContainer As Container)
    
    $treeView = New TreeView(argContainer) As "TreeView"
    $treeView.Foreground = Color.DarkCyan
    $treeView.Drop = True
    
    setCategory()
    
End

Public Sub TreeView_Activate()
    
    Debug $treeView.Key
    
    If $treeView.Key Begins "http://" Then Raise MenuEventWithArg(NagatoMenuPipe.WebNew, $treeView.Key) 
    
End

Public Sub TreeView_MouseDrag()
    
    If $treeView.Key Not Begins "http://" Then Return
    
    $title = $treeView.Current.Text
    Drag.Icon = Stock["medium/file"]
    $treeView.Drag($treeView.Key, "text/plain")
    
End

Public Sub TreeView_DragMove()
    
    If Not $treeView.FindAt(Drag.X, Drag.Y) Then
        $treeView[$treeView.Item.Key].Selected = True
    Else
        Return
    End If
    
End

Public Sub TreeView_Drop()
    
    Dim yukiCategory As String
    
    If $title = "" Then Return
    If Drag.Data Not Begins "http://" Then Return
    
    yukiCategory = IIf($treeView.Item.Key Begins "http://", $treeView.Item.ParentKey, $treeView.Item.Key)
    
    Try $treeView.Remove(Drag.Data)
    Try $treeView.Add(Drag.Data, $title,, yukiCategory)
    
    If Not Error Then NagatoDBWeb.ChangeBookmarkCategory(Drag.Data, yukiCategory)
    
    $title = ""
    
End
