' Gambas class file

Inherits NagatoMenuPipe

Private Const MenuName As String = "StarMenu"

Private $rootMenu As Menu

Private Sub initializeRootMenu(argButton As MenuButton)
    
    $rootMenu = New Menu(HaruhiMain)  
    $rootMenu.Name = MenuName
    
    argButton.Menu = MenuName
    
End

Private Sub addFontMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "FontMenu"
    
    With yukiMenu
        .Text = ("Select WebView Font")
        .Picture = MikuruIcon.Get("web")
    End With
    
End

Private Sub addAboutMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "AboutMenu"
    
    With yukiMenu
        .Text = ("About nagato-web")
        .Picture = MikuruIcon.Get("star")
    End With
    
End

Private Sub addQuitMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "QuitMenu"
    
    With yukiMenu
        .Text = ("Quit")
        .Picture = MikuruIcon.Get("button-cross")
    End With
    
End

Private Sub initializeCategoryMenus()
    
    addFontMenu()
    addAboutMenu()
    addQuitMenu()
    
End

Public Sub _new(argButton As MenuButton)
    
    initializeRootMenu(argButton)
    initializeCategoryMenus()
    
End

Public Sub FontMenu_Click()
    
    If Not Dialog.SelectFont() Then
        NagatoSettings.WebViewFont = Dialog.Font.Name
    Endif
    
End

Public Sub AboutMenu_Click()
    
    HaruhiAbout.ShowDialog()
    
End

Public Sub QuitMenu_Click()
    
    If HaruhiQuit.ShowDialog() = HaruhiQuit.ResponseQuit Then HaruhiMain.Close()
    
End
