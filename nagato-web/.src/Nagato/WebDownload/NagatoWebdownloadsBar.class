' Gambas class file

Private $container As HBox
Private $downloadsHBox As HBox

Private Sub initializeDownloadsHBox()
    
    $downloadsHBox = New HBox($container)
    
    With $downloadsHBox
        .Expand = True
    End With
    
End

Private Sub initializeCloseButton()
    
    Dim yukiWidget As New MenuButton($container) As "CloseButton"
    
    With yukiWidget
        .Width = $container.Height
        .Expand = False
        .Picture = MikuruIcon.Get("button-cross")
        .Border = False
        .Arrow = False
    End With
    
End

Public Sub SetWebDownload(argWebDownload As WebDownload)
    
    Dim yukiDownload As NagatoWebDownload
    
    argWebDownload.Path = Temp$("download_" & File.Name(argWebDownload.Url))
    
    Dialog.Title = ("Download")
    Dialog.Path = MikuruSafePath.Get(NagatoSettings.DownloadDirectory &/ File.Name(argWebDownload.Url))
    
    If Not Dialog.SaveFile() Then
        yukiDownload = New NagatoWebDownload($downloadsHBox, argWebDownload, Dialog.Path) As "Download"
        $container.Visible = True
    Else
        argWebDownload.Cancel()
    Endif
    
End

Public Sub _new(argContainer As Container)
    
    $container = New HBox(argContainer)
    $container.H = 32
    $container.Visible = False
    initializeDownloadsHBox()
    initializeCloseButton()
    
End

Public Sub CloseButton_Click()
    
    $container.Visible = False
    
End

Public Sub Download_Finish()
    
    If WebDownloads.Count = 0 Then $container.Visible = False
    
End
