' Gambas class file

Private $webDownload As WebDownload
Private $container As HBox
Private $textLabel As TextLabel
Private $progressBar As ProgressBar
Private $destinationPath As String
Private $timer As New Timer As "Timer"

Event Finish

Private Sub destroyAll()
    
    $container.Children.Clear()
    $container.Delete()
    
End

Private Sub initializeContainer(argContainer As Container)
    
    $container = New HBox(argContainer)
    
    With $container
        .Expand = True
    End With
    
End

Private Sub initializeTextLabel(argName As String)
    
    $textLabel = New TextLabel($container)
    
    With $textLabel
        .Text = argName
        .Alignment = Align.Left
        .Expand = True
    End With
    
End

Private Sub initializeProgressBar()
    
    $progressBar = New ProgressBar($container)
    
    With $progressBar
        .Expand = True
        .Value = 0
    End With
    
End

Public Sub _new(argContainer As Container, argWebDownload As WebDownload, argDestinationPath As String)
    
    initializeContainer(argContainer)
    initializeTextLabel(File.Name(argWebDownload.Url))
    initializeProgressBar()
    $webDownload = argWebDownload
    $destinationPath = argDestinationPath
    
    $timer.Start()
    
End

Public Sub Timer_Timer()
    
    $progressBar.Value = $webDownload.Progress
    
    If $webDownload.Progress = 1 Then
        Move $webDownload.Path To $destinationPath
        $timer.Stop()
        $webDownload.Delete()
        Raise Finish
        destroyAll()
    Endif
    
Catch
    $timer.Stop
    Message.Warning("YUKI.N > Something wrong is occured.", ("OK"))
    Try $webDownload.Delete()
    Return
    
End
