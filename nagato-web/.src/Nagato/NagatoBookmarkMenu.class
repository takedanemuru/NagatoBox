' Gambas class file

Inherits NagatoMenuPipe

Private Const MenuName As String = "WebBookmarkMenu"

Private $rootMenu As Menu

Private Sub initializeRootMenu(argButton As MenuButton)
    
    $rootMenu = New Menu(HaruhiMain) As "RootMenu"
    $rootMenu.Name = MenuName
    
    argButton.Menu = MenuName
    
End

Private Sub addBookmarkCurrentPageMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "BookmarkCurrentPageMenu"
    
    With yukiMenu
        .Text = ("Bookmark current page")
        .Picture = MikuruIcon.Get("toggle-expand-alt")
    End With
    
End

Private Sub addEditBookmarkMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "EditBookmarkMenu"
    
    With yukiMenu
        .Text = ("Edit bookmark")
        .Picture = MikuruIcon.Get("document-edit")
    End With
    
End

Private Sub addBookmarkItems(argPrentMenu As Menu, argCategory As String)
    
    Dim yukiResult As Result = NagatoDBWeb.GetBookmarkResultFoundByCategoryName(argCategory)
    Dim yukiMenu As Menu
    
    For Each yukiResult
        If yukiResult[NagatoDBWeb.ColumnUserDefinedPageName] = "" Then Continue
        yukiMenu = New Menu(argPrentMenu) As "BookmarkItemMenu"
        yukiMenu.Text = MikuruText.GetCollapsedText(yukiResult[NagatoDBWeb.ColumnPageName], 360)
        yukiMenu.Tag = yukiResult[NagatoDBWeb.ColumnUrl]
    Next
    
End

Private Sub initializeCategoryMenus()
    
    Dim yukiMenu As Menu
    Dim yukiCategory As String
    
    For Each yukiCategory In NagatoDBWeb.CategoryNames
        If Not NagatoDBWeb.HasBookmarkInCategory(yukiCategory) Then Continue
        yukiMenu = New Menu($rootMenu) 
        yukiMenu.Text = yukiCategory
        addBookmarkItems(yukiMenu, yukiCategory)
    Next
    
End

Private Sub refreshMenus()
    
    $rootMenu.Children.Clear()
    addBookmarkCurrentPageMenu()
    addEditBookmarkMenu()
        MikuruMenuSeparator.Set($rootMenu)
    initializeCategoryMenus()
    
End

Public Sub _new(argButton As MenuButton)
    
    initializeRootMenu(argButton)
    MikuruMenuSeparator.Set($rootMenu) ' use as dummy menu
    
End

Public Sub BookmarkCurrentPageMenu_Click()
    
    Raise MenuEvent(NagatoMenuPipe.UrlAddBookmark)
    
End

Public Sub BookmarkItemMenu_Click()
    
    Raise MenuEventWithArg(NagatoMenuPipe.WebNew, Last.Tag)
    
End

Public Sub RootMenu_Show()
    
    refreshMenus()
    
End
