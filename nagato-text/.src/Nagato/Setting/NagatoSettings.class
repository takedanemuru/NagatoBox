' Gambas class file

Create Static

Property Read RecentPaths As String[]
Property Read StickyPaths As String[]

Private $settings As New Settings

Public Sub PushRecentPath(argPath As String)
    
    Dim yukiPaths As String[] = Me.RecentPaths
    
    Debug argPath
    
    If Not Exist(argPath) Then Return
    
    Try yukiPaths.Remove(yukiPaths.Find(argPath))
    yukiPaths.Add(argPath, 0)
    yukiPaths.Resize(10)
    
    $settings["file/recent_paths"] = yukiPaths
    $settings.Save()
    
End

Public Sub LoadFormSettings(argForm As Form)
    
    With argForm
        .X = $settings["form/x", .X]
        .Y = $settings["form/y", .Y]
        .Height = $settings["form/height", .Height]
        .Width = $settings["form/width", .Width]
    End With
    
End

Public Sub SaveFormSettings(argForm As Form)
    
    With argForm
        $settings["form/x"] = .X
        $settings["form/y"] = .Y
        $settings["form/height"] = .Height
        $settings["form/width"] = .Width
    End With
    
End

Private Function RecentPaths_Read() As String[]

    Dim yukiPaths As String[] = $settings["file/recent_paths", [""]]
    Dim yukiCount As Integer

    For yukiCount = yukiPaths.Max DownTo 0
        If Not Exist(yukiPaths[yukiCount]) Then yukiPaths.Remove(yukiCount)
    Next

    Return yukiPaths

End

Private Function StickyPaths_Read() As String[]

    Dim yukiPaths As String[] = $settings["file/sticky_paths", [""]]
    Dim yukiCount As Integer

    For yukiCount = yukiPaths.Max DownTo 0
        If Not Exist(yukiPaths[yukiCount]) Then yukiPaths.Remove(yukiCount)
    Next

    Return yukiPaths

End
