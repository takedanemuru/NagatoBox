' Gambas class file

Inherits NagatoMenuSignal

Property Text As String

Private $textArea As TextArea

Private Function getCurrentPositionInUTF8(argQuery As String) As Integer ' read comment about return value.
    
    ' this function should return -1 when there's no matching word.
    
    Dim yukiSearchStartFrom As Integer = String.Len(Left$($textArea.Text, $textArea.Pos))
    Dim yukiPosition As Integer = String.InStr($textArea.Text, argQuery, yukiSearchStartFrom + 1, gb.IgnoreCase)

    'retry searching form beginning of the text when String.Instr couldn't find any matching.
    $textArea.pos = 1
    If yukiPosition = 0 Then yukiPosition = String.InStr($textArea.Text, argQuery, 0, gb.IgnoreCase)
    
    Return yukiPosition - 1
    
End

Public Sub SearchText(argText As String)
    
    Dim yukiStartPosition As Integer = getCurrentPositionInUTF8(argText)
    
    If yukiStartPosition = -1 Then Return
    
    $textArea.Select(yukiStartPosition, String.Len(argText))
    
End

Public Sub ReplaceAll(argTarget As String, argReplacement As String)
    
    Dim yukiCount As Integer
    
    If argReplacement = "" Then Return
    If $textArea.Text = "" Then Return
    
    yukiCount = MikuruText.GetReplacementCount($textArea.Text, argTarget)
    
    If yukiCount = 0 Then
        Message.Error(("Query not found"))
    Else
        If 2 = Message.Question(Subst$(("&1 found, replace them all ?"), yukiCount), ("Cancel"), ("Replace All")) Then
            $textArea.Text = Replace$($textArea.Text, argTarget, argReplacement, gb.IgnoreCase)
        Endif
    Endif
    
End

Public Sub _new(argContainer As Container)
    
    $textArea = New TextArea(argContainer) As "TextArea"
    
    With $textArea
        .Expand = True
        .SetFocus()
    End With
    
End

Public Sub TextArea_KeyPress()
    
    If Key.Control Then
        If Key.Code = Key["t"] Then Raise Signal(MikuruSignal.ViewNewTab, [""])
        If Key.Code = Key["s"] Then Raise Signal(MikuruSignal.FileSave, [""])
        If Key.Code = Key["f"] Then Raise Signal(MikuruSignal.EditSearch, [""])
    End If
    
End

Public Sub TextArea_Change()
    
    Raise Signal(MikuruSignal.EditChange, [""])
    
End

Private Function Text_Read() As String

    Return $textArea.Text

End

Private Sub Text_Write(Value As String)

    $textArea.Text = Value

End
