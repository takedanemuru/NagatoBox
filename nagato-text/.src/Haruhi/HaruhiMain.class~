' Gambas class file

Private Sub newTab(Optional argPath As String = "")
    
    Dim yukiTextPad As NagatoTextPad
    
    Inc MainTabs.Count
    MainTabs.Index = MainTabs.Count - 1

    refreshRecentPaths(argPath)
    yukiTextPad = New NagatoTextPad(MainTabs, argPath) As "TextPad"
    
End

Private Sub activateMe()
    
    Dim yukiDesktopWindow As New DesktopWindow(Me.Id)
    
    yukiDesktopWindow.Desktop = Desktop.Current
    yukiDesktopWindow.Activate()
    
End

Private Sub refreshRecentPaths(argPath As String)
    
    Dim yukiPath As String
    
    If Exist(argPath) Then NagatoSettings.PushRecentPath(argPath)
    
    RecentFilesIconView.Clear()
    
    For Each yukiPath In NagatoSettings.RecentPaths
        If Not RecentFilesIconView.Exist(yukiPath) Then
            RecentFilesIconView.Add(yukiPath, File.Name(yukiPath), MikuruTextTile.Get(yukiPath))
        End If
    Next
    
End

Public Sub SetPath(argPath As String)
    
    refreshRecentPaths(argPath)
    activateMe()
    newTab(argPath)
    
End

Public Sub OpenWithPath(Optional argPath As String = "")
    
    Dim yukiTextPad As NagatoTextPad
    
    refreshRecentPaths(argPath)
    
    Inc MainTabs.Count
    MainTabs.Index = MainTabs.Count - 1
    
    yukiTextPad = New NagatoTextPad(MainTabs, argPath) As "TextPad"
    
    Object.Lock(Me)
        Me.Show()
        NagatoSettings.LoadFormSettings(Me)
    Object.Unlock(Me)
    
End

Public Sub InitializeRecentIconView()
    
    refreshRecentPaths("")
    
End

Public Sub Form_Close()
    
    NagatoSettings.SaveFormSettings(Me)
    NagatoDBus.Unregister()
    
End

Public Sub TextPad_Signal(argSignal As Integer, argValue As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.ViewNewTab
            newTab()
        Case MikuruSignal.ViewNewTabWithPath
            newTab(argValue[0])
        Case MikuruSignal.FileNewPath
            refreshRecentPaths(argValue[0])
    End Select
    
End

Public Sub MainTabs_Close(Index As Integer)

    Dim yukiCount As Integer

    If Index = 0 Then Return
    If MainTabs.Count = 1 Then Return

    For yukiCount = MainTabs[Index].Children.Count - 1 DownTo 0
        MainTabs[Index].Children[yukiCount].Delete()
    Next
    
    MainTabs[Index].Delete()

End

Public Sub RecentFilesIconView_Activate()

    Debug RecentFilesIconView.Key

    OpenWithPath(RecentFilesIconView.Key)

End
