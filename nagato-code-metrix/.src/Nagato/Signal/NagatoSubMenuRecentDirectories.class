' Gambas class file

Inherits NagatoButtonMenu

Private Function getProjectIcon2(argDirectory As String) As Picture
    
    Dim yukiPath As String = MikuruDataFile[argDirectory &/ ".project", "Icon"]
    
    Return Picture.Load(argDirectory &/ yukiPath)
    
Catch
    Return Null
    
End

Private Sub setNull()
    
    Dim yukiMenu As New Menu(Me._$parentMenu)
    
    With yukiMenu
        .Text = ("Not Found")
    End With
    
End

Private Sub setRecents()
    
    Dim yukiDirectory As String
    Dim yukiMenu As Menu
    
    For Each yukiDirectory In NagatoSettings.RecentDirectories
        yukiMenu = New Menu(Me._$parentMenu) As "RecentDirectoryMenu"
        With yukiMenu
            .Text = File.Name(yukiDirectory)
            .Tag = yukiDirectory
            .Picture = getProjectIcon2(yukiDirectory)
        End With
    Next
    
End

Public Sub _InitializeParentMenu(argRootObject As Object, argMenuName As String) ' override
    
    Me._$parentMenu = New Menu(argRootObject) As "ParentMenu"
    
    With Me._$parentMenu
        .Name = argMenuName
        .Text = ("Recent Selected")
        .Picture = MikuruIcon["clock"]
    End With
    
End

Public Sub _RefreshChildMenus() ' override
    
    If NagatoSettings.RecentDirectories = Null Then
        setNull()
    Else
        setRecents()
    Endif
    
End

Public Sub RecentDirectoryMenu_Click()
    
    NagatoSettings.PushDirectory(Last.Tag)
    
    Raise Signal(MikuruSignal.FileOpenRecent, [Last.Tag])
    
End
