' Gambas class file

Create Static

Inherits NagatoDB

Public Const TableGeo As String = "geo"
Public Const ColumnId As String = "id"
Public Const ColumnVisibleName As String = "visible_name"
Public Const ColumnLatitude As String = "latitude"
Public Const ColumnLongitude As String = "longitude"

Public _$uniqueName As String = "Geo"


Private Sub ensureGeoTable()
    
    Dim yukiTable As Table
    
    Try Me._$connection.Open()
    
    If Not Me._$connection.Tables.Exist(TableGeo) Then
        yukiTable = Me._$connection.Tables.Add(TableGeo)
        yukiTable.Fields.Add(ColumnId, db.Serial)
        yukiTable.Fields.Add(ColumnVisibleName, db.String)
        yukiTable.Fields.Add(ColumnLatitude, db.Float)
        yukiTable.Fields.Add(ColumnLongitude, db.Float)
        yukiTable.PrimaryKey = [ColumnId]
        yukiTable.Update()
    Endif
    
End

Public Function HasBookmark(argLatitude As Float, argLongitude As Float) As Boolean
    
    Dim yukiResult As Result = Me._$connection.Find(TableGeo, "latitude = &1 and longitude = &2", argLatitude, argLongitude)
    
    Return IIf(yukiResult.Count = 0, False, True)
    
End

Public Sub SetBookmark(argName As String, argLatitude As Float, argLongitude As Float)
    
    Dim yukiResult As Result
    
    If Me.HasBookmark(argLatitude, argLongitude) Then
        Me._$connection.Delete(TableGeo, "latitude = &1 and longitude = &2", argLatitude, argLongitude)
    End If
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Create(TableGeo)
        yukiResult[ColumnVisibleName] = argName
        yukiResult[ColumnLatitude] = argLatitude
        yukiResult[ColumnLongitude] = argLongitude
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Debug Error.Text
    Me._$connection.Rollback()
    
End

Public Function HasAnyBookmark() As Boolean
    
    Dim yukiResult As Result = Me._$connection.Find(TableGeo)
    
    Return IIf(yukiResult.Count > 0, True, False)
    
End

Public Function GetAllResult() As Result
    
    Dim yukiResult As Result = Me._$connection.Find(TableGeo)
    
    Return yukiResult
    
End

Public Sub _new()
    
    Me._SetConnection()
    ensureGeoTable()
    
End
