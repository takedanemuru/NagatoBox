' Gambas class file

Inherits NagatoMenu

Private Sub initializeParentMenu(argRootMenu As Menu)
    
   Me._$rootMenu = New Menu(argRootMenu) As "ParentMenu"
    
    With Me._$rootMenu
        .Text = ("Wikipedia")
    End With
    
End

Private Sub addLanguageMenus()
    
    Dim yukiMenu As Menu
    Dim yukiCollection As Collection = NagatoSettingsWikipedia.Languages
    Dim yukiValue As String
    
    For Each yukiValue In yukiCollection
        yukiMenu = New Menu(Me._$rootMenu) As "LanguageMenu"
        yukiMenu.Text = yukiValue
        yukiMenu.Tag = yukiCollection.Key
        yukiMenu.Picture = MikuruIcon.GetCheck(NagatoSettingsWikipedia.LanguageKey = yukiCollection.Key)
    Next
    
End

Public Sub _new(argRootMenu As Menu)
    
    initializeParentMenu(argRootMenu)
    Me._AddMenuItem("", 0) ' as dummy menu to catch "ParentMenu_Show" event
    
End

Public Sub ParentMenu_Show()
    
    Me._$rootMenu.Children.Clear()
    Me._AddMenuItem(("Refresh"), NagatoMenuPipe.MenuRefreshWikipediaArticles, MikuruIcon.Get("button-rotate-cw"))
    Me._AddMenuItem("", 0) ' as spacer
    addLanguageMenus()
    
End

Public Sub LanguageMenu_Click()
    
    If NagatoSettingsWikipedia.LanguageKey = Last.Tag Then Return

    NagatoSettingsWikipedia.LanguageKey = Last.Tag
    Raise MenuEvent(NagatoMenuPipe.MenuRefreshWikipediaArticles)
    
End
