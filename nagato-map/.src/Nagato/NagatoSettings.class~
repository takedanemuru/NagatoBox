' Gambas class file

Create Static

Property WikipediaRadius As Integer
Property WikipediaLimit As Integer
Property Read WikipediaLanguages As Collection
Property WikipediaLanguageKey As String

Private $settings As New Settings
Private $languages As Collection = ["en": ("English"), "ja": ("Japanese")]

Event WikipediaApiChanged

Public Sub LoadFormSettings(argForm As Form)
    
    argForm.X = $settings["form/x", argForm.X]
    argForm.Y = $settings["form/y", argForm.Y]
    argForm.Height = $settings["form/height", argForm.Height]
    argForm.Width = $settings["form/width", argForm.Width]
    
End

Public Sub SaveFormSettings(argForm As Form)
    
    $settings["form/x"] = argForm.X
    $settings["form/y"] = argForm.Y
    $settings["form/height"] = argForm.Height
    $settings["form/width"] = argForm.Width
    
End

Private Function WikipediaRadius_Read() As Integer

    Return $settings["wikipedia/radius", 100000]

End

Private Sub WikipediaRadius_Write(Value As Integer)

    If $settings["wikipedia/radius"] = Value Then Return

    $settings["wikipedia/radius"] = Value
    $settings.Save()

    Raise WikipediaApiChanged

End

Private Function WikipediaLimit_Read() As Integer

    Return $settings["wikipedia/limit", 100]

End

Private Sub WikipediaLimit_Write(Value As Integer)

    If $settings["wikipedia/limit"] = Value Then Return

    $settings["wikipedia/limit"] = Value
    $settings.Save()

    Raise WikipediaApiChanged

End

Private Function WikipediaLanguages_Read() As Collection

    Return $languages

End

Private Function WikipediaLanguageKey_Read() As String

    Return $settings["wikipedia/language_key", "en"]

End

Private Sub WikipediaLanguageKey_Write(Value As String)

    $settings["wikipedia/language_key"] = Value

    Raise WikipediaApiChanged

End
