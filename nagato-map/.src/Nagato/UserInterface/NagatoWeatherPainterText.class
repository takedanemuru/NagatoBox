' Gambas class file

Inherits NagatoWeatherPainter

Private $textRect As New Rect
Private $currentWeather As NagatoWeatherElement

Private Sub setTextRect()
    
    With $textRect
        .X = 8
        .Y = 8
        .W = Me._$drawingArea.W - 16
        .H = Me._$drawingArea.H - 16
    End With
    
End

Private Function getTitle() As String
    
    Dim yukiCondition As String = $currentWeather[MikuruWeatherKey.IconName]
    
    Return Subst$("<h2>&1</h2>&2", yukiCondition, String$(8, "<br>"))
    
End

Private Function getHeaders() As String
    
    Dim yukiReturn As String = "<h2> </h2><br>"
    
    yukiReturn &= "Temp.:<br>"
    yukiReturn &= "Wind(S):<br>"
    yukiReturn &= "Wind(D):<br>"
    yukiReturn &= "Hum.:<br>"
    yukiReturn &= "Cloud:<br>"
    yukiReturn &= "Press.:<br>"
    yukiReturn &= "Prec.:<br>"
    
    Return yukiReturn
    
End

Private Function getStatus() As String
    
    Dim yukiReturn As String = "<h2> </h2><br>"
    
    With $currentWeather
        yukiReturn &= Subst$("&1Â°C<br>", .[MikuruWeatherKey.Temperature])
        yukiReturn &= Subst$("&1m<br>", .[MikuruWeatherKey.WindSpeed])
        yukiReturn &= Subst$("&1<br>", .[MikuruWeatherKey.WindDirection])
        yukiReturn &= Subst$("&1%<br>", .[MikuruWeatherKey.Humidity])
        yukiReturn &= Subst$("&1%<br>", .[MikuruWeatherKey.Cloudiness])
        yukiReturn &= Subst$("&1HPa<br>", .[MikuruWeatherKey.Pressure])
        yukiReturn &= Subst$("&1mm<br>", .[MikuruWeatherKey.Precipitation])
   End With
    
    Return yukiReturn
    
End

Public Sub _call(argWeatherElement As NagatoWeatherElement)
    
    $currentWeather = argWeatherElement
    setTextRect()

    Paint.Brush = Paint.Color(Color.White)
    Paint.Font.Bold = True

    With $textRect
        Paint.DrawRichText(getTitle(), .X, .Y, .W, .H, Align.Center)
        Paint.DrawRichText(getHeaders(), .X, .Y, .W, .H, Align.Left)
        Paint.DrawRichText(getStatus(), .X, .Y, .W, .H, Align.Right)
    End With
    
End
