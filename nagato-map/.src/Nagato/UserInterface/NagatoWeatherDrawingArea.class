' Gambas class file

Private $drawingArea As DrawingArea
Private $observer As Observer
Private $backgroundPainter As NagatoWeatherPainterBackground
Private $iconPainter As NagatoWeatherPainterIcon
Private $textPainter As NagatoWeatherPainterText
Private $conditions As New NagatoWeatherLongitudeConditions

Private Sub setWeatherInfomation()
    
    $conditions.Refresh()
    
    Paint.Begin($drawingArea)
        $backgroundPainter($conditions.GetBackground())
        $iconPainter($conditions.GetIcon())
        $textPainter($conditions.CurrentWeather)
    Paint.End()
    
End

Private Sub setPleaseWait()
    
    Paint.Begin($drawingArea)
        $backgroundPainter(Color.DarkBlue)
        $iconPainter(MikuruIcon.Get("question", 48).Image)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.DrawRichText("Please wait...", 0, 0, $drawingArea.W, $drawingArea.H, Align.Center)
    Paint.End()
    
End

Public Sub Refresh()
    
    $drawingArea.Refresh()
    
End

Public Sub _new(argDrawingArea As DrawingArea)
    
    $drawingArea = argDrawingArea
    $backgroundPainter = New NagatoWeatherPainterBackground($drawingArea)
    $iconPainter = New NagatoWeatherPainterIcon($drawingArea)
    $textPainter = New NagatoWeatherPainterText($drawingArea)
    $observer = New Observer($drawingArea) As "Asakura"
    
End

Public Sub Asakura_Draw()
    
    If NagatoWeatherCache.IsReady
        setWeatherInfomation()
    Else
        setPleaseWait()
    End If
    
End

Public Sub SetLongitude(argLongitude As Float)

    $conditions.SetLongitude(argLongitude)

End
