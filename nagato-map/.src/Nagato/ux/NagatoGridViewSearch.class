' Gambas class file

Inherits NagatoGridView

Private Sub initializeSearchGrid(argContainer As Container)
    
    Me._$gridView = New GridView(argContainer) As "SearchGrid"

    With Me._$gridView
        .Columns.Count = 3
        .Rows.Count = 0
        .Header = GridView.Horizontal
        .Expand = True
        .Mode = Select.Single
        .Columns[0].Text = ("Name")
        .Columns[1].Text = ("Latitude")
        .Columns[2].Text = ("Longitude")
    End With
    
End

Private Sub clearGrid()
    
    With Me._$gridView
        .Clear()
        .Rows.Count = 0
        .Width = 300
        .Columns[0].W = 200
    End With
    
End

Private Sub setData(argCollection As Collection)
    
    Dim yukiGeoElement As NagatoGeoElement
    
    For Each yukiGeoElement In argCollection
        With Me._$gridView
            Inc .Rows.Count
            Me._$gridView[.Rows.Max, 0].Text = yukiGeoElement.VisibleName
            Me._$gridView[.Rows.Max, 0].Alignment = Align.Left
            Me._$gridView[.Rows.Max, 1].Text = yukiGeoElement.Latitude
            Me._$gridView[.Rows.Max, 1].Alignment = Align.Right
            Me._$gridView[.Rows.Max, 2].Text = yukiGeoElement.Longitude
            Me._$gridView[.Rows.Max, 2].Alignment = Align.Right
            Me._SetBackgroundColor()
        End With
    Next
    
End

Private Function getLatitude(argRow As Integer) As Float
    
    Return CFloat(Me._$gridView[argRow, 1].Text)
    
End

Private Function getLongitude(argRow As Integer) As Float
    
    Return CFloat(Me._$gridView[argRow, 2].Text)
    
End

Public Sub RefreshSearchGridData(argCollection As Collection)
    
    clearGrid()
    setData(argCollection)
    
End

Public Sub _new(argContainer As Container)
    
    initializeSearchGrid(argContainer)
    
End

Public Sub SearchGrid_Activate()
    
    If Not Me._IsValidRow(Me._$gridView.Row) Then Return
    
    HaruhiMain.SetLocation(getLatitude(Me._$gridView.Row), getLongitude(Me._$gridView.Row), 12)
    Raise MenuEvent(NagatoMenuPipe.MenuRefreshWikipediaArticles)
    
End
