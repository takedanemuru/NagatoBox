' Gambas class file

Inherits NagatoMenuPipe

Private $tabStrip As TabStrip
Private $searchGrid As GridView

Private Sub initializeSearchGrid()
    
    $searchGrid = New GridView($tabStrip) As "SearchGrid"

    With $searchGrid
        .Columns.Count = 3
        .Rows.Count = 1
        .Header = GridView.Horizontal
        .Expand = True
        .Mode = Select.Single
        .Columns[0].Text = ("Name")
        .Columns[1].Text = ("Latitude")
        .Columns[2].Text = ("Longitude")
    End With
    
End

Private Sub clearGrid()
    
    With $searchGrid
        .Clear()
        .Rows.Count = 1
        .Width = 300
        .Columns[0].W = 200
    End With
    
End

Private Sub setData(argCollection As Collection)
    
    Dim yukiGeoElement As NagatoGeoElement
    
    For Each yukiGeoElement In argCollection
        With $searchGrid
            $searchGrid[.Rows.Max, 0].Text = yukiGeoElement.VisibleName
            $searchGrid[.Rows.Max, 0].Alignment = Align.Left
            $searchGrid[.Rows.Max, 1].Text = yukiGeoElement.Latitude
            $searchGrid[.Rows.Max, 1].Alignment = Align.Right
            $searchGrid[.Rows.Max, 2].Text = yukiGeoElement.Longitude
            $searchGrid[.Rows.Max, 2].Alignment = Align.Right
            Inc .Rows.Count
        End With
    Next
    
End

Private Function getLatitude(argRow As Integer) As Float
    
    Return CFloat($searchGrid[argRow, 1].Text)
    
End

Private Function getLongitude(argRow As Integer) As Float
    
    Return CFloat($searchGrid[argRow, 2].Text)
    
End

Public Function isValidRow(argRow As Integer) As Boolean
    
    If $searchGrid[argRow, 1].Text = "" Then Return False
    If $searchGrid[argRow, 2].Text = "" Then Return False
    
    Return True
    
End

Public Sub RefreshSearchGridData(argCollection As Collection)
    
    clearGrid()
    setData(argCollection)
    
End

Public Sub _new(argTabStrip As TabStrip)
    
    $tabStrip = argTabStrip
    initializeSearchGrid()
    
End

Public Sub SearchGrid_Activate()
    
    If Not isValidRow($searchGrid.Row) Then Return
    
    HaruhiMain.SetLocation(getLatitude($searchGrid.Row), getLongitude($searchGrid.Row), 12)
    Raise MenuEvent(NagatoMenuPipe.MenuRefreshWikipediaArticles)
    
End
