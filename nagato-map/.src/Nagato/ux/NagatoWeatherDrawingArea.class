' Gambas class file

Property Longitude As Float

Private Const IconSize As Integer = 96

Private $drawingArea As DrawingArea
Private $observer As Observer
Private $currentWeather As NagatoWeatherElement
Private $backgroundPainter As NagatoWeatherPainterBackground
Private $textPainter As NagatoWeatherPainterText
Private $longitude As Float

Private Function getWeatherIcon() As Image
    
    Dim yukiId As Integer = $currentWeather[MikuruWeatherKey.IconId]
    
    Return NagatoWeatherIcon.GetWithLongitude(yukiId, $longitude, IconSize)
    
End

Private Sub setWeatherInfomation(argBackground As Integer)
    
    Paint.Begin($drawingArea)
        $backgroundPainter.Paint(argBackground)
        Paint.DrawImage(getWeatherIcon(), ($drawingArea.W - IconSize) / 2, 16)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        $textPainter.Draw($currentWeather)
    Paint.End()
    
Catch
    Return
    
End

Private Sub setPleaseWait(argBackground As Integer)
    
    Paint.Begin($drawingArea)
        $backgroundPainter.Paint(argBackground)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.DrawImage(MikuruIcon.Get("question", 48).Image, ($drawingArea.W - 48) / 2, 16)
        Paint.DrawRichText("Please wait...", 0, 0, $drawingArea.W, $drawingArea.H, Align.Center)
    Paint.End()
    
End

Public Sub Refresh()
    
    $drawingArea.Refresh()
    
End

Public Sub _new(argDrawingArea As DrawingArea)
    
    $drawingArea = argDrawingArea
    $backgroundPainter = New NagatoWeatherPainterBackground($drawingArea)
    $textPainter = New NagatoWeatherPainterText($drawingArea)
    $observer = New Observer($drawingArea) As "Asakura"
    
End

Public Sub Asakura_Draw()
    
    Dim yukiBackground As Integer
    
     If NagatoWeatherCache.IsReady
        $currentWeather = NagatoWeatherCache.GetWeatherElementMostRecent()
        yukiBackground = NagatoWeatherCondition.GetColor($currentWeather[MikuruWeatherKey.IconId], MikuruTime.IsDay($longitude))
        setWeatherInfomation(yukiBackground)
    Else
        setPleaseWait(Color.DarkBlue)
    End If
    
End

Private Function Longitude_Read() As Float

    Return $longitude

End

Private Sub Longitude_Write(Value As Float)

    $longitude = Value

End
