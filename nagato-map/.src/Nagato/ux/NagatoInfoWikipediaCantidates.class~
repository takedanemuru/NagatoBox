' Gambas class file

Private $tabstrip As TabStrip
Private $wikipediaUrls As New String[]
Private $positions As New PointF[]
Private $wikipediaGrid As GridView

Private $wikipediaGeoData As New NagatoWikipediaGeoData As "WikipediaGeoData"

Event NewArticle(argUrl As String)

Private Sub initializeWikipediaListGrid()
    
    $tabStrip.Index = 1
    
    $wikipediaGrid = New GridView($tabStrip) As "CantidatesGrid"
    
    With $wikipediaGrid
        .Header = GridView.None
        .Columns.Count = 1
        .Rows.Count = 1
        .Expand = True
        .Mode = Select.Single
    End With
    
End

Private Sub setWikipediaArticles()
    
    Dim yukiElement As NagatoWikipediaGeoDataElement
    
    $wikipediaGrid.Clear()
    $wikipediaGrid.Rows.Count = 1
    
    $positions.Clear()
    $wikipediaUrls.Clear()
    
    For Each yukiElement In $wikipediaGeoData.Elements
        With $wikipediaGrid
            .[.Rows.Max, 0].Text = yukiElement.Title
            .[.Rows.Max, 0].Alignment = Align.Left
            $wikipediaUrls.Add(yukiElement.Url)
            $positions.Add(Object.New("PointF", [yukiElement.Latitude, yukiElement.Longitude]))
            If Odd(.Rows.Max) Then 
                $wikipediaGrid[.Rows.Max, 0].Background = Color.Merge(Color.White, Color.Violet, 0.1)
            End If
            Inc .Rows.Count
        End With
    Next
    
    $tabStrip.Index = 1
    
End

Public Sub Request(argLatitude As Float, argLongitude As Float)
    
    $wikipediaGeoData.Start("ja", argLatitude, argLongitude)
    
End

Public Sub _new(argTabstrip As TabStrip)
    
    $tabstrip = argTabstrip
    initializeWikipediaListGrid()
    
End

Public Sub WikipediaGeoData_Finished()
    
    setWikipediaArticles()
    
End

Public Sub CantidatesGrid_Activate()
    
    Dim yukiElement As NagatoWikipediaGeoDataElement = $wikipediaGeoData.Elements[$wikipediaGrid.Row]
    
    If $wikipediaGrid[$wikipediaGrid.Row, 0].Text <> "" Then
        ' Raise NewArticle($wikipediaUrls[$wikipediaGrid.Row])
        ' HaruhiMain.SetMarker($positions[$wikipediaGrid.Row].X, $positions[$wikipediaGrid.Row].Y)
        Raise NewArticle(yukiElement.Url)
        HaruhiMain.SetMarker(yukiElement.Latitude, yukiElement.Longitude)
    End If
    
End
