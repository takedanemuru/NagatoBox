' Gambas class file

Private $tabStrip As TabStrip
Private $bookmarkGrid As GridView

Private Sub setBookmarkData()
    
    Dim yukiResult As Result = NagatoDBGeo.GetAllResult()
    Dim yukiName As String
    Dim yukiLatitude As Float
    Dim yukiLongitude As Float
    
    For Each yukiResult
        With NagatoDBGeo
            yukiName = yukiResult[.ColumnVisibleName]
            yukiLatitude = yukiResult[.ColumnLatitude]
            yukiLongitude = yukiResult[.ColumnLongitude]
            $bookmarkGrid[$bookmarkGrid.Rows.Max, 0].Text = yukiName
            $bookmarkGrid[$bookmarkGrid.Rows.Max, 1].Text = yukiLatitude
            $bookmarkGrid[$bookmarkGrid.Rows.Max, 2].Text = yukiLongitude
            Inc $bookmarkGrid.Rows.Count
        End With
    Next
    
End

Private Sub initializeBookmarkGrid()
    
    $tabStrip.Index = 4
    
    $bookmarkGrid = New GridView($tabStrip) As "BookmarkGrid"

    With $bookmarkGrid
        .Columns.Count = 3
        .Rows.Count = 1
        .Header = GridView.Horizontal
        .Expand = True
        .Mode = Select.Single
        .Columns[0].Text = ("Name")
        .Columns[1].Text = ("Latitude")
        .Columns[2].Text = ("Longitude")
    End With
    
    If NagatoDBGeo.HasAnyBookmark() Then setBookmarkData()
    
End

Public Sub Refresh()
    
    $bookmarkGrid.Clear()
    $bookmarkGrid.Rows.Count = 1
    
    setBookmarkData()
    
End

Public Sub _new(argTabstrip As TabStrip)
    
    $tabStrip = argTabstrip
    
    initializeBookmarkGrid()
    
End

Public Sub BookmarkGrid_Activate()
    
    Dim yukiRowIndex As Integer = $bookmarkGrid.Row
    Dim yukiLatitude As Float
    Dim yukiLongitude As Float
    
    If $bookmarkGrid[yukiRowIndex, 1].Text <> "" And If $bookmarkGrid[yukiRowIndex, 2].Text <> "" Then
        yukiLatitude = CFloat($bookmarkGrid[yukiRowIndex, 1].Text)
        yukiLongitude = CFloat($bookmarkGrid[yukiRowIndex, 2].Text)
        HaruhiMapView.SetMarker(yukiLatitude, yukiLongitude)
    Endif
    
End
