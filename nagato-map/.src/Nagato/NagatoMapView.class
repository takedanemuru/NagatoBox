' Gambas class file

Inherits NagatoMenuPipe

Property Read MapPoint As MapPoint
Property Read Screenshot As Picture

Private $mapView As MapView
Private $mapLayers As NagatoMapViewLayers
Private $marker As New NagatoMapMarker
Private $lineScale As New NagatoLineScale

Public Sub SetLocation(argLatitude As Float, argLongitude As Float, argZoom As Integer)
    
    With $mapView.Map
        .Center.Lat = argLatitude
        .Center.Lon = argLongitude
        .Zoom = argZoom
        .Refresh()
    End With
    
End

Public Sub SetMarker(argLatitude As Float, argLongitude As Float)
    
    $marker.SetArrow(argLatitude, argLongitude)
    Me.SetLocation(argLatitude, argLongitude, 15)
        
End

Public Sub ChangeLayer(argType As Integer)
    
    $mapLayers.SetVisibleLayer(argType)
    $mapView.Refresh()
    
End

Public Sub _new(argContainer As Container)
    
    $mapView = New MapView(argContainer) As "MapView"
    $mapView.Map.Zoom = 4
    $mapView.Expand = True
    $mapView.SetFocus()
    
    $mapLayers = New NagatoMapViewLayers($mapView)
    
End

Public Sub MapView_Draw()
    
    If $marker.Visible Then $marker.Draw($mapView)
    If NagatoSettings.ShowScale Then $lineScale.Draw($mapView)
    
    Raise MenuEvent(NagatoMenuPipe.MapNewLocation)
    
End

Private Function Screenshot_Read() As Picture

    Return Desktop.Screenshot($mapView.ScreenX, $mapView.ScreenY, $mapView.W, $mapView.H)

End

Private Function MapPoint_Read() As MapPoint

    Return $mapView.Map.Center

End
