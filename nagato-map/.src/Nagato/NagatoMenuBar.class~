' Gambas class file

Inherits NagatoMenuPipe

Private $container As Container
Private $locationLabel As TextLabel
Private $gearButton As MenuButton

Event MapSelect(argMapType As Integer)

Private Sub addSearchButton()
    
    Dim yukiButton As New Button($container) As "MenuButton"
    
    With yukiButton
        .Border = False
        .AutoResize = True
        .Picture = MikuruIcon.Get("search")
        .Text = ("Search")
    End With
    
End

Private Sub addMapSelectButton(argText As String, argTag As Integer, argValue As Boolean)
    
    Dim yukiButton As New ToggleButton($container) As "MapSelectButton"
    
    With yukiButton
        .AutoResize = True
        .Text = argText
        .Tag = argTag
        .Value = argValue
        .Radio = True
    End With
    
End

Private Sub addLocationLabel()
    
    $locationLabel = New TextLabel($container)
    
    With $locationLabel
        .Text = "YUKI .N > Can you see this ?"
        .Alignment = Align.Left
        .Expand = True
        .Padding = 2
        .AutoResize = True
    End With
    
End

Private Sub addGearButton()
    
    Dim yukiMenu As NagatoButtonMenu
    
    $gearButton = New MenuButton($container)
    
    With $gearButton
        .Arrow = False
        .Width = $container.Height
        .AutoResize = True
        .Picture = MikuruIcon.Get("star")
        .Border = False
    End With
    
    yukiMenu = New NagatoButtonMenu(HaruhiMain, $gearButton) As "Menu"
    
End

Private Sub initializeWidgets()
    
    addSearchButton()
    addMapSelectButton("GoogleEarth", HaruhiMain.GoogleMap, False)
    addMapSelectButton("OpenStreetMap", HaruhiMain.OpenStreetMap, True)
    addLocationLabel()
    addGearButton()
    
End

Public Sub SetLocation(argLatitude As Float, argLongitude As Float)
    
    $locationLabel.Text = Subst$(("<b>Latitude:</b> &1, <b>Longitude:</b> &2"), argLatitude, argLongitude)
    
Catch
    Return
    
End

Public Sub _new(argContainer As Container)
    
    $container = argContainer
    initializeWidgets()
    
End

Public Sub MapSelectButton_Click()
    
    Raise MapSelect(Last.Tag)
    
End

Public Sub MenuButton_Click()
    
    NagatoLeftPanel.Toggle()
    
End
