' Gambas class file

Inherits NagatoMenuPipe

Private $mapView As MapView
Private $marker As New NagatoMapMarker
Private $lineScale As New NagatoLineScale
Private $observer As Observer

Event NewPoint(argLatitude As Float, argLongitude As Float)

Private Sub setMapTile()
    
    With MikuruOpenStreetMap
        $mapView.Map.AddTile(.Name, .Pattern, Null).Copyright = .Copyright
    End With 
    
    MikuruGoogleMap.AddTile($mapView)
    
End

Private Sub setVisibleLayer(argMapType As Integer)
    
    With $mapView
        If argMapType = HaruhiMain.OpenStreetMap Then
            .Map[MikuruOpenStreetMap.Name].Visible = True
            .Map[MikuruGoogleMap.Name].Visible = False
        Else
            .Map[MikuruOpenStreetMap.Name].Visible = False
            .Map[MikuruGoogleMap.Name].Visible = True
        End If
    End With
    
End

Public Sub SetLocation(argLatitude As Float, argLongitude As Float, argZoom As Integer)
    
    With $mapView.Map
        .Center.Lat = argLatitude
        .Center.Lon = argLongitude
        .Zoom = argZoom
        .Refresh()
    End With
    
End

Public Sub SetMarker(argLatitude As Float, argLongitude As Float)
    
    $marker.SetArrow(argLatitude, argLongitude)
    
    With $mapView.Map
        .Center.Lat = argLatitude
        .Center.Lon = argLongitude
        .Zoom = 15
        .Refresh()
    End With
    
End

Public Sub ChangeLayer(argType As Integer)
    
    setVisibleLayer(argType)
    $mapView.Refresh()
    
End

Public Sub _new(argMapView As MapView)
    
    $mapView = argMapView
    $observer = New Observer($mapView) As "Asakura"
    
    $mapView.Map.Zoom = 4
    setMapTile()
    setVisibleLayer(HaruhiMain.OpenStreetMap)
    $mapView.SetFocus()
    
End

Private Sub drawSomethig()
    
    Paint.Begin(Draw.Device)
        Paint.Brush = Paint.Color(Color.Violet + 16777216 * 128)
        Paint.Rectangle(20, $mapView.H - 40, 200, 20)
        Paint.Fill()
    Paint.End()
    
End

Public Sub Asakura_Draw()
    
    If $marker.Visible Then $marker.Draw($mapView)
    
    drawSomethig()
    
    Raise NewPoint($mapView.Map.Center.Lat, $mapView.Map.Center.Lon)
    
End
