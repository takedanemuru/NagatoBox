' Gambas class file

Private $shutterSound As New NagatoSoundEffect
Private $screenshooter As New NagatoScreenshot As "ScreenShooter"

Private Sub initializePreview()
    
    Dim yukiPreview As Picture = $screenshooter.Thumbnail
    
    With PreviewPictureBox
        .Picture = yukiPreview
        .H = yukiPreview.H
        .W = yukiPreview.W
    End With
    
End

Private Sub initializeDirectoryButtonBox()
    
    With DirectoryButtonBox
        .Text = NagatosettingsScreenShooter.Directory
        .Picture = Stock["small/home"]
    End With
    
End

Private Sub initializeFileNameTextBox()
    
    Dim yukiTimeStamp As String = Format$(Now, ("yyyy/mm/dd_hh:nn:mm"))
    
    FileNameTextBox.Text = Subst$("screenshot_&1.png", yukiTimeStamp)
    FileNameTextBox.Select(0, String.Len(FileNameTextBox.Text) - 4)
    
End

Private Function getPath() As String
    
    Dim yukiPath As String = DirectoryButtonBox.Text &/ FileNameTextBox.Text 
    Dim yukiBaseName As String = DirectoryButtonBox.Text &/ File.BaseName(FileNameTextBox.Text)
    Dim yukiCount As Integer = 0
    
    If Not Exist(yukiPath) Then Return yukiPath 
    
    Do
        Inc yukiCount
        If Exist(Subst$("&1(&2).png", yukiBaseName, yukiCount)) Then Continue
        Return Subst$("&1(&2).png", yukiBaseName, yukiCount)
    Loop
    
End

Public Sub Call()
    
    $screenshooter.Get()
    
    If $screenshooter.Thumbnail Then
        $shutterSound.Play()
        Me.Center()
        Me.Show()
        initializeDirectoryButtonBox()
        initializeFileNameTextBox()
    Else
        Message.Error(("YUKI.N > Active Screen Not Found."), ("OK"))
    End If
    
End

Public Sub CancelButton_Click()

    Me.Close()

End

Public Sub DirectoryButtonBox_Click()

    Dialog.Path = DirectoryButtonBox.Text
    Dialog.Title = ("Select directory")

    If Dialog.SelectDirectory() Then Return

    With DirectoryButtonBox
        .Text = Dialog.Path
        .Picture = IIf(Dialog.Path = User.Home, Stock["small/home"], Stock["small/directory"])
        CheckBox1.Visible = True
    End With

    CheckBox1.Visible = True

End

Public Sub SaveButton_Click()

    $screenshooter.Save(getPath())
    If CheckBox1.Value Then NagatosettingsScreenShooter.Directory = DirectoryButtonBox.Text 
    Me.Close()

End

Public Sub ScreenShooter_Ready()
    
    initializePreview()
    
End
