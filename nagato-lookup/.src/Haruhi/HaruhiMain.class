' Gambas class file

Private Const HeightHide As Integer = 1
Private Const HeightShow As Integer = 28

Private $mainContainer As NagatoMainContainer
Private $netWm As NagatoNetWm
Private $lock As New NagatoLock

Private Sub initializeForm()
    
    Me.X = 0
    Me.Y = 0
    Me.Width = Screen.Width
    Me.Height = HeightHide
    Me.Background = NagatoSettingsForm.Foreground
    
End

Public Sub Form_Open()
    
    Application.MainWindow = Me
    $netWm = New NagatoNetWm(Me.Id)
    initializeForm()
    $mainContainer = New NagatoMainContainer(MainHBox) As "MainContainer"
    NagatoNotification.Show()
    
End

Public Sub Form_Close()
    
    NagatoDBus.Unregister()
    
End

Public Sub Form_Enter()
    
    Me.Height = HeightShow
    $netWm.SetHeight(Me.H)
    $mainContainer.Toggle(False)
    
End

Public Sub Form_Leave()
    
    If $lock.Locked Then Return
    
    Me.Height = HeightHide
    $netWm.SetHeight(HeightHide)
    $mainContainer.Toggle(True)
    
End

Public Sub Form_Move()
    
    Me.X = 0
    Me.Y = 0
    
End

Public Sub MainContainer_Signal(argSignal As Integer, argValues As Variant[])
    
    $lock(argSignal, argValues[0])
    If argSignal = MikuruSignal.WidgetHide And If Mouse.ScreenX > HeightShow Then Form_Leave()
    
End
