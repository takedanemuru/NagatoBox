' Gambas class file

Private Const IconSize As Integer = 64
Private Const NotFoundKey As String = "NotFound"

Private $iconViewHandler As NagatoIconViewHandler

Private Sub addDesktopFileIcon(argDesktopFile As NagatoDesktopFile)
    
    Dim yukiIcon As Picture = argDesktopFile.GetIcon(IconSize).Image.Stretch(IconSize, IconSize).Picture
    
    ' To avoid key error, because KDE apps are added twice in menu-cache. 
    If ApplicationsIconView.Exist(argDesktopFile.Path) Then Return
    
    ApplicationsIconView.Add(argDesktopFile.Path, argDesktopFile.CommonName, yukiIcon)
    
End

Private Sub ensureVisible()
    
    Dim yukiDesktopWindow As New DesktopWindow(Me.Id)
    
    yukiDesktopWindow.Desktop = Desktop.Current
    
End

Private Sub setNotFound()
    
    ApplicationsIconView.Add(NotFoundKey, "Not found", MikuruIcons.Get("exclamation", IconSize))
    
End

Private Sub showResult()
    
    If ApplicationsIconView.Count > 0 Then
        ensureVisible()
    Else
        setNotFound()
    End If

    Me.Show()
    
End


Private Function containsSearchQuery(argDesktopFile As NagatoDesktopFile, argQuery As String) As Boolean
    
    With argDesktopFile
        If File.BaseName(.Path) Like Subst$("*&1*", argQuery) Then Return True
        If .CommonName Like Subst$("*&1*", argQuery) Then Return True
        If .Name Like Subst$("*&1*", argQuery) Then Return True
        If .GenericName Like Subst$("*&1*", argQuery) Then Return True
        If .Comment Like Subst$("*&1*", argQuery) Then Return True
        If .LocaleGenericName Like Subst$("*&1*", argQuery) Then Return True
        If .LocaleComment Like Subst$("*&1*", argQuery) Then Return True
    End With
    
    Return False
    
End

Public Sub SetIconViewWithSearchQuery(argQuery As String)
    
    Dim yukiDesktopFile As NagatoDesktopFile

    ApplicationsIconView.Clear()

    For Each yukiDesktopFile In NagatoMenuCache3.GetApplications("All")
        If Not containsSearchQuery(yukiDesktopFile, argQuery) Then Continue
        addDesktopFileIcon(yukiDesktopFile)
    Next
    
    showResult()
    
End

Public Sub SetIconViewWithAllSettingsApps()
    
    Dim yukiDesktopFile As NagatoDesktopFile

    ApplicationsIconView.Clear()

    For Each yukiDesktopFile In NagatoMenuCache3.GetApplications("Settings")
        addDesktopFileIcon(yukiDesktopFile)
    Next
    
    showResult()
    
End

Public Sub Form_Open()

    Me.Center()
    
    If Not Object.IsValid($iconViewHandler) Then
        $iconViewHandler = New NagatoIconViewHandler(ApplicationsIconView)
    End If

End

Public Sub ApplicationsIconView_Click()

    Dim yukiDesktopFile As NagatoDesktopFile
    Dim yukiProperty As String
    
    If Last.Key = NotFoundKey Then Return
    
    yukiDesktopFile = New NagatoDesktopFile(Last.Key)
    
    yukiProperty = Subst$("<b>Name:</b> &1<br>", yukiDesktopFile.Name)
    yukiProperty &= Subst$("<b>Generic Name:</b> &1<br>", yukiDesktopFile.GenericName)
    yukiProperty &= Subst$("<b>Exec:</b> &1<br>", yukiDesktopFile.Exec)
    yukiProperty &= "<b>Description:</b> " & yukiDesktopFile.Comment

    PropertyLabel.H = PropertyLabel.Font.RichTextHeight(yukiProperty, PropertyLabel.W)
    PropertyLabel.Text = yukiProperty

End
