' Gambas class file

Private Const ApplicationsDirectory As String = "/usr/share/applications"

Private $defaultApplication As NagatoDefaultApplication
Private $path As String

Private Function isConfirmed(argDesktopFileName As String) As Boolean
    
    Dim yukiTemplate As String = ("YUKI.N > We are going to open <b>&1</b> with <b>&2</b>...")
    Dim yukiMessage As String = Subst$(yukiTemplate, File.Name($path), argDesktopFileName)
    
    Return Choose(Message.Question(yukiMessage, ("Cancel"), ("OK")), False, True)
    
End

Private Function isDefaultApplication(argPath As String) As Boolean ' argPath is path to DesktopFile
    
    If Not $defaultApplication.NotFound Then Return False
    
    Return ($defaultApplication.DesktopFile.Path = argPath)
    
Catch
    Return False
    
End

Private Sub setIcons(argDirectory As String, Optional argFilter As String)
    
    Dim yukiPath As String
    Dim yukiDesktopFile As DesktopFile
    Dim yukiIcon As Picture
    
    ApplicationsListView.Clear()
    
    For Each yukiPath In Dir(argDirectory).Sort(gb.IgnoreCase)
        Try yukiDesktopFile = New DesktopFile(argDirectory &/ yukiPath)
        If Error Then Continue
        If yukiDesktopFile.Name = "" Then Continue
        If argFilter And If yukiDesktopFile.Name Not Like Subst$("*&1*", argFilter) Then Continue
        yukiIcon = MikuruIcons.GetDesktopEntryIcon(argDirectory &/ yukiPath)
        ApplicationsListView.Add(yukiDesktopFile.Path, yukiDesktopFile.Name, yukiIcon)
    Next
    
End

Public Sub ShowAllApplications(argPath As String)
    
    $path = argPath
    $defaultApplication = New NagatoDefaultApplication(argPath)
    setIcons(ApplicationsDirectory)
    Me.Show()
    Me.Center()
    
End


Public Sub ApplicationsListView_Click()

    Dim yukiDesktopFile As New DesktopFile(Last.Key)
    
    If Not isConfirmed(yukiDesktopFile.Name) Then Return
    
    yukiDesktopFile.Run($path)

    If Not isDefaultApplication(yukiDesktopFile.Path) Then MikuruChangeDefaultApplication(yukiDesktopFile, $path)

    Me.Close()

End

Public Sub FilterButtonBox_Activate()

    If FilterButtonBox.Text = "" Then
        setIcons(ApplicationsDirectory)
    Else
        setIcons(ApplicationsDirectory, FilterButtonBox.Text)
    Endif

End

Public Sub FilterButtonBox_Click()

    FilterButtonBox_Activate()

End
