' Gambas module file

Private Function getCommand(argExec As String, argAsSuperUser As Boolean) As String
    
    Dim yukiReturn As String = IIf(argAsSuperUser, "gksu", "")
    Dim yukiCommand As String
    
    For Each yukiCommand In Split(argExec, " ", "")
        If yukiCommand Not Begins "%" Then yukiReturn &= (" " & yukiCommand)
    Next
        
    Return yukiReturn
    
End

Private Sub execute(argPath As String, Optional argAsSuperUser As Boolean = False)
    
    Dim yukiDesktopFile As New DesktopFile(argPath)
    Dim yukiCommand As String = getCommand(yukiDesktopFile.Exec, argAsSuperUser)
    Dim yukiOutput As String
    
    If yukiCommand Then
        Try Shell yukiCommand 
    Else
        Try Shell File.BaseName(argPath)
    End If
    
    If Not Error Then NagatoSettingsRecentApplications.Push(argPath)
    
End

Private Function getSanitizedDesktopFiles(argListPath As String, argFilePath As String) As String[]
    
    Dim yukiMimeType As String = DesktopMime.FromFile(argFilePath).Type
    Dim yukiIndex As Integer
    Dim yukiDesktopFiles As String[] = MikuruConfFlie(argListPath, yukiMimeType)
    
    For yukiIndex = yukiDesktopFiles.Max DownTo 0
        If Not Exist("/usr/share/applications" &/ yukiDesktopFiles[yukiIndex]) Then
            yukiDesktopFiles.Remove(yukiIndex, 1)
        Endif
    Next
    
    Return yukiDesktopFiles
    
End

Public Function GetDefaultDesktopFiles(argPath As String) As String[]
    
    Dim yukiDefaultLocal As String[] = getSanitizedDesktopFiles(MikuruApplicationDirectory.LocalDefaultList, argPath)
    Dim yukiDefaultGlobal As String[] = getSanitizedDesktopFiles(MikuruApplicationDirectory.GlobalDefaultList, argPath)
    Dim yukiNonDefaultGlobal As String[] = getSanitizedDesktopFiles(MikuruApplicationDirectory.GlobalMimeList, argPath)
    
    If yukiDefaultLocal.Count > 0 Then Return yukiDefaultLocal
    If yukiDefaultGlobal.Count > 0 Then Return yukiDefaultGlobal

    Return yukiNonDefaultGlobal
    
End

Public Sub Launch(argPath As String)
    
    execute(argPath)
    
End
