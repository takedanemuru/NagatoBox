' Gambas class file

Property Read ProgramName As String
Property Read Name As String
Property Read CommonName As String
Property Read GenericName As String
Property Read Comment As String
Property Read LocaleName As String
Property Read LocaleGenericName As String
Property Read LocaleComment As String
Property Read Path As String
Property Read Exec As String

Private $locale As String
Private $language As String
Private $name As String = ""
Private $genericName As String = ""
Private $comment As String = ""
Private $localGenericName As String = ""
Private $localeComment As String = ""
Private $languageGenericName As String = ""
Private $languageComment As String = ""
Private $desktopFile As DesktopFile


Public Sub SetData(argLine As String)
    
    If argLine Begins "Name=" Then
        $name = Replace$(argLine, "Name=", "")
    Else If argLine Begins "GenericName=" Then
        $genericName = Replace$(argLine, "GenericName=", "")
    Else If argLine Begins "Comment=" Then
        $comment = Replace$(argLine, "Comment=", "")
    Else If argLine Begins Subst$("GenericName[&1]=", $locale) Then
        $localGenericName = Replace$(argLine, Subst$("GenericName[&1]=", $locale), "")
    Else If argLine Begins Subst$("Comment[&1]=", $locale) Then
        $localeComment = Replace$(argLine, Subst$("Comment[&1]=", $locale), "")
    Else If argLine Begins Subst$("GenericName[&1]=", $language) Then
        $languageGenericName = Replace$(argLine, Subst$("GenericName[&1]=", $language), "")
    Else If argLine Begins Subst$("Comment[&1]=", $language) Then
        $localeComment = Replace$(argLine, Subst$("Comment[&1]=", $language), "")
    End If 
    
End

Public Sub _new(argDesktopFile As DesktopFile)
    
    $locale = Replace$(System.Language, "." & System.Charset, "")
    $language = Left$($locale, 2)
    $desktopFile = argDesktopFile
    
End

Private Function Name_Read() As String

    Return $desktopFile.Name
    
Catch
    Return ""

End

Private Function Path_Read() As String

    Return $desktopFile.Path
    
Catch
    Return ""

End

Private Function GenericName_Read() As String

    If $genericName <> "" Then
        Return $genericName
    Else 
        Return $desktopFile.Name
    Endif
    
Catch
    Return ""

End

Private Function Comment_Read() As String

    Return $comment

End

Private Function ProgramName_Read() As String

    Return $desktopFile.ProgramName
    
Catch
    Return ""

End

Private Function CommonName_Read() As String

    Return $name

Catch 
    Return ""

End

Private Function LocaleName_Read() As String

    Return $desktopFile.Name

End

Private Function LocaleGenericName_Read() As String

    If $localGenericName <> "" Then
        Return $localGenericName
    Else If $languageGenericName <> "" Then
        Return $languageGenericName
    Else
        Return ""
    Endif

End

Private Function LocaleComment_Read() As String

    If $localeComment <> "" Then
        Return $localeComment
    Else If $languageComment <> "" Then
        Return $languageComment
    Else
        Return ""
    Endif

End

Private Function Exec_Read() As String

    Return $desktopFile.Exec

Catch
    Return ""

End
