' Gambas class file

Property Read ShowIn As Boolean
Property Read Exec As String
Property Read Path As String 
Property Read HasAction As Boolean

Private $names As NagatoDesktopFileNames
Private $categories2 As New NagatoDesktopFileCategories
Private $hidden As New NagatoDesktopFileHidden
Private $actions As NagatoDesktopActions

Private Enum FlagNone, FlagEntry, FlagAction

Public Sub setProperty(argLine As String)
    
    If argLine Begins "Categories=" Then $categories2.SetData(argLine)
    $names.SetData(argLine)
    $hidden.SetData(argLine)
    
End

Private Sub setPropertyFromDesktopFile(argPath As String)
    
    Dim yukiFlag As Integer = FlagNone
    Dim yukiLine As String
    Dim yukiStream As Stream 
    
    yukiStream = Open argPath For Input
    
    Do While Not Eof(yukiStream)
        Line Input #yukiStream, yukiLine
        If yukiLine Begins "[Desktop Action" Then $actions = New NagatoDesktopActions
        If yukiFlag = FlagEntry And If yukiLine Begins "[Desktop Action" Then yukiFlag = FlagAction
        If yukiLine = "[Desktop Entry]" Then yukiFlag = FlagEntry
        If yukiFlag = FlagEntry Then setProperty(yukiLine)
        If yukiFlag = FlagAction Then $actions.Set(yukiLine)
    Loop
    
    If yukiFlag = FlagAction Then $actions.Finish()
    
   Close #yukiStream
    
End

Public Sub _new(argPath As String, argDesktopFile As DesktopFile)
    
    $names = New NagatoDesktopFileNames(argDesktopFile)
    setPropertyFromDesktopFile(argPath)
    
End

Public Function GetName(argTranslate As Boolean) As String
    
    If argTranslate And If $names.LocaleName Then Return $names.LocaleName
    If $names.CommonName Then Return $names.CommonName
    If $names.Name Then Return $names.Name
    
    Return $names.ProgramName
    
End

Public Function GetGenericName(argTranslate As Boolean) As String
    
    Return IIf(argTranslate, $names.LocaleGenericName, $names.GenericName)
    
End

Public Function GetComment(argTranslate As Boolean) As String
    
    Return IIf(argTranslate, $names.LocaleComment, $names.Comment)
    
End

Public Function HasCategory(argCategory As String) As Boolean
    
    Return $categories2.HasCategory(argCategory)
    
End

Private Function Path_Read() As String

    Return $names.Path

End

Private Function ShowIn_Read() As Boolean

    Return $hidden.ShowIn

End

Private Function Exec_Read() As String

    Return $names.Exec

End

Private Function HasAction_Read() As Boolean

    Return $actions.Count > 0

End
