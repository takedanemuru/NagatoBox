' Gambas class file

Inherits NagatoMenu

Private $menu As Menu

Private Function getDesktopFilesWithMimeType() As String[]
    
    Dim yukiDefaults As String[] = MikuruApplications.GetDefaultDesktopFiles(Me._$path)
    Dim yukiMimeType As String = DesktopMime.FromFile(Me._$path).Type
    Dim yukiNonDefaults As String[] = MikuruConfFlie(MikuruApplicationDirectory.GlobalMimeList, yukiMimeType)
    Dim yukiApplication As String
    
    For Each yukiApplication In yukiNonDefaults.Sort(gb.Natural)
        If yukiDefaults.Exist(yukiApplication) Then Continue
        yukiDefaults.Add(yukiApplication)
    Next
    
    Return yukiDefaults
    
End

Private Sub addSelectApplicationMenu()
    
    Dim yukiMenu As New Menu($menu) As "SelectApplicationMenu"
    
    With yukiMenu
        .Picture = MikuruIcons.Get("gear")
        .Text = ("Select application")
    End With
    
End

Private Sub addExecuteMenus()
    
    Dim yukiAdditionalDesktopFile As String
    Dim yukiChecker As New String[]
    
    For Each yukiAdditionalDesktopFile In getDesktopFilesWithMimeType()
        If yukiChecker.Exist(yukiAdditionalDesktopFile) Then Continue
        Object.New("NagatoOpenWithMenu", [$menu, yukiAdditionalDesktopFile, Me._$path])
        yukiChecker.Add(yukiAdditionalDesktopFile)
    Next
    
End

Public Sub _SetMenu() 'override
    
    $menu = New Menu(Me._$parentMenu) As "FileMenu"
    
    With $menu
        .Text = MikuruCollapsedText(File.Name(Me._$path), HaruhiMain.Font, Screen.W Div 4)
        .Picture = Desktop.GetFileIcon(Me._$path, 24)
    End With
    
    Object.New("NagatoMenuItemDummy", [$menu])
    
End
 
Public Sub FileMenu_Show()
    
    $menu.Children.Clear()
    
    addSelectApplicationMenu()
        Object.New("NagatoMenuItemDummy", [$menu])
    addExecuteMenus()
    
End

Public Sub SelectApplicationMenu_Click()
    
    HaruhiSelectApplication.ShowAllApplications(Me._$path)
    
End
