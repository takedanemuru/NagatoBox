' Gambas class file

Property Read Picture As Picture
Property Read Thumbnail As Picture

Private Const ThumbnailSize As Integer = 200

Private $screenshot As Picture

Event Ready

Private Function getActiveScreenScreenshot() As Picture
    
    Dim yukiDesktopWindow As New DesktopWindow(Desktop.ActiveWindow)
    
    Return yukiDesktopWindow.GetScreenshot(False)
    
End

Public Sub Get() As Boolean
    
    If NagatoSettingsScreenShooter.OnlyActiveWindow Then
        $screenshot = getActiveScreenScreenshot()
    Else
        $screenshot = Desktop.Screenshot()
    End If
    
    If Object.IsValid($screenshot) Then Raise Ready
    
    Return Object.IsValid($screenshot)
    
End

Public Sub Save(argPath As String)
    
    $screenshot.Save(argPath)
    
End

Private Function Picture_Read() As Picture

    Return $screenshot

End

Private Function Thumbnail_Read() As Picture

    Dim yukiRate As Float = Min(ThumbnailSize / $screenshot.H, ThumbnailSize / $screenshot.W)
    
    With $screenshot
        Return .Image.Stretch(.W * yukiRate, .H * yukiRate).Picture
    End With

Catch
    Return Null

End
