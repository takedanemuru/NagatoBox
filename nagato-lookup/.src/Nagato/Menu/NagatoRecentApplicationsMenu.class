' Gambas class file

Private $menuButton As MenuButton
Private $rootMenu As Menu

Private Sub addApplicationMenu(argPath As String)
    
    Dim yukiDesktopFile As New NagatoDesktopFile(argPath)
    Dim yukiMenu As New Menu($rootMenu) As "ApplicationMenu"
    
    With yukiMenu
        .Text = yukiDesktopFile.GetName(True)
        .Tag = argPath
        .Picture = yukiDesktopFile.GetIcon(32)
    End With
    
End

Private Sub initializeMenus()
    
    Dim yukiPath As String
    
    For Each yukiPath In NagatoSettings.RecentApplications
        If Exist(yukiPath) Then addApplicationMenu(yukiPath)
    Next
    
End

Private Sub initializeMenuButton(argMenuButton As MenuButton, argUniqueName As String)
    
    $menuButton = argMenuButton
    $menuButton.Menu = argUniqueName
    
End

Private Sub initializeRootMenu(argUniqueName As String)
    
    Dim yukiDummyMenu As Menu
    
    $rootMenu = New Menu(HaruhiMain) As "RootMenu"
    $rootMenu.Visible = False
    $rootMenu.Name = argUniqueName
    
    yukiDummyMenu = New Menu($rootMenu)
    yukiDummyMenu.Text = "can you see this ?"
    
End

Public Sub _new(argMenuButton As MenuButton, Optional argUniqueName As String = "recent")
    
    initializeMenuButton(argMenuButton, argUniqueName)
    initializeRootMenu(argUniqueName)
    
End

Public Sub RootMenu_Show()
    
    $rootMenu.Children.Clear()
    initializeMenus()
    
End

Public Sub ApplicationMenu_Click()
    
    MikuruApplications.Launch(Last.Tag)
    NagatoSettings.PushApplicationPath(Last.Tag)
    
End
