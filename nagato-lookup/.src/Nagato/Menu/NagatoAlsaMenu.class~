' Gambas class file

Private $parentMenu As Menu
Private $observer As Observer
Private $channel As String
Private $volume As Integer
    
Private Function addIndicationMenu() As Boolean
    
    Dim yukiMenu As New Menu($parentMenu)
    
    If MikuruAlsa.GetAlsaVolume("Master").Count > 0 Then
        $channel = "Master"
        $volume = MikuruAlsa.GetAlsaVolume($channel)[0]
        yukiMenu.Picture = MikuruIcons.Get("speaker")
        yukiMenu.Text = Subst$("ALSA Master: &1%", $volume)
        Return True
    Else If MikuruAlsa.GetAlsaVolume("PCM").Count > 0 Then
        $channel = "PCM"
        $volume = MikuruAlsa.GetAlsaVolume($channel)[0]
        yukiMenu.Picture = MikuruIcons.Get("speaker")
        yukiMenu.Text = Subst$("ALSA PCM: &1%", $volume)
        Return True
    Else
        yukiMenu.Text = ("Couldn't find valid alsa volume channel.")
        Return False
    Endif
    
End

Private Sub addVolumeMenu()
    
    Dim yukiCount As Integer
    Dim yukiMenu As Menu
    
    For yukiCount = 100 DownTo 10 Step 10
        yukiMenu = New Menu($parentMenu) As "AlsaMenu"
        yukiMenu.Text = Subst$(("&1%"), yukiCount)
        yukiMenu.Tag = yukiCount
        If yukiCount = $volume Then yukiMenu.Picture = MikuruIcons.Get("button-check")
    Next
    
End

Private Sub addMuteMenu()
    
    Dim yukiMenu As New Menu($parentMenu) As "AlsaMenu"
    
    With yukiMenu
        .Text = ("Mute")
        .Picture = MikuruIcons.Get("speaker-mute")
        .Tag = 0
    End With
    
End

Private Sub initializeMenu()
    
    If addIndicationMenu() Then
        addDummyMenu()
        addVolumeMenu()
        addDummyMenu()
        addMuteMenu()
    Endif
    
End

Private Sub addDummyMenu()
    
    Dim yukiMenu As New Menu($parentMenu)
    
    yukiMenu.Text = ""
    
End

Private Sub addParentMenu(argRootMenu As Menu)
    
    $parentMenu = New Menu(argRootMenu) 
    
    With $parentMenu
        .Picture = MikuruIcons.Get("speaker")
        .Text = ("Alsa volume")
    End With
    
End

Public Sub _new(argMenu As Menu)
    
    addParentMenu(argMenu)
    $observer = New Observer($parentMenu) As "Asakura"
    addDummyMenu()
    
End

Public Sub Asakura_Show()
    
    $parentMenu.Children.Clear()
    initializeMenu()
    
End

Public Sub AlsaMenu_Click()
    
    Try Shell Subst$("amixer sset &1 &2%", $channel, Last.Tag)
    
End
