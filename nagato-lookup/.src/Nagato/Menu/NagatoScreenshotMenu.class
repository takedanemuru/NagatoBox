' Gambas class file

Private $parentMenu As Menu
Private $timer As New Timer As "Timer"
Private $count As Integer

Private Sub initializeParentMenu(argRootMenu As Menu)
    
    $parentMenu = New Menu(argRootMenu) As "ParentMenu"
    
    With $parentMenu
        .Text = ("Screenshot")
        .Picture = MikuruIcons.Get("camera")
    End With
    
End

Private Sub addMenu(argText As String, argDelay As Integer)
    
    Dim yukiMenu As New Menu($parentMenu) As "ScreenshotMenu"
    
    With yukiMenu
        .Text = argText
        .Tag = argDelay
    End With
    
End

Private Sub addDummyMenu()
    
    Dim yukiDummyMenu As New Menu($parentMenu) As "DummyMenu"
    
    yukiDummyMenu.Text = "dummy"
    
End

Private Sub addIsOnlyActiveMenu()
    
    Dim yukiMenu As New Menu($parentMenu) As "IsOnlyActiveMenu"
    
    yukiMenu.Text = ("Only active window")
    yukiMenu.Picture = IIf(NagatosettingsScreenShooter.OnlyActiveWindow, MikuruIcons.Get("button-check"), Null)
    
End

Public Sub _new(argRootMenu As Menu)
    
    initializeParentMenu(argRootMenu)
    addDummyMenu()
    
End

Public Sub ParentMenu_Show()
    
    $parentMenu.Children.Clear()
    
    addIsOnlyActiveMenu()
        MikuruMenuSeparator.Set($parentMenu)
    addMenu("Now", 0)
        MikuruMenuSeparator.Set($parentMenu)
    addMenu("5 seconds later", 5)
    addMenu("15 seconds later", 15)
    addMenu("30 seconds later", 30)
    addMenu("1 minute later", 60)
    
End

Public Sub ScreenshotMenu_Click()
    
    If Last.Tag = 0 Then
        HaruhiSave.Call()
    Else
        $count = Last.Tag 
        $timer.Start()
    End If
    
End

Public Sub IsOnlyActiveMenu_Click()
    
    NagatosettingsScreenShooter.OnlyActiveWindow = Not NagatosettingsScreenShooter.OnlyActiveWindow
    
End

Public Sub Timer_Timer()
    
    Dec $count
    
    If $count = 0 Then
        HaruhiSave.Call()
        $timer.Stop
    Endif
    
End
