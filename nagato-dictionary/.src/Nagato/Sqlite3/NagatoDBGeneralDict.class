' Gambas class file

Inherits NagatoDB

Create Static

Public Const TableGeneralDict As String = "GeneralDict"
Public Const ColumnId As String = "id"
Public Const ColumnJapanese As String = "japanese"
Public Const ColumnPronunciation As String = "pronunciation"
Public Const ColumnEnglish As String = "english"

Public _$uniqueName As String = "edict.sqlite3" ' for DB_NAME

Private Sub checkAndCreateTable()
    
    Dim yukiTable As Table
    
    Try Me._$connection.Open()
    
    If Not Me._$connection.Tables.Exist(TableGeneralDict) Then
        yukiTable = Me._$connection.Tables.Add(TableGeneralDict)
        yukiTable.Fields.Add(ColumnId, db.Serial)
        yukiTable.Fields.Add(ColumnJapanese, db.String)
        yukiTable.Fields.Add(ColumnPronunciation, db.String)
        yukiTable.Fields.Add(ColumnEnglish, db.String)
        yukiTable.PrimaryKey = [ColumnId]
        yukiTable.Update()
    Endif

End

Public Function Count() As Integer
    
    Dim yukiResult As Result = Me._$connection.Find(TableGeneralDict)
    
    Return yukiResult.Count
    
End

Public Function Get(argJapanese As String) As Result
    
    Dim yukiQuery As String = Subst$("SELECT * FROM GeneralDict WHERE japanese LIKE &1", Quote$(argJapanese))
    
    Return Me._$connection.Exec(yukiQuery)
    
End
 
 Public Function GetWithEnglish(argEnglish As String) As Result
    
    Dim yukiQuery As String = Subst$("SELECT * FROM GeneralDict WHERE english LIKE &1", Quote$(argEnglish))
    
    Return Me._$connection.Exec(yukiQuery)
    
End

Public Function GetWithPronunciation(argPronunciation As String) As Result
    
    Dim yukiQuery As String = Subst$("SELECT * FROM GeneralDict WHERE pronunciation LIKE &1", Quote$(argPronunciation))
    
    Return Me._$connection.Exec(yukiQuery)
    
End

 Public Function GetAll() As Result
    
    Dim yukiResult As Result = Me._$connection.Find(TableGeneralDict)
    
    Return yukiResult
    
End
 
' For maintainance only
 
' Public Sub Push(argJapanese As String, argPronunciation As String, argEnglish As String)
'     
'     Dim yukiResult As Result
'     
'     Me._$connection.Begin()
'         yukiResult = Me._$connection.Create(TableGeneralDict)
'         yukiResult[ColumnJapanese] = argJapanese
'         yukiResult[ColumnPronunciation] = argPronunciation
'         yukiResult[ColumnEnglish] = argEnglish
'         yukiResult.Update()
'     Me._$connection.Commit()
'     
' Catch
'     Debug Error.Text
'     Me._$connection.Rollback()
'     
' End

Public Sub _new()
    
    Me._SetConnection()
    checkAndCreateTable()
    
End
