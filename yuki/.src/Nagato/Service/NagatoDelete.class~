' Gambas class file

Private $pathDirectory As New String[]
Private $pathsFile As New String[]
Private $pathsXdgUserDir As New String[]
Private $pathUnwritable As New String[]

Private Sub show(argHeader As String, argArray As String[])
    
    Dim yukiPath As String
    
    If argArray.Count = 0 Then Return
    
    Print argHeader
    
    For Each yukiPath In argArray
        Print yukiPath
    Next
    
End

Private Sub deleteFiles()
    
    MikuruTrash($pathsFile)
    MikuruTrash($pathDirectory)
    
End

Private Sub showAll()
    
    show("[Directories]", $pathDirectory)
    show("[Files]", $pathsFile)
    show("[XDG User Dir] *You have no permission, should NOT be removed", $pathsXdgUserDir)
    show("[Undeletable]  *You have no permission, should NOT be removed", $pathUnwritable)
    
End

Private Sub hasdeletableFiles()
    
    showAll()
    
    MikuruUserInput("YUKI.N > We are going to remove listed files, OK? (y/n)")
    
    If MikuruUserInput.Has("y") Then deleteFiles()
    
End

Private Sub noDeletableFiles()
    
    showAll()
    
    MikuruUserInput("YUKI.N > You requested to remove listed file(s), but you have no permission. Press [ENTER] to continue")
    
End

Public Sub Add(argPath As String) ' requires full path
    
    If Not Exist(argPath) Then Return
    
    If Not MikuruAccess.IsWritable(argPath, False) Then
        $pathUnwritable.Add(argPath)
    Else If NagatoXdg.IsUserDir(argPath) Then
        $pathsXdgUserDir.Add(argPath)
    Else If IsDir(argPath) Then
        $pathDirectory.Add(argPath)
    Else
        $pathsFile.Add(argPath)
    Endif
    
End

Public Sub Dialog()
    
    With MikuruArray
        If .Count($pathsFile, $pathDirectory) > 0 Then
            hasdeletableFiles()
        Else If .Count($pathsXdgUserDir, $pathUnwritable) > 0 Then
            noDeletableFiles()
        Else
            MikuruUserInput("YUKI.N > Invalide request. press [ENTER] to continue")
        End If
    End With
    
End
