' Gambas class file

Property Read Online As Boolean
Property Read Percentage As String

Private Const PowerSupplyClassDirectory As String = "/sys/class/power_supply"

Private Function Online_Read() As Boolean

    Dim yukiDeviceDirectory As String
    Dim yukiPath As String
    
    For Each yukiDeviceDirectory In Dir(PowerSupplyClassDirectory)
        yukiPath = PowerSupplyClassDirectory &/ yukiDeviceDirectory 
        If File.Load(yukiPath &/ "type") <> "Mains\n" Then Continue
        If File.Load(yukiPath &/ "online") = "1\n" Then Return True
    Next

    Return False

End

Private Function Percentage_Read() As String

    Dim yukiDeviceDirectory As String
    Dim yukiPath As String
    Dim yukiChargeFull As Integer = 0
    Dim yukiChargeNow As Integer = 0
    
    For Each yukiDeviceDirectory In Dir(PowerSupplyClassDirectory)
        yukiPath = PowerSupplyClassDirectory &/ yukiDeviceDirectory 
        If File.Load(yukiPath &/ "type") <> "Battery\n" Then Continue
        yukiChargeFull += CInteger(Replace(File.Load(yukiPath &/ "charge_full"), "\n", ""))
        yukiChargeNow += CInteger(Replace(File.Load(yukiPath &/ "charge_now"), "\n", ""))
    Next

    If yukiChargeFull = 0 Then Return "0.00%"

    Return Format(yukiChargeNow / yukiChargeFull, "0.00%")

End
