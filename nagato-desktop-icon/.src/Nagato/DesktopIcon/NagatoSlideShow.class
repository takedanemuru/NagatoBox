' Gambas class file

Inherits NagatoIcon

Private Const FormHeight As Integer = 194
Private Const FormWidth As Integer = 128

Private $timer As New Timer As "Timer"
Private $paths As New String[] 
Private $image As Image
Private $offset As New Point
Private $miniumCount As Integer
Private $scrollReverse As Boolean = False

Private Function getImagePath() As String
    
    Dim yukiPath As String
    Dim yukiRetryCount As Integer
    
    Do
        yukiPath = $paths[Int(Rnd(0, $paths.Count))]
        If Exist(yukiPath) And If DesktopMime.FromFile(yukiPath).Type Begins "image" Then Return yukiPath
        Inc yukiRetryCount
        If yukiRetryCount > 5 Then Return "pictures/about.png"
    Loop
    
End

Private Function setImage() As Image
    
    Dim yukiImage As Image = Image.Load(getImagePath())
    Dim yukiRate As Float = Max(FormWidth / yukiImage.W, FormHeight / yukiImage.H)
    
    $image = yukiImage.Stretch(yukiImage.W * yukiRate, yukiImage.H * yukiRate)
    
End

Private Sub setScrollDirection()
    
    If Odd(Int(Rnd(0, 7))) Then $scrollReverse = Not $scrollReverse
    
End

Private Sub setOffset()
    
    If 0 >= Max($image.W - ($offset.X + FormWidth), $image.H - ($offset.Y + FormHeight)) Then
        If $miniumCount > 30 Then
            $miniumCount = 0
            setImage()
            setScrollDirection()
            $offset.X = 0
            $offset.Y = 0
        End If
    Else
        If ($image.W - FormWidth) > ($image.H - FormHeight) Then
            Inc $offset.X
            If $offset.X > ($image.W - FormWidth) Then $offset.X = ($image.W - FormWidth)
        Else
            Inc $offset.Y 
            If $offset.Y > ($image.H - FormHeight) Then $offset.Y = ($image.H - FormHeight)
        Endif
    Endif
    
    Inc $miniumCount
    
End

Private Sub setFormPicture(Optional argBackground As Integer = Color.LightGray)
    
    Dim yukiBaseImage As New Image(FormWidth, FormHeight, argBackground)
    Dim yukiOffsetX As Integer
    Dim yukiOffsetY As Integer
    
    If $paths.Count = 0 Then Return
    
    setOffset()
    
    yukiOffsetX = IIf($scrollReverse, -1 * ($image.W - (FormWidth + $offset.X)), -1 * $offset.X)
    yukiOffsetY = IIf($scrollReverse, -1 * ($image.H - (FormHeight + $offset.Y)), -1 * $offset.Y)
    
    Paint.Begin(yukiBaseImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.DrawImage($image, yukiOffsetX, yukiOffsetY)
    Paint.End()
    
    Me._$form.Picture = yukiBaseImage.Picture
    
End

Private Sub initializePaths()
    
    Dim yukiDirectory As String = NagatoSettings.SlideShowDirectory
    Dim yukiPath As String
    Dim yukiCount As Integer
    
    $paths.Clear()
    
    For Each yukiPath In RDir(yukiDirectory, "*", gb.File)
        If Desktopmime.FromFile(yukiDirectory &/ yukiPath).Type Begins "image" Then 
            $paths.Add(yukiDirectory &/ yukiPath)
            Inc yukiCount
            If yukiCount = 500 Then Break
        Endif
    Next
    
End

Public Sub _AddStarMenu() 'override
    
    Dim yukiMenu As NagatoButtonMenuSlideShow
    
    yukiMenu = New NagatoButtonMenuSlideShow(Me._$form, Me._$starButton) As "StarMenu"
    
End

Public Sub _new()
    
    Me._InitializeForm(FormHeight)
    Me._$form.Show
    initializePaths()
    $timer.Delay = 100
    $timer.Enabled = True
    
    setImage()
    setFormPicture()
    
End

Public Sub Timer_Timer()
    
    setFormPicture()
    
End

Public Sub StarMenu_ChangeDirectory()
    
    Debug NagatoSettings.SlideShowDirectory
    
    $timer.Stop()
        initializePaths()
    $timer.Start()
    
End
