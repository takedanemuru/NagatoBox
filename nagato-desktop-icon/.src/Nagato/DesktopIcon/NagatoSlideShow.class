' Gambas class file

Inherits NagatoIcon

Private Const FormHeight As Integer = 194
Private Const FormWidth As Integer = 128

Private $timer As New Timer As "Timer"
Private $imagePaths As NagatoSlideShowImagePaths
Private $imagePosition As New NagatoSlideShowImagePosition As "ImagePosition"
Private $image As Image

Private Function setImage() As Image
    
    Dim yukiImage As Image = Image.Load($imagePaths.GetImagePath())
    Dim yukiRate As Float = Max(FormWidth / yukiImage.W, FormHeight / yukiImage.H)
    
    $image = yukiImage.Stretch(yukiImage.W * yukiRate, yukiImage.H * yukiRate)
    $imagePosition.SetImage($image)
    
End

Private Sub setFormPicture(Optional argBackground As Integer = Color.LightGray)
    
    Dim yukiBaseImage As New Image(FormWidth, FormHeight, argBackground)
    
    If $imagePaths.Count = 0 Then Return
    
    $imagePosition.SetOffset()
    
    Paint.Begin(yukiBaseImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.DrawImage($image, $imagePosition.X, $imagePosition.Y)
    Paint.End()
    
    Me._$form.Picture = yukiBaseImage.Picture
    
End

Public Sub _AddStarMenu() 'override
    
    Dim yukiMenu As NagatoButtonMenuSlideShow
    
    yukiMenu = New NagatoButtonMenuSlideShow(Me._$form, Me._$starButton) As "StarMenu"
    
End

Public Sub _new()
    
    Me._InitializeForm(FormHeight)
    Me._$form.Show
    $imagePaths = New NagatoSlideShowImagePaths
    $timer.Delay = 100
    $timer.Enabled = True
    
    setImage()
    setFormPicture()
    
End

Public Sub Timer_Timer()
    
    setFormPicture()
    
End

Public Sub StarMenu_ChangeDirectory()
    
    $timer.Stop()
        $imagePaths.Refresh()
    $timer.Start()
    
End

Public Sub ImagePosition_Change()
    
    setImage()
    
End
