' Gambas class file

Private $currentWeather As NagatoWeatherElement
Private $textRect As New Rect
Private $font As Font
Private $margin As Integer

Private Sub setTextRect()
    
    With $textRect
        .X = $margin
        .Y = 124
        .W = 112
        .H = $font.RichTextHeight(getTitle()) 
    End With
    
End

Private Function getTitle() As String
    
    Return Subst$("<h2>&1</h2>&2", Upper$(NagatoWeatherCondition.GetText($currentWeather.IconId)), String$(8, "<br>"))
    
End

Private Function getHeaders() As String
    
    Dim yukiReturn As String = "<h2> </h2><br>"
    
    yukiReturn &= "Temp.:<br>"
    yukiReturn &= "Wind(S):<br>"
    yukiReturn &= "Wind(D):<br>"
    yukiReturn &= "Hum.:<br>"
    yukiReturn &= "Cloud:<br>"
    yukiReturn &= "Press.:<br>"
    yukiReturn &= "Prec.:<br>"
    
    Return yukiReturn
    
End

Private Function getStatus() As String
    
    Dim yukiReturn As String = "<h2> </h2><br>"
    
    With $currentWeather
        yukiReturn &= Subst$("&1Â°C<br>", .Temperature)
        yukiReturn &= Subst$("&1m<br>", .WindSpeed)
        yukiReturn &= Subst$("&1<br>", .WindDirection)
        yukiReturn &= Subst$("&1%<br>", .Humidity)
        yukiReturn &= Subst$("&1%<br>", .Cloudiness)
        yukiReturn &= Subst$("&1HPa<br>", .Pressure)
        yukiReturn &= Subst$("&1mm<br>", .Precipitation)
    End With
    
    Return yukiReturn
    
End

Private Sub drawWatherText()
    
    setTextRect()

    With $textRect
        Paint.DrawRichText(getTitle(), .X, .Y, .W, .H, Align.Center)
        Paint.DrawRichText(getHeaders(), .X, .Y, .W, .H, Align.Left)
        Paint.DrawRichText(getStatus(), .X, .Y, .W, .H, Align.Right)
    End With
    
End

Public Sub Paint(argWeatherElement As NagatoWeatherElement)
    
    $currentWeather = argWeatherElement
    drawWatherText()
    
End

Public Sub _new(argFont As Font, argMargin As Integer)
    
    $font = argFont
    $margin = argMargin
    
End
