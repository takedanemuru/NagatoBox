' Gambas class file

Inherits NagatoIconStandard

Public Function _GetText() As String
    
    Return File.Name(Me._$path)
    
End

Public Sub _AddStarMenu() 'override
    
    Dim yukiMenu As NagatoButtonMenuFileIcon
    
    yukiMenu = New NagatoButtonMenuFileIcon(Me._$form, Me._$starButton, Me._$path) As "StarMenu"
    
End

Public Sub _OnActivate()
    
    Dim yukiDesktopFiles As String[] = MikuruApplications.GetDefaultDesktopFiles(Me._$path)
    Dim yukiDesktopFile As DesktopFile
    
    If yukiDesktopFiles.Count = 0 Then
        Message.Warning(("YUKI.N > Sorry, but I couldn't find any application to open this file."), ("OK"))
    Else
        yukiDesktopFile = New DesktopFile(MikuruApplications.GlobalApplicationDirectory &/ yukiDesktopFiles[0])
        yukiDesktopFile.Run(Me._$path)
    End If
    
End

Public Sub StarMenu_MenuEvent(argEvent As Integer)
    
    Select Case argEvent
        Case NagatoMenuPipe.FileOpen
            Me._OnActivate()
        Case NagatoMenuPipe.FileRename
            HaruhiRename.SetPath(Me._$path, Me._$icon.Picture)
            HaruhiRename.ShowDialog()
    End Select
    
End
