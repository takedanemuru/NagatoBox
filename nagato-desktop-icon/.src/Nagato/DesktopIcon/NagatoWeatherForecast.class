' Gambas class file

Inherits NagatoIcon

Private $timer As New Timer As "Timer"
Private $currentWeather As NagatoWeatherElement
Private $textPainter As NagatoWeatherForecastPainterText

Private Function getWeatherIcon() As Image
    
    If Hour(Now()) >= 6 And If 18 > Hour(Now()) Then
            Return NagatoWeatherIcon.GetIconImageDay($currentWeather.IconId).Stretch(96, 96)
    Else
            Return NagatoWeatherIcon.GetIconImageNight($currentWeather.IconId).Stretch(96, 96)
    End If
    
End

Private Sub setWeatherInfomation(argBackground As Integer)
    
    Dim yukiBaseImage As Image = New Image(Me.FormWidth, 320, argBackground)
    
    Paint.Begin(yukiBaseImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.DrawImage(getWeatherIcon(), 16, 8)
        $textPainter.Paint($currentWeather)
    Paint.End()
    
    Me._$form.Picture = yukiBaseImage.Picture
    
Catch
    Return
    
End

Private Sub setPleaseWait(argBackground As Integer)
    
    Dim yukiBaseImage As Image = New Image(Me.FormWidth, 320, argBackground)
    
    Paint.Begin(yukiBaseImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.DrawImage(MikuruIcon.Get("question", 48).Image, 40, 8)
        Paint.DrawRichText("Please wait...", 0, 0, Me.FormWidth, Me._$form.Height, Align.Center)
    Paint.End()
    
    Me._$form.Picture = yukiBaseImage.Picture
    
Catch
    Return
    
End

Private Sub setFormPicture(Optional argBackground As Integer = Color.DarkBlue)
    
    If NagatoWeatherCache.IsReady
        setWeatherInfomation(Color.Lighter(NagatoWeatherCondition.GetColor($currentWeather.IconId)))
    Else
        setPleaseWait(argBackground)
    End If
    
End

Public Sub _AddStarMenu() 'override
    
    Dim yukiMenu As NagatoButtonMenuWeatherIcon
    
    yukiMenu = New NagatoButtonMenuWeatherIcon(Me._$form, Me._$starButton) As "StarMenu"
    
End

Public Sub _new()
    
    Me._InitializeForm(320)
    Me._$form.Show
    $textPainter = New NagatoWeatherForecastPainterText(Me._$form.Font, Me.TextMargin)
    
    setFormPicture()
    $timer.Enabled = True
    
End

Public Sub Timer_Timer()
    
    If NagatoWeatherCache.IsReady Then
        $currentWeather = NagatoWeatherCache.GetWeatherElementMostRecent()
    End If
    
    setFormPicture()
    
End

Public Sub StarMenu_LocationChanged()
    
    NagatoWeatherCache.Activate()
    
End
