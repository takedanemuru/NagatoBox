' Gambas class file

Private $api As New NagatoXkcdApi As "Api"
Private $index As New NagatoComicIndex

Private Sub setImageView()
    
    With MainImageView
        .Image = $api.Image
        .Tooltip = $api.Alt
        .W = $api.W
        .H = $api.H
    End With
    
End

Private Sub setForm()
    
    With Me
        .Title = Subst$("(#&1)  &2", $api.Number, $api.SafeTitle)
        If .Closed Then .Show()
        '.Center() ' doesn't work correctlly. why ?
        Me.X = (Screen.W - $api.W) Div 2
        Me.Y = (Screen.H - $api.H) Div 2
    End With
    
End

Private Sub selectEpisode()
    
    Dim yukiResponse As Integer
    
    HaruhiSelectEpisode.SetProperties($index.Max, $api.Number)
    
    yukiResponse = HaruhiSelectEpisode.ShowDialog()
    
    If yukiResponse <> 0 Then $api.GetSelect(yukiResponse)
    
End

Public Sub Start()

    $api.GetCurrent()

End

Public Sub Api_Finished()
    
    $index.Reset($api.Number)
    
    setImageView()
    setForm()
    
End

Public Sub MainImageView_DblClick()

    $api.GetSelect($index.GetRandom())

End

Public Sub Form_Menu()
    
    Dim yukiMenu As New NagatoContextMenu(Me) As "ContextMenu"
    
    yukiMenu.PopUp()
    
End

Public Sub ContextMenu_Signal(argSignal As Integer)
    
    Select Case argSignal
        Case NagatoContextMenu.MenuRandomEpisode
            $api.GetSelect($index.GetRandom())
        Case NagatoContextMenu.MenuSelectEpisode
            selectEpisode()
        Case NagatoContextMenu.MenuAbout
            
        Case NagatoContextMenu.MenuQuit
            Me.Close()
    End Select
    
End
