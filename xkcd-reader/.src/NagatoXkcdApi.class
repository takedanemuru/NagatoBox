' Gambas class file

Property Read Image As Image
Property Read Number As Integer
Property Read SafeTitle As String
Property Read Alt As String

Private Const ApiCurrent As String = "http://xkcd.com/info.0.json"

Private $imageDownloader As NagatoImageDownloader
Private $buffer As String
Private $number As Integer
Private $safeTitle As String
Private $alt As String

Event Finished

Private Sub parsingJson()
    
    Dim yukiCollection As Collection = JSON.Decode($buffer)
    
    $number = yukiCollection["num"]
    $safeTitle = yukiCollection["safe_title"]
    $alt = yukiCollection["alt"]
    
    $imageDownloader = New NagatoImageDownloader(yukiCollection["img"]) As "ImageDownLoader"
    
End

Public Sub GetCurrent()
    
    Dim yukiHttpClient As New HttpClient As "HttpClient"
    
    $buffer = ""
    
    With yukiHttpClient 
        .Async = True
        .Timeout = 30
        .URL = ApiCurrent
        .Get()
    End With
    
End

Public Sub HttpClient_Read()
    
    $buffer &= Read #Last, Lof(Last)
    
End

Public Sub HttpClient_Finished()
    
    parsingJson()
    
End

Public Sub ImageDownloader_Finished()
    
    Raise Finished()
    
End

Private Function Image_Read() As Image

    Return $imageDownloader.Image

End

Private Function Number_Read() As Integer

    Return $number

End

Private Function SafeTitle_Read() As String

    Return $safeTitle

End

Private Function Alt_Read() As String

    Return $alt

End
