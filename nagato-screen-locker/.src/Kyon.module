' Gambas module file

Private $image As Image

Private Sub getShadeColor() As Integer
    
    Select Case CInt(Rnd(0, 8))
        Case 0
            Return Color.Gray
        Case 1
            Return Color.Violet
        Case 2
            Return Color.Blue
        Case 3
            Return Color.Orange
        Case 4
            Return Color.DarkCyan
        Case 5
            Return Color.DarkBlue
        Case 6
            Return Color.DarkMagenta
        Case 7
            Return Color.Red
        Default
            Return Color.White
    End Select
    
End

Private Sub showLockScreen()
    
    Dim yukiTempPath As String = Temp() & ".png"
    Dim yukiOutput As String
    
    $image.Picture.Save(yukiTempPath)
    
    Exec ["i3lock", "-i", yukiTempPath] To yukiOutput
    
    Print yukiOutput
    
End

Private Sub initializeImage()
    
    $image = Desktop.Screenshot().Image
    $image = $image.Blur(1)
    
End

Private Sub paintShade()
    
    With Paint
        .Brush = .Color(getShadeColor() + 16777216 * 128)
        .Rectangle(0, 0, Screen.W, Screen.H)
        .Fill()
    End With
    
End

Private Sub paintMessage()
    
    Dim yukiMessage As String = ("YUKI.N > INPUT PASSWORD TO UNLOCK")
    
    With Paint
        .Brush = .Color(Color.White)
        .Font.Size = 36
        .Font.Bold = True
        .DrawText(yukiMessage, 0, 0, Screen.W, Screen.H, Align.Center)
    End With
    
End

Private Sub paintNagato()
    
    Dim yukiPicture As Picture = Picture.Load("Pictures/render_yuki.png")
    
    Paint.DrawPicture(yukiPicture, 0, Max(0, Screen.H - yukiPicture.H))
    
End

Private Sub paintScreen()
    
    Paint.Begin($image)
        paintShade()
        paintNagato()
        paintMessage()
    Paint.End()
    
End

Public Sub Main()
    
    initializeImage()
    paintScreen()
    showLockScreen()
    
End
