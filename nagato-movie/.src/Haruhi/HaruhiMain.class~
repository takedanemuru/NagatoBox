' Gambas class file

Private $menuBar As NagatoMenuBar
Private $player As NagatoPlayer

Private Sub openFile()
    
    Dialog.Title = ("Open file")
    Dialog.Path = NagatoSettings.LastDirectory &/ ""
    
    If Not Dialog.OpenFile(False) Then
        $player.SetPathAndPlay(Dialog.Path)
        NagatoSettings.LastDirectory = File.Dir(Dialog.Path)
    Endif
    
End

Public Sub SetPathAndPlay(argPath As String)
    
    $player.SetPathAndPlay(argPath)
    
End

Public Sub Form_Open()

    $menuBar = New NagatoMenuBar(MenuHBox) As "MenuBar"
    $player = New NagatoPlayer(MediaDrawingArea) As "Player"

End

Public Sub Form_Close()
    
    NagatoSettings.SaveFormSettings(Me)
    
End

Public Sub Form_Resize()
    
    $player.SetBackground()
    
End

Public Sub MenuBar_NewPosition(argPosition As Float)
    
    $player.SetPosition(argPosition)
    
End

Public Sub MenuBar_MenuEvent(argEvent As Integer)
    
    With MikuruMenuEvent
        Select Case argEvent
           Case .MediaPlay
                $player.Play()
            Case .MediaPause
                $player.Pause()
            Case .FileOpen
                openFile()
            Case .MediaScreenShot
                Debug "screenshot"
            Case .AppAbout
                HaruhiAbout.ShowDialog()
            Case .AppClose
                Me.Close()
        End Select
    End With
    
End

Public Sub MediaTimer_Timer()

    $menuBar.SetWidgetProperty($player.Position, $player.Duration)

End
