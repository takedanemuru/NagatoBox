' Gambas class file

Inherits NagatoMenuPipe

Private $container As Container
Private $progressSlider As Slider
Private $progressLabel As TextLabel
Private $gearButton As MenuButton

Event NewPosition(argPosition As Float)

Private Sub addButton(argText As String, argTag As Integer, Optional argIcon As Picture)
    
    Dim yukiMenu As New MenuButton($container) As "MenuButton"
    
    With yukiMenu
        .Arrow = False
        .AutoResize = True
        .Border = False
        .Text = argText
        .Tag = argTag
        If argIcon Then .Picture = argIcon
    End With
    
End

Private Sub addProgressSlider()
    
     $progressSlider = New Slider($container) As "ProgressSlider"
    
    With $progressSlider
        .Value = 0
        .Expand = True
    End With
    
End

Private Sub addProgressLabel()
    
    $progressLabel = New TextLabel($container)
    
    With $progressLabel
        .Text = ""
        .AutoResize = True
        .Alignment = Align.Center
    End With
    
End

Private Sub addGearButton()
    
    Dim yukiMenu As NagatoButtonMenu
    
    $gearButton = New MenuButton($container)
    
    With $gearButton
        .Arrow = False
        .AutoResize = True
        .Border = False
        .Picture = MikuruIcon.Get("star")
    End With
    
    yukiMenu = New NagatoButtonMenu(HaruhiMovie, $gearButton) As "Menu"
    
End

Private Sub initializeWidget()
    
    With MikuruMenuEvent
        addButton("play", .MediaPlay, MikuruIcon.Get("button-play"))
        addButton("", .MediaPause, MikuruIcon.Get("button-pause"))
        'addButton("", .MediaBack, MikuruIcon.Get("button-rew"))
        'addButton("", .MediaNext, MikuruIcon.Get("button-ff"))
        addProgressSlider()
        addProgressLabel()
        addGearButton()
    End With
    
End

Public Sub SetWidgetProperty(argPosition As Float, argDuration As Float)
    
    $progressLabel.Text = Subst$((" <b>&1</b> / &2 "), MikuruText.GetTime(argPosition), MikuruText.GetTime(argDuration))
    $progressLabel.Width = $progressLabel.Font.RichTextWidth($progressLabel.Text)
    
    Object.Lock($progressSlider)
        Try $progressSlider.Value = 100 * (argPosition / argDuration)
    Object.Unlock($progressSlider)
    
End

Public Sub _new(argContainer As Container)
    
    $container = argContainer
    initializeWidget()
    
End

Public Sub ProgressSlider_Change()
    
    Raise NewPosition($progressSlider.Value)
    
End

Public Sub MenuButton_Click()
    
    Raise MenuEvent(Last.Tag)
    
End
