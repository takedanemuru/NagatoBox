' Gambas class file

Property Read Position As Float
Property Read Duration As Float

Private $drawingArea As DrawingArea
Private $mediaPlayer As MediaPlayer
Private $mediaControl As MediaControl
Private $visualizers As NagatoVisualizers

Private Sub initializeVisualizer()
    
    $visualizers = New NagatoVisualizers($mediaPlayer)
    $mediaPlayer.Video.Visualisation = $visualizers.GetVisualizer(NagatoVisualizers.Corona)
    
End

Private Sub playPath(argPath As String)
    
    Dim yukiPath As String = argPath
    
    With $mediaPlayer
        Try .Stop()
        .URL = Media.URL(yukiPath)
        $mediaControl.SetWindow($drawingArea)
        .Play()
    End With
    
End

Public Sub SetBackground()
    
    $drawingArea.Clear()
    $drawingArea.Background = Color.Lighter(Color.Violet)
    
    Draw.Begin($drawingArea)
        Draw.Font.Size = 12
        Draw.Foreground = Color.White
        Draw.RichText("<b>YUKI.N > Open file to play.</b>", 0, 0, $drawingArea.W, $drawingArea.H, Align.Center)
    Draw.End()
    
End

Private Sub initializeMediaPlayerAndControl()
    
    $mediaPlayer = New MediaPlayer As "MediaPlayer"
    $mediaControl = New MediaControl($mediaPlayer, "ximagesink") As "MediaControl"
    $mediaPlayer.Video.Output = $mediaControl
    $mediaPlayer.Audio.Volume = 1
    
End

Public Sub SetPathAndPlay(argPath As String)
    
    Me.SetBackground()
    If argPath Then playPath(argPath)
    
End

Public Sub SetPosition(argPosition As Float)
    
    $mediaPlayer.Position = $mediaPlayer.Duration * (argPosition / 100)
    
End

Public Sub Play()
    
    Try $mediaPlayer.Play()
    
End

Public Sub Pause()
    
    Try $mediaPlayer.Pause
    
End

Public Sub _new(argDrawingArea As DrawingArea)
    
    $drawingArea = argDrawingArea
    initializeMediaPlayerAndControl()
    initializeVisualizer()
    
End

Private Function Position_Read() As Float

    Return $mediaPlayer.Position

End

Private Function Duration_Read() As Float

    Return $mediaPlayer.Duration

End
