' Gambas class file

Inherits NagatoObject

Property Read Sign As String
Property Read HasNumber As Boolean
Property Read Number As Float 
Property Read Text As String

Private $isNegative As Boolean = False
Private $number As Float = 0
Private $hasNumber As Boolean = False
Private $floatingPoint As Integer = 0

Public Sub SwitchSign()
        
    $isNegative = Not $isNegative
    $number *= -1
    
End

Public Sub PushNumber(argNumber As Integer)
        
    If Not $hasNumber Then $hasNumber = True
    
    If $floatingPoint = 0 Then
        $number = $number * 10 + argNumber
    Else
        $number += ((1 / 10) ^ $floatingPoint * argNumber)
        Inc $floatingPoint
    Endif
    
    If $isNegative And If $number > 0 Then $number *= -1
    
End

Public Sub Back()
    
    $number = 0
    $floatingPoint = 0
    $hasNumber = False
    
End

Public Sub PushDot() As Boolean ' returns false if rejected.
    
    If $floatingPoint <> 0 Then Return False
    
    If Not $hasNumber Then $hasNumber = True
    $floatingPoint = 1
    
    Return True
    
End

Private Function Sign_Read() As String

    If $isNegative And If Str$($number) Not Begins "-" Then Return "-"

    Return ""

End

Private Function HasNumber_Read() As Boolean
        
    Return $hasNumber
        
End

Private Function Number_Read() As Float

    Return $number

End

Private Function Text_Read() As String

    Return MikuruNumber.GetString($number, $floatingPoint)

End
