' Gambas class file

Private $messageLcd As NagatoLcdInterface
Private $resultLcd As NagatoLcdInterface
Private $bufferLcd As NagatoLcdInterface
Private $greeter As NagatoGreeter
Private $calc As NagatoCalc 
Private $formHandler As NagatoFormHandler

Public Sub _new()
    
    $formHandler = New NagatoFormHandler(Me) As "FormHandler"
    
End

Public Sub Form_Open()

    $calc = New NagatoCalc As "Calc"
    $messageLcd = New NagatoLcdInterface(ResultLCD2)
    $resultLcd = New NagatoLcdInterface(ResultLCD)
    $bufferLcd = New NagatoLcdInterface(BufferLCD)
    BackButton.Text = String.Chr$(8594) 'back arrow
    $greeter = New NagatoGreeter($bufferLcd)
    
End

Public Sub NumberButton_Click()
        
    $calc.SendNumber(Last.Tag)
        
End

Public Sub DotButton_Click()
    
    $calc.SendDot()
        
End

Public Sub ReverseButton_Click()

    $calc.SwitchSign()

End

Public Sub EnterButton_Click()

    $calc.SendEnter()

End

Public Sub FunctionButton_Click()

    $calc.SendFunction(Last.Tag)

End

Public Sub UndoButton_Click()

    $calc.Undo()

End

Public Sub ClearButton_Click()

    ResultLCD2.Visible = False
    $calc.ClearAll()
    $resultLcd.ShowMessage("clear all")

End

Public Sub BackButton_Click()

    $calc.Back()

End

Public Sub FormHandler_KeyPressSpecial(argType As Integer)
    
    Select Case argType
        Case MikuruSpecialKeyType.Back
            BackButton_Click()
        Case MikuruSpecialKeyType.Clear
            ClearButton_Click()
        Case MikuruSpecialKeyType.Dot
            DotButton_Click()
        Case MikuruSpecialKeyType.Enter
            EnterButton_Click()
        Case MikuruSpecialKeyType.Escape
            ClearButton_Click()
        Case MikuruSpecialKeyType.Undo
            UndoButton_Click()
    End Select
    
End

Public Sub FormHandler_KeyPressFunction(argType As Integer)
    
    $calc.SendFunction(argType)
    
End

Public Sub FormHandler_KeyPressNumber(argNumber As Integer)
    
    $calc.SendNumber(argNumber)
    
End

Public Sub Calc_Signal(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.CalcResultChanged
            $resultLcd.ShowResultState(argValues[0])
        Case MikuruSignal.CalcBufferChanged
            Try $greeter.Stop()
            $bufferLcd.ShowBufferState2(argValues[0])
        Case MikuruSignal.ErrorDivisionByZero
            ResultLCD2.Visible = True
            $messageLcd.ShowMessage("error division by zero")
    End Select
    
End
