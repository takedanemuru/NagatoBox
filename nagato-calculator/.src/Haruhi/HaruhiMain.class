' Gambas class file

Private $calc As NagatoCalc 
Private $formHandler As NagatoFormHandler
Private $lcdHandler As NagatoLCDHandler
Private $starButton As NagatoMenuButtonStar

Private Sub specialKeysEvents(argType As Integer)
    
    Select Case argType
        Case MikuruSpecialKeyType.Back
            $calc.Back()
        Case MikuruSpecialKeyType.Clear, MikuruSpecialKeyType.Escape
            $lcdHandler.ClearAll()
            $calc.ClearAll()
        Case MikuruSpecialKeyType.Dot
            $calc.SendDot()
        Case MikuruSpecialKeyType.Enter
            $calc.SendEnter()
        Case MikuruSpecialKeyType.Undo
            $calc.Undo()
        Case MikuruSpecialKeyType.SwitchSign
            $calc.SwitchSign()
    End Select
    
End

Public Sub Form_Open()

    $formHandler = New NagatoFormHandler(Me) As "FormHandler"
    $calc = New NagatoCalc As "Calc"
    $lcdHandler = New NagatoLCDHandler(ErrorLCD, ResultLCD, BufferLCD)
    BackButton.Text = String.Chr$(8594) 'back arrow
    $starButton = New NagatoMenuButtonStar(StarButton) As "StarButton"
    
End

Public Sub NumberButton_Click()
        
    $calc.SendNumber(Last.Tag)
        
End

Public Sub FunctionButton_Click()

    $calc.SendFunction(Last.Tag)

End

Public Sub SpecialKeys_Click()
    
    specialKeysEvents(Last.Tag)
    
End

Public Sub FormHandler_Signal(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.KeyPressFunction
            $calc.SendFunction(argValues[0])
        Case MikuruSignal.KeyPressSpecial
            specialKeysEvents(argValues[0])
        Case MikuruSignal.KeyPressNumber
            $calc.SendNumber(argValues[0])
    End Select
    
End

Public Sub Calc_Signal(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.CalcResultChanged
            $lcdHandler.SetResult(argValues[0])
        Case MikuruSignal.CalcBufferChanged
            $lcdHandler.SetBuffer(argValues[0])
        Case MikuruSignal.ErrorDivisionByZero
            $lcdHandler.SetError(("error division by zero"))
    End Select
    
End
