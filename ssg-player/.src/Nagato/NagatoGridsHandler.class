' Gambas class file

Private $grids As GridView

Event Play(argTitle As String)
Event Download(argTitle As String)

Private Sub initializeGrids()
    
    With $grids
        .Expand = True
        .ScrollBar = Scroll.Vertical
        .Header = GridView.Horizontal
        .Columns.Count = 2
        .Columns[0].W = 24
        .Columns[1].Title = ("Title")
        .Grid = False
        .Mode = Select.Single
    End With
    
End

Private Sub setIcon()
    
    Dim yukiRow As Integer
    
    For yukiRow = 0 To $grids.Rows.Max
        $grids[yukiRow, 0].Picture = IIf(yukiRow = $grids.Row, MikuruIcon["button-play", 16], Null)
    Next
    
End

Private Sub setBackgroundColor()
    
    Dim yukiIndex As Integer
    
    For yukiIndex = 0 To $grids.Columns.Max
        $grids[$grids.Rows.Max, yukiIndex].Background = Color.Merge(Color.White, Color.Blue, 0.2)
    Next
    
End

Public Sub Set(argCollection As Collection)
    
    Dim yukiTitle As String
    
    For Each yukiTitle In argCollection
        Inc $grids.Rows.Count
        $grids[$grids.Rows.Max, 1].Text = argCollection.Key
        If Odd($grids.Rows.Max) Then setBackgroundColor()
    Next
    
End

Public Sub _new(argContainer As Container)
    
    $grids = New GridView(argContainer) As "GridView"
    
    initializeGrids()
    
End

Public Sub GridView_DblClick()
    
    setIcon()
    
    Raise Play($grids[$grids.Row, 1].Text)
    
End

Public Sub GridView_Menu()
    
    Dim yukiMenu As New NagatoContextMenuMainGrids($grids.Parent) As "ContextMenu"
    
    yukiMenu.PopUp()
    
End

Public Sub ContextMenu_Click(argTag As Integer)
    
    Select Case argTag
        Case NagatoContextMenuMainGrids.MenuPlay
            setIcon()
            Raise Play($grids[$grids.Row, 1].Text)
        Case NagatoContextMenuMainGrids.MenuDownload
            Raise Download($grids[$grids.Row, 1].Text)
    End Select
    
End
