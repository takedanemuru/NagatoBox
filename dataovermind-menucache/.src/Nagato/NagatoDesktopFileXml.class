' Gambas class file

Private $id As Integer

Private Function removeArgumentFromExec(argExec As String) As String
    
    Dim yukiArray As String[] = Split(argExec, " ", "", True)
    Dim yukiIndex As Integer
    
    For yukiIndex = yukiArray.Max DownTo yukiIndex
        If yukiArray[yukiIndex] Begins "%" Then yukiArray.Remove(yukiIndex, 1)
    Next
    
    Return yukiArray.Join(" ")
    
End

Private Function getStandardXml(argDesktopFile As DesktopFile) As String
    
    Dim yukiXml As String = File.Load("Text/MenuItemTemplate")
    Dim yukiLabel As String = Quote$(argDesktopFile.Name)
    Dim yukiExecute As String = removeArgumentFromExec(argDesktopFile.Exec)
    
    yukiXml = Subst$(yukiXml, yukiLabel, yukiExecute)
    
    Return Replace$(yukiXml, "&", "and")
    
End

Private Function getDesktopActionXmlTemplate(argDesktopFile As DesktopFile) As String
    
    Dim yukiXml As String = File.Load("Text/MenuItemTemplateHasAction")
    Dim yukiLabel As String = Quote$(argDesktopFile.Name)
    Dim yukiExecute As String = removeArgumentFromExec(argDesktopFile.Exec)
    
    Inc $id
    
    yukiXml = Subst$(yukiXml, Quote$("nagato-openbox-menu-" & $id), yukiLabel, yukiExecute)
    
    Return Replace$(yukiXml, "&", "and")
    
End

Public Function getDesktopActionXml(argDesktopFile As DesktopFile, argFile As String) As String
    
    Dim yukiTemplate As String = getDesktopActionXmlTemplate(argDesktopFile)
    Dim yukiLine As String
    Dim yukiDesktopAction As New NagatoDesktopAction("")
    
    For Each yukiLine In Split(argFile, "\n", "", True)
        If yukiLine Begins "[Desktop Action" Then 
            If yukiDesktopAction.Ready Then yukiTemplate &= yukiDesktopAction.Xml
            yukiDesktopAction = New NagatoDesktopAction(yukiLine)
        Endif
        If yukiLine Begins "Name=" Then yukiDesktopAction.SetName(yukiLine)
        If yukiLine Begins "Exec=" Then yukiDesktopAction.SetExec(yukiLine)
    Next
    
    If yukiDesktopAction.Ready Then yukiTemplate &= yukiDesktopAction.Xml

    Return yukiTemplate & "</menu>\n"
    
End

Public Function Get(argDesktopFile As DesktopFile) As String
    
    Dim yukiFile As String = File.Load(argDesktopFile.Path)
    
    If InStr(yukiFile, "[Desktop Action") Then 
        Return getDesktopActionXml(argDesktopFile, yukiFile)
    Else
        Return getStandardXml(argDesktopFile)
    End If
    
End
