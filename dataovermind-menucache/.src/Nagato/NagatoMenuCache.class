' Gambas class file

Create Static

Property Read Categories As String[]

Private $collecion As New NagatoDesktopFileCollection
Private $desktopFileXml As New NagatoDesktopFileXml
Private $categories As New NagatoCategories
Private $observer As NagatoDirectoryObserver

Private Function getPipeMenuXml(argCategory As String) As String
    
    Dim yukiXml As String = ("<openbox_pipe_menu>" & "\n")
    Dim yukiDesktopFile As DesktopFile
    
    For Each yukiDesktopFile In $collecion[argCategory]
        yukiXml &= $desktopFileXml.Get(yukiDesktopFile)
    Next
    
    Return (yukiXml & "</openbox_pipe_menu>")
    
End

Private Sub saveCategoryMenuFile()
    
    Dim yukiCategory As String
    
    For Each yukiCategory In $categories.All
        File.Save(MikuruPath.MenuFileDirectory &/ yukiCategory, getPipeMenuXml(yukiCategory))
    Next

End

Public Sub Activate()
    
    Me.Refresh()
    
    $observer = New NagatoDirectoryObserver("/usr/share/applications") As "Asakura"
    
End

Public Sub Refresh()
    
    $collecion.Refresh()
    saveCategoryMenuFile()
    
End

Public Sub Get(argCategory As String) As String
    
    If Not $collecion.Exist(argCategory) Then 
        Return File.Load("Text/DummyMenuItem")
    Else
        Return getPipeMenuXml(argCategory)
    End If
    
End

Public Sub Asakura_Modefied(argFileName As String)
    
    Me.Refresh()
    
End

Public Sub Asakura_Deleted(argFileName As String)
    
    Me.Refresh()
    
End

Private Function Categories_Read() As String[]

    Return $categories.All

End
