' Gambas class file

Private Const ApplicationDirectory As String = "/usr/share/applications"

Private $collecion As New Collection

Private Function isMediaDesktopFile(argCategory As String) As Boolean
    
    If argCategory = "AudioVideo" Then Return True
    If argCategory = "Audio" Then Return True
    If argCategory = "Video" Then Return True
    
    Return False
    
End

Private Function getPosition(argKey As String, argDesktopFile As DesktopFile) As Integer
    
    Dim yukiDesktopFiles As DesktopFile[] = $collecion[argKey]
    Dim yukiIndex As Integer
    
    For yukiIndex = 0 To yukiDesktopFiles.Max
        If String.Upper(yukiDesktopFiles[yukiIndex].Name) > String.Upper(argDesktopFile.Name) Then
            Return yukiIndex
        End If
    Next
    
    Return yukiDesktopFiles.Count
    
End

Private Function hasDesktopFile(argKey As String, argDesktopFile As DesktopFile) As Boolean
    
    Dim yukiDesktopFiles As DesktopFile[] = $collecion[argKey]
    Dim yukiIndex As Integer
    
    For yukiIndex = 0 To yukiDesktopFiles.Max
        If yukiDesktopFiles[yukiIndex].Exec = argDesktopFile.Exec Then Return True
    Next
    
    Return False
    
End

Private Sub setDesktopFileToCollection(argKey As String, argDesktopFile As DesktopFile)
    
    If $collecion.Exist(argKey) Then
        If hasDesktopFile(argKey, argDesktopFile) Then Return
        $collecion[argKey].Add(argDesktopFile, getPosition(argKey, argDesktopFile))
    Else
        $collecion.Add([argDesktopFile], argKey)
    Endif
    
End

Private Sub setDesktopFile(argPath As String)
    
    Dim yukiDesktopFile As New DesktopFile(argPath)
    Dim yukiIndex As Integer
    Dim yukiCategory As String
    
    If yukiDesktopFile.Categories = Null Then Return
    
    For yukiIndex = 0 To yukiDesktopFile.Categories.Max
        yukiCategory = yukiDesktopFile.Categories[yukiIndex] 
        If yukiCategory = "" Then Continue
        setDesktopFileToCollection(yukiCategory, yukiDesktopFile)
        If isMediaDesktopFile(yukiCategory) Then setDesktopFileToCollection("Media", yukiDesktopFile)
    Next
    
End

Public Sub Refresh()
    
    Dim yukiPath As String
    
    For Each yukiPath In RDir(ApplicationDirectory)
        If File.Ext(ApplicationDirectory &/ yukiPath) <> "desktop" Then Continue
        setDesktopFile(ApplicationDirectory &/ yukiPath)
    Next
    
End

Public Function Exist(argCategory As String) As Boolean
    
    Return $collecion.Exist(argCategory)
    
End

Public Sub _get(argCategory As String) As DesktopFile[]
    
    Return $collecion[argCategory]
    
End
