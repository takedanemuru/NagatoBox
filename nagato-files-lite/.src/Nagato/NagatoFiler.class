' Gambas class file

Inherits NagatoWidget

Private $currentPath As String
Private $vBox As VBox
Private $menuPanel As NagatoMenuPanel
Private $gridView As NagatoGridView

Private Sub initializeContainer(argContainer As Container)
    
    $vBox = New VBox(argContainer)
    
    With $vBox
        .Expand = True
        .Visible = NagatoSettingsView[Me._$uniqueName, MikuruProperty.ViewShowPane] 
    End With
    
End

Private Sub moveDirectory(argPath As String)
    
    $currentPath = argPath
    $menuPanel.SetPath(argPath)
    $gridView.SetPath(argPath)
    
End

Public Sub Toggle(argUniqueName As String)
    
    If argUniqueName = Me._$uniqueName Then Return
    
    $vBox.Visible = Not $vBox.Visible
    NagatoSettingsView.Set(Me._$uniqueName, MikuruProperty.ViewShowPane, $vBox.Visible)
    
End

Public Sub _OnInitialize(argContainer As Container)
    
    initializeContainer(argContainer)
    $menuPanel = New NagatoMenuPanel($vBox, Me._$uniqueName) As "TFEI"
    $gridView = New NagatoGridView($vBox, Me._$uniqueName) As "TFEI"
    moveDirectory(User.Home)
    
End

Public Sub TFEI_Signal(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.UserInputMoveToDirectory
            moveDirectory(argValues[0])
        Case MikuruSignal.UserInputMoveToParent
            If Exist(File.Dir($currentPath)) Then moveDirectory(File.Dir($currentPath))
        Case MikuruSignal.UserInputFilter
            $gridView.Filter(argValues[0])
        Case MikuruSignal.AppToggleExtra
            Raise Signal(argSignal, [Me._$uniqueName])
        Default
            Raise Signal(argSignal, argValues)
    End Select
    
End
