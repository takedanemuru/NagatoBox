' Gambas class file

Private $drawingArea As DrawingArea
Private $currentPath As String
Private $iconPainter As NagatoPainterIcon
Private $pathPainter As NagatoPainterPath
Private $propertyPainter As NagatoPainterProperty
Private $isTrash As Boolean = False

Private Sub initializeDrawingArea(argContainer As Container, argBackground As Integer)
    
    $drawingArea = New DrawingArea(argContainer) As "DrawingArea"
    
    With $drawingArea
        .Expand = True
        .Background = argBackground
        .Font.Bold = True
    End With
    
End

Public Sub SetProperty(argPath As String)
    
    If Exist(argPath) Then
        $isTrash = False
        $currentPath = argPath
        $drawingArea.Refresh()
    End If

End

Public Sub SetTrashProperty(argTrashName As String)
    
    If Exist(argTrashName) Then
        $isTrash = True
        $currentPath = argTrashName
        $drawingArea.Refresh()
    Endif
    
End

Public Sub Refresh()
    
    $drawingArea.Refresh()
    
End

Public Sub _new(argContainer As Container, argBackground As Integer)
    
    initializeDrawingArea(argContainer, argBackground)
    
    $iconPainter = New NagatoPainterIcon($drawingArea)
    $pathPainter = New NagatoPainterPath($drawingArea)
    $propertyPainter = New NagatoPainterProperty($drawingArea)
    
End

Public Sub DrawingArea_Draw()
    
    Dim yukiY As Integer
    
    Paint.Begin($drawingArea)
        yukiY = $iconPainter.Paint($currentPath)
        yukiY = $pathPainter.Paint(yukiY, $currentPath)
        $propertyPainter.Paint(yukiY, $currentPath, $isTrash)
    Paint.End()
    
Catch
    Debug Error.Text
    Try Paint.End()
    
End
