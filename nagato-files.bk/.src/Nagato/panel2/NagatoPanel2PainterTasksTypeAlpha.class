' Gambas class file

Inherits NagatoPanel2PainterTasks

Private $windows As NagatoDesktopWindowsTypeAlpha

Private Function getText(argDesktopWindow As DesktopWindow, argWidth As Integer) As String
    
    Return MikuruCollapsedText.Get(argDesktopWindow.Name, Paint.Font, argWidth)
    
End

Private Function getDesktopWindowFromPositionX(argX As Integer) As DesktopWindow
    
    Dim yukiIndex As Integer = (argX + Me.Margin) Div $windows.Width
    
    Return $windows[Min(yukiIndex, $windows.Max)]
    
End

Public Sub _OnInitialize()
    
    $windows = New NagatoDesktopWindowsTypeAlpha
    
End

Private Function getTaskRect(argWindowIndex As Integer) As Rect
    
    Dim yukiRect As New Rect
    
    With yukiRect
        .X = $windows.Width * argWindowIndex + Me.Margin
        .Y = 0
        .H = Me._$drawingArea.H
        .W = $windows.Width
    End With
    
    Return yukiRect
    
End

Public Sub _PaintText(argDesktopWindow As DesktopWindow, argRect As Rect)
    
    With argRect
        Paint.DrawRichText(getText(argDesktopWindow, .W - 28), .X + 24, .Y, .W - 24, .H, Align.Left)
    End With
    
Catch
    Return
    
End

Public Sub _PaintIcon(argDesktopWindow As DesktopWindow, argRect As Rect)
    
    Paint.DrawPicture(Me._GetIcon(argDesktopWindow), argRect.X + 4, argRect.Y + 4)
    
End

Private Sub paintTask(argWindowIndex As Integer)
    
    Dim yukiRect As Rect = getTaskRect(argWindowIndex)
    Dim yukiDesktopWindow As DesktopWindow = $windows[argWindowIndex]

    Paint.Brush = Paint.Color(NagatoSettingsPanel.Foreground)
    Me._PaintIcon(yukiDesktopWindow, yukiRect)
    Me._PaintText(yukiDesktopWindow, yukiRect)
    Paint.Fill()
    
End

Public Sub _OnLeftClick(argX As Integer)
    
    If 0 > $windows.Max Then Return
    Me._ActivateDesktopWindow(getDesktopWindowFromPositionX(argX))
    
End

Public Sub _OnRightClick(argX As Integer)
    
    If 0 > $windows.Max Then Return 
    Me._PopUpContextMenu(getDesktopWindowFromPositionX(argX))
    
End

Public Sub Paint(Optional argX As Integer) As Integer
    
    Dim yukiIndex As Integer

    $windows.Refresh(argX - Me.Margin)

    For yukiIndex = 0 To $windows.Max
        paintTask(yukiIndex)
    Next
    
    Return Me.Margin ' dummy, should return right margin. 
    
End
