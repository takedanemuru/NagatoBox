' Gambas class file

Inherits NagatoSubMenu

Public Function _GetMimeType() As String
    
    If IsDir(Me._$path) Then
        Return "inode/directory"
    Else
        Return DesktopMime.FromFile(Me._$path).Type
    Endif
    
End

Public Function _GetDesktopFilesWithMimeType() As String[]
    
    Dim yukiReturn As String[]
    
    yukiReturn = MikuruConfFlie.GetItems(MikuruApplications.GlobalMimeList, Me._GetMimeType())
    
    Return yukiReturn.Sort(gb.Natural)
    
End

Public Sub _InitializeParentMenu(argRootMenu As Menu) 'override
    
    Me._$rootMenu = New Menu(argRootMenu)
    
    With Me._$rootMenu
        .Text = ("Open with ...")
    End With
    
End

Public Sub _InitializeMenu() ' override
    
    Dim yukiDesktopFile As String
    Dim yukiChecker As New String[]
    Dim yukiMimeTypes As String[] = Me._GetDesktopFilesWithMimeType()
    Dim yukiDefaultApp As New NagatoDefaultApplication(Me._$path)
        
    Object.New("NagatoMenuItemSelectApplication", [Me._$rootMenu, Me._$path])
    If yukiMimeTypes.Count > 0 Then Object.New("NagatoMenuItemDummy", [Me._$rootMenu, Me._$path])
    
    For Each yukiDesktopFile In yukiMimeTypes
        If Not yukiChecker.Exist(yukiDesktopFile) Then
            If Not yukiDefaultApp.NotFound Then
                If yukiDesktopFile = yukiDefaultApp.Name Then Continue
            Endif
            Object.New("NagatoOpenWithDesktopEntryMenu", [Me._$rootMenu, Me._$path, yukiDesktopFile])
            yukiChecker.Add(yukiDesktopFile)
        End If
    Next
    
Catch
    Debug Error.Text
    Return
    
End
