' Gambas class file

Inherits NagatoSubMenu

Private Function hasThemeFile(argDirectory As String) As Boolean
    
    If Exist(argDirectory &/ "index.theme") Then Return True
    If Exist(argDirectory &/ "icon-theme.cache") Then Return True
    
    Return False
    
End

Private Function hasData(argDirectory As String) As Boolean
    
    Dim yukiPath As String
    
    For Each yukiPath In Dir(argDirectory, "*", gb.Directory)
        If yukiPath <> "cursors" Then Return True
    Next
    
    Return False
    
End

Public Sub _InitializeMenu() 'override
    
    Dim yukiDirectory As String = "/usr/share/icons"
    Dim yukiPath As String
    
    For Each yukiPath In Dir(yukiDirectory, "*", gb.Directory).Sort(gb.IgnoreCase)
        If Not hasThemeFile(yukiDirectory &/ yukiPath) Then Continue
        If Not hasData(yukiDirectory &/ yukiPath) Then Continue
        Object.New("NagatoMenuItemSelectIconTheme", [Me._$rootMenu, yukiPath])
    Next
    
End

Public Sub _InitializeParentMenu(argRootMenu As Menu) ' override
    
    Me._$rootMenu = New Menu(argRootMenu)
    
    With Me._$rootMenu
        .Text = ("Select Icon Theme")
        .Picture = MikuruIcon.Get("information-balloon")
    End With
    
End
