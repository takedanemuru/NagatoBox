' Gambas class file

Public Enum ActionNone, ActionPlay, ActionPause, ActionNext, ActionBack, ActionAddMusics

Private Const ServiceName As String = "org.gambas.nagato-player-x"
Private Const ObjectPath As String = "/org/nagato/player/x/player"
Private Const InterfaceName As String = "org.gambas.nagato.player.x.nagatodbusinterface"

Private $proxy As DBusProxy

Private Sub addMusics()
    
    Dialog.Path = Desktop.GetDirectory("MUSIC")
    If Dialog.OpenFile(True) Then Return
    
    $proxy.AddMusics(Dialog.Paths)
    
End

Public Sub Ensure()
    
    If Not Object.IsValid($proxy) Then $proxy = DBus[ServiceName][ObjectPath, InterfaceName]
    
End

Public Sub _call(argAction As Integer)
    
    Select Case argAction
        Case ActionPlay
            $proxy.Play()
        Case ActionPause
            $proxy.Pause()
        Case ActionNext
            $proxy.Next()
        Case ActionBack
            $proxy.Back()
        Case ActionAddMusics
            addMusics()
    End Select
    
End
