' Gambas class file

Property Read IsEmpty As Boolean

Private Const NoFilesKey As String = "nofiles"
Private Const IconSize As Integer = 64
Private Const FontSize As Integer = 16

Private $iconView As IconView
Private $obsever As Observer

Event Drop(argPaths As String[])

Private Sub initializeIconView()
    
     With $iconView
        .Clear()
        .Foreground = Color.Orange
        .Font.Size = FontSize
        .Picture = Picture.Load("Pictures/yukichan_29.jpg")
    End With
    
End

Public Sub SetEmptyView()
    
    initializeIconView()
    $iconView.Add(NoFilesKey, ("Drop file(s) here."))
    
End

Public Sub SetFileIcons()
    
    Dim yukiPath As String
    
    initializeIconView()
    
    For Each yukiPath In NagatoFileStore.Paths
        shiftToNormalMode()
        $iconView.Add(yukiPath, File.Name(yukiPath), Desktop.GetFileIcon(yukiPath, IconSize))
    Next

    $iconView.Refresh()

End

Private Sub shiftToNormalMode()
    
    If Me.IsEmpty Then $iconView.Clear()
    $iconView.Font.Size = HaruhiMain.Font.Size
    
End

Public Sub Add(argPaths As String[])
    
    Dim yukiPath As String 
    Dim yukiDesktopMime As DesktopMime
    
    For Each yukiPath In argPaths
        shiftToNormalMode()
        Debug yukiPath
        yukiDesktopMime = DesktopMime.FromFile(yukiPath)
        $iconView.Add(yukiPath, File.Name(yukiPath), Desktop.GetFileIcon(yukiPath, IconSize))
    Next
    
End

Public Sub Remove(argPaths As String[])
    
    Dim yukiPath As String 
    
    For Each yukiPath In argPaths
        shiftToNormalMode()
        Try $iconView.Remove(yukiPath)
    Next
    
    If $iconView.Count = 0 Then Me.SetEmptyView()
    
End

Public Sub _new(argIconView As IconView)
    
    $iconView = argIconView
    $obsever = New Observer($iconView) As "Asakura"
    
End

Public Sub Asakura_Drop()
    
    If Drag.Format <> "text/uri-list" Then Return
    
    Raise Drop(NagatoDrag2.GetPaths(Drag.Data))
    
End

Private Function IsEmpty_Read() As Boolean

    If $iconView.Count = 1 And If $iconView.Exist(NoFilesKey) Then
        Return True
    Else
        Return False
    Endif

End
