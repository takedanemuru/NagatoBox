' Gambas class file

Inherits NagatoIcon

Property Read TrashDir As String
Property Read TrashFilesDir As String

Private $directoryObserver As NagatoDirectoryObserver
Private $text As String
Private $onMouse As Boolean

Private Function getText() As String
    
    Dim yukiPath As String
    Dim yukiCount As Integer 
    Dim yukiTotalSize As Long 
    Dim yukiReturn As String
    
    For Each yukiPath In RDir(Me.TrashFilesDir)
        Inc yukiCount
        yukiTotalSize += Stat(Me.TrashFilesDir &/ yukiPath).Size
    Next
    
    If yukiCount = 0 Then
        Return ("Trash<br>-Empty-")
    Else
        yukiReturn = Format$(yukiCount, "###,###,##0 items")
        yukiReturn &= "<br>"
        yukiReturn &= Format$(yukiTotalSize / (1024 ^ 2), "###,###,##0.##MiB")
        Return yukiReturn
    End If
    
End

Private Sub setFormPicture(Optional argBackground As Integer = Color.Lighter(Color.Blue))
    
    Dim yukiBaseImage As Image
    
    Me._$form.Height = Me._$form.Font.RichTextHeight($text) + 72
    yukiBaseImage = New Image(Me.FormWidth, Me._$form.Height, argBackground)
    
    Paint.Begin(yukiBaseImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.DrawImage(MikuruIcon.Get("bin", 48).Image, 40, Me.Margin)
        Paint.Font.Bold = True
        Paint.DrawRichText($text, Me.Margin, 64, Me.TextWidth, Me._$form.Height - 72, Align.Center)
    Paint.End()
    
    Me._$form.Picture = yukiBaseImage.Picture
    
End

Private Sub initializeObserver()
    
    $directoryObserver = New NagatoDirectoryObserver As "DirectoryObserver"
    
    With $directoryObserver
        .IntervalSecond = 2
        .Directory = Me.TrashFilesDir
    End With
    
End

Public Sub _AddStarMenu() 'override
    
    Dim yukiMenu As NagatoButtonMenuTrashBin
    
    yukiMenu = New NagatoButtonMenuTrashBin(Me._$form, Me._$starButton) As "StarMenu"
    
End

Public Sub _new()
    
    Me._$form = New Form As "Icon"
    $text = getText()
    Me._InitializeForm(Me._$form.Font.RichTextHeight($text) + 72)
    Me._$form.Show
    setFormPicture()
    initializeObserver()
    Me._$form.Drop = True
    
End

Public Sub _OnMouseEnter()
    
    $onMouse = True
    setFormPicture(NagatoSettings.BackgroundHighlight)
    
End

Public Sub _OnMouseLeave()
    
    $onMouse = False
    setFormPicture(NagatoSettings.BackgroundFile)
    
End

Public Sub Icon_Drop()
    
    Dim yukiPath As String
    
    If Drag.Format <> "text/uri-list" Then Return
    
    Debug Drag.Data
    
    For Each yukiPath In NagatoDrag2.GetPaths(Drag.Data)
        Try Shell Subst$("trash-put &1", yukiPath)
    Next
    
End

Public Sub DirectoryObserver_Modefied(argFileName As String)
    
    Debug argFileName & "is modefied"
    
    $text = getText()
    setFormPicture()
    
End

Public Sub DirectoryObserver_Deleted(argFileName As String)
    
    Debug argFileName & "is deleted"
    
    $text = getText()
    setFormPicture()
    
End

Private Function TrashDir_Read() As String

    Return User.Home &/ ".local/share/Trash"

End

Private Function TrashFilesDir_Read() As String

    Return Me.TrashDir &/ "files"

End
