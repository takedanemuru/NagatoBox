' Gambas class file

Inherits NagatoIconThumbnail

Private Const IconSize As Integer = 128

Private $pdfDocument As PdfDocument

Public Sub _SetIcon() 
    
    Dim yukiIcon As Image
    Dim yukiRate As Float
    
    If NagatoDBThumbnailer.HasThumbnail(Me._$path, Me.FormWidth) Then
        Me._$icon = NagatoDBThumbnailer.Get(Me._$path, Me.FormWidth).Image
    Else
        ' pdf document's page index starts from 1 not 0.
        yukiIcon = $pdfDocument[1].Image
        yukiRate = Me.FormWidth / yukiIcon.Width
        Me._$icon = yukiIcon.Stretch(yukiIcon.W * yukiRate, yukiIcon.H * yukiRate)
        NagatoDBThumbnailer.Push(Me._$path, Me.FormWidth, Me._$icon.Picture)
    End If

    Me._$formHeight = Me._$icon.H
    
End

Private Sub drawText()
    
    paint.Brush = paint.Color(Color.White)
    paint.Font.Bold = True
    With Me._$textArea
        paint.RichText(Me._$text, .X, Me._$icon.H - (.H + 1 * Me.Margin), .W, .H, Align.Center)
    End With
    paint.Fill()
    
End

Public Function _GetText() As String 'override
    
    Dim yukiTitle As String = File.Name(Me._$path)
    Dim yukiPage As String
    
    $pdfDocument = New PdfDocument
    $pdfDocument.Open(Me._$path)
    yukiPage = "-" & $pdfDocument.Count & IIf($pdfDocument.Count = 1, " page-", " pages-")
    
    Return yukiTitle & "<br>" & yukiPage
    
End

Public Sub _SetFormPicture(argBackground As Integer)
    
    Dim yukiBaseImage As New Image(Me.FormWidth, Me._$icon.H, argBackground)
    
    With paint
        .Begin(yukiBaseImage)
        .DrawImage(Me._$icon, 0, 0)
        Me._DrawMaskArea(argBackground)
        drawText()
        .End()
    End With
    
    Me._$form.Picture = yukiBaseImage.Picture
    
End

Public Sub _SetForm()
    
    Me._InitializeForm(Me._$icon.H)
    
    Me._$form.Name = File.Name(Me._$path)
        
End

Public Sub _AddStarMenu() 'override
    
    Dim yukiMenu As NagatoButtonMenuFileIcon
    
    yukiMenu = New NagatoButtonMenuFileIcon(Me._$form, Me._$starButton, Me._$path) As "StarMenu"
    
End
