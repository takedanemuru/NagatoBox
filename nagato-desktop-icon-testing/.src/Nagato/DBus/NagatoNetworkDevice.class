' Gambas class file

Private Const InterfaceMain As String = "org.freedesktop.NetworkManager.Device"
Private Const InterfaceWired As String = "org.freedesktop.NetworkManager.Device.Wired"
Private Const InterfaceWireless As String = "org.freedesktop.NetworkManager.Device.Wireless"

Property Read ReadableType As String
Property Read Type As Integer
Property Read Speed As Integer
Property Read HardwareAddress As String

Private $path As String
Private $proxy As DBusProxy
Private $signalDevice As DBusSignal

Event PropertiesChanged

Public Sub _new(argObjectPath As String)
    
    $path = argObjectPath
    $proxy = DBus[NagatoNetworkManager.ApplicationName][argObjectPath, InterfaceMain]
    
    Select Case Me.Type
        Case 1 'wired
            $signalDevice = New DBusSignal(DBus.System, InterfaceWired, True) As "DeviceSignal"
        Case 2 'wireless
            $signalDevice = New DBusSignal(DBus.System, InterfaceWireless, True) As "DeviceSignal"
    End Select
    
End

Public Sub DeviceSignal_Signal(argSignal As String, argValues As Variant[])
    
    Debug Me.Speed
    
    Raise PropertiesChanged
    
End

Private Function Type_Read() As Integer

    Return $proxy.DeviceType

End

Private Function Speed_Read() As Integer

    Dim yukiProxy As DBusProxy

   Select Case Me.Type
       Case 1 'wired
           yukiProxy = DBus[NagatoNetworkManager.ApplicationName][$path, InterfaceWired]
           Return yukiProxy.Speed
       Case 2 'wireless
           yukiProxy = DBus[NagatoNetworkManager.ApplicationName][$path, InterfaceWireless]
           Return yukiProxy.Bitrate
       Default
           Return 0
   End Select

End

Private Function HardwareAddress_Read() As String

    Dim yukiProxy As DBusProxy

   Select Case Me.Type
       Case 1 'wired
           yukiProxy = DBus[NagatoNetworkManager.ApplicationName][$path, InterfaceWired]
           Return yukiProxy.HwAddress
       Case 2 'wireless
           yukiProxy = DBus[NagatoNetworkManager.ApplicationName][$path, InterfaceWireless]
           Return yukiProxy.HwAddress
       Default
           Return ""
   End Select

End

Private Function ReadableType_Read() As String

    Select Case $proxy.DeviceType
        Case 1
            Return ("Wired")
        Case 2
            Return ("Wireless")
        Default
            Return ("Unknown")
    End Select

End
