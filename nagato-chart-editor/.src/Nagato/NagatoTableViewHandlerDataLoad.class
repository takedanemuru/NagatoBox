' Gambas class file

Inherits NagatoTableViewHandler

Private $csv As CsvFile

Private Sub clear()
    
    Me._$tableView.Clear()
    Me._$tableView.Rows.Count = 0
    
End

Private Sub setColumnHeaders(argHeaders As String[])
    
    Dim yukiIndex As Integer
    
    Me._$tableView.Columns.Count = argHeaders.Count
    
    For yukiIndex = 0 To argHeaders.Max
        Me._$tableView.Columns[yukiIndex].Text = argHeaders[yukiIndex]
    Next
    
End

Private Sub setRow(argCollection As Collection)
    
    Dim yukiColumn As Integer
    Dim yukiData As String
    
    For yukiColumn = 0 To $csv.Fields.Max
        yukiData = CString(argCollection[$csv.Fields[yukiColumn]])
        Me._$tableView[Me._$tableView.Rows.Max, yukiColumn].Text = yukiData
        If Not IsNumber(yukiData) Then Continue
        Me._$tableView[Me._$tableView.Rows.Max, yukiColumn].Alignment = Align.Right
    Next
    
End

Private Sub setRows()
    
    Do
        Inc Me._$tableView.Rows.Count
        setRow($csv.Read())
    Loop Until $csv.Eof
    
End

Private Sub setCsvFile(argPath As String)
    
    $csv = New CsvFile(argPath)
    
    clear()
    setColumnHeaders($csv.Fields)
    setRows()
    
End

Public Sub New()
    
    If Not MikuruDialogPortalTableHeaders() Then Return
    
    clear()
    setColumnHeaders(MikuruDialogPortalTableHeaders.Headers)
    Me._$tableView.Rows.Count = 1
    
End

Public Sub OpenFile()
    
    Dialog.Title = ("Select CSV File")
    Dialog.Path = User.Home
    Dialog.Filter = ["*.csv", "csv file"]
    If Not Dialog.OpenFile(False) Then setCsvFile(Dialog.Path)
    
End
