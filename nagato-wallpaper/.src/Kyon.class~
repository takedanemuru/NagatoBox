' Gambas class file

Private $previewRatio As Float
Private $currentPath As String
Private $currentColor As Integer

Private Sub initializePreviewDrawingArea()
    
    $previewRatio = PreviewDrawingArea.Width / Screen.Width
    PreviewDrawingArea.Height = Screen.Height * $previewRatio
    
End

Private Function getUnresizedThumbnail() As Image
    
    Dim yukiImage As Image = Image.Load($currentPath)
    
    yukiImage = yukiImage.Stretch(yukiImage.Width * $previewRatio, yukiImage.Height * $previewRatio)
    
    Return yukiImage
    
End

Private Function getFillRate() As Float
    
    Dim yukiImage As Image = getUnresizedThumbnail()

    Return Min(PreviewDrawingArea.H / yukiImage.H, PreviewDrawingArea.W / yukiImage.W)
    
End
   
Private Function getThumbnailForFillMode() As Image
    
    Dim yukiRate As Float = getFillRate()
    Dim yukiImage As Image = getUnresizedThumbnail()
    
    yukiImage = yukiImage.Stretch(yukiImage.Width * yukiRate, yukiImage.Height * yukiRate)
    
    Return yukiImage
    
End

Private Sub paintCenter()
    
    Dim yukiImage As Image = getUnresizedThumbnail()
    Dim yukiXPosition As Float = (PreviewDrawingArea.W - yukiImage.W) / 2
    Dim yukiYPosition As Float = (PreviewDrawingArea.H - yukiImage.H) / 2
    
    Paint.DrawImage(yukiImage, yukiXPosition, yukiYPosition)
    
End

Private Function getStartPoint(argImage As Image) As PointF
    
    Dim yukiOriginPoint As PointF = getOriginPoint(argImage)
    Dim yukiStartPont As New PointF
    
    yukiStartPont.X = yukiOriginPoint.X - (Int(yukiOriginPoint.X / argImage.W + 1) * argImage.W)
    yukiStartPont.Y = yukiOriginPoint.Y - (Int(yukiOriginPoint.Y / argImage.H + 1) * argImage.H)
    
    Return yukiStartPont
    
End

Private Sub paintTile()
    
    Dim yukiImage As Image = getUnresizedThumbnail()
    Dim yukiStartPont As PointF = getStartPoint(yukiImage)
    Dim yukiCurrentPoint As New PointF
    
    yukiCurrentPoint.Y = yukiStartPont.Y
    
    Repeat
        yukiCurrentPoint.X = yukiStartPont.X
        Repeat
            Paint.DrawImage(yukiImage, yukiCurrentPoint.X, yukiCurrentPoint.Y)
            yukiCurrentPoint.X += yukiImage.Width
        Until yukiCurrentPoint.X > PreviewDrawingArea.Width
        yukiCurrentPoint.Y += yukiImage.Height
    Until yukiCurrentPoint.Y > PreviewDrawingArea.Height
    
End

Private Sub paintFill()
    
    Dim yukiImage As Image = Image.Load($currentPath).Stretch(PreviewDrawingArea.W, PreviewDrawingArea.H)
    
    Paint.DrawImage(yukiImage, 0, 0)
    
End

Private Function getOriginPoint(argImage As Image) As PointF
    
    Dim yukiPoint As New PointF

    With PreviewDrawingArea
        yukiPoint.X = IIf(argImage.W = .W, 0, (.W - argImage.W) / 2)
        yukiPoint.Y = IIf(argImage.H = .W, 0, (.H - argImage.H) / 2)
    End With
    
    Return yukiPoint
    
End

Private Sub paintFull()
    
    Dim yukiImage As Image = getThumbnailForFillMode()
    Dim yukiPoint As PointF = getOriginPoint(yukiImage)
    
    Paint.DrawImage(yukiImage, yukiPoint.X, yukiPoint.Y)
    
End

Public Sub Form_Open()

    Me.Center()
    $currentPath = NagatoSettings.LastPath
    $currentColor = NagatoSettings.LastColor
    ColorButton1.Color = $currentColor
    initializePreviewDrawingArea()
    ModeComboBox.Index = MikuruRenderMode.Center

End

Public Sub ApplyButton_Click()

    If $currentPath = "" Then
        Return
    Else
        NagatoHSetRoot.SetWallPaper($currentPath, $currentColor, ModeComboBox.Index)
        NagatoSettings.LastPath = $currentPath
    End If

End

Public Sub SelectButton_Click()

    If NagatoSettings.LastPath <> "" Then Dialog.Path = File.Dir(NagatoSettings.LastPath)

    If Not Dialog.OpenFile() Then
        $currentPath = Dialog.Path
        PreviewDrawingArea.Refresh()
    Endif

End

Public Sub PreviewDrawingArea_Draw()
    
    If $currentPath = "" Then Return
    
    With Paint
        .Begin(PreviewDrawingArea)
        Select Case ModeComboBox.Index
            Case MikuruRenderMode.Tile
                paintTile()
            Case MikuruRenderMode.Full
                paintFull
            Case MikuruRenderMode.Fill
                paintFill()
            Default
                paintCenter()
        End Select
        .End()
    End With
    
End


Public Sub ColorButton1_Change()

    ColorButton1.Foreground = ColorButton1.Color
    PreviewDrawingArea.Background = ColorButton1.Color
    $currentColor = ColorButton1.Color
    NagatoSettings.LastColor = ColorButton1.Color

End

Public Sub ModeComboBox_Click()

    PreviewDrawingArea.Refresh()

End
