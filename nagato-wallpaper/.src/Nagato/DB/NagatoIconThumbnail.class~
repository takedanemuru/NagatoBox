' Gambas class file

Inherits NagatoIcon

Public _$formHeight As Integer
Public _$icon As Image
Public _$path As String
Public _$mime As String

Public _$textArea As New Rect
Public _$text As String
Public _$textHeight As Integer

Public Sub _SetIcon() 'virtual
    
    
    
End

Public Sub _SetForm() 'virtual
    
    
    
End

Public Function _GetText() As String 'virtual, MUST be overridden
    
    
    
End

Public Sub _SetTextRect()
    
    With Me._$textArea
        .X = Me.Margin
        .Y = Max(128 - (Me._$textHeight + 1 * Me.Margin), 0)
        .W = Me.FormWidth - 2 * Me.Margin 
        .H = Me._$textHeight 
    End With
    
End

Public Sub _DrawMaskArea(argColor As Integer)
    
    Dim yukiOpacity As Integer = IIf(argColor = NagatoSettings.BackgroundHighlight, 64, 192)
    
    Paint.Brush = Paint.Color(argColor + 16777216 * yukiOpacity)
    Paint.Rectangle(0, Me._$formHeight - (Me._$textHeight + 2 * Me.Margin), Me.FormWidth, Me._$textHeight + 2 * Me.Margin)
    Paint.Fill()
    
End

Public Sub _SetFormPicture(argColor As Integer) 'virtual, MUST be overridden
    
    Debug argColor
    
End

Public Sub _OpenFile()
    
    Dim yukiDesktopFile As DesktopFile
    
    If DesktopFile.FromMime(Me._$mime).Count = 0 Then
        Message.Warning(("YUKI.N > Sorry, but I couldn't find any application to open this file."), ("OK"))
    Else
        yukiDesktopFile = DesktopFile.FromMime(Me._$mime)[0]
        yukiDesktopFile.Run(Me._$path)
    End If
    
End

Public Sub _OnMouseEnter()
    
    Me._SetFormPicture(NagatoSettings.BackgroundHighlight)
    
End

Public Sub _OnMouseLeave()
    
    Me._SetFormPicture(NagatoSettings.BackgroundFile)
    
End

Public Sub _new(argName As String)
    
    Me._$path = MikuruPath.Desktop &/ argName
    Me._$mime = Desktopmime.FromFile(Me._$path).Type
    Me._$text = Me._GetText()
    Me._$form = New Form As "Icon"
    Me._$form.Font.Bold = True
    Me._SetIcon()
    Me._$textHeight = Me._$form.Font.RichTextHeight(Me._$text, Me.TextWidth)
    Me._SetTextRect()
    Me._SetForm()
    Me._SetFormPicture(NagatoSettings.BackgroundFile)
    Me._$form.Show()
    
End

Public Sub Icon_DblClick()
    
    Me._OpenFile()
    
End

Public Sub StarMenu_MenuEvent(argEvent As Integer)
    
    Select Case argEvent
        Case NagatoMenuPipe.FileOpen
            Me._OpenFile()
        Case NagatoMenuPipe.FileRename
            HaruhiRename.SetPath(Me._$path, Me._$icon.Picture)
            HaruhiRename.ShowDialog()
    End Select
    
End
