' Gambas class file

Create Static

Property LastPath As String
Property Read RecentPath As String[]
Property LastColor As Integer
Property LastMode As Integer

Private Const HistoryMax As Integer = 30

Private $settings As New Settings

Private Sub overwriteRecentPaths(argPath As String)
    
    Dim yukiRecents As String[] = Me.RecentPath.Copy()
    Dim yukiTargetIndex As Integer = yukiRecents.Find(argPath) ' .Find(value) returns -1 if value not found.
    
    If yukiTargetIndex > -1 Then yukiRecents.Remove(yukiTargetIndex)
    yukiRecents.Add(argPath, 0)
    yukiRecents.Resize(HistoryMax)
    
    $settings["background/recent"] = yukiRecents.Copy()
    $settings.Save()
    
End

Private Function getPerDesktopKey(argKey As String) As String
    
    Return argKey & "_" & MikuruHidden.DesktopType
    
End

Public Sub Refresh(argWallpaperMode As Integer)
    
    With Me
        .LastPath = MikuruHidden.PathOriginalImage
        .LastColor = NagatoPreview.Color
        .LastMode = argWallpaperMode
    End With
    
End

Public Sub PushToRecentPaths(argPath As String)
    
    overwriteRecentPaths(argPath)
    
End

Private Function LastPath_Read() As String

    Return $settings[getPerDesktopKey("background/last_path"), MikuruHidden.PathOriginalImage]

End

Private Sub LastPath_Write(Value As String)

    $settings[getPerDesktopKey("background/last_path")] = Value
    $settings.Save()

End

Private Function LastColor_Read() As Integer

    Return $settings[getPerDesktopKey("background/last_color"), Color.White]

End

Private Sub LastColor_Write(Value As Integer)

    $settings[getPerDesktopKey("background/last_color")] = Value
    $settings.Save()

End

Private Function LastMode_Read() As Integer

    Return $settings[getPerDesktopKey("background/last_mode"), MikuruRenderMode.Center]

End

Private Sub LastMode_Write(Value As Integer)

    $settings[getPerDesktopKey("background/last_mode")] = Value
    $settings.Save()

End

Private Function RecentPath_Read() As String[]

    Return $settings["background/recent", [""]]

End
