' Gambas class file

Private $drawingArea As DrawingArea

Private Sub setBackgroundColor()
    
    With Paint
        .Brush = .Color(NagatoPreviewProperties.Color)
        .Rectangle(0, 0, $drawingArea.W, $drawingArea.H)
        .Fill()
    End With
    
End

Public Sub _new(argContainer As Container)
    
    $drawingArea = New DrawingArea(argContainer) As "Asakura"
    $drawingArea.Expand = True
    $drawingArea.Refresh()
    
End

Public Sub Refresh(Optional argBackgroundColor As Integer)
    
    If argBackgroundColor Then $drawingArea.Background = argBackgroundColor
    
    $drawingArea.Refresh()
    
End

Public Sub GetProperty(argPropertyName As String) As Variant
    
    With $drawingArea
        Select Case argPropertyName
            Case "PreviewH"
                Return .H
            Case "PreviewW"
                Return .W
            Case "PreviewRatio"
                Return .W / Screen.Width
        End Select
    End With
    
End

Public Sub Asakura_Drop()

    ' PreviewDrawingArea can accept only "text/uri-list"

    If Drag.Type = Drag.Text And If MikuruDrag.CanAccept(Drag.Data) Then
        NagatoPreviewProperties.Path = MikuruDrag.GetDragPath(Drag.Data)
        $drawingArea.Refresh()
    End If

End

Public Sub Asakura_Draw()
    
    If Not NagatoPreviewProperties.IsValid Then Return
    
    Paint.Begin($drawingArea)
        setBackgroundColor()
        NagatoThumbnail.Set(NagatoPreviewProperties.Mode)
    Paint.End()
    
End
