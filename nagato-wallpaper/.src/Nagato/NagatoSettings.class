' Gambas class file

Create Static

Property LastPath As String
Property Read RecentPath As String[]
Property LastColor As Integer
Property LastMode As Integer
Property LastCalendarPosition As Integer

Private $settings As New Settings

Public Sub Refresh(argWallpaperMode As Integer, argCalendarMode As Integer)
    
    With Me
        .LastPath = NagatoPreview.Path
        .LastColor = NagatoPreview.Color
        .LastMode = argWallpaperMode
        .LastCalendarPosition = argCalendarMode
    End With
    
End

Public Sub PushToRecentPaths(argPath As String)
    
    Dim yukiPaths As String[] = $settings["background/recent", [""]]
    
    Try yukiPaths.Remove(yukiPaths.Find(argPath), 1)
    yukiPaths.Add(argPath, 0)
    yukiPaths.Resize(20)
    
    $settings["background/recent"] = yukiPaths
    $settings.Save()
    
End

Private Function LastPath_Read() As String

    Return $settings["background/last_path", ""]

End

Private Sub LastPath_Write(Value As String)

    $settings["background/last_path"] = Value
    $settings.Save()

End

Private Function LastColor_Read() As Integer

    Return $settings["background/last_color", Color.White]

End

Private Sub LastColor_Write(Value As Integer)

    $settings["background/last_color"] = Value
    $settings.Save()

End

Private Function LastMode_Read() As Integer

    Return $settings["background/last_mode", MikuruRenderMode.Center]

End

Private Sub LastMode_Write(Value As Integer)

    $settings["background/last_mode"] = Value
    $settings.Save()

End

Private Function RecentPath_Read() As String[]

    Dim yukiPaths As String[] = $settings["background/recent", [""]]

    Return yukiPaths

End

Private Function LastCalendarPosition_Read() As Integer

    Return $settings["calendar/last_position", MikuruCalendar.None]

End

Private Sub LastCalendarPosition_Write(Value As Integer)

    $settings["calendar/last_position"] = Value
    $settings.Save()

End
