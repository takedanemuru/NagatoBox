' Gambas class file

Inherits NagatoCalendar

Private $boxSize As Integer
Private $position As Integer
Private $topOffset As Integer

Private Sub paintBox(argX As Integer, argY As Integer, argColor As Integer)
    
    Paint.Brush = Paint.Color(argColor)
        Paint.Rectangle(argX, argY, $boxSize - 2, $boxSize - 2)
    Paint.Fill()
    
End

Private Sub paintText(argX As Integer, argY As Integer, argText As String)
    
    Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.Font.Size = $boxSize * 0.5
        Paint.RichText(argText, argX, argY, $boxSize - 2, $boxSize - 2, Align.Center)
    Paint.Fill()
    
End

Private Sub paintHeader(argCount As Integer)
    
    Dim yukiWeekdayIndex As Integer
    Dim yukiWeekdayText As String
    Dim yukiBackground As Integer

    yukiWeekdayIndex = ((MikuruDate.FirstWeekDay + argCount) Mod 7)
    yukiWeekdayText = MikuruDate.GetWeekDaySingleText(yukiWeekdayIndex)
    yukiBackground = IIf(yukiWeekdayIndex = 0, MikuruColor.Sunday, MikuruColor.BoxHeader)
    paintBox(Me.Padding + argCount * $boxSize, $topOffset, yukiBackground)
    paintText(Me.Padding + argCount * $boxSize, $topOffset, yukiWeekdayText)
    
End

Private Function getBodyBackground(argCount As Integer) As Integer
    
    With MikuruColor
        If argCount + 1 = Day(Now()) Then
            Return .BoxHighlight
        Else
            Return IIf(((MikuruDate.FirstWeekDay + argCount) Mod 7) = 0, .SundayBox, .BoxVoid)
        End If
    End With
    
End

Private Sub paintBody(argCount As Integer)
    
    Dim yukiDateText As String
    
    paintBox(Me.Padding + argCount * $boxSize, $topOffset + $boxSize, getBodyBackground(argCount))
    yukiDateText = IIf(argCount >= MikuruDate.LastDayOfMonth, "", CStr(argCount + 1))
    paintText(Me.Padding + argCount * $boxSize, $topOffset + $boxSize, yukiDateText)
    
End

Private Sub paintCalendar()
    
    Dim yukiCount As Integer
    
    Paint.Begin(Me._$baseImage)
        For yukiCount = 0 To 30
            paintHeader(yukiCount)
            paintBody(yukiCount)
        Next
    Paint.End()
    
End

Private Sub setPosition()
    
    If $position = 1 Then
        $topOffset = Me.Padding
    Else ' $position =2
        $topOffset = Desktop.H - $boxSize * 2
    Endif
    
End

Public Sub Paint(argBaseImage As Image, argPosition As Integer)
    
    $position = argPosition
    setPosition()
    $boxSize = CInt((Desktop.W - Me.Padding * 2) / 31)
    Me._$baseImage = argBaseImage
    
    paintCalendar()
    
End
