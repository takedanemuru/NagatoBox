' Gambas class file

Private Sub initializeButton()
        
    Select Case NagatoSettings.Mode
        Case 0
            ModeButton0.Value = True
        Case 1
            ModeButton1.Value = True
        Case 2
            ModeButton2.Value = True
    End Select

End

Private Sub refreshLabel()
        
        Dim yukiMinute As String = Format$(NagatoSettings.Duration Div 60, "0")
        Dim yukiSecond As String = Format$(NagatoSettings.Duration Mod 60, "0#")
        
        DurationTextLabel.Text = Subst$("&1:&2", yukiMinute, yukiSecond)
        
End

Private Sub initializeSlider()
    
    Slider1.Value = NagatoSettings.Duration
    
End

Private Sub addLibrary()
    
    Dim yukiPath As String
    
    For Each yukiPath In NagatoSettings.UserLibrary
        If Not Exist(yukiPath) Then Continue
        SoundListView.Add(yukiPath, File.BaseName(yukiPath))
    Next
    
End

Private Sub refreshListView()
        
    With SoundListView
        .Clear()
        .Add("sounds/welcumz.wav", "welcomz")
        .Add("sounds/choir.wav", "choir")
        .Add("sounds/ubuntu.wav", "ubuntu")
    End With

    If NagatoSettings.UserLibrary Then addLibrary()

    setApplyIconToListView()
        
End

Private Sub setApplyIconToListView()
        
        If SoundListView.Exist(NagatoSettings.UserDefinedPath) Then
                SoundListView[NagatoSettings.UserDefinedPath].Picture = Stock["large/apply"]
                SoundListView[NagatoSettings.UserDefinedPath].Selected = True
        Endif
        
End

Public Sub Form_Open()
        
        Application.MainWindow = Me
        
        refreshListView()
        initializeButton()
        refreshLabel()
        initializeSlider()
        
End

Public Sub PlayButton_Click()

    If Object.IsValid(SoundListView.Current) Then MikuruSound.Play(SoundListView.Key)
    
End

Public Sub Form_Resize()
        
        PictureBox1.W = Me.W
        TabStrip1.W = Me.W
        TabStrip1.Height = Me.H - 90
        
End


Public Sub ModeButton_Click()

        NagatoSettings.Mode = Last.Tag

End

Public Sub Slider1_Change()

        NagatoSettings.Duration = Slider1.Value
        refreshLabel()

End

Public Sub SoundListView_Click()
        
        NagatoSettings.UserDefinedPath = SoundListView.Key
        refreshListView()
        
End

Public Sub AddButton_Click()

    Dim yukiMime As String

    Dialog.Title = ("Select sound file")
    Dialog.Filter = ["*.wav", "wav files"]

    If Not Dialog.OpenFile(False) Then
        yukiMime = DesktopMime.FromFile(Dialog.Path).Type 
        If yukiMime = "audio/wav" Or If yukiMime = "audio/x-wav" Then
            NagatoSettings.PushLibrary(Dialog.Path)
            refreshListView()
        Endif
    Endif

End

Public Sub DeleteButton_Click()

    NagatoSettings.DeleteLibrary(SoundListView.Key)
    refreshListView()

End
