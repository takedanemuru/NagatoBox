' Gambas class file

Private $gridView As GridView
Private $observer As Observer

Private Sub setFonts()
    
    Dim yukiFontName As String
    
    For Each yukiFontName In Fonts
        Inc $gridView.Rows.Count
        $gridView[$gridView.Rows.Max, 0].Text = yukiFontName
        $gridView[$gridView.Rows.Max, 0].Font.Name = yukiFontName
        $gridView[$gridView.Rows.Max, 1].Text = NagatoSettings.Text
        $gridView[$gridView.Rows.Max, 1].Font.Name = yukiFontName
        $gridView[$gridView.Rows.Max, 1].Font.Bold = NagatoSettings.Bold
        $gridView[$gridView.Rows.Max, 1].Font.Italic = NagatoSettings.Italic
    Next
    
End

Private Sub initializeFontGridView()
    
    With $gridView
        .Font.Size = 12
        .Expand = True
        .Header = GridView.Horizontal
        .Columns.Count = 2
        .Columns[0].W = Application.MainWindow.W
        .Columns[0].Text = "Font"
        .Columns[1].Text = "Preview"
        .Rows.H = .Font.Size * 2
        .Mode = Select.Single
    End With
    
    setFonts()
    
End

Private Sub refresh()
    
    Dim yukiRow As Integer
    
    For yukiRow = 0 To $gridView.Rows.Max
        $gridView[yukiRow, 1].Text = NagatoSettings.Text
        $gridView[yukiRow, 1].Font.Bold = NagatoSettings.Bold
        $gridView[yukiRow, 1].Font.Italic = NagatoSettings.Italic
    Next
    
End

Public Sub _new(argContainer As Container)
    
    $gridView = New GridView(argContainer) As "GridView"
    initializeFontGridView()
    
    $observer = New Observer(NagatoSettings) As "Asakura"
    
End

Public Sub Asakura_Change()
    
    refresh()
    
End
