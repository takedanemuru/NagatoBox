' Gambas module file

Private $id As Integer
Private $name As String

Private Function isUserProcess(argId As Integer) As Boolean
    
    Return Access(Subst$("/proc/&1/environ", argId), gb.Read)
    
End

Private Function getKillMessage() As String
    
    Return Subst$(("Are you sure to kill &1(Process ID: &2) ?"), $name, $id)
    
End

Private Sub killProcess()
    
    If Message.Warning(getKillMessage(), ("Cancel"), ("OK")) = 2 Then
        Try Shell Subst$("kill &1", $id)
    Endif
    
End

Private Sub killRootProcess()
    
    Dim yukiWarning1 As String = ("WARNING : THIS IS NOT A USER PROCESS.\n") 
    Dim yukiWarning2 As String = ("You need root privileges to kill this process.\n") 
    
    If Message.Warning(yukiWarning1 & yukiWarning2, ("Cancel"), ("OK")) = 1 Then Return
    
    Try Shell Subst$("pkexec kill &1", $id)
    
End

Public Sub _call(argId As Integer, argName As String)
    
    If argName = "" Then Return
    
    $id = argId
    $name = argName
    
    If isUserProcess(argId) Then
        killProcess()
    Else
        killRootProcess()
    End If
    
End
