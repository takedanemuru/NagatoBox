' Gambas module file

Private $sortAscend As Boolean = True
Private $targetHeader As Integer = 0
Private $headers As String[] = ["id", "status", "name", "cpu", "rss", "virtual"]
            
Private Function isLesser(argGridData As Variant, argRowData As Variant) As Boolean
    
    If $sortAscend Then
        Return (argGridData > argRowData)
    Else
        Return (argRowData > argGridData)
    Endif
    
End

Private Sub setGridData(argRowData As Collection, ByRef refGridData As Variant[])
    
    Dim yukiCount As Integer
    
    For yukiCount = 0 To refGridData.Max
        If isLesser(refGridData[yukiCount][$headers[$targetHeader]], argRowData[$headers[$targetHeader]]) Then
            refGridData.Add(argRowData, yukiCount)
            Return
        Endif
    Next
    
    refGridData.Add(argRowData)
    
End

Public Function GetData() As Variant[]
    
    Dim yukiDirectory As String
    Dim yukiGridData As New Variant[]
    
    For Each yukiDirectory In Dir("/proc", "*[0-9]", gb.Directory)
        If Not Exist("/proc" &/ yukiDirectory &/ "stat") Then Continue
        setGridData(MikuruProcStatus[yukiDirectory], ByRef yukiGridData)
    Next
    
    Return yukiGridData
    
End

Public Sub SwitchSort(argColumnIndex As Integer)
    
    If $targetHeader = argColumnIndex Then
        $sortAscend = Not $sortAscend
    Else
        $targetHeader = argColumnIndex
    Endif
    
End
