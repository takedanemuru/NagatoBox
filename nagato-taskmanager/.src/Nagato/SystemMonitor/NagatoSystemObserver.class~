' Gambas class file

Create Static

Property Read CpuUsage As Float
Property Read MemoryUsage As Float
Property Read SwapUsage As Float
Property Read ProcessCount As Integer
Property Read Activated As Boolean
Property Read SystemLoad As Float

Private Const SystemSnapShotInterval As Integer = 2000 ' milliseconds

Private $timer As Timer
Private $observationStep As Integer
Private $cpuUsage As Float
Private $memoryUsage As Float
Private $swapUsage As Float
Private $processCount As Integer

Event Finished

Private Sub setProperty()
    
    $cpuUsage = NagatoCpuObserver.getCpuRate()
    $memoryUsage = NagatoMemoryObserver.GetMemoryRate()
    $swapUsage = NagatoSwapObserver.GetSwapPercentage()
    $processCount = NagatoProcessObserver.GetProcessCount()

End

Public Sub Refresh()
    
    $timer.Start()
    
End

Public Sub Stop()
    
    $timer.Stop()
    
End

Public Sub _new()
    
    $timer = New Timer As "Timer"
    $timer.Delay = SystemSnapShotInterval
    $timer.Start()
    
End

Public Sub Timer_Timer()
    
    Select Case $observationStep
        Case 0
            NagatoCpuObserver.setPrevData()
            Inc $observationStep
        Case 1
            NagatoCpuObserver.setCurrentData()
            NagatoMemoryObserver.SetVariables()
            setProperty()
            Raise Finished
    End Select
    
End

Private Function CpuUsage_Read() As Float

    Return $cpuUsage

End

Private Function MemoryUsage_Read() As Float

    Return $memoryUsage

End

Private Function SwapUsage_Read() As Float

    Return $swapUsage

End

Private Function Activated_Read() As Boolean

    Return $timer.Enabled

End

Private Function ProcessCount_Read() As Integer

    Return $processCount

End

Private Function SystemLoad_Read() As Float

    Return Max($cpuUsage, $memoryUsage)

End
