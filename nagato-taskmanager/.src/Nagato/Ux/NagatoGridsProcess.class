' Gambas class file

Inherits NagatoGrids

Private $columnTitles As NagatoSystemGridsColumnTitles
Private $mouseHandler As NagatoGridViewHandlerMouse
Private $data As NagatoSystemGridsData

Private Sub setRowData(argProcessData As NagatoProcessData)
    
    With Me._$gridView
        Inc .Rows.Count
        Me._SetText(0, argProcessData.Id)
        Me._SetText(1, argProcessData.Status)
        Me._SetText(2, argProcessData.Name)
        Me._SetText(3, argProcessData.Cpu, True)
        Me._SetText(4, argProcessData.Rss, True)
        Me._SetText(5, argProcessData.Virtual, True)
        If Odd(.Rows.Max) Then Me._SetBackGroundColor()
        If MikuruProcessTemporaryVariables.IsSelectedProcessId(.[.Rows.Max, 0].Text) Then
            .Select(.Rows.Max, 1)
        End If
    End With
    
End

Private Sub setRows()
    
    Dim yukiIndex As Integer
    
    For yukiIndex = 0 To $data.Max
        If MikuruProcessTemporaryVariables.IsFiltered($data[yukiIndex].Name) Then Continue
        setRowData($data[yukiIndex])
    Next
    
End

Private Sub setGridView()
    
    $data.Refresh()
    
    Me._Clear()
        setRows()
    Me._Restore()
    
End

Public Sub _InitializeGridView()
    
    Me._$gridView.Rows.Count = 0
    $columnTitles = New NagatoSystemGridsColumnTitles
    Me._InitializeColumnTitles($columnTitles.All)
    Me._$gridView.ScrollBar = Scroll.Both
    Me._$gridView.Mode = Select.Single
    NagatoSettings.LoadGridsSettings(Me._$gridView)
    $mouseHandler = New NagatoGridViewHandlerMouse(Me._$gridView) As "TFEI"
    $data = New NagatoSystemGridsData
    
End

Public Sub Refresh()
    
    If Not $mouseHandler.Lock Then setGridView()
    If Me._$gridView.Rows.Count = 0 Then setGridView()
    
End

Public Sub TFEI_ColumnClick(argColumnIndex As Integer)
    
    $data.SwitchSort(argColumnIndex)
    setGridView()
    
End
