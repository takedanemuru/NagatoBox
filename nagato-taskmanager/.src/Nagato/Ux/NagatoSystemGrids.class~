' Gambas class file

Inherits NagatoGrids

Property Read LastSelectedId As String
Property Read LastSelectedName As String

Private $columnTitles As NagatoSystemGridsColumnTitles
Private $mouseHandler As NagatoSystemGridsMouseHandler
Private $data As NagatoSystemGridsData

Private Sub setRowData(argProcessData As NagatoProcessData)
    
    With Me._$gridView
        Inc .Rows.Count
        Me._SetText(0, argProcessData.Id)
        Me._SetText(1, argProcessData.Status)
        Me._SetText(2, argProcessData.Name)
        Me._SetText(3, argProcessData.Cpu, True)
        Me._SetText(4, argProcessData.Rss, True)
        Me._SetText(5, argProcessData.Virtual, True)
        If Odd(.Rows.Max) Then Me._SetBackGroundColor()
        If .[.Rows.Max, 0].Text = $mouseHandler.LastSelectedId Then .Select(.Rows.Max, 1)
    End With
    
End

Private Sub setGridView()
    
    Dim yukiIndex As Integer
    
    $data.Refresh()
    
    Me._Clear()
        For yukiIndex = 0 To $data.Max
            setRowData($data[yukiIndex])
        Next
    Me._Restore()
    
End

Public Sub _InitializeGridView()
    
    Me._$gridView.Rows.Count = 0
    $columnTitles = New NagatoSystemGridsColumnTitles
    Me._InitializeColumnTitles($columnTitles.All)
    Me._$gridView.ScrollBar = Scroll.Both
    NagatoSettings.LoadGridsSettings(Me._$gridView)
    
    $mouseHandler = New NagatoSystemGridsMouseHandler(Me._$gridView) As "TFEI"
    $data = New NagatoSystemGridsData
    
End

Public Sub Refresh()
    
    If Not $mouseHandler.Lock Then setGridView()
    If Me._$gridView.Rows.Count = 0 Then setGridView()
    
End

Public Sub Unselect()
    
    $mouseHandler.Unselect()
    
End

Public Sub TFEI_ColumnClick(argColumnIndex As Integer)
    
    $data.SwitchSort(argColumnIndex)
    setGridView()
    
End

Private Function LastSelectedId_Read() As String

    Return $mouseHandler.LastSelectedId

End

Private Function LastSelectedName_Read() As String

    Return $mouseHandler.LastSelectedName

End
