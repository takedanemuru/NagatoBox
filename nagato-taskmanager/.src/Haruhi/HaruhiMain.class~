' Gambas class file

Property Read IsSelected As Boolean

Private $timer As New Timer As "Timer"
Private $starButton As NagatoStarButton
Private $systemGraph As NagatoSystemGraph
Private $systemLabel As NagatoSystemLabel
Private $systemGrids As NagatoSystemGrids

Private $refreshLock As Boolean = False

Public Sub Form_Open()
    
    Application.MainWindow = Me
    NagatoSettings.LoadFormSettings(Me)
    
    NagatoDBus.Register()
    NagatoSystemObserver.Refresh()
    
    $systemGrids = New NagatoSystemGrids(SystemMonitorGrid) As "SystemGrids"
    $starButton = New NagatoStarButton(HBox1) As "StarButton"
    $systemGraph = New NagatoSystemGraph(GraphArea)
    $systemLabel = New NagatoSystemLabel(SystemLabel)
    
    $timer.Start()
    
End

Public Sub Form_Close()
    
    NagatoDBus.Unregster()
    NagatoSettings.SaveFormSettings(Me)
    
End

Public Sub Timer_Timer()
    
    $systemGraph.Refresh()
    $systemLabel.Refresh()
    If Not $refreshLock Then $systemGrids.Refresh()
    
End

Private Sub kill()
    
    If $systemGrids.LastSelected = "" Then Return
    
    If Message.Warning(Subst$(("Are you sure to kill &1 ?"), $systemGrids.LastSelected), ("Cancel"), ("OK")) = 2 Then
        Try Shell Subst$("kill &1", $systemGrids.LastSelected)
    Endif
    
End

Public Sub SystemGrids_Kill()
    
    kill()
    
End
 
Public Sub StarButton_Signal(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case NagatoStarMenu.MenuUnselect
            $systemGrids.Unselect()
        Case NagatoStarMenu.MenuKill
            kill()
        Case NagatoStarMenu.MenuAbout
            HaruhiAbout.ShowDialog()
        Case NagatoStarMenu.MenuQuit
            Me.Close()
    End Select
    
End

Private Function IsSelected_Read() As Boolean

    If $systemGrids.LastSelected = "" Then 
        Return False
    Else
        Return True
    Endif

End

Public Sub SystemMonitorGrid_Enter()

    Debug "lock"

    $refreshLock = True

End

Public Sub SystemMonitorGrid_Leave()

    Debug "unlock"

    $refreshLock = False

End
