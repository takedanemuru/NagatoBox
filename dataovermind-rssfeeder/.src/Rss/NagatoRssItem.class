' Gambas class file

Property Read Valid As Boolean
Property Read Title As String
Property Read Description As String
Property Read Link As String
Property Read ThumbnailPath As String

Private $valid As Boolean = False
Private $title As String
Private $description As String
Private $link As String
Private $thumbnailPath As String 
Private $thumbnailWidth As Integer
Private $sanitizer As New NagatoText

Private Function sanitize(argText As String) As String
    
    Return $sanitizer.Sanitize(argText, "<link>", "</link>", "<![CDATA[", "]]>", "<!--", "-->", "\n")
    
End

Public Sub _new(argXmlElement As XmlElement)
    
    Dim yukiNode As XmlNode
    
    For Each yukiNode In argXmlElement.Children
        Select Case yukiNode.Name
            Case "title"
                $title = yukiNode.Value
            Case "description"
                $description = yukiNode.Value
            Case "link"
                $link = IIf(yukiNode.Value, yukiNode.Value, sanitize(yukiNode.ToString()))
            Case "media:thumbnail"
                If CInt(yukiNode.Attributes["width"]) > $thumbnailWidth Then $thumbnailPath = yukiNode.Attributes["url"]
            Case "enclosure"
                If yukiNode.Attributes["type"] Begins "image" Then $thumbnailPath = yukiNode.Attributes["url"]
        End Select
    Next
    
    If $title <> "" And If $link <> "" Then $valid = True
    
End

Private Function Valid_Read() As Boolean

    Return $valid

End

Private Function Title_Read() As String

    Return $title

End

Private Function Description_Read() As String

    Return $description

End

Private Function Link_Read() As String

    Return $link

End

Private Function ThumbnailPath_Read() As String

    Return $thumbnailPath

End
