' Gambas class file

Inherits NagatoObject

Property Read Text As String

Private $hSplit As HSplit
Private $sourceEditor As NagatoSourceEditor
Private $previewer As NagatoPreviewer

Private Sub initializeHSplit(argContainer As Container)
    
    $hSplit = New HSplit(argContainer) As "HSplit"
    
    With $hSplit
        .Expand = True
    End With
    
End

Private Sub setSource()
    
    If MikuruArguments.Path = "" Then
        Me.RefreshPreview()
    Else
        Me.SetPath(MikuruArguments.Path)
    End If
    
End

Public Sub RefreshPreview()
    
    If $sourceEditor.Text Then $previewer.ShowPreview($sourceEditor.Text)
    
End

Public Sub SetPath(argPath As String)
    
    $sourceEditor.SetPath(argPath) 
    $previewer.ShowPreview($sourceEditor.Text)
    
End

Public Sub Export()
    
    $previewer.Export()
    
End

Public Sub SetZoom(argSignal As Integer)
    
    $previewer.SetZoom(argSignal)
    
End

Public Sub _new(argContainer As Container)
    
    initializeHSplit(argContainer)
    $sourceEditor = New NagatoSourceEditor($hSplit) As "TFEI"
    $previewer = New NagatoPreviewer($hSplit) As "TFEI"
    $hSplit.Layout = MikuruSettings.Layout
    setSource()
    
End

Public Sub HSplit_Arrange()
    
    MikuruSettings.Layout = $hSplit.Layout
    
End

Private Function Text_Read() As String

    Return $sourceEditor.Text

End
