' Gambas class file

Inherits NagatoObject

Private $graphGenerator As NagatoGraphGenerator
Private $imageView As ImageView

Private Sub zoom()
    
    Select Case MikuruSettings.ZoomType
        Case MikuruSettings.ZoomFit
            $imageView.ZoomFit()
        Case MikuruSettings.ZoomNormal
            $imageView.Zoom = 1
        Case MikuruSettings.ZoomRating
            $imageView.Zoom = MikuruSettings.ZoomRate
    End Select
    
End

Private Sub saveZoomSettings(argSignal As Integer)
    
    Select Case argSignal
        Case MikuruSignal.ZoomIn
            MikuruSettings.ZoomType = MikuruSettings.ZoomRating
            MikuruSettings.ZoomRate *= 1.2
        Case MikuruSignal.ZoomOut
            MikuruSettings.ZoomType = MikuruSettings.ZoomRating
            MikuruSettings.ZoomRate *= 0.8
        Case MikuruSignal.ZoomFit
            MikuruSettings.ZoomType = MikuruSettings.ZoomFit
        Case MikuruSignal.ZoomNormal
            MikuruSettings.ZoomType = MikuruSettings.ZoomNormal
    End Select
    
End

Public Sub ShowPreview(argSourceText As String)
    
    $graphGenerator.Refresh(argSourceText)
    $imageView.Image = $graphGenerator.Image
    zoom()
    
End

Public Sub SetZoom(argSignal As Integer)
    
    saveZoomSettings(argSignal)
    zoom()
    
End

Public Sub Export(argSourcePath As String)
    
    Dialog.Title = ("Save image")
    Dialog.Path = File.Dir(argSourcePath) &/ File.BaseName(argSourcePath) & ".png"
    
    If Dialog.SaveFile() Then Return
    
    If Exist(Dialog.Path) Then Kill Dialog.Path
    
    $imageView.Image.Save(Dialog.Path)
    
End

Public Sub _new(argContainer As Container)
    
    $imageView = New ImageView(argContainer) 
    $graphGenerator = New NagatoGraphGenerator
    
End
