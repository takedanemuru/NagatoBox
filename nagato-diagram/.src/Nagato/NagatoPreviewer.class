' Gambas class file

Inherits NagatoObject

Private $graphGenerator As NagatoGraphGenerator
Private $imageView As ImageView

Public Sub ShowPreview(argSourceText As String)
    
    $graphGenerator.Refresh(argSourceText)
    $imageView.Image = $graphGenerator.Image
    
End

Public Sub SetZoom(argSignal As Integer)
    
    Select Case argSignal
        Case MikuruSignal.ZoomIn
            $imageView.Zoom *= 1.2
        Case MikuruSignal.ZoomOut
            $imageView.Zoom *= 0.8
        Case MikuruSignal.ZoomFit
            $imageView.ZoomFit()
        Case MikuruSignal.ZoomNormal
            $imageView.Zoom = 1
    End Select
    
End

Public Sub Export()
    
    Dialog.Title = ("Save image")
    Dialog.Path = Desktop.GetDirectory("DOCUMENTS") &/ ("your_graph.png")
    
    If Dialog.SaveFile() Then Return
    
    $imageView.Image.Save(Dialog.Path)
    
End

Public Sub _new(argContainer As Container)
    
    $imageView = New ImageView(argContainer) 
    $graphGenerator = New NagatoGraphGenerator
    
End
