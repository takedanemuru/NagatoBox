' Gambas class file

Inherits NagatoObject

Private $currentPath As String
Private $saved As Boolean = True

Private Sub setNewPath(argPath As String)
    
    $currentPath = argPath
    $saved = True
    MikuruSettings.SaveRecntPath(argPath)
    
End

Public Sub SourceChanged()
    
    $saved = False
    
End

Public Sub Open()
    
    Dialog.Title = ("Select file")
    Dialog.Path = Desktop.GetDirectory("DOCUMENTS") 
    Dialog.Filter = ["*.gv;*.dot", "dot file"]
    
    If Dialog.OpenFile(False) Then Return
    
    Raise Signal(MikuruSignal.SourceNew, [Dialog.Path])
    
    setNewPath(Dialog.Path)
    
End

Public Sub Save(argSource As String)
    
    If $currentPath = "" Then
        Me.SaveAs(argSource)
    Else
        File.Save($currentPath, argSource)
        $saved = True
    Endif
    
End

Public Sub SaveAs(argSource As String)
    
    Dialog.Title = ("Save As...")
    Dialog.Path = Desktop.GetDirectory("DOCUMENTS") &/ ("your_file.gv")
    
    If Dialog.SaveFile() Then Return

    File.Save(Dialog.Path, argSource)
    
    setNewPath(Dialog.Path)
    
End

Public Function IsClosable(argSource As String) As Boolean
    
    If $saved Then Return True
    
    Select Case Message.Warning(("YUKI.N > your changes not saved."), ("Cancel"), ("Save"), ("Discard changes"))
        Case 1 'Cancel
            Return False
        Case 2 'Save
            Me.Save(argSource)
            Return $saved
        Case 3 'Discard
            Return True
    End Select
    
End

Public Sub IsOverridable(argSource As String) As Boolean
    
    Dim yukiIsOverridable As Boolean = Me.IsClosable(argSource)
    
    If yukiIsOverridable Then $currentPath = ""
    
    Return yukiIsOverridable
    
End

Public Sub _new()
    
    setNewPath(MikuruArguments.Path)
    
End
