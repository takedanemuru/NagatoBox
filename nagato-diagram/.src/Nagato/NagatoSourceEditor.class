' Gambas class file

Inherits NagatoWidget

Property Read Text As String

Private $editor As TextEditor

Private Sub initializeHighlightProperties()
    
    With $editor.Styles
        .[Highlight.Background].Color = 16247
        .[Highlight.String].Color = Color.Orange
        .[Highlight.Number].Color = Color.Red
        .[Highlight.Keyword].Color = Color.Red
        .[Highlight.Normal].Color = Color.White
        .[Highlight.Symbol].Color = Color.White
        .[Highlight.Function].Color = Color.White
        .[Highlight.Operator].Color = Color.Yellow
        .[Highlight.Comment].Color = Color.Green
    End With
    
End

Private Sub initializeEditorProperties()
    
    With $editor
        .Highlight = "c"
        .TabSize = 4
        .Font.Size = 12
        .Font.Bold = True
        .Text = File.Load("Text/Template_1")
        .ShowLineNumber = True
        .LineNumberOffset = True
        .ShowSpaces = True
        .ShowModified = True
    End With
    
End

Private Sub pressWithControlAndShiftKey(argKeyCode As Integer)
    
    If argKeyCode = Key["s"] Then Raise Signal(MikuruSignal.FileSaveAs, Null)
    
End

Private Sub pressWithControlKey(argKeyCode As Integer)
    
    If argKeyCode = Key["s"] Then Raise Signal(MikuruSignal.FileSave, Null)
    
End

Public Sub _OnInitializeWidget()
    
    $editor = New TextEditor(Me._$container) As "TextEditor"
    
    initializeEditorProperties()
    initializeHighlightProperties()
    
End

Public Sub SetPath(argPath As String)
    
    Try $editor.Text = File.Load(argPath)
    
End

Private Function Text_Read() As String

    Return $editor.Text

End

Public Sub TextEditor_KeyPress()
    
    If Key.Control And If Key.Shift Then 
        pressWithControlAndShiftKey(Key.Code)
    Else If Key.Control Then
        pressWithControlKey(Key.Code)
    Endif
    
End

Public Sub TextEditor_Change()
    
    Raise Signal(MikuruSignal.SourceChanged, Null)
    
End

