' Gambas class file

Private $grids As GridView

Private Sub initializeGridView()
    
    With $grids
        .Header = GridView.Horizontal
        .Resizable = True
        .Expand = True
        .Columns.Count = 4
        .Columns[0].Text = ("Type")
        .Columns[0].Width = 100
        .Columns[0].Alignment = Align.Left
        .Columns[1].Text = ("Name")
        .Columns[1].Width = 250
        .Columns[1].Alignment = Align.Left
        .Columns[2].Text = ("Lines")
        .Columns[2].Alignment = Align.Left
        .Columns[2].Width = 150
        .Columns[3].Text = ("Lines exclude void")
        .Columns[3].Alignment = Align.Left
        .Columns[3].Width = 150
        .Rows.Count = 0
    End With
    
End

Private Sub setBackgroundColor()
    
    Dim yukiCount As Integer
    
    For yukiCount = 0 To 3
        $grids[$grids.Rows.Max, yukiCount].Background = Color.Pink
    Next
    
End

Public Sub Fill(argGridData As Variant[])
    
    Dim yukiCount As Integer
    
    For yukiCount = 0 To argGridData.Max
        Inc $grids.Rows.Count
        $grids[$grids.Rows.Max, 0].Text = argGridData[yukiCount]["type"]
        $grids[$grids.Rows.Max, 1].Text = argGridData[yukiCount]["name"]
        $grids[$grids.Rows.Max, 2].Text = Format$(argGridData[yukiCount]["lines"], "#,###,###,###")
        $grids[$grids.Rows.Max, 3].Text = Format$(argGridData[yukiCount]["withoutvoid"], "#,###,###,###")
        $grids[$grids.Rows.Max, 2].Alignment = Align.Right
        $grids[$grids.Rows.Max, 3].Alignment = Align.Right
        If argGridData[yukiCount]["lines"] > 100 Then setBackgroundColor()
    Next
    
End

Public Sub SetSumRow(argLines As Integer, argLinesWithoutVoid As Integer)
    
    Inc $grids.Rows.Count
    
    With $grids
        .[.Rows.Max, 1].Text = ("TOTAL")
        .[.Rows.Max, 2].Text = Format$(argLines, "#,###,###")
        .[.Rows.Max, 2].Alignment = Align.Right
        .[.Rows.Max, 3].Text = Format$(argLinesWithoutVoid, "#,###,###")
        .[.Rows.Max, 3].Alignment = Align.Right
    End With
    
End

Public Sub Clear()
    
    $grids.Clear()
    $grids.Rows.Count = 0
    
End

Public Sub _new(argGrids As GridView)
    
    $grids = argGrids
    
    initializeGridView()
    
End
