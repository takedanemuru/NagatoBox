' Gambas class file

Property Read Count As Integer
Property Read Data As Variant[]

Private $sort As New NagatoSort
Private $targetColumn As Integer 
Private $gridData As New Variant[]
Private $linesOfCode As Integer
Private $linesOfCodeWithoutVoid As Integer

Private Function ensureFirstRow(argRowData As Collection) As Boolean
    
    If $gridData.Count = 0 Or If $targetColumn = -1 Then
       $gridData.Add(argRowData)
       Return True
    Else
        Return False
    Endif
    
End

Private Function getGridData(argCount As Integer) As Variant
    
    Return $gridData[argCount][MikuruColumn[$targetColumn]]
    
End

Private Function getRowData(argRowData As Collection) As Variant
    
    Return argRowData[MikuruColumn[$targetColumn]]
    
End

Private Function setRowData(argRowData As Collection) As Boolean
    
    Dim yukiCount As Integer
    
    For yukiCount = 0 To $gridData.Max
        If $sort.IsLesser(getGridData(yukiCount), getRowData(argRowData)) Then
            $gridData.Add(argRowData, yukiCount)
            Return True
        Endif
    Next
    
    Return False
    
End

Public Sub AddToGridData(argType As String, argPath As String)
    
    Dim yukiRowData As New NagatoDataRow(argType, argPath)
    
    $linesOfCode += yukiRowData.Collection[MikuruColumn.KeyLines]
    $linesOfCodeWithoutVoid += yukiRowData.Collection[MikuruColumn.KeyLinesWithoutVoid]
    
    If ensureFirstRow(yukiRowData.Collection) Then Return
    If setRowData(yukiRowData.Collection) Then Return
    
    $gridData.Add(yukiRowData.Collection)
    
End

Public Sub GetLinesOfCode(argIgnoreVoid As Boolean) As Integer
    
    If argIgnoreVoid Then 
        Return $linesOfCodeWithoutVoid
    Else
        Return $linesOfCode
    Endif
    
End

Public Sub Clear()
    
    $gridData.Clear()
    $linesOfCode = 0
    $linesOfCodeWithoutVoid = 0
    
End

Public Sub Reset()
    
    $targetColumn = -1
    $sort.Ascend = True
    
End

Public Sub SetTargetIndex(argIndex As Integer)
    
    If argIndex = $targetColumn Then
        $sort.Ascend = Not $sort.Ascend
    Else
        $targetColumn = argIndex
    Endif
    
End

' Public Sub TFEI_Signal(argEvent As Integer, argArgs As Variant[])
'     
'     Debug argEvent
'     
' End

Private Function Count_Read() As Integer

    Return $gridData.Count

End

Private Function Data_Read() As Variant[]

    Return $gridData

End
