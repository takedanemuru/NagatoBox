' Gambas class file

Create Static

Inherits NagatoObject

Property Read State As Integer

Private $player As New MediaPlayer As "MediaPlayer"
Private $inputPath As String
Private $targetPath As String

Private Sub play(argPath As String)
    
    Try $player.Stop()
    $player.URL = Media.URL(argPath)
    $player.Play()
    
End

Public Sub openFile()
    
    Dialog.Title = ("Select Audio File")
    Dialog.Path = User.Home
    If Dialog.OpenFile(False) Then Return
    
    Try $player.Stop()
    $inputPath = Dialog.Path
    
    Raise Signal(MikuruSignal.SourceFileNew, [Dialog.Path])
    
End

Public Sub save()
    
    Dialog.Title = ("Save File")
    Dialog.Path = User.Home &/ "your_file.mp3"
    If Dialog.SaveFile() Then Return
    
    Copy $targetPath To Dialog.Path
    
End

Public Sub SetEffect(argEffectType As Integer)
    
    If Not Exist($inputPath) Then Return
    
    Select Case argEffectType
        Case MikuruSignal.EffectKaraoke
            $targetPath = MikuruEffects.Karaoke($inputPath)
        Case MikuruSignal.EffectLoudness
            $targetPath = MikuruEffects.Loudness($inputPath)
        Case MikuruSignal.EffectSilence
            $targetPath = MikuruEffects.Silence($inputPath)
        Case MikuruSignal.EffectBass
            $targetPath = MikuruEffects.BassBoost($inputPath)
        Case MikuruSignal.EffectNorm
            $targetPath = MikuruEffects.Normalize($inputPath)
    End Select
    
    If Exist($targetPath) Then Raise Signal(MikuruSignal.TargetFileNew, [$targetPath])
    
End

Public Sub _call(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.SourceFileNew
            openFile()
        Case MikuruSignal.SourceFilePlay
            play($inputPath)
            Raise Signal(MikuruSignal.SourceFilePlay, Null)
        Case MikuruSignal.SourceFileStop
            Try $player.Stop()
            Raise Signal(MikuruSignal.SourceFileStop, Null)
        Case MikuruSignal.TargetFilePlay
            play($targetPath)
            Raise Signal(MikuruSignal.TargetFilePlay, Null)
        Case MikuruSignal.TargetFileStop
            Try $player.Stop()
            Raise Signal(MikuruSignal.TargetFileStop, Null)
        Case MikuruSignal.TargetFileSave
            If Exist($targetPath) Then save()
    End Select
    
End

Private Function State_Read() As Integer

    Return $player.State

End
