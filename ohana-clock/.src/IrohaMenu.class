' Gambas class file

Private $rootMenu As Menu

Private Enum MenuNone = 0
Private Enum MenuSize400 = 400, MenuSize300 = 300, MenuSize200 = 200
Private Enum MenuSkipTaskBar = 1001, MenuSticky = 1002 
Private Enum MenuAbout = 2001, MenuQuit = 2002

Public Sub addMenuItem(argText As String, argTag As Integer, Optional argIcon As Picture)
    
    Dim yukiMenu As New Menu($rootMenu) As "MenuItem"
    
    With yukiMenu
        .Text = argText
        .Tag = argTag
        If argIcon Then .Picture = argIcon
    End With
    
End

Public Sub Popup()
    
    $rootMenu.Popup()
    
End

Public Sub _new(argParent As Form)
    
    $rootMenu = New Menu(argParent)
    addMenuItem(("Large"), MenuSize400, IrohaIcons.GetCheckIcon(IrohaSettings.Size = 400))
    addMenuItem(("Medium"), MenuSize300, IrohaIcons.GetCheckIcon(IrohaSettings.Size = 300))
    addMenuItem(("Small"), MenuSize200, IrohaIcons.GetCheckIcon(IrohaSettings.Size = 200))
        addMenuItem("", MenuNone)
    addMenuItem(("Skip Taskbar"), MenuSkipTaskBar, IrohaIcons.GetCheckIcon(Ohana.SkipTaskbar))
    addMenuItem(("Sticky"), MenuSticky, IrohaIcons.GetCheckIcon(Ohana.Sticky))
        addMenuItem("", MenuNone)
    addMenuItem(("About"), MenuAbout, IrohaIcons["star"])
        addMenuItem("", MenuNone)
    addMenuItem(("Close"), MenuQuit, IrohaIcons["button-power"])
    
End

Public Sub MenuItem_Click()
    
    Select Case Last.Tag
        Case MenuSize200, MenuSize300, MenuSize400
            IrohaSettings.Size = Last.Tag
        Case MenuSkipTaskBar
             Ohana.SkipTaskbar = Not Ohana.SkipTaskbar
        Case MenuSticky
            Ohana.Sticky = Not Ohana.Sticky
        Case MenuAbout
            Message.Title = ("About ohana-clock")
            Message.Info(IrohaText.Info, ("OK"))
        Case MenuQuit
            Ohana.Close()
    End Select
    
End
