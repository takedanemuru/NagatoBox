' Gambas class file

Private $rootMenu As Menu

Private Enum MenuNone = 0
Private Enum MenuSize400 = 400, MenuSize300 = 300, MenuSize200 = 200
Private Enum MenuSkipTaskBar = 1001, MenuSticky = 1002 
Private Enum MenuAbout = 2001, MenuQuit = 2002

Public Sub addMenuItem(argText As String, argTag As Integer, Optional argIcon As Picture)
    
    Dim yukiMenu As New Menu($rootMenu) As "MenuItem"
    
    With yukiMenu
        .Text = argText
        .Tag = argTag
        If argIcon Then .Picture = argIcon
    End With
    
End

Public Sub Popup()
    
    $rootMenu.Popup()
    
End

Public Sub _new(argParent As Form)
    
    $rootMenu = New Menu(argParent)
    addMenuItem(("Large"), MenuSize400, MikuruIcons.GetCheckIcon(NagatoSettings.Size = 400))
    addMenuItem(("Medium"), MenuSize300, MikuruIcons.GetCheckIcon(NagatoSettings.Size = 300))
    addMenuItem(("Small"), MenuSize200, MikuruIcons.GetCheckIcon(NagatoSettings.Size = 200))
        addMenuItem("", MenuNone)
    addMenuItem(("Skip Taskbar"), MenuSkipTaskBar, MikuruIcons.GetCheckIcon(HaruhiMain.SkipTaskbar))
    addMenuItem(("Sticky"), MenuSticky, MikuruIcons.GetCheckIcon(HaruhiMain.Sticky))
        addMenuItem("", MenuNone)
    addMenuItem(("About"), MenuAbout, MikuruIcons["star"])
        addMenuItem("", MenuNone)
    addMenuItem(("Close"), MenuQuit, MikuruIcons["button-power"])
    
End

Public Sub MenuItem_Click()
    
    Select Case Last.Tag
        Case MenuSize200, MenuSize300, MenuSize400
            NagatoSettings.Size = Last.Tag
        Case MenuSkipTaskBar
             HaruhiMain.SkipTaskbar = Not HaruhiMain.SkipTaskbar
        Case MenuSticky
            HaruhiMain.Sticky = Not HaruhiMain.Sticky
        Case MenuAbout
            HaruhiAbout.ShowDialog()
        Case MenuQuit
            HaruhiMain.Close()
    End Select
    
End
