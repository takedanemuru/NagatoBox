' Gambas class file

Private $uninitialized As Boolean
Private $edited As Boolean
Private $heightRate As Float
Private $widthRate As Float

Event NewSize(argHeight As Integer, argWidth As Integer)

Public Sub SetImage(argImage As Image)
    
    With argImage
        HeightSpinBox.Value = .Height
        WidthSpinBox.Value = .Width
        $heightRate = .Height / .Width
        $widthRate = .Width / .Height
    End With
    
    $uninitialized = False
    
End

Public Sub Form_Open()
    
    Me.Center()
    $edited = False
    $uninitialized = True
    
End

Public Sub HeightSpinBox_Change()

    If $uninitialized Then Return

    If LinkCheckBox.Value = CheckBox.True Then
        Object.Lock(WidthSpinBox)
        WidthSpinBox.Value = Int(HeightSpinBox.Value * $widthRate)
        Object.Unlock(WidthSpinBox)
    Endif

    $edited = True

End

Public Sub WidthSpinBox_Change()

    If $uninitialized Then Return

     If LinkCheckBox.Value = CheckBox.True Then
         Object.Lock(HeightSpinBox)
        HeightSpinBox.Value = Int(WidthSpinBox.Value * $heightRate)
        Object.Unlock(HeightSpinBox)
    Endif

    $edited = True

End

Public Sub ApplyButton_Click()
    
    Raise NewSize(HeightSpinBox.Value, WidthSpinBox.Value)
    
    Me.Close()
    
End

Public Sub CancelButton_Click()

    Me.Close()

End
