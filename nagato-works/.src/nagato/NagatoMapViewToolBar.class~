' Gambas class file

Inherits NagatoToolBar

Private $slider As Slider

Event ChangeMap(argMapType As Integer)
Event SearchQuery(argQuery As String)
Event ChangeZoom(argZoom As Integer)

Private Sub addOpenStreetMapButton()
    
    Dim yukiToolButton As New ToolButton(Me._$parent) As "MapButton"
    
    With yukiToolButton
        .Radio = True
        .Toggle = True
        .Value = True
        .Text = ("OpenStreetMap")
        .AutoResize = True
        .Tag = NagatoMapViewMapView.OpenStreetMap
    End With
    
End

Private Sub addGoogleMapButtion()
    
    Dim yukiToolButton As New ToolButton(Me._$parent) As "MapButton"
    
    With yukiToolButton
        .Radio = True
        .Toggle = True
        .Value = False
        .Text = ("GoogleMap")
        .AutoResize = True
        .Tag = NagatoMapViewMapView.GoogleMap
    End With
    
End

Private Sub addZoomInButton()
    
    Dim yukiZoomInButton As New ToolButton(Me._$parent) As "ZoomInButton"
    
    With yukiZoomInButton
        .Picture = Stock["medium/zoom-in"]
        .Width = Me._$parent.Height
        .Tag = MikuruMenuEventView.ZoomIn
    End With
    
End

Private Sub addZoomOutButton()
    
    Dim yukiZoomOutButton As New ToolButton(Me._$parent) As "ZoomOutButton"
    
    With yukiZoomOutButton
        .Picture = Stock["medium/zoom-out"]
        .Width = Me._$parent.Height
        .Tag = MikuruMenuEventView.ZoomOut
    End With
    
End

Private Sub addZoomButtons()
    
    addZoomInButton()
    addZoomOutButton
    
End

Private Sub addZoomSlider()
    
    $slider = New Slider(Me._$parent) As "Slider"
    
    With $slider
        .Expand = True
        .Step = 1
        .Mark = True
        .MinValue = 1
        .MaxValue = 18
    End With
    
End

Private Sub addSearchBox()
    
    Dim yukiSearchBox As NagatoSearchBox
    
    yukiSearchBox = New NagatoSearchBox(Me._$parent) As "SearchBox"
    
End

Private Sub addGearButton()
    
    Dim yukiGearButton As NagatoGearButton
    Dim yukiMenu As NagatoMapViewMenu
    
    yukiGearButton = New NagatoGearButton(Me._$parent) 
    yukiMenu = New NagatoMapViewMenu(yukiGearButton.RootMenu) As "Menu"
    
End

Public Sub _Initialize() 'override.
    
    addOpenStreetMapButton()
    addGoogleMapButtion()
    addZoomButtons()
    addZoomSlider()
    addSearchBox()
    addGearButton()
    
End

Public Sub AdjustZoom(argZoom As Integer)
    
    Object.Lock($slider)
        $slider.Value = argZoom
    Object.Unlock($slider)
    
End

Public Sub Slider_Change()
    
    Debug $slider.Value
    
    Raise ChangeZoom($slider.Value)
    
End

Public Sub MapButton_Click()
    
    Raise ChangeMap(Last.Tag)
    
End

Public Sub ZoomInButton_Click()
    
    Inc $slider.Value
    
End

Public Sub ZoomOutButton_Click()
    
    Dec $slider.Value
    
End

Public Sub SearchBox_SearchQuery(argText As String)
    
    Raise SearchQuery(argText)
    
End
