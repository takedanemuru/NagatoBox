' Gambas class file

Inherits NagatoSuperMenu

Private $iconSizeTags As String[] ' initialize in _AddOtherMenu() to avoid inherit problem.

Event Refresh
Event Change(argNewIconSizeIndex As Integer)

Private Sub addRefreshMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu) As "RefreshMenu"
    
    With yukiMenu
        .Text = ("Refresh")
        .Picture = Stock["medium/refresh"]
    End With
    
End

Private Sub addIconSizeRootMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu)
    
    yukiMenu.Text = ("icon size")
    addIconSizeChildMenus(yukiMenu)
    
End

Private Sub addIconSizeIndivisualMenu(argParentMenu As Menu, argIndex As Integer)
    
    Dim yukiMenu As New Menu(argParentMenu) As "NewIconSizeMenu"
    
    With yukiMenu
        .Text = $iconSizeTags[argIndex]
        .Tag = argIndex
    End With
    
End

Private Sub addIconSizeChildMenus(argParentMenu As Menu)
    
    Dim yukiCounter As Integer 
    
    For yukiCounter = 0 To $iconSizeTags.Max
        addIconSizeIndivisualMenu(argParentMenu, yukiCounter)
    Next
    
End

Public Sub _AddOtherMenu() ' override
    
    addRefreshMenu()
    $iconSizeTags = [("small"), ("medium"), ("large"), ("huge"), ("stupid")]
    addIconSizeRootMenu()
    
End

Public Sub RefreshMenu_Click()
    
    Raise Refresh
    
End

Public Sub NewIconSizeMenu_Click()
    
    Raise Change(Last.tag)
    
End

