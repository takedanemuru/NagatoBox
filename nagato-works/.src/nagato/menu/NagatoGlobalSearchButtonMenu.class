' Gambas class file

Inherits NagatoSuperMenu

Private $webBoonmarkRootMenu As Menu

Private Sub addDirectoryBookmarkMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu)
    Dim yukiDirectoryMenu As NagatoSubMenuDirectoryBookmark
    
    yukiDirectoryMenu = New NagatoSubMenuDirectoryBookmark(yukiMenu)
    
    With yukiMenu
        .Text = ("Directories")
        .Picture = Stock["medium/directory"]
    End With
    
End

Private Sub addWebBookmarkRootMenu()
    
    $webBoonmarkRootMenu = New Menu(Me._$rootMenu)
   
    With $webBoonmarkRootMenu
        .Text = ("Web bookmark")
        .Picture = Stock["medium/internet"]
    End With
    
End

Private Sub addWebBookmarkMenu()
    
    If NagatoDBWeb.CategoryCount = 0 Then
        addThereIsNoBookmarkMenu()
    Else
        addCategoryMenu()
    Endif
    
End

Private Sub addThereIsNoBookmarkMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu)
    
    yukiMenu.Text = ("no bookmark")
    
End

Private Sub addBookmarkMenu(argParentMenu As Menu, argCategory As String)
    
    Dim yukiCache As NagatoWebBookmarkObject[] = NagatoWebbookmarkCache.GetCache(argCategory)
    Dim yukiObject As NagatoWebBookmarkObject
    Dim yukiMenu As Menu
    
    If yukiCache = Null Then Return
    
    For Each yukiObject In yukiCache
        yukiMenu = New Menu(argParentMenu) As "BookmarkMenu"
        With yukiMenu
            .Text = yukiObject.UserDefinedTitle
            .Tag = yukiObject.Url
            .Picture = yukiObject.Icon
        End With
    Next
    
End

Private Sub addCategoryMenu()
    
    Dim yukiMenu As Menu
    Dim yukiCategory As String
    
    For Each yukiCategory In NagatoWebbookmarkCache.Categories
        If Not NagatoDBWeb.HasBookmarkInCategory(yukiCategory) Then Continue
        yukiMenu = New Menu(Me._$rootMenu)
        yukiMenu.Text = yukiCategory
        addBookmarkMenu(yukiMenu, yukiCategory)
    Next
    
End

Public Sub _AddOtherMenu() ' override
    
    addDirectoryBookmarkMenu()
    addWebBookmarkRootMenu()
    addWebBookmarkMenu()
    
End
