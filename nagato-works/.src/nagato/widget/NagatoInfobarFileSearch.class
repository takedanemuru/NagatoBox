' Gambas class file

Inherits NagatoInfoBar

Private $searchBox As NagatoSearchBox

Event SearchQuery(argQuery As String)

Private Sub addExactMachCheckBox()
    
    Dim yukiCheckBox As New CheckBox(Me.$infobar) As "CheckBoxExactMatch"
    
    With yukiCheckBox
        .Value = NagatoSettingsFileSearch.ExactMatch
        .Text = ("Exact mach")
        .AutoResize = True
    End With
    
End

Private Sub addRecursiveSearchCheckBox()
    
    Dim yukiCheckBox As New CheckBox(Me.$infobar) As "CheckBoxRecursive"
    
    With yukiCheckBox
        .Value = NagatoSettingsFileSearch.Recursive
        .Text = ("Recursive search")
        .AutoResize = True
    End With
    
End

Private Sub addIncludeHiddenCheckBox()
    
    Dim yukiCheckBox As New CheckBox(Me.$infobar) As "CheckBoxHidden"
    
    With yukiCheckBox
        .Value = NagatoSettingsFileSearch.Hidden
        .Text = ("Search hidden")
        .AutoResize = True
    End With
    
End

Private Sub addSpacer()
    
    Dim yukiSpacer As New Label(Me.$infobar)
    
    With yukiSpacer
        .Expand = True
    End With
    
End

Private Sub addSearchBox()
    
    $searchBox = New NagatoSearchBox(Me.$infobar) As "SearchBox"
    
End

Private Sub addCloseButton()
    
    Dim yukiButton As New ToolButton(Me.$infobar) As "CloseButton"
    
    With yukiButton
        .Width = Me.$size
        .Picture = Stock["medium/delete"]
        .Tooltip = ("Close search bar")
    End With
    
End

Private Sub initializeWidgets()
    
    Me.$infobar.Padding = 2
    addExactMachCheckBox()
    addRecursiveSearchCheckBox()
    addIncludeHiddenCheckBox()
    addSpacer()
    addSearchBox()
    addCloseButton()
    
End

Public Sub Hide()
    
    Me.$infobar.Visible = False
    
End

Public Sub Show()
    
    Me.$infobar.Visible = True
    $searchBox.SetFocus()
    
End

Public Sub _new(argParent As Container)
    
    Me.initializeInfobar(argParent)
    initializeWidgets()
    
End

Public Sub CheckBoxExactMatch_Click()
    
    With NagatoSettingsFileSearch
        .ExactMatch = Not .ExactMatch
    End With
    
End

Public Sub CheckBoxRecursive_Click()
    
    With NagatoSettingsFileSearch
        .Recursive = Not .Recursive
    End With
    
End

Public Sub CheckBoxHidden_Click()
    
    With NagatoSettingsFileSearch
        .Hidden = Not .Hidden
    End With
    
End

Public Sub SearchBox_SearchQuery(argQuery As String)
    
    Raise SearchQuery(argQuery)
    
End

Public Sub CloseButton_Click()
    
    Me.Hide()
    
End
