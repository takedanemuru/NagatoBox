' Gambas class file

Inherits NagatoInfoBar

Private $download As WebDownload
Private $internalTimer As Timer
Private $label As TextLabel
Private $progressBar As ProgressBar
Private $toolButton As ToolButton
Private $destinationPath As String

Event Finished

Private Sub initializeTimer()
    
    $internalTimer = New Timer As "internalTimer"
    $internalTimer.Delay = 250
    
End

Private Sub addIcon()
    
    Dim yukiButton As New ToolButton(Me.$infobar)
    
    With yukiButton
        .Expand = False
        .Width = Me.$size
        .Picture = Stock["medium/down"]
    End With
    
End

Private Sub addLabel()
    
    $label = New TextLabel(Me.$infobar)
    
    With $label
        .Expand = True
        .Transparent = True
        .Text = "<b>now downloading from : </b>" & $download.Url
        .Width = .Font.TextWidth(.Text)
        .Alignment = Align.Center
    End With
    
End

Private Sub addProgressBar()
    
   $progressBar = New ProgressBar(Me.$infobar)
   
   With $progressBar
        .Label = True
        .Value = 0
        .Width = 200
   End With 
    
End

Private Sub addCloseButton()
    
    $toolButton = New ToolButton(Me.$infobar) As "CloseButton"
    
    With $toolButton
        .Width = Me.$size
        .Picture = Stock["medium/delete"]
        .Tooltip = ("Close DownloadBar")
    End With
    
End

Private Sub initializeWidgets()
    
    addIcon()
    addLabel()
    addProgressBar()
    addCloseButton()
    $internalTimer.Start()
    
End

Public Sub _new(argParent As Container, argDownload As WebDownload, argDestinationPath As String)
    
    $destinationPath = argDestinationPath
    $download = argDownload
    initializeTimer()
    Me.initializeInfobar(argParent)
    initializeWidgets()
    
End

Public Sub internalTimer_Timer()
    
    $progressBar.Value = $download.Progress
        Debug $download.Progress
        Debug $download.Path
        Debug $download.Url
    If $download.Status = WebDownload.Finished Then
        $internalTimer.Stop()
        $toolButton.Picture = Stock["medium/ok"]
        Move $download.Url To $destinationPath
    Endif
    
End

Public Sub CloseButton_Click()
    
    $internalTimer.Stop()
    Me.$infobar.Delete
    
End
