' Gambas class file

Inherits NagatoToolButtons

Private $fontNameBox As ComboBox
Private $fontSizeBox As SpinBox
Private $fontColorButton As ColorButton

Event NewFontName(argNewName As String)
Event NewFontSize(argNewSize As Integer)
Event NewFontColor(argNewColor As Integer)

Private Sub setFonts()
    
    Dim yukiFont As String
    
    For Each yukiFont In Fonts
        $fontNameBox.Add(yukiFont)
    Next
    
End

Private Sub addFontButton()
    
    Dim yukiFontButton As New ToolButton(Me._$parent) As "FontButton"
    
    With yukiFontButton
        .Width = Me.$size
        .Picture = Stock["medium/font"]
        .Tooltip = ("set font")
    End With
    
End

Private Sub addFontNameBox()
    
   $fontNameBox = New ComboBox(Me._$parent) As "FontNameBox"
   
   With $fontNameBox
        .Width = 200
        .ReadOnly = True
        .Tooltip = ("select font name")
   End With 

   setFonts()
    
End

Private Sub addFontSizeBox()
    
    $fontSizeBox = New SpinBox(Me._$parent) As "FontSizeBox"
    
    With $fontSizeBox
        .Width = 50
        .Tooltip = ("select font size")
        .Value = 11
    End With
    
End

Private Sub addFontColorButton()
    
    $fontColorButton = New ColorButton(Me._$parent) As "FontColorButton"
    
    With $fontColorButton
        .Width = Me.$size
    End With
    
End

Private Sub constructWidgets()
    
    addFontButton()
    addFontNameBox()
    addFontSizeBox()
    addFontColorButton()
    
End

Public Sub _new(argParent As Container)
    
    Me.initialize(argParent)
    constructWidgets()
    
End

Public Sub Refresh(argFont As Font, argColor As Integer)
    
    $fontNameBox.Index = $fontNameBox.List.Find(SConv$(argFont.Name))
    $fontSizeBox.Value = argFont.Size
    $fontColorButton.Color = argColor
    
End

Public Sub FontNameBox_Click()
    
    Raise NewFontName($fontNameBox.Current.Text)
    
End

Public Sub FontSizeBox_Change()
    
    Raise NewFontSize($fontSizeBox.Value)
    
End

Public Sub FontColorButton_Change()
    
    Raise NewFontColor($fontColorButton.Color)
    
End
