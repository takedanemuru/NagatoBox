' Gambas class file

Inherits NagatoToolButtonMenu

Private Sub addUnlimitedMenu(argMenu As Menu)
    
    Dim yukiMenu As New Menu(argMenu) As "UnlimitedMenu"
    
    With yukiMenu
        .Text = ("Unlimited")
        .Tag = +Inf
        .Toggle = True
        .Checked = Not NagatoSearchFileMenuConfig.LimitResult
    End With
    
End

Private Sub addLimitedMenu(argMenu As Menu)
    
    Dim yukiArray As Integer[] = [10, 20, 50, 100, 200, 500, 1000]
    Dim yukiLimit As Integer
    Dim yukiMenu As Menu
    
    For Each yukiLimit In yukiArray
        yukiMenu = New Menu(argMenu) As "LimitedMenu"
        With yukiMenu
            .Text = yukiLimit
            .Tag = yukiLimit
            .Toggle = True
            If NagatoSearchFileMenuConfig.LimitResult And If NagatoSearchFileMenuConfig.LimitResultMax = yukiLimit Then
                .Checked = True
            Else
                .Checked = False
            Endif
        End With
    Next
    
End

Private Sub addSeparatorMenu(argMenu As Menu)
    
    Dim yukiMenu As New Menu(argMenu)
    
    yukiMenu.Text = ""
    
End

Private Sub addParentMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu)
    
    yukiMenu.Text = ("LimitResult")
    addUnlimitedMenu(yukiMenu)
        addSeparatorMenu(yukiMenu)
    addLimitedMenu(yukiMenu)
    
End

Public Sub _new(argRootMenu As Menu)
    
    Me._$rootMenu = argRootMenu
    addParentMenu()
    
End

Public Sub UnlimitedMenu_Click()
    
    With NagatoSearchFileMenuConfig
        .LimitResult = False
        .LimitResultMax = Last.Tag
    End With
    
End

Public Sub LimitedMenu_Click()
    
    With NagatoSearchFileMenuConfig
        .LimitResult = True
        .LimitResultMax = Last.Tag
    End With
    
End
