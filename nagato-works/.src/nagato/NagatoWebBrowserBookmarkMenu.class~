' Gambas class file

Inherits NagatoDynamicSubMenu

Event AddBookmark

Private Sub addAddBookmarkMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu) As "AddBookmarkMenu"
    
    With yukiMenu
        .Text = ("Bookmark this page")
        .Picture = Stock["medium/add"]
    End With
    
End

Private Sub addEditBookmarkMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu) As "EditBookmarkMenu"
    
    With yukiMenu
        .Text = ("Edit bookmarks")
        .Picture = Stock["medium/edit"]
    End With
    
End

Private Sub addThereIsNoBookmarkMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu)
    
    yukiMenu.Text = ("no bookmark")
    
End

Private Sub addBookmarkMenu(argParentMenu As Menu, argCategory As String)
    
    Dim yukiCache As NagatoWebBookmarkObject[] = NagatoWebbookmarkCache.GetCache(argCategory)
    Dim yukiObject As NagatoWebBookmarkObject
    Dim yukiMenu As Menu
    
    If yukiCache = Null Then Return
    
    For Each yukiObject In yukiCache
        yukiMenu = New Menu(argParentMenu) As "BookmarkMenu"
        With yukiMenu
            .Text = yukiObject.UserDefinedTitle
            .Tag = yukiObject.Url
            .Picture = yukiObject.Icon
        End With
    Next
    
End

Private Sub addCategoryMenu()
    
    Dim yukiMenu As Menu
    Dim yukiCategory As String
    
    For Each yukiCategory In NagatoWebbookmarkCache.Categories
        If Not NagatoDBWeb.HasBookmarkInCategory(yukiCategory) Then Continue
        yukiMenu = New Menu(Me._$rootMenu)
        yukiMenu.Text = yukiCategory
        addBookmarkMenu(yukiMenu, yukiCategory)
    Next
    
End

Public Sub _RefreshMenu() 'override
    
    addAddBookmarkMenu()
    addEditBookmarkMenu()
        MikuruMenuSeparator.Set(Me._$rootMenu)
    If NagatoDBWeb.CategoryCount = 0 Then
        addThereIsNoBookmarkMenu()
    Else
        addCategoryMenu()
    Endif
    
End

Public Sub Activate()
    
    Me._RefreshMenu()
    
End

Public Sub AddBookmarkMenu_Click()
    
    Raise AddBookmark
    
End

Public Sub BookmarkMenu_Click()
    
    NagatoDBWeb.ChangeLastAccessed(Last.Tag)
    NagatoTabHandler.NewTabWithPath(MikuruFunctionType.WebBrowser, Last.Tag)
    
End

Public Sub EditBookmarkMenu_Click()
    
    NagatoTabHandler.NewTab(MikuruFunctionType.WebBookmarkEditor)
    
End

Public Sub Asakura_Show() 'override
    
    Me._$rootMenu.Children.Clear()
    Me._RefreshMenu()

End
