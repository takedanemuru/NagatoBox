' Gambas module file

Private Const IconSize As Integer = 128
Private Const TextAreaHeight As Integer = 64
Private Const Padding As Integer = 16

Private Function getBaseImageHeight() As Integer
    
    Return IconSize + TextAreaHeight + 3 * Padding
    
End

Private Function getBaseImageWidth() As Integer
    
    Return IconSize + 2 * Padding
    
End

Private Sub drawRectangle()
    
    With Paint
        .Brush = .Color(Color.Orange)
        .Rectangle(0, 0, getBaseImageWidth(), getBaseImageHeight(), Padding)
        .Fill()
    End With
    
End

Private Function getIconPosition(argSize As Integer) As Integer
    
    Return Padding + (IconSize - argSize) / 2
    
End

Private Function getResizedIcon(argIcon As Picture) As Picture
    
    Dim yukiImage As Image
    Dim yukiRate As Float
    
    If IconSize >= Max(argIcon.Width, argIcon.Height) Then Return argIcon
    
    yukiImage = argIcon.Image
    yukiRate = IconSize / Max(argIcon.Width, argIcon.Height)
    
    Return yukiImage.Stretch(yukiImage.W * yukiRate, yukiImage.H * yukiRate).Picture
    
End

Private Sub drawIcon(argIcon As Picture)
    
    Dim yukiIcon As Picture = getResizedIcon(argIcon)
    
    Debug yukiIcon.Height
    Debug yukiIcon.Width
    
    Paint.DrawPicture(yukiIcon, getIconPosition(argIcon.W), getIconPosition(argIcon.H))
    
End

Private Sub drawFileName(argText As String)
    
    With Paint
        .Brush = Paint.Color(Color.White)
        .Rectangle(Padding, IconSize + 2 * Padding, IconSize, TextAreaHeight)
        .Stroke(True)
        .Clip()
        .Brush = Paint.Color(Color.White)
        .DrawText(argText, Padding, IconSize + 2 * Padding, IconSize, TextAreaHeight, Align.Center)
    End With
    
End

Public Function SetIconSingle(argIcon As Picture, argText As String)
    
    Dim yukiBaseImage As New Image(getBaseImageWidth(), getBaseImageHeight(), Color.Transparent)
    
    With Paint
        .Begin(yukiBaseImage)
        drawRectangle()
        drawIcon(argIcon)
        drawFileName(argText)
        .End()
    End With
    
    Drag.Icon = yukiBaseImage.Picture
    
End
