' Gambas class file

Private $fileStore As NagatoFileStore
Private $iconView As NagatoIconView

Private Function getArchiveType() As String
    
    Select Case TypeComboBox.Index
        Case 0
            Return ".zip"
        Case 1
            Return ".tar"
        Case 2
            Return ".bz2"
        Case 3
            Return ".gz"
        Case 4
            Return ".rar"
        Case 5
            Return ".lzh"
        Case 6
            Return ".xz"
    End Select
    
End

Public Sub SetPaths(argPaths As String[])
    
    $fileStore.SetPaths(argPaths)
    
End

Public Sub _new()

    $iconView = New NagatoIconView(FilesIconView) As "IconView"
    $fileStore = New NagatoFileStore As "FileStore"

End

Public Sub Form_Open()

    Me.Center()
    TypeComboBox.Index = 0
    $iconView.SetEmptyView()
    
End

Public Sub OpenFilesButton_Click()

    Dialog.Title = ("Open file")
    Dialog.Path = User.Home

    If Not Dialog.OpenFile(True) Then
        $fileStore.SetPaths(Dialog.Paths)
    Endif

End

Public Sub IconView_Drop(argPaths As String[])

    $fileStore.SetPaths(argPaths)
    
End

Public Sub ClearButton_Click()

    $fileStore.Clear()

End

Public Sub ApplyButton_Click()

    If $iconView.IsEmpty Then Return 
    
    Dialog.Title = ("Set path to create")
    Dialog.Path = MikuruSafePath.Get(User.Home &/ "new_archive" & getArchiveType())
    
    If Not Dialog.SaveFile() Then 
        NagatoPatool.Create($fileStore.PathsInLine, Dialog.Path)
    End If

End

Public Sub FileStore_New(argPaths As String[])
    
    $iconView.Add(argPaths)
    
End

Public Sub FileStore_Remove(argPaths As String[])
    
    $iconView.Remove(argPaths)
    
End

Public Sub FileStore_Clear()
    
    $iconView.SetEmptyView()
    
End
