' Gambas module file

Private Function getPathToSave(argExtend As String) As String
    
    Dialog.Title = ("Save file")
    Dialog.Path = User.Home &/ "your_document." & argExtend
    
    Return IIf(Dialog.SaveFile(), "", Dialog.Path)
    
End

Private Sub toPdf()
    
    Dim yukiPath As String = getPathToSave("pdf")

    If Not yukiPath Then Return

    'Exec ["wkhtmltopdf", "--encoding", "UTF-8", "--page-size", "A4", "--dpi", "300", MikuruHtmlConverter.TempFile, yukiPath] Wait
    Exec ["wkhtmltopdf", "--encoding", "UTF-8", "--page-size", "A4", "--dpi", "300", MikuruHtmlConverter.TempFile, yukiPath] Wait

    Message.Info("YUKI.N > It's done", ("OK"))
    
End

Private Sub toHtml()
    
    Dim yukiPath As String = getPathToSave("html")
    
    Copy MikuruHtmlConverter.TempFile To yukiPath
    
    Message.Info("YUKI.N > It's done", ("OK"))
    
End

Private Sub toOdt()
    
    Dim yukiPath As String = getPathToSave("odt")

    If Not yukiPath Then Return

    Exec ["pandoc", MikuruHtmlConverter.TempFileRelative, "-o", yukiPath] Wait

    Message.Info("YUKI.N > It's done", ("OK"))
    
End

Private Sub toEpub()
    
    Dim yukiPath As String = getPathToSave("epub")
    
    If Not yukiPath Then Return

    Exec ["pandoc", "-t", "epub3", "-c", NagatoCss.Get(), "-S", MikuruHtmlConverter.TempFileRelative, "-o", yukiPath] Wait
    
    Message.Info("YUKI.N > It's done", ("OK"))
    
End

Private Sub toPlainText()
    
    Dim yukiTempPathSource As String = Temp$() & ".txt"
    Dim yukiPath As String = getPathToSave("txt")
    Dim yukiOutput As String
    
    If Not yukiPath Then Return
    
    File.Save(yukiTempPathSource, MikuruHtmlConverter.Source)
    
    Exec ["pandoc", "-f", "markdown_github", "-t", "plain", yukiTempPathSource] To yukiOutput
    
    File.Save(yukiPath, yukiOutput)
    
    Message.Info("YUKI.N > It's done", ("OK"))
    
End

Public Sub _call(argSignal As Integer)
    
    If Not MikuruHtmlConverter.Source Then Return
    
    Select Case argSignal
        Case MikuruSignal.PublishHtml
            toHtml()
        Case MikuruSignal.PublishPdf
            toPdf()
        Case MikuruSignal.PublishPlainText
            toPlainText()
        Case MikuruSignal.PublishOdt
            toOdt()
        Case MikuruSignal.PublishEpub3
            toEpub()
    End Select
    
End
