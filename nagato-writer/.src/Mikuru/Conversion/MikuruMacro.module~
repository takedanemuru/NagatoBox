' Gambas module file

Property RelativePath As String

Private Const MacroJekyllNow As String = "![_config.yml]({{ site.baseurl }}"

Private $relativePath As New NagatoRelativePath

Private Function getReplacepattern(argLevel As Integer) As String
    
    Return String$(argLevel, "../")
    
End

Private Function replaceRelativeImagePath(argText As String) As String
    
    Dim yukiLevel As Integer
    Dim yukiText As String = argText
    Dim yukiReplacement As String
    
    For yukiLevel = $relativePath.DirectoryLevel DownTo 1
        yukiReplacement = $relativePath.ParentDirectories[yukiLevel - 1] & "/"
        yukiText = Replace$(yukiText, getReplacepattern(yukiLevel), yukiReplacement)
    Next
    
    Return yukiText
    
End

Public Function ConvertToAbsolute(argSourceText As String) As String
    
    Dim yukiText As String = argSourceText
    
    yukiText = replaceRelativeImagePath(yukiText)
    yukiText = Replace$(yukiText, "](./", Subst$("](&1/", $relativePath.ParentDirectoryUrl))
    yukiText = Replace$(yukiText, MacroJekyllNow, Subst$("![](&1/", File.Dir($relativePath.ParentDirectoryUrl)))
    
    Return yukiText
    
End

Public Function ConvertToStandAloneHtml(argSourceText As String) As String
    
    Dim yukiText As String = argSourceText
    
    yukiText = replaceRelativeImagePath(yukiText)
    yukiText = Replace$(yukiText, "](./", Subst$("](&1/", $relativePath.ParentDirectoryPath))
    yukiText = Replace$(yukiText, MacroJekyllNow, Subst$("![](&1/", File.Dir($relativePath.ParentDirectoryPath)))
    
    Return yukiText
    
End

Public Function ConvertToEpub3(argSourceText As String) As String
    
    Dim yukiText As String = argSourceText
    
    yukiText = replaceRelativeImagePath(yukiText)
    
    Return Replace$(yukiText, "](./", $relativePath.ParentDirectoryPath)
    
End

Private Function RelativePath_Read() As String

    Return $relativePath.ParentDirectoryUrl

End

Private Sub RelativePath_Write(Value As String)

    $relativePath.SetPath(Value)

End
