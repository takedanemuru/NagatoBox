' Gambas class file

Property Read Headers As Collection
Property Read Buffer As String
Property Read Path As String

Private $buffer As String
Private $path As String
Private $headers As New NagatoSourceHeaders

Private Sub changePath(argPath As String)
    
    $path = argPath
    MikuruMacro.RelativePath = argPath
    
End

Public Function SetPath(argPath As String) As String
    
    Dim yukiPath As String = IIf(Stat(argPath).Type = gb.Link, Stat(argPath).Link, argPath)
    
    changePath(yukiPath)
    $buffer = File.Load(yukiPath)
    $headers.Refresh($buffer)
    HaruhiMain.Title = File.Name(yukiPath)

    Return $buffer
    
End

Public Sub New() As String
    
    $buffer = ""
    $path = ""
    
    Return $buffer
    
End

Public Sub Open() As Boolean
    
    Dialog.Title = ("Open file")
    Dialog.Path = User.Home
    If Dialog.OpenFile(False) Then Return False
    If DesktopMime.FromFile(Dialog.Path).Type Not Begins "text" Then Return False
    
    Try $buffer = File.Load(Dialog.Path)
    changePath(Dialog.Path)
    Return True
    
Catch
    Return False
    
End

Public Sub Save(argText As String)
    
    If argText = $buffer Then Return
    
    If $path = "" Then 
        Me.SaveAs(argText)
    Else
        $buffer = argText
        $headers.Refresh(argText)
        File.Save($path, argText)
    Endif
    
End

Public Sub SaveAs(argText As String)
    
    Dialog.Path = Subst$("&1/your_file.md", User.Home)
    Dialog.Title = ("Save file as ...")
    If Dialog.SaveFile() Then Return
    
    changePath(Dialog.Path)
    $buffer = argText
    
    File.Save($path, argText)
    
End

Private Function Buffer_Read() As String

    Return $buffer

End

Private Function Path_Read() As String

    Return $path

End

Private Function Headers_Read() As Collection

    Return $headers.Headers

End
