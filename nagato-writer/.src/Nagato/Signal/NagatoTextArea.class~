' Gambas class file

Inherits NagatoUxContainer

Property Text As String

Private $textArea As TextArea
Private $observer As Observer
Private $keyboardHandler As NagatoTextAreaKeyboard
Private $searchAndReplace As NagatoTextAreaSearchAndReplace

Private Sub initializeTextArea()
    
    $textArea = New TextArea(Me._$parentContainer) As "TextArea"
    
    With $textArea
        .Expand = True
        .Foreground = Color.Violet
        .Wrap = True
        .SetFocus()
    End With
    
End

Public Sub _initialize() ' override
    
    initializeTextArea()
    
    $keyboardHandler = New NagatoTextAreaKeyboard($textArea) As "TFEI"
    $searchAndReplace = New NagatoTextAreaSearchAndReplace($textArea) As "TFEI"
    $observer = New Observer(HaruhiReplacement) As "Asakura"
    
End

Public Sub Asakura_SignalStringTag(argTag As String, argValues As Variant[])
    
    Select Case argTag
        Case "Previous"
            $searchAndReplace.SearchTextBack(argValues[0])
        Case "Next"
            $searchAndReplace.SearchText(argValues[0])
        Case "Replace"
            $searchAndReplace.ReplaceOne(argValues[0], argValues[1])
        Case "ReplaceAll"
            $searchAndReplace.ReplaceAll(argValues[0], argValues[1])
    End Select
    
End

Public Sub Asakura_Opened()
    
    HaruhiReplacement.X = $textArea.ScreenX + $textArea.W + 64
    HaruhiReplacement.Y = $textArea.Y + 64
    
End

Public Sub TextArea_Change()
    
    If NagatoSettings.LivePreview Then Raise Signal(MikuruSignal.ViewPreview, Null)
    
End

Private Function Text_Read() As String

    Return $textArea.Text

End

Private Sub Text_Write(Value As String)

    Object.Lock($textArea)
        $textArea.Text = Value
    Object.Unlock($textArea)
    
End
