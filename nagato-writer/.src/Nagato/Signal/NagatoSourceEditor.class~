' Gambas class file

Inherits NagatoUxContainer

Property Read Text As String
Property Read Path As String

Private $hBox As HBox
Private $fileHandler As New NagatoFileHandler As "TFEI"
Private $headersList As NagatoSourceHeadersList
Private $textArea As NagatoTextArea 
Private $appClosure As New NagatoAppClosure As "TFEI"
Private $observer As Observer

Private Function getHeaderIndex(argLine As Integer) As Integer
    
    Dim yukiLine As String
    Dim yukiKey As Integer
    
    For Each yukiLine In $fileHandler.Headers
        yukiKey = CInt($fileHandler.Headers.Key) 
        If argLine > yukiKey Then Continue
        If argLine = yukiKey Then Return $fileHandler.GetIndex(yukiKey) 
        Return $fileHandler.GetIndex(yukiKey) - 1
    Next
    
    Return -1
    
End

Public Sub _Initialize()
    
    $hBox = New HBox(Me._$parentContainer)
    $headersList = New NagatoSourceHeadersList($hBox) As "TFEI"
    $textArea = New NagatoTextArea($hBox) As "TFEI"
    $observer = New Observer(NagatoSettingsView) As "Asakura"

End

Public Sub FileEvent(argEvent As Integer, Optional argArgs As Variant[] = Null)
    
    If argEvent > (MikuruSignal.FileNone + 100) And If Not $fileHandler.EnsureSaved($textArea.Text) Then Return
    
    Select Case argEvent
        Case MikuruSignal.FileNewMarkdown
            $textArea.Text = $fileHandler.New()
        Case MikuruSignal.FileOpenPath
            $textArea.Text = $fileHandler.SetPath(argArgs[0])
        Case MikuruSignal.FileOpen
            If $fileHandler.Open() Then $textArea.Text = $fileHandler.Buffer
        Case MikuruSignal.FileSave
            $fileHandler.Save($textArea.Text)
        Case MikuruSignal.FileSaveAs
            $fileHandler.SaveAs($textArea.Text)
    End Select
    
    If Not NagatoSettings.LivePreview Then Raise Signal(MikuruSignal.ViewPreview, Null)
    NagatoSettings.PushRecentPath($fileHandler.Path)
    $headersList.Refresh($fileHandler.Headers)
    If $fileHandler.Path Then HaruhiMain.Title = File.Name($fileHandler.Path)

End

Public Sub EditEvent(argEvent As Integer, Optional argValues As Variant[])
    
    Select Case argEvent
        Case MikuruSignal.EditInsertBullets
            $textArea.InsertBullets()
        Case MikuruSignal.EditInsertNumbers
            $textArea.InsertNembers()
        Case MikuruSignal.EditInsertImage
            $textArea.InsertImageAbsolute($fileHandler.Path)
        Case MikuruSignal.EditInsertImageRelative
            $textArea.InsertImagePathRelative($fileHandler.Path)
        Case MikuruSignal.EditInsertUrl
            $textArea.InsertUrlTemplate()
        Case MikuruSignal.EditMoveToLine
            $textArea.MoveToLine(argValues[0])
    End Select
    
End

Public Sub GetSaveStatus() As Integer
    
    If $textArea.Text = $fileHandler.Buffer Then Return NagatoAppClosure.ResponseHasBeenSaved

    Return $appClosure.GetStatus()
    
End

Public Sub TFEI_Signal(argSignal As Integer, argValues As Variant[])
    
    If argSignal = MikuruSignal.ViewMoveCursor Then
        Raise Signal(MikuruSignal.ViewMoveTag, [getHeaderIndex(argValues[0])])
    Else
        Raise Signal(argSignal, argValues)
    Endif
    
End

Public Sub Asakura_RequestReload()
    
    Raise Signal(MikuruSignal.FileSave, Null)
    
End

Private Function Text_Read() As String

    Return $textArea.Text

End

Private Function Path_Read() As String

    Return $fileHandler.Path

End
