' Gambas class file

Inherits NagatoWorkingFile

Property Read IDs As String[]
Property Read BottomId As String

Private $htmlDocument As HtmlDocument
Private $tags As New NagatoHtmlTags

Private Sub setIds(argTargetPath As String)
    
    $htmlDocument = New HtmlDocument(argTargetPath)
    $tags.Refresh($htmlDocument)
    
End

Private Sub setHiddenBottomId(argTargetPath As String)
    
    $htmlDocument.Body.NewElement("div")
    $htmlDocument.Body.LastChild.NewAttribute("id", $tags.BottomId)
    File.Save(argTargetPath, $htmlDocument.Content)
    
End

Private Sub convert(argSourcePath As String, argTargetPath As String)
    
    Exec MikuruPandocCommand.GetPreviewCommand(argSourcePath, argTargetPath) Wait
    
    setIds(argTargetPath)
    setHiddenBottomId(argTargetPath)
    
End

Public Sub Set(argSourceText As String)
    
    Dim yukiSourcePath As String = Temp$()
    
    Me._ResetWorkingPath()
    File.Save(yukiSourcePath, MikuruMacro.ConvertToAbsolute(argSourceText))
    convert(yukiSourcePath, Me._$workingPath)
    
End

Private Function IDs_Read() As String[]

    Return $tags.Ids

End

Private Function BottomId_Read() As String

    Return $tags.BottomId

End
