' Gambas class file

Inherits DBusObject

Create Static

Property Read Ready As String
Property Read FeedCount As Integer
Property Read FeedReady As Boolean

Private $reservations As New String[]

Public Sub Refresh()
    
    NagatoRss.Refresh()
    
End

Public Sub CheckIn(argId As String) As Boolean
    
    If $reservations.Exist(argId) Then Return False
    
    $reservations.Add(argId)
    NagatoRss.Refresh()
    
    Return True
    
End

Public Sub CheckOut(argId As String) As Boolean
    
    Dim yukiIndex As Integer = $reservations.Find(argId)
    
    If yukiIndex = -1 Then Return False
    
    $reservations.Remove(yukiIndex)
    If $reservations.Count = 0 Then Me.Stop()
    
    Return True
    
End

Public Sub Stop() As Boolean
    
    NagatoDBus.Unregister()
    
    If Not Error Then Return True
    
Catch
    Return False
    
End

Public Sub GetFeedTitle(argFeedIndex As Integer) As String
    
    Return NagatoRss[argFeedIndex].Title
    
End

Public Sub GetFeedItemsCount(argFeedIndex As Integer) As Integer
    
    Return NagatoRss[argFeedIndex].Items.Count
    
End

Public Sub GetFeedItem(argFeedIndex As Integer, argItemIndex As Integer) As String[]
    
    Dim yukiArray As New String[]
    
    With NagatoRss[argFeedIndex].Items[argItemIndex]
        yukiArray.Add(.Title)
        yukiArray.Add(.Description)
        yukiArray.Add(.Link)
        yukiArray.Add(.ThumbnailPath)
    End With
    
    Return yukiArray
    
End

Private Function Ready_Read() As String

    Return "YUKI.N >  Can you see this ?"

End

Private Function FeedCount_Read() As Integer

    Return NagatoRss.Count

End

Private Function FeedReady_Read() As Boolean

    Return NagatoRss.Ready

End
