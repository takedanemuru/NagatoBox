' Gambas class file

Private $image As Image

Private Sub drawBackGround()
    
    Dim yukiRate As Float = Max(DrawingArea1.W / $image.W, DrawingArea1.H / $image.H)
    Dim yukiImage As Image = $image.Stretch($image.W * yukiRate, $image.H * yukiRate)
    Dim yukiX As Float = -1 * (yukiImage.W - DrawingArea1.W) / 2
    Dim yukiY As Float = -1 * (yukiImage.H - DrawingArea1.H) / 2
    
    Paint.DrawImage(yukiImage, yukiX, yukiY)
    
End

Private Sub drawDate()
    
    Dim yukiDateText As String = Format$(Now(), ("mm-dd-yyyy"))
    Dim yukiHeight As Float
    
    Paint.Font.Bold = True
    Paint.Font.Size = 80
    Do
        If (DrawingArea1.W - 80) >= Paint.Font.RichTextWidth(yukiDateText) Then Break
        Paint.Font.Size -= 2
        If Paint.Font.Size = 0 Then Break
    Loop
    yukiHeight = Paint.Font.RichTextHeight(yukiDateText)
    Paint.RichText(yukiDateText, 40, 0, DrawingArea1.W - 80, yukiHeight * 2, Align.Center)
    
End

Public Sub _new()

    $image = Image.Load("pictures/Nagato.Yuki.full.1430103.jpg")

End

Public Sub Form_Open()

    Me.Center()

    DrawingArea1.Refresh()

End

Public Sub DrawingArea1_Draw()

    Paint.Begin(DrawingArea1)
        Paint.Brush = Paint.Color(Color.White + 16777216 * 64)
        drawBackGround()
        drawDate()
        Paint.Fill()
    Paint.End()

End

Public Sub Form_Resize()

    Me.W = Max(400, Me.W)
    Me.H = Max(300, Me.H)

    DrawingArea1.Refresh()

End
