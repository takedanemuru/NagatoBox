' Gambas class file

Create Static

Property TodaysTask As String
Property Read TodaysTaskLastUpdated As Date

Private $settings As New Settings

Public Sub LoadFormSettings(argForm As Form)
    
    With argForm
        .X = $settings[Subst$("&1/x", .Name), .X]
        .Y = $settings[Subst$("&1/y", .Name), .Y]
        .H = $settings[Subst$("&1/h", .Name), .H]
        .W = $settings[Subst$("&1/w", .Name), .W]
    End With
    
End

Public Sub SaveFormSettings(argForm As Form)
    
    With argForm
        $settings[Subst$("&1/x", .Name)] = .X
        $settings[Subst$("&1/y", .Name)] = .Y
        $settings[Subst$("&1/h", .Name)] = .H
        $settings[Subst$("&1/w", .Name)] = .W
    End With
    
    $settings.Save()
    
End

Private Function TodaysTask_Read() As String

    Dim yukiTimeDiff As Integer = DateDiff($settings["todays_task/last_updated"], Now(), gb.Hour)
    
    Return IIf(24 > yukiTimeDiff, $settings["todays_task/body", ""], "")

End

Private Sub TodaysTask_Write(Value As String)

    $settings["todays_task/body"] = Value
    $settings["todays_task/last_updated"] = Now()
    $settings.Save()

End

Private Function TodaysTaskLastUpdated_Read() As Date

    Return $settings["todays_task/last_updated", Date(2000)]

End
