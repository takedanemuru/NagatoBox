' Gambas class file

Inherits NagatoDB

Property Read Count As Integer

Public _$uniqueName As String = "NagatoDBGitHub" ' override

Public Const TableGitHub As String = "git_hub"
Public Const ColumnId As String = "id"
Public Const ColumnUserName As String = "user_name"
Public Const ColumnRepository As String = "repository"

Private Sub ensureTable()
    
    Dim yukiTable As Table
    
    If Not Me._$connection.Tables.Exist(TableGitHub) Then
        yukiTable = Me._$connection.Tables.Add(TableGitHub)
        yukiTable.Fields.Add(ColumnId, db.Serial)
        yukiTable.Fields.Add(ColumnUserName, db.String)
        yukiTable.Fields.Add(ColumnRepository, db.String)
        yukiTable.PrimaryKey = [ColumnId]
        yukiTable.Update()
    End If
    
End

Public Sub Push(argRepository As String, argUserName As String)
    
    Dim yukiResult As Result
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Create(TableGitHub)
        yukiResult[ColumnRepository] = argRepository
        yukiResult[ColumnUserName] = argUserName
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Debug Error.Text
    Me._$connection.Rollback()
    
End

Public Sub _new()
    
    Me._SetConnection()
    ensureTable()
    
End

Private Function Count_Read() As Integer

    Return Me._$connection.Find(Me.TableGitHub).Count

End
