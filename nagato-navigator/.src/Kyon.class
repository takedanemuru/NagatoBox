' Gambas class file

Property Read Settings As Settings

Private _$settings As New Settings
Private $contextMenuRoot As New Menu(Me)
Private $contextMenu As NagatoContextMenu
Private $timer As New Timer As "Timer"
Private $autoReloadLock As Boolean = False

Private Sub popUpContextMenu()
    
    $contextMenuRoot.Children.Clear()
    $contextMenu = New NagatoContextMenu($contextMenuRoot) As "ContextMenu"
    $contextMenuRoot.Popup()
    
End

Private Sub initializeForm()
    
    NagatoSettingsCore.LoadFormPosition(Me, 0, Screen.Height - Me.Height)
    Me.Height = 100 * MikuruUX.Grid
    Me.Width = 55 * MikuruUX.Grid
    
End

Private Sub setDesktop()
    
    With NagatoSettingsDesktop
        If Desktop.Type = "?" Then NagatoFehWallpaper.ExecuteLastCommand()
        Desktop.Count = .Count
    End With
    
End

Private Sub reloadData()
    
    If Minute(Now) = 0 And If Not $autoReloadLock Then
        $autoReloadLock = True
        NagatoRss.Refresh()
        If Not NagatoOSDGMail.Closed Then NagatoGMailPoP3.Refresh()
        NagatoWeatherCache.Activate()
    Endif
    
    If Minute(Now) = 5 And If $autoReloadLock Then $autoReloadLock = False
    
End

Public Sub CallNagato(Optional argX As Integer)
    
    Dim yukiWindow As New DesktopWindow(Me.Handle)
    
    HaruhiBalloon.Close()
    yukiWindow.Desktop = Desktop.Current
    Me.Raise()
    
    If argX Then Me.X = argX
    
End

Public Sub CallNagatoWithBalloon()
    
    Dim yukiWindow As New DesktopWindow(Me.Handle)
    
    yukiWindow.Desktop = Desktop.Current
    Me.Raise()
    HaruhiBalloon.Show()
    
End

Public Sub QuitApplication()
    
    Me.Close()
    
End

Public Sub Form_Open()

    MikuruUniquenes.Check()

    setDesktop()
    MikuruEasterEgg.Message()
    Application.MainWindow = Me
    If NagatoNetworkManager.NetworkingEnabled Then 
        NagatoWeatherCache.Activate()
        NagatoRss.Refresh()
        NagatoGMailPoP3.Activate()
        MikuruWebRadio.EnsureDefault()
    End If
    initializeForm()
    NagatoDBReminder.Connect()
    MikuruDirectory.Activate("memo")
    NagatoApplicationsConfig.Recent = NagatoSettingsCore.GetRecentApps()
    NagatoMenuCache.Activate()
    NagatoMusicQueue.Activate()
    NagatoTrashHandler.Activate()
    $timer.Start()
    NagatoOSDManeuver.ShowAll()
    'NagatoOpenbox.Activate()
    HaruhiInfobar2.Show()
    
    'test
    'NagatoMusicLibrary.Initialize()
    
End

Public Sub Form_Close()
    
    ' close database-connection and save music queue.
    NagatoSettingsCore.SaveFormPosition(Me)
    'NagatoOpenbox.RevertBottomMargin()
    If NagatoMusicQueue.Count <> 0 Then NagatoSettingsMusic.SetQueue(NagatoMusicQueue.PathList)
    NagatoGMailPoP3.Save()
    NagatoDBReminder.Close()
    $contextMenuRoot.Children.Clear()
    $timer.Stop()
    Quit

End

Public Sub Form_Menu()
    
    popUpContextMenu()
    
End

Public Sub Form_KeyPress()
    
    Select Key.Code
        Case Key.Left
            Me.X -= 8 * MikuruUX.Grid
        Case Key.Right
            Me.X += 8 * MikuruUX.Grid
    End Select
    
End

Public Sub Form_MouseDown()
    
    popUpContextMenu()
    
End

Public Sub Form_Move()
    
    Me.Y = Screen.Height - Me.Height
    
End

Public Sub Timer_Timer()
    
    HaruhiInfobar2.Refresh()
    NagatoOSDManeuver.RefreshAll()
    
    reloadData()
    
End

Private Function Settings_Read() As Settings

    Return _$settings

End
