' Gambas class file

Private $menuBar As NagatoRssMenuBar

Private Sub initializeGrids()
    
    RssGrids.W = Me.W
    RssGrids.Header = GridView.Horizontal
    RssGrids.Columns.Count = 2
    RssGrids.Columns[0].Title = ("Title")
    RssGrids.Columns[0].W = RssGrids.W / 2
    RssGrids.Columns[1].Title = ("Url")
    
End

Private Sub setBackgroundColor(argRowIndex As Integer)
    
    Dim yukiColumnIndex As Integer
    
    For yukiColumnIndex = 0 To RssGrids.Columns.Max
         RssGrids[argRowIndex, yukiColumnIndex].Background = Color.Merge(Color.White, Color.Violet, 0.25)
    Next
    
End

Private Sub setData()
    
    Dim yukiCollection As Collection = NagatoSettings.FeedLines.Copy()
    Dim yukiUrl As String
    
    For Each yukiUrl In yukiCollection
        Inc RssGrids.Rows.Count
        RssGrids[RssGrids.Rows.Max, 0].Text = yukiCollection.Key
        RssGrids[RssGrids.Rows.Max, 1].Text = yukiUrl
        If Odd(RssGrids.Rows.Count) Then setBackgroundColor(RssGrids.Rows.Max)
    Next
    
End

Public Sub Form_Open()

    If Not Object.IsValid($menuBar) Then $menuBar = New NagatoRssMenuBar(MenuHBox) As "MenuBar"

    Me.Center()
    initializeGrids()
    setData()

End

Private Sub sendRemoveSignal()
    
    Dim yukiRow As Integer
    Dim yukiFlag As Boolean = False
    
    For yukiRow = 0 To RssGrids.Rows.Max
        If RssGrids.Rows[yukiRow].Selected Then
            NagatoSettings.RemoveFeed(RssGrids[yukiRow, 0].Text)
            yukiFlag = True
        Endif
    Next
    
    If yukiFlag Then NagatoRss.Refresh()
    
End

Public Sub MenuBar_Signal(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case NagatoRssMenuBar.MenuAdd
            HaruhiAddRss.ShowDialog()
        Case NagatoRssMenuBar.MenuRemove
            sendRemoveSignal()
    End Select
    
End
