' Gambas class file

Private $fortune As String

Private Function getBalloonImage(argHeight As Integer, argWidth As Integer) As Image
    
    Dim yukiBaseImage As New Image(argWidth + 2 * MikuruUx.Grid, argHeight + 8 * MikuruUx.Grid, Color.Transparent)
    Dim yukiBaseLine As Integer = argHeight + 2 * MikuruUx.Grid
    
    Paint.Begin(yukiBaseImage)
        With MikuruUx
            Paint.Brush = Paint.Color(Color.DarkBlue)
            Paint.Rectangle(0, 0, yukiBaseImage.W, argHeight + 2 * MikuruUx.Grid, 2 * MikuruUx.Grid)
            Paint.Polygon([4 * .Grid, yukiBaseLine, 6 * .Grid, yukiBaseLine, 8 * .Grid, yukiBaseImage.H, 4 * .Grid, yukiBaseLine])
            Paint.Fill()
        End With
    Paint.End()
    
    Return yukiBaseImage
    
End

Private Sub setForm()
    
    Dim yukiTextWidth As Integer = Min(Me.Font.RichTextWidth($fortune), 40 * MikuruUx.Grid)
    Dim yukiTextHeight As Integer = Me.Font.RichTextHeight($fortune, yukiTextWidth) 
    Dim yukiBaseImage As Image = getBalloonImage(yukiTextHeight, yukiTextWidth)
    
    Paint.Begin(yukiBaseImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font = Me.Font
        Paint.RichText($fortune, MikuruUx.Grid, MikuruUx.Grid, yukiTextWidth, yukiTextHeight, Align.TopLeft)
        Paint.Fill()
    Paint.End()
    
    Me.Width = yukiBaseImage.Width
    Me.Height = yukiBaseImage.Height
    Me.Picture = yukiBaseImage.Picture
    
End

Private Sub setFortune()
    
    Dim yukiFortune As String
    
    Shell "fortune" To yukiFortune 
    
    yukiFortune = Replace$(yukiFortune, gb.Cr, "<br>")
    $fortune = "YUKI.N > ...<br><br>" & Replace$(yukiFortune, gb.Lf, "<br>")
    
End

Public Sub NewFortune()
    
    Dim yukiWindow As New DesktopWindow(Me.Id)
    
    Me.Show()
    yukiWindow.Desktop = Desktop.Current
    
    setFortune()
    setForm()
    
End

Public Sub Form_Open()

    Me.X = 120
    Me.Y = 30

End

Public Sub Form_DblClick()

    Me.Close()

End
