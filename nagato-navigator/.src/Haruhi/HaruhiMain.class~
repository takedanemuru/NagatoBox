' Gambas class file

Private Const Type As String = "_NET_WM_WINDOW_TYPE"
Private Const TypeDock As String = "_NET_WM_WINDOW_TYPE_DOCK"

Private $button As NagatoStarButton
Private $drawingArea As DrawingArea
Private $contentBar As NagatoContentBar
Private $pager As NagatoPager
Private $clock As NagatoClock
Private $observer As Observer

Private Sub addDrawingArea()
    
    $drawingArea = New DrawingArea(HBox1) 
    
    With $drawingArea
        .Expand = True
        .Drop = True
    End With
    
End

Private Sub setForm()
    
    Me.H = 3 * MikuruUx.Grid
    Me.Y = Screen.H - Me.H
    Me.X = 0
    Me.W = Screen.W
    
    Shell Subst("xprop -id &1 -f &2 32a -set &2 &3 && openbox --restart", Me.Id, Type, TypeDock) Wait
    
End

Public Sub Form_Open()

    setForm()
    NagatoTimer.Activate()

    Me.Background = NagatoSettings.BackgroundColor
    $button = New NagatoStarButton(HBox1) As "StarButton"
    addDrawingArea()
    $contentBar = New NagatoContentBar($drawingArea) As "contentBar"
    $pager = New NagatoPager(HBox1) As "Pager"
    $clock = New NagatoClock(HBox1) As "Clock"
    $observer = New Observer(NagatoTimer) As "Asakura"
    
End

Public Sub Form_Close()
    
    NagatoDBus.Unregister()
    
End

Public Sub Form_Enter()

    If Not NagatoSettings.ShowNagato Then Return

    If Mouse.ScreenY > Screen.H - 2 Then HaruhiPersona.EnsureVisible()
        
End

Public Sub StarButton_Signal(argSignal As Integer, argValues As Variant[])
    
    If argSignal = MikuruSignal.ModeChanged Then
        $contentBar.Swich(argValues[0])
    Endif
    
End

Public Sub Asakura_Timer()
    
    setForm()
    
End
