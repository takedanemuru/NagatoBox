' Gambas class file

Private Function getCurrentApplicationList() As Object[]
    
    Select Case CategoryComboBox.Index
        Case 0
            Return (NagatoMenuCache.AllApplications)
        Case 1
            Return (NagatoMenuCache.NagatoSoftwareCompilationApplications)
        Case 2
            Return (NagatoMenuCache.Developments)
        Case 3
            Return (NagatoMenuCache.Educations)
        Case 4
            Return (NagatoMenuCache.Games)
        Case 5
            Return (NagatoMenuCache.Graphics)
        Case 6
            Return (NagatoMenuCache.Networks)
        Case 7
            Return (NagatoMenuCache.Medias)
        Case 8
            Return (NagatoMenuCache.Offices)
        Case 9
            Return (NagatoMenuCache.Settings)
        Case 10
            Return (NagatoMenuCache.Systems)
        Case 11
            Return (NagatoMenuCache.Utilities)
        Case 12
            Return (NagatoMenuCache.Kde4)
    End Select
    
End

Private Sub addDesktopFileIcon(argDesktopFile As NagatoDesktopFile)
    
    Dim yukiIcon As Picture = argDesktopFile.GetIcon(8 * MikuruUX.Grid)
    
    ' To avoid key error, because KDE apps are added twice in menu-cache. 
    If ApplicationsIconView.Exist(argDesktopFile.Path) Then Return
    
    ApplicationsIconView.Add(argDesktopFile.Path, argDesktopFile.CommonName, yukiIcon)
    
End

Private Sub setListView(argApplicationList As Object[])
    
    Dim yukiDesktopFile As NagatoDesktopFile

    ApplicationsIconView.Clear()

    For Each yukiDesktopFile In argApplicationList
        addDesktopFileIcon(yukiDesktopFile)
    Next
    
End

Private Function containsSearchQuery(argDesktopFile As NagatoDesktopFile) As Boolean
    
    With argDesktopFile
        If .CommonName Like Subst$("*&1*", SearchButtonBox.Text) Then Return True
        If .Name Like Subst$("*&1*", SearchButtonBox.Text) Then Return True
        If .GenericName Like Subst$("*&1*", SearchButtonBox.Text) Then Return True
        If .Comment Like Subst$("*&1*", SearchButtonBox.Text) Then Return True
        If .LocaleGenericName Like Subst$("*&1*", SearchButtonBox.Text) Then Return True
        If .LocaleComment Like Subst$("*&1*", SearchButtonBox.Text) Then Return True
    End With
    
    Return False
    
End

Private Sub setListViewWithSearchQuery()
    
    Dim yukiDesktopFile As NagatoDesktopFile

    ApplicationsIconView.Clear()

    For Each yukiDesktopFile In getCurrentApplicationList()
        If Not containsSearchQuery(yukiDesktopFile) Then Continue
        addDesktopFileIcon(yukiDesktopFile)
    Next
    
End

Public Sub Form_Show()

    setListView(NagatoMenuCache.AllApplications)

End

Public Sub ApplicationsIconView_Activate()

    NagatoDesktopFile.Execute(ApplicationsIconView.Key)

End

Public Sub CategoryComboBox_Click()

    setListView(getCurrentApplicationList())
    
End

Public Sub SearchButtonBox_Click()

    If Replace$(SearchButtonBox.Text, " ", "") = "" Then Stop Event

    setListViewWithSearchQuery()

End

Public Sub SearchButtonBox_KeyPress()

    If Key.Code = 16777220 Then 
        SearchButtonBox_Click()
    Else If Key.Code = Key.Enter Then
        SearchButtonBox_Click()
    End If 

End
