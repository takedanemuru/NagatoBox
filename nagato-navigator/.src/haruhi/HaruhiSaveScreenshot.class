' Gambas class file

Private Const MaxSize As Integer = 200

Private $screenshot As Picture

Private Function getPreview() As Picture
    
    Dim yukiRate As Float = Min(MaxSize / $screenshot.H, MaxSize / $screenshot.W)
    
    With $screenshot
        Return .Image.Stretch(.W * yukiRate, .H * yukiRate).Picture
    End With
    
End

Private Sub initializePreview()
    
    Dim yukiPreview As Picture = getPreview()
    
    With PreviewPictureBox
        .Picture = yukiPreview
        .H = yukiPreview.H
        .W = yukiPreview.W
    End With
    
End

Private Sub initializeDirectoryButtonBox()
    
    With DirectoryButtonBox
        .Text = NagatoSettingsScreenshooter.Directory
        .Picture = IIf(.Text = User.Home, Stock["small/home"], Stock["small/directory"])
    End With
    
End

Private Sub initializeFileNameTextBox()
    
    Dim yukiTimeStamp As String = Format$(Now, ("yyyy/mm/dd_hh:nn:mm"))
    
    FileNameTextBox.Text = Subst$("screenshot_&1", yukiTimeStamp)
    
End

Private Function getPath() As String
    
    Dim yukiPath As String = DirectoryButtonBox.Text &/ FileNameTextBox.Text 
    Dim yukiCount As Integer = 0
    
    If Not Exist(yukiPath & ".png") Then Return yukiPath & ".png"
    
    Do
        Inc yukiCount
        If Not Exist(Subst$("&1(&2).png", yukiPath, yukiCount)) Then Return Subst$("&1(&2).png", yukiPath, yukiCount)
    Loop
    
End

Public Sub Ask(argPicture As Picture)
    
    Me.Center()
    Me.Show()
    $screenshot = argPicture
    initializePreview()
    initializeDirectoryButtonBox()
    initializeFileNameTextBox()
    
End

Public Sub SaveButton_Click()

    $screenshot.Save(getPath())
    If CheckBox1.Value Then NagatoSettingsScreenshooter.Directory = DirectoryButtonBox.Text
    HaruhiBalloon.SetText(("Screenshot is saved."))
    Me.Close()

End

Public Sub CancelButton_Click()

    Me.Close()

End

Public Sub DirectoryButtonBox_Click()

    Dialog.Path = User.Home
    Dialog.Title = ("Select directory")

    If Not Dialog.SelectDirectory() Then
        With DirectoryButtonBox
            .Text = Dialog.Path
            .Picture = IIf(Dialog.Path = User.Home, Stock["small/home"], Stock["small/directory"])
            CheckBox1.Visible = True
        End With
    Endif

End
