' Gambas class file

Inherits NagatoSignal

Private $button As MenuButton
Private $rootMenu As Menu
Private $observer As Observer
Private $deviceIndex As Integer = 0

Private Sub initializeButton(argContainer As Container)
    
    $button = New MenuButton(argContainer) As "NetworkButton"
    
    With $button
        .Picture = MikuruIcon.GetNetworkDeviceIcon($deviceIndex)
        .Border = False
        .Foreground = NagatoSettings.ForegroundColor
        .AutoResize = True
        .Arrow = False
    End With
    
End

Private Sub addRootMenu()
    
    Dim yukiDummyItem As Menu
    
    $rootMenu = New Menu(HaruhiMain) As "RootMenu"
    $rootMenu.Name = "NetworkDevicesRootMenu"
    
    yukiDummyItem = New Menu($rootMenu)
    yukiDummyItem.Text = "Not Ready"
     
    $button.Menu = "NetworkDevicesRootMenu"
    
End

Public Sub _new(argContainer As Container)
    
    Debug NagatoNetworkManager.DeviceCount
    
    initializeButton(argContainer)
    addRootMenu()
    $observer = New Observer(NagatoTimer) As "Asakura"
    
End

Private Sub setButtonText()
    
    Dim yukiSpeed As Float = NagatoNetworkManager.GetSpeed($deviceIndex)
    Dim yukiText As String = Format$(yukiSpeed / 1000, "##0Mbps")
    
    If $button.Text <> yukiText Then
        $button.Text = yukiText
        '$button.Picture = MikuruIcon.GetNetworkDeviceIcon($deviceIndex)
    Endif
    
End

Public Sub Asakura_Timer()
    
    $button.Visible = NagatoSettings.ShowNetworkButton
    
    If Not $button.Visible Then Return
    If NagatoNetworkManager.DeviceCount = 0 Then Return
    
    Debug NagatoNetworkManager.DeviceCount
    
    '$button.Picture = MikuruIcon.GetNetworkDeviceIcon($deviceIndex)
    setButtonText()

End

Public Sub RootMenu_Show()
    
    Dim yukiMenu As NagatoNetworkDevicesMenu
    
    $rootMenu.Children.Clear()
    yukiMenu = New NagatoNetworkDevicesMenu($rootMenu) As "NetworkDevicesMenu"
    
End

Public Sub NetworkDevicesMenu_Selected(argIndex As Integer, argPicture As Picture)
    
    $deviceIndex = argIndex
    $button.Picture = argPicture
    
End
