' Gambas class file

Inherits NagatoSignal

Private $container As Container
Private $button As MenuButton
Private $rootMenu As Menu 
Private $preferenceMenu As NagatoPreferenceMenu
Private $modeMenu As NagatoModeMenu

Private Sub addButton()
    
    $button = New MenuButton($container) 
    
    With $button
        .Width = $container.H
        .Expand = False
        .Picture = MikuruIcon.Get("star")
        .Arrow = False
        .Border = False
    End With
    
End

Private Sub addRootMenu()
    
    $rootMenu = New Menu(HaruhiMain)
    $rootMenu.Name = "RootMenu"
    $button.Menu = "RootMenu"
    
End

Private Sub addMenuItem(argText As String, argTag As Integer, Optional argIconName As String)
    
    Dim yukiMenu As New Menu($rootMenu) As "MenuItem"
    
    With yukiMenu
        .Text = (argText)
        .Tag = argTag
        If argIconName Then .Picture = MikuruIcon.Get(argIconName)
    End With
    
End

Public Sub _new(argContainer As Container)
    
    $container = argContainer
    addButton()
    addRootMenu()
    
    $preferenceMenu = New NagatoPreferenceMenu($rootMenu) As "NagatoSignal"
    $modeMenu = New NagatoModeMenu($rootMenu) As "NagatoSignal"
    
    addMenuItem("", 0)
    addMenuItem(("About"), 1, "star")
    'addMenuItem(("Close"), 2, "button-cross")
    
End

Public Sub MenuItem_Click()
    
    Select Case Last.Tag
        Case 1
            HaruhiAbout.ShowDialog()
        Case 2
            HaruhiMain.Close()
    End Select
    
End
