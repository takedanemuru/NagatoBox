' Gambas class file

Inherits NagatoSignal

Public Enum MenuOpen, MenuEdit, MenuSelect

Private $rootMenu As Menu

Event Selected(argIndex As Integer)

Private Sub initializeRootMenu()
    
    $rootMenu = New Menu(HaruhiMain)
    
    With $rootMenu
        .Visible = False
    End With
    
End

Private Sub addMenuItem(argText As String, argTag As Integer, Optional argIconName As String)
    
    Dim yukiMenu As New Menu($rootMenu) As "MenuItem"
    
    With yukiMenu
        .Text = argText
        .Tag = argTag
        If argIconName Then .Picture = MikuruIcon.Get(argIconName)
    End With
    
End

Private Sub initializeRssMenus()
    
    Dim yukiMenu As Menu
    Dim yukiCount As Integer
    
    For yukiCount = 0 To NagatoRss.Feeds.Max
        yukiMenu = New Menu($rootMenu) As "RssMenu"
        yukiMenu.Text = NagatoRss.Feeds[yukiCount].Title
        yukiMenu.Tag = yukiCount
    Next
    
End

Public Sub PopUp()
    
    $rootMenu.Popup()
    
End

Public Sub _new()
    
    initializeRootMenu()
    addMenuItem(("Open with browser"), Me.MenuOpen, "web")
    addMenuItem(("Edit feed"), Me.MenuEdit, "document-edit")
    addMenuItem("", 0) 'as blank  
    initializeRssMenus()
    
End

Public Sub MenuItem_Click()
    
    Raise Signal(Last.Tag, [""])
    
End

Public Sub RssMenu_Click()
    
    Raise Signal(Me.MenuSelect, [Last.Tag])
    
End
