' Gambas class file

Private $drawingArea As DrawingArea
Private $tasks As New NagatoTasks
Private $observer As Observer

Private Sub addDrawingArea(argContainer As Container)
    
    $drawingArea = New DrawingArea(argContainer) As "DrawingArea"
    
    With $drawingArea
        .Expand = True
    End With
    
End

Private Sub paintTask(argWindow As DesktopWindow, argWidth As Integer, ByRef refX As Integer)
    
    Dim yukiText As String
    
    Paint.DrawImage(argWindow.Icon.Stretch(2 * MikuruUx.Grid, 2 * MikuruUx.Grid), refX + 4, 4)
    yukiText = MikuruCollapsedText.Get(argWindow.Name, $drawingArea.Font, argWidth - 3 * MikuruUx.Grid)
    Paint.Text(yukiText, refX + 3 * MikuruUx.Grid, 0, argWidth - 3 * MikuruUx.Grid, $drawingArea.H, Align.Left)
    refX += argWidth
    
Catch
    Debug "ignore"
    Return
    
End

Public Sub _new(argContainer As Container)
    
    addDrawingArea(argContainer)
    $observer = New Observer(NagatoTimer) As "Asakura"
    
End

Public Sub Asakura_Timer()
    
    $drawingArea.Refresh()
    
End

Public Sub DrawingArea_Draw()
    
    Dim yukiWidth As Integer
    Dim yukiX As Integer
    Dim yukiWindow As DesktopWindow
    Dim yukiTasks As DesktopWindow[]
    
    If $tasks.Count = 0 Then Return
    
    yukiTasks = $tasks.Get()
    
    yukiWidth = $drawingArea.W / yukiTasks.Count
    
    Paint.Begin($drawingArea)
    Paint.Brush = Paint.Color(Color.White)
        For Each yukiWindow In yukiTasks
            paintTask(yukiWindow, yukiWidth, ByRef yukiX)
        Next
    Paint.Fill()
    Paint.End()
    
End

Public Sub DrawingArea_MouseDown()
    
    Dim yukiTasks As DesktopWindow[] = $tasks.Get()
    Dim yukiWidth As Integer = $drawingArea.W / yukiTasks.Count
    Dim yukiIndex As Integer = Mouse.X / yukiWidth
    
    If Mouse.Left Then
        Try Debug yukiTasks[yukiIndex].Name
        Try Desktop.Current = yukiTasks[yukiIndex].Desktop
        Try yukiTasks[yukiIndex].Activate()
    End If
    
End
