' Gambas class file

Private $observer As Observer
Private $painter As Object
Private $drawingArea As DrawingArea
Private $taskIcons As NagatoTasksIconOnly

Private Sub initializeDrawingArea(argContainer As Container)
    
    $drawingArea = New DrawingArea(argContainer) As "DrawingArea"
    
    With $drawingArea
        .Expand = True
        .Drop = True
    End With
    
End

Public Sub Swich(argMode As Integer)
    
    Select Case argMode
        Case NagatoModeMenu.ModeTasks
            NagatoTimer.Delay = 1000
            $taskIcons.Visible = False
            $painter = New NagatoTaskBar($drawingArea)
        Case NagatoModeMenu.ModeTasks2
            NagatoTimer.Delay = 1000
            $taskIcons.Visible = False
            $painter = New NagatoTaskBarTint2Mode($drawingArea)
        Case NagatoModeMenu.ModeSystem
            NagatoTimer.Delay = 1000
            $taskIcons.Visible = True
            $painter = New NagatoSystemMonitor($drawingArea)
        Case NagatoModeMenu.ModeMusic
            NagatoTimer.Delay = 1000
            $taskIcons.Visible = True
            $painter = New NagatoMusic($drawingArea)
        Case NagatoModeMenu.ModeRss
            NagatoTimer.Delay = 100
            $taskIcons.Visible = True
            $painter = New NagatoRssTicker($drawingArea)
    End Select
    
End

Public Sub _new(argContainer As Container)
    
    $taskIcons = New NagatoTasksIconOnly(argContainer)
    $taskIcons.Visible = False
    initializeDrawingArea(argContainer)
    $observer = New Observer(NagatoTimer) As "Asakura"
    $painter = New NagatoTaskBarTint2Mode($drawingArea)
    
End

Public Sub Asakura_Timer()
    
    $taskIcons.Refresh()
    $drawingArea.Refresh()
    
End

Public Sub DrawingArea_Draw()
    
    $painter.Paint()
    
End

Public Sub DrawingArea_MouseDown()
    
    If Mouse.Left Then
        $painter.LeftClick(Mouse.X, Mouse.Y)
    Else If Mouse.Right Then
        $painter.RightClick(Mouse.X, Mouse.Y)
    Endif
    
End

Public Sub DrawingArea_MouseDrag()
    
    $drawingArea.Drop = True
    
    $painter.DragBegin(Mouse.X, Mouse.Y)
    
End

Public Sub DrawingArea_Drop()
    
    $painter.Drop(Drag.X, Drag.Y)
    
End
