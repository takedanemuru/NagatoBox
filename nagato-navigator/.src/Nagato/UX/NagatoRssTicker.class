' Gambas class file

Inherits NagatoPainter

Private $count As Integer 
Private $feedIndex As Integer
Private $itemIndex As Integer

Private Sub setIndex(Optional argFeedIndex As Integer = 0)
    
    $feedIndex = argFeedIndex
    $itemIndex = 0
    $count = 0
    
End

Private Sub refreshFeed()
    
    NagatoRss.Refresh()
    setIndex()
    
End

Private Sub changeItem()
    
    $count = 0
    Inc $itemIndex
    
    If $itemIndex > NagatoRss.Feeds[$feedIndex].Items.Max Then
        $itemIndex = 0
        Inc $feedIndex
        If $feedIndex > NagatoRss.Feeds.Max Then refreshFeed()
    Endif
    
End

Private Function getCursor() As String
    
    If $count > String.Len(NagatoRss.Feeds[$feedIndex].Items[$itemIndex].Title) And If Odd($count Div 4) Then
        Return ""
    Else
        Return "_"
    Endif
    
End

Private Function getTitle() As String
    
    Dim yukiCount As String = Subst$("(&1/&2)", $itemIndex + 1, NagatoRss.Feeds[$feedIndex].Items.Count)
    
    If NagatoRss.Feeds[$feedIndex].Title = "" Then
        Return ""
    Else
        Return NagatoRss.Feeds[$feedIndex].Title & " " & yukiCount & "  "
    End If
    
End

Private Function getBody() As String
    
  Return String.Left(NagatoRss.Feeds[$feedIndex].Items[$itemIndex].Title, $count)
    
End

Private Function getTicker() As String
    
    Inc $count
    If $count = (String.Len(NagatoRss.Feeds[$feedIndex].Items[$itemIndex].Title) + 50) Then
        changeItem()
        Me._$drawingArea.Tooltip = NagatoRss.Feeds[$feedIndex].Items[$itemIndex].Description
    End If
    
    Return getTitle() & getBody() & getCursor()
    
Catch
    Return "YUKI.N > RSS Feed is not ready."
    setIndex()
    
End

Private Sub getText() As String
    
    If NagatoRss.Feeds.Count = 0 Then
        Return "YUKI.N > RSS Feed is not ready."
    Else
        Return getTicker()
    Endif
    
End

Public Sub Paint()
    
    Paint.Begin(Me._$drawingArea)
        Paint.Brush = Paint.Color(NagatoSettings.ForegroundColor)
        Paint.Font.Bold = True
        Paint.Text(getText(), 0, 0, Me._$drawingArea.W, Me._$drawingArea.H, Align.Left)
        Paint.Fill()
    Paint.End()
    
End

Public Sub LeftClick(argMouseX As Integer, argMouseY As Integer)
    
    HaruhiPersona.SetText(NagatoRss.Feeds[$feedIndex].Items[$itemIndex].Description)
    
End

Public Sub RightClick(argMouseX As Integer, argMouseY As Integer)
    
    Dim yukiMenu As New NagatoContextMenuRss As "ContextMenu"
    
    yukiMenu.PopUp()
    
End

Public Sub ContextMenu_Signal(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case NagatoContextMenuRss.MenuOpen
            Shell Subst$("nagato-web &1", NagatoRss.Feeds[$feedIndex].Items[$itemIndex].Link)
        Case NagatoContextMenuRss.MenuRefresh
            refreshFeed()
        Case NagatoContextMenuRss.MenuEdit
            HaruhiRssEdit.ShowDialog()
        Case NagatoContextMenuRss.MenuSelect
            setIndex(argValues[0])
    End Select
    
End
