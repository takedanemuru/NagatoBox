' Gambas class file

Private $watcher As DesktopWatcher
Private $drawingArea As DrawingArea

Private Sub refresh()
    
    $drawingArea.W = 3 * MikuruUx.Grid * Desktop.Count
    $drawingArea.Refresh()
    
End

Private Function getBoxRectangle(argIndex As Integer) As Rect
    
    Dim yukiRect As New Rect
    
    With yukiRect
        .H = 3 * MikuruUx.Grid - 4
        .W = 3 * MikuruUx.Grid - 4
        .X = 3 * MikuruUx.Grid * argIndex + 2
        .Y = 2
    End With
    
    Return yukiRect
    
End

Private Sub paintBox(argIndex As Integer, argRectangle As Rect)
    
    Paint.Brush = Paint.Color(IIf(argIndex = Desktop.Current, Color.Yellow, Color.White))
        Paint.Rectangle(argRectangle.X, argRectangle.Y, argRectangle.W, argRectangle.H)
    Paint.Fill()
    
End

Private Sub paintNumber(argIndex As Integer, argRectangle As Rect)
    
    Paint.Brush = Paint.Color(NagatoSettings.BackgroundColor)
        Paint.Text(argIndex + 1, argRectangle.X, argRectangle.Y, argRectangle.W, argRectangle.H, Align.Center)
    Paint.Fill()
    
End

Public Sub _new(argContainer As Container)
    
    $drawingArea = New DrawingArea(argContainer) As "DrawingArea"
    refresh()
    
    $watcher = New DesktopWatcher(True) As "Watcher"
    
End

Public Sub DrawingArea_Menu()
    
    Dim yukiMenu As New NagatoContextMenuPager
    
    yukiMenu.PopUp()
    
End

Public Sub DrawingArea_Draw()
    
    Dim yukiCount As Integer
    Dim yukiRect As Rect
    
    Paint.Begin($drawingArea)
        For yukiCount = 0 To Desktop.Count - 1
            yukiRect = getBoxRectangle(yukiCount)
            paintBox(yukiCount, yukiRect)
            paintNumber(yukiCount, yukiRect)
        Next
    Paint.End()
    
End

Public Sub DrawingArea_MouseDown()
    
    If Mouse.Left Then Desktop.Current = Mouse.X Div (3 * MikuruUx.Grid)
    
End

Public Sub Watcher_Count()
    
    refresh()
    
End

Public Sub Watcher_Change()
    
    refresh()
    
End
