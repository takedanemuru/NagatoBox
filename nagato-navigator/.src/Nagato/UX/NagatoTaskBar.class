' Gambas class file

Inherits NagatoPainter

Private $tasks As New NagatoTasks

Private Sub paintBox(argWidth As Integer, argX As Integer)
    
    Paint.Brush = Paint.Color(Color.Pink)
        Paint.Rectangle(argX, 0, argWidth, 3 * MikuruUx.Grid)
    Paint.Fill()
    
End

Private Sub paintTask(argWindow As DesktopWindow, argWidth As Integer, ByRef refX As Integer)
    
    Dim yukiText As String
    
    If argWindow.Id = Desktop.ActiveWindow Then paintBox(argWidth, refX)
    
    Paint.Brush = Paint.Color(NagatoSettings.ForegroundColor)
    Paint.DrawImage(argWindow.Icon.Stretch(2 * MikuruUx.Grid, 2 * MikuruUx.Grid), refX + 4, 4)
    yukiText = MikuruCollapsedText.Get(argWindow.Name, Me._$drawingArea.Font, argWidth - 3 * MikuruUx.Grid)
    Paint.Text(yukiText, refX + 3 * MikuruUx.Grid, 0, argWidth - 3 * MikuruUx.Grid, Me._$drawingArea.H, Align.Left)
    refX += argWidth
    Paint.Fill()
    
Catch
    Return
    
End

Public Sub Paint()
    
    Dim yukiWidth As Integer
    Dim yukiX As Integer
    Dim yukiWindow As DesktopWindow
    Dim yukiTasks As DesktopWindow[]
    
    If $tasks.Count = 0 Then Return
    
    yukiTasks = $tasks.Get()
    yukiWidth = Me._$drawingArea.W / yukiTasks.Count
    
    Paint.Begin(Me._$drawingArea)
        For Each yukiWindow In yukiTasks
            paintTask(yukiWindow, yukiWidth, ByRef yukiX)
        Next
    Paint.End()
    
Catch
    Return
    
End

Public Sub LeftClick(argMouseX As Integer, argMouseY As Integer)
    
    Dim yukiTasks As DesktopWindow[] = $tasks.Get()
    Dim yukiWidth As Integer = Me._$drawingArea.W / yukiTasks.Count
    Dim yukiIndex As Integer = argMouseX / yukiWidth
    
    If Mouse.Left Then
        Try Debug yukiTasks[yukiIndex].Name
        Try Desktop.Current = yukiTasks[yukiIndex].Desktop
        Try yukiTasks[yukiIndex].Activate()
    End If
    
Catch
    Return 'to avoid division by zero
    
End

Public Sub Watcher_Change()
    
    Me.Paint()
    
End

Public Sub Watcher_Count()
    
    Me.Paint()
    
End
