' Gambas class file

Create Static

Property Read Activated As Boolean
Property Read DeviceCount As Integer

Private Const Service As String = "org.gambas.nagato.network.manager"
Private Const ObjectPath As String = "/org/nagato/network/manager"
Private Const Interface As String = "org.gambas.nagato.network.manager.nagatointerface"

Private $proxy As DBusProxy

Public Function GetSpeed(argDeviceIndex As Integer) As Float
    
    If Not Object.IsValid($proxy) Then Return 0
    If argDeviceIndex > Me.DeviceCount - 1 Then Return 0
    
    Return $proxy.GetDeviceSpeed(argDeviceIndex)
    
Catch
    Return 0
    
End

Public Function GetDeviceTypeReadable(argDeviceIndex As Integer) As String
    
    Return $proxy.GetDeviceTypeReadable(argDeviceIndex)
    
Catch
    Return ""
    
End

Public Function GetDeviceAddress(argDeviceIndex As Integer) As String
    
    Return $proxy.GetDeviceAddress(argDeviceIndex)
    
Catch
    Return ""
    
End

Public Sub Activate()
    
    If Not DBus.Session.Applications.Exist(Service) Then
        Shell "nagato-network-manager -n"
    Endif
    
End

Private Sub initializeProxy()
    
    $proxy = DBus["Session://" & Service][ObjectPath, Interface]
    
End

Private Function DeviceCount_Read() As Integer

    If Not DBus.Session.Applications.Exist(Service) Then
        Return 0
    Else If Not Object.IsValid($proxy) Then 
        initializeProxy()
        Return 0
    Else
        Return $proxy.DeviceCount
    End If

Catch
    Return 0

End

Private Function Activated_Read() As Boolean

    Return DBus.Session.Applications.Exist(Service)

End
