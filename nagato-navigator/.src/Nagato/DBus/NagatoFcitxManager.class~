' Gambas class file

Create Static

Property Read HasService As Boolean
Property Read Activated As Boolean
Property Read Status As String
Property Read Title As String

Private Const Service As String = "org.fcitx.Fcitx-0"
Private Const ObjectPath As String = "/StatusNotifierItem"
Private Const Interface As String = "org.kde.StatusNotifierItem"

Private $proxy As DBusProxy

Public Sub Activate()
    
    $proxy = DBus["Session://" & Service][ObjectPath, Interface]
    
End

Private Function HasService_Read() As Boolean
    
    Dim yukiProxy As DBusProxy = DBus["Session://org.freedesktop.DBus"]["/", "org.freedesktop.DBus"]
    
    If DBus.Session.Applications.Exist(Service) Then Return True
    
    Return yukiProxy.ListActivatableNames().Exist(Service)
    
End

Private Function Activated_Read() As Boolean

    Return DBus.Session.Applications.Exist(Service)

End

Private Function Status_Read() As String

    If Not Object.IsValid($proxy) Then Return ""

    Return $proxy.Status

End

Private Function Title_Read() As String

    If Not Object.IsValid($proxy) Then Return ""

    Return $proxy.Title

End
