' Gambas class file

Inherits NagatoSignal

Private $rootMenu As Menu

Private Sub addDummyItem()
    
    Dim yukiMenu As New Menu($rootMenu)
    
    yukiMenu.Text = "Not Ready"
    
End

Private Sub addRootMenu(argMenuButton As MenuButton)
    
    $rootMenu = New Menu(HaruhiMain) As "RootMenu"
    $rootMenu.Name = "CalendarRootMenu"
    argMenuButton.Menu = $rootMenu.Name
    
    addDummyItem()
    
End

Private Sub addIssueMenu(argParentMenu As Menu, argIssues As String[])
    
    Dim yukiMenu As Menu
    Dim yukiIssue As String
    Dim yukiCount As Integer
    
    For Each yukiIssue In argIssues
        yukiMenu = New Menu(argParentMenu)
        yukiMenu.Text = yukiIssue
        If yukiCount >= 30 Then Break
        Inc yukiCount
    Next
    
End

Private Sub addTodaysTaskMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "ShowCalendarMenu"
    
    yukiMenu.Text = ("Show calendar")
    yukiMenu.Picture = MikuruIcon.Get("calendar")
    
End

Private Sub addSpacer()
    
    Dim yukiMenu As New Menu($rootMenu)
    
    yukiMenu.Text = ""
    
End

Private Sub refreshChildMenus()
    
    Dim yukiIndex As Integer
    Dim yukiMenu As Menu
    Dim yukiIssues As String[]
    
    addTodaysTaskMenu()
    addSpacer()
    
    For yukiIndex = 0 To NagatoCalendarConnection.GetRepositoryCount() - 1
        yukiMenu = New Menu($rootMenu)
        yukiIssues = NagatoCalendarConnection.GetRepositoryIssues(yukiIndex)
        yukiMenu.Text = NagatoCalendarConnection.GetRepositoryName(yukiIndex)
        yukiMenu.Text &= Subst$(" (&1)", yukiIssues.Count)
        If yukiIssues.Count > 0 Then addIssueMenu(yukiMenu, yukiIssues)
    Next
    
End

Public Sub _new(argMenuButton As MenuButton)
    
    addRootMenu(argMenuButton)
    
End

Public Sub RootMenu_Show()
    
    $rootMenu.Children.Clear()
    refreshChildMenus()
    
End
