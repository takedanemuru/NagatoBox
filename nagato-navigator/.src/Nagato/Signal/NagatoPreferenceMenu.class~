' Gambas class file

Inherits NagatoSubMenu

Private Enum MenuNagato, MenuNetwork, MenuPower, MenuForeground, MenuBackground, MenuFormat

Private Sub setBackground()
    
    If HaruhiColorChooser.ShowDialog() = HaruhiColorChooser.ResponseApply Then
        HaruhiMain.Background = MikuruDialog.Color
        NagatoSettings.BackgroundColor = MikuruDialog.Color
    Endif
    
End

Private Sub setForeground()
    
    HaruhiColorChooser.SetColor(NagatoSettings.ForegroundColor)
    
    If HaruhiColorChooser.ShowDialog() = HaruhiColorChooser.ResponseApply Then
        NagatoSettings.ForegroundColor = MikuruDialog.Color
    Endif
    
End

Public Sub _InitializeParentMenu(argRootMenu As Menu)
    
    Me._$parentMenu = New Menu(argRootMenu) As "ParentMenu"
    
    With Me._$parentMenu
        .Text = ("Preference")
        .Picture = MikuruIcon.Get("equalizer")
    End With
    
End

Private Sub refreshMenu()
    
    Me._$parentMenu.Children.Clear()
    
    With NagatoSettings
        Me._AddMenu(("Show Nagato"), MenuNagato, IIf(.ShowNagato, MikuruIcon.Get("button-check"), Null))
        Me._AddMenu(("Show Network Button"), MenuNetwork, IIf(.ShowNetworkButton, MikuruIcon.Get("button-check"), Null))
        Me._AddMenu(("Show Power Button"), MenuPower, IIf(.ShowPowerButon, MikuruIcon.Get("button-check"), Null))
            Me._AddMenu((""), 0)
        Me._AddMenu(("Form color"), MenuBackground, MikuruColorChip.Get(.BackgroundColor))
        Me._AddMenu(("Font color"), MenuForeground, MikuruColorChip.Get(.ForegroundColor))
            Me._AddMenu((""), 0)
        Me._AddMenu(("Clock format"), MenuFormat, MikuruIcon.Get("alarm-clock"))
    End With
    
End

Public Sub _InitializeMenus()
    
    Me._AddMenu("", 0) ' as dummy menu to get "Show" event
    
End

Public Sub ParentMenu_Show()
    
    refreshMenu()
    
End

Public Sub MenuItem_Click()
    
    Select Case Last.Tag
        Case MenuNagato
            NagatoSettings.ShowNagato = Not NagatoSettings.ShowNagato
        Case MenuPower
            NagatoSettings.ShowPowerButon = Not NagatoSettings.ShowPowerButon
        Case MenuNetwork
            NagatoSettings.ShowNetworkButton = Not NagatoSettings.ShowNetworkButton
        Case MenuBackground
            setBackground()
        Case MenuForeground
            setForeground()
        Case MenuFormat
            HaruhiClockFormat.ShowDialog()
    End Select
    
End
