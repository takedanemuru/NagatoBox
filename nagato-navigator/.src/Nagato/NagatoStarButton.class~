' Gambas class file

Inherits NagatoSignal

Private $container As Container
Private $button As MenuButton
Private $rootMenu As Menu 
Private $preferenceMenu As NagatoPreferenceMenu
Private $modeMenu As NagatoModeMenu

Private Sub addButton()
    
    $button = New MenuButton($container) 
    
    With $button
        .Width = $container.H
        .Expand = False
        .Picture = MikuruIcon.Get("star")
        .Arrow = False
        .Border = False
    End With
    
End

Private Sub addRootMenu()
    
    $rootMenu = New Menu(HaruhiMain) As "RootMenu"
    $rootMenu.Name = "RootMenu"
    $button.Menu = "RootMenu"
    
End

Private Sub addMenuItem(argText As String, argTag As Integer, Optional argIconName As String)
    
    Dim yukiMenu As New Menu($rootMenu) As "MenuItem"
    
    With yukiMenu
        .Text = (argText)
        .Tag = argTag
        If argIconName Then .Picture = MikuruIcon.Get(argIconName)
    End With
    
End

Private Sub addTaskMenuItem(argDesktopWindow As DesktopWindow)
    
    Dim yukiMenu As New Menu($rootMenu) As "TaskMenuItem"
    
    With yukiMenu
        .Text = argDesktopWindow.Name
        .Tag = argDesktopWindow
        Try .Picture = argDesktopWindow.Icon.Picture
    End With
    
End

Private Sub addTaskMenu()
    
    Dim yukiDesktopWindow As DesktopWindow
    
    For Each yukiDesktopWindow In Desktop.Windows
        If Not yukiDesktopWindow.SkipTaskbar Then addTaskMenuItem(yukiDesktopWindow)
        'addMenuItem(yukiDesktopWindow.Name, yukiDesktopWindow.Id)
    Next
    
End

Public Sub _new(argContainer As Container)
    
    $container = argContainer
    addButton()
    addRootMenu()
    addMenuItem("can you see this ?", 0)
    
End

Public Sub MenuItem_Click()
    
    Select Case Last.Tag
        Case 1
            HaruhiAbout.ShowDialog()
        Case 2
            HaruhiMain.Close()
    End Select
    
End

Public Sub TaskMenuItem_Click()
    
    Dim yukiDesktopWindow As DesktopWindow = Last.Tag
    
    Desktop.Current = yukiDesktopWindow.Desktop
    yukiDesktopWindow.Activate()
    
End

Public Sub RootMenu_Show()
    
    $rootMenu.Children.Clear()
    
    $preferenceMenu = New NagatoPreferenceMenu($rootMenu) As "NagatoSignal"
    $modeMenu = New NagatoModeMenu($rootMenu) As "NagatoSignal"
    
        addMenuItem("", 0)
    addTaskMenu()
        addMenuItem("", 0)
    addMenuItem(("About nagato-navigator"), 1, "star")
    'addMenuItem(("Close"), 2, "button-cross")
    
End
