' Gambas class file

Create Static

Property ClockFormat As String
Property ForegroundColor As Integer
Property BackgroundColor As Integer
Property ShowNagato As Boolean
Property FeedLines As Collection

Private $settings As New Settings

Public Sub AddFeed(argTitle As String, argUrl As String)
    
    Dim yukiCollection As Collection = Me.FeedLines.Copy()
    
    If yukiCollection.Exist(argTitle) Then Return
    
    yukiCollection.Add(argUrl, argTitle)
    Me.FeedLines = yukiCollection
    
End

Public Sub RemoveFeed(argTitle As String)
    
    Dim yukiCollection As Collection = Me.FeedLines.Copy()
    
    yukiCollection.Remove(argTitle)
    Me.FeedLines = yukiCollection
    
End

Private Function ClockFormat_Read() As String

    Return $settings["clock/format", ("hh:nn:ss")]

End

Private Sub ClockFormat_Write(Value As String)

    $settings["clock/format"] = Value
    $settings.Save()

End

Private Function ForegroundColor_Read() As Integer

    Return $settings["color/foreground", Color.White]

End

Private Sub ForegroundColor_Write(Value As Integer)

    $settings["color/foreground"] = Value
    $settings.Save()

End

Private Function BackgroundColor_Read() As Integer

    Return $settings["color/background", Color.Lighter(Color.Violet)]

End

Private Sub BackgroundColor_Write(Value As Integer)

    $settings["color/background"] = Value
    $settings.Save()

End

Private Function ShowNagato_Read() As Boolean

    Return $settings["nagato/show", True]

End

Private Sub ShowNagato_Write(Value As Boolean)

    $settings["nagato/show"] = Value
    $settings.Save()

End

Private Function FeedLines_Read() As Collection

    Return $settings["rss/feed_lines", NagatoRssFeedLines.GetDefault()]

End

Private Sub FeedLines_Write(Value As Collection)

    $settings["rss/feed_lines"] = Value
    $settings.Save()

End
