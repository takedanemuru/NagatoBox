' Gambas module file

Property Read WebRadioDirectory As String
Property Read PlsList As NagatoPls[]
Property Read Radio As String
Property Read Title As String

Private $plsList As New NagatoPls[]
Private $plsCollection As New Collection
Private $player As MediaPlayer
Private $radio As String = ("Internet radio")
Private $title As String = ("Select tune")

Private Sub initializeDirectory()
    
    Mkdir (Me.WebRadioDirectory) 
    File.Save(Me.WebRadioDirectory &/ "AnimeNfo.pls", File.Load("Radio/AnimeNfo.pls"))
    File.Save(Me.WebRadioDirectory &/ "LiveIreland.pls", File.Load("Radio/liveireland.pls"))
    File.Save(Me.WebRadioDirectory &/ "SkyFmSmoothJazz.pls", File.Load("Radio/skyfm_smoothjazz.pls"))
    
End

Private Sub initializePlsList()
    
    Dim yukiPath As String
    Dim yukiPls As NagatoPls
    
    For Each yukiPath In Dir(Me.WebRadioDirectory)
        yukiPls = New NagatoPls(Me.WebRadioDirectory &/ yukiPath)
        If yukiPls.Ready Then $plsList.Add(yukiPls)
    Next
    
End

Private Sub initializePlsCollection()
    
    Dim yukiPls As NagatoPls
    
    For Each yukiPls In $plsList
        If Not $plsCollection.Exist(yukiPls.Url) Then $plsCollection.Add(yukiPls, yukiPls.Url)
    Next
    
End

Public Sub EnsureDefault()
    
    If Not Exist(Me.WebRadioDirectory) Then initializeDirectory()
    
    initializePlsList()
    initializePlsCollection()
    $player = New MediaPlayer As "RadioPlayer"
    $player.Buffering = False
    
End

Public Sub Stop()
    
    Try $player.Stop()
    
End

Public Sub SetUrlAndPlay(argUrl As String)
    
    Try $player.Stop()
    $player.URL = argUrl
    
    If $player.State = MediaPlayer.Null Then
        If Not Error Then
            $player.Play()
            $radio = $plsCollection[argUrl].Title
            $title = ""
        End If
    Else
        Debug "Couldn't connect"
    End If

Catch
    Debug Error.Text
    
End

Private Function WebRadioDirectory_Read() As String

    Return User.Home &/ ".nagato-web-radio"

End

Private Function PlsList_Read() As NagatoPls[]

    Return $plsList

End

Public Sub RadioPlayer_Tag(argTags As MediaTagList)
    
    If argTags["title"] <> "" Then $title = argTags["title"]
    
End

Private Function Title_Read() As String

    Return $title

End

Private Function Radio_Read() As String

    Return $radio

End
