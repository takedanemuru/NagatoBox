' Gambas class file

Create Static

Property Read TrashPath As String ' act as const
Property Read TrashPathInfo As String ' act as const
Property Read ItemCount As Integer
Property Read ItemSize As Integer

Private $itemCount As Integer
Private $itemSize As Integer
Private $directoryObserver As NagatoDirectoryObserver

Private Sub setItemCountAndSize(argPath As String)
    
    Dim yukiPath As String
    Dim yukiFullPath As String
    
    For Each yukiPath In Dir(argPath)
        yukiFullPath = argPath &/ yukiPath
        Inc $itemCount
        $itemSize += Stat(yukiFullPath).Size
        If IsDir(yukiFullPath) Then setItemCountAndSize(yukiFullPath)
    Next
    
End

Private Sub refreshProperties()
    
    $itemCount = 0
    $itemSize = 0
    
    setItemCountAndSize(Me.TrashPath)
    
    
End

Public Sub EmptyAll()
    
    Dim yukiResponse As Integer = Message.Delete(("Are you sure to empty trash bin ?"), ("Cancel"), ("Empty")) 
    
    If yukiResponse = 2 Then
        Shell "trash-empty" Wait
    Endif
    
End

Public Sub Activate()
    
    $directoryObserver = New NagatoDirectoryObserver As "DirectoryObserver"
    $directoryObserver.Directory = Me.TrashPathInfo
    
    refreshProperties()
    
End

Public Sub DirectoryObserver_Deleted(argFileName As String)
    
    Debug argFileName
    
    refreshProperties()
    
End

Public Sub DirectoryObserver_Modefied(argFileName As String)
    
    Debug argFileName

    refreshProperties()
    
End

Private Function ItemCount_Read() As Integer

    Return $itemCount

End


Private Function ItemSize_Read() As Integer

    Return $itemSize

End

Private Function TrashPath_Read() As String

    Dim yukiTargetPath As String = User.Home &/ ".local/share/Trash/files"

    If Not Exist(yukiTargetPath) Then Mkdir yukiTargetPath

    Return yukiTargetPath

End

Private Function TrashPathInfo_Read() As String

    Dim yukiTargetPath As String = User.Home &/ ".local/share/Trash/info"

    If Not Exist(yukiTargetPath) Then Mkdir yukiTargetPath

    Return yukiTargetPath

End
