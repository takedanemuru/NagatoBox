' Gambas class file

Private Const ApplicationName As String = "system://org.freedesktop.NetworkManager"
Private Const ObjectPath As String = "/org/freedesktop/NetworkManager/AccessPoint"

Private $accessPoints As New String[]

Private Function getAccessPointElements() As XmlElement[]
    
    Dim yukiXmlDocument As New XmlDocument
    
    yukiXmlDocument.FromString(DBus[ApplicationName]._Introspect(ObjectPath))
    
    Return yukiXmlDocument.All
    
End

Private Sub refreshAccessPoints()
    
    Dim yukiXmlElement As XmlElement
    Dim yukiAttribute As String

    $accessPoints.Clear()

    For Each yukiXmlElement In getAccessPointElements()
        yukiAttribute = yukiXmlElement.GetAttribute("name")
        If yukiAttribute <> "" Then $accessPoints.Add(yukiAttribute)
    Next
    
End

Private Function getProxy(argIndex As Integer) As DBusProxy
    
    Dim yukiObjectPath As String = ObjectPath &/ $accessPoints[argIndex]
    
    Return DBus[ApplicationName][yukiObjectPath, Interface & ".AccessPoint"]
    
End 

Public Function GetHwAddress(argIndex As Integer) As String
    
    Return getProxy(argIndex).HwAddress
    
End

Public Sub _new()
    
    refreshAccessPoints()
    
End
