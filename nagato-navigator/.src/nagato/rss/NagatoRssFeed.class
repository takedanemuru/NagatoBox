' Gambas class file

Property Read Max As Integer

Private $buffer As String 
Private $titles As New String[]
Private $links As New String[]

Event Error
Event Finish(argFeed As NagatoRssFeed)

Private Function getTextBodyTitle(argText As String) As String
    
    Dim yukiResult As String 
    
    yukiResult = String.Mid$(argText, String.InStr(argText, "<title>"))
    yukiResult = Replace$(yukiResult, "<title>", "")
    yukiResult = Replace$(yukiResult, "</title>", "")
    yukiResult = Replace$(yukiResult, "<![CDATA[", "")
    yukiResult = Replace$(yukiResult, "]]>", "")
    
    Return yukiResult
    
End

Private Function getTextBodyLink(argText As String) As String
    
    Dim yukiResult As String = String.Mid$(argText, String.InStr(argText, "<link>"))
    
    yukiResult = Replace$(yukiResult, "<link>", "")
    yukiResult = Replace$(yukiResult, "</link>", "")
    
    Return yukiResult
    
End

Private Sub setItems()
    
    Dim yukiData As String[] = Split($buffer, "\n", "", True, False)
    Dim yukiLine As String
    
    For Each yukiLine In yukiData
        If String.InStr(yukiLine, "<title>") > 0 Then
            $titles.Add(getTextBodyTitle(yukiLine))
        Else If String.InStr(yukiLine, "<link>") > 0 Then
            $links.Add(getTextBodyLink(yukiLine))
        Endif
    Next
    
    Raise Finish(Me)
    
End

Public Sub _new(argPath As String)
    
    Dim yukiHttpClient As New HttpClient As "HttpClient"
    
    $buffer = ""
    
    With yukiHttpClient
        .URL = argPath
        .Async = True
        .Timeout = 15
        .Get()
    End With
    
End

Public Sub GetTitle(argIndex As Integer) As String
    
    Return $titles[argIndex]
    
Catch
    Return ""
    
End

Public Sub GetLink(argIndex As Integer) As String
    
    Return $links[argIndex]
    
End

Public Sub GetMax() As Integer
    
    Return $titles.Max
    
End

Public Sub HttpClient_Read()
    
    Dim yukiBuffer As String
    
    yukiBuffer = Read #Last, Lof(Last)
    $buffer &= yukiBuffer
    
End

Public Sub HttpClient_Error()
    
    Debug "RSS Error"
    
    Raise Error
    
End

Public Sub HttpClient_Finished()
        
        setItems()
        
End

Private Function Max_Read() As Integer

    Return $titles.Max

End
