' Gambas class file

Private $rootMenu As Menu
Private $desktopWindow As DesktopWindow

Private Sub addDesktopMenu(argMenu As Menu) 
    
    Dim yukiCount As Integer
    Dim yukiMenu As Menu
    
    For yukiCount = 0 To Desktop.Count - 1
        yukiMenu = New Menu(argMenu) As "DesktopMenu"
        With yukiMenu
            .Text = Subst$(("Desktop #&1"), yukiCount + 1)
            .Enabled = Not (yukiCount = $desktopWindow.Desktop)
            .Tag = yukiCount
        End With
    Next
    
End

Private Sub addMoveToMenu()
    
    Dim yukiMenu As New Menu($rootMenu)
    
    With yukiMenu
        .Text = ("Move To...")
    End With
    
    addDesktopMenu(yukiMenu)
    
End

Private Sub addMaximizeMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "MaximizeMenu"
    
    With yukiMenu
        .Text = IIf($desktopWindow.Maximized, ("Unmaximize"), ("Maximize"))
    End With
    
End

Private Sub addMinimizeMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "MinimizeMenu"
    
    With yukiMenu
        .Text = IIf($desktopWindow.Minimized, ("Unminimize"), ("Minimize"))
    End With
    
End

Private Sub addCloseMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "CloseMenu"
    
    With yukiMenu
        .Picture = MikuruMonoIconBlue.Get("button-cross", 2 * MikuruUX.Grid)
        .Text = ("Close")
    End With
    
End

Private Sub addShowDesktopMenu()
    
    Dim yukiMenu As New Menu($rootMenu) As "ShowDesktopMenu"
    
    With yukiMenu
        .Text = ("Show Desktop")
        .Picture = MikuruMonoIconBlue.Get("desktop", 2 * MikuruUX.Grid)
    End With
    
End

Public Sub PopUp()
    
    $rootMenu.PopUp()
    
End

Public Sub _new(argDesktopWindow As DesktopWindow)
    
    $desktopWindow = argDesktopWindow
    
    $rootMenu = New Menu(HaruhiInfoBar)
    addMoveToMenu()
    addMaximizeMenu()
    addMinimizeMenu()
        MikuruMenuSeparator.Set($rootMenu)
    addCloseMenu()
        MikuruMenuSeparator.Set($rootMenu)
    addShowDesktopMenu()
    
End

Public Sub MaximizeMenu_Click()
    
    $desktopWindow.Maximized = Not $desktopWindow.Maximized
    
End

Public Sub MinimizeMenu_Click()
    
    $desktopWindow.Minimized = Not $desktopWindow.Minimized
    
End

Public Sub CloseMenu_Click()
    
    $desktopWindow.Close()
    
End

Public Sub DesktopMenu_Click()
    
    $desktopWindow.Desktop = Last.Tag
    
End
