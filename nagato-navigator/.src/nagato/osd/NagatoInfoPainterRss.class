' Gambas class file

Inherits NagatoInfoPainter

Private $feed As NagatoRssFeed
Private $feedIndex As Integer = 0
Private $itemIndex As Integer = 1
Private $counter As Integer

Private Sub showInBrowser()
    
    HaruhiMiniBrowser.SetUrlAndShowCenter($feed.GetLink($itemIndex))
    
Catch
    Return
    
End

Private Sub setPreviousFeed()
    
    Dec $itemIndex
    
    If 1 > $itemIndex Then
        Dec $feedIndex
        If 0 > $feedIndex Then $feedIndex = NagatoRss.Max
        $feed = NagatoRss.GetFeed($feedIndex)
        $itemIndex = $feed.Max
    End If
    
End

Private Sub setNextFeed()
    
    Inc $feedIndex
    If $feedIndex > NagatoRss.Max Then $feedIndex = 0
    
    $feed = NagatoRss.GetFeed($feedIndex)
    
End

Private Sub setNewItem()
    
    Inc $itemIndex
    
    If $itemIndex > $feed.Max Then
        $itemIndex = 1
        setNextFeed()
    Endif
    
End

Private Function getText() As String
    
    If NagatoRss.Ready Then
        If Not Object.IsValid($feed) Then $feed = NagatoRss.GetFeed($feedIndex)
        Return $feed.GetTitle($itemIndex)
    Else
        $feedIndex = 0
        $itemIndex = 1
        Return "now loading..."
    Endif
    
End

Private Sub setCounter()
    
    If $counter = 5 Then
        $counter = 0
        setNewItem()
    Else
        Inc $counter
    Endif
    
End

Public Sub Refresh()
    
    Me._$drawingArea.Refresh()
    setCounter()
    
End

Public Sub _InitializeDrawingArea()
    
    With Me._$drawingArea
        .Expand = True
        .Width = Me._$drawingArea.Parent.Width
        .Background = NagatoSettingsInfobar.LabelColor
    End With
    
End

Public Sub DrawingArea_Draw()
    
    If Not NagatoSystemObserver.Activated Then NagatoSystemObserver.Refresh()
    
    With Paint
        .Begin(Me._$drawingArea)
        .Font.Size = 9
        .Font.Bold = True
        .Brush = .Color(Color.White)
        .Text(getText(), 16, 0, Me._$drawingArea.Parent.Width - 16, 24, Align.Left)
        .Fill()
        .End()
    End With
    
End

Public Sub DrawingArea_Menu()
    
    Dim yukiMenu As New NagatoInfoRssContextMenu As "ContextMenu"
    
    yukiMenu.PopUp()
    
End

Public Sub ContextMenu_Previous()
    
    setPreviousFeed()
    
End

Public Sub ContextMenu_Next()
    
    setNextFeed()
    
End

Public Sub ContextMenu_Browser()
    
    showInBrowser()
    
End

Public Sub ContextMenu_NewFeed(argIndex As Integer)
    
    $feed = NagatoRss.GetFeed(argIndex)
    
End
