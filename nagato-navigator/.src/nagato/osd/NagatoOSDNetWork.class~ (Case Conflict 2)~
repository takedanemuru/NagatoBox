' Gambas class file

Inherits NagatoOSD

Public _$uniqueName As String = "NagatoOSDNetwork"

Private Sub addAccesspointsMenu(argParentMenu As Menu)
    
    Dim yukiMenu As New Menu(argParentMenu)
    Dim yukiSubMenu As NagatoAccessPointsMenu
    
    With yukiMenu
        .Text = ("Access points")
        .Picture = MikuruMonoIconBlue.Get("wireless-router", 2 * MikuruUX.Grid)
    End With
    
    yukiSubMenu = New NagatoAccessPointsMenu(yukiMenu)

End

Private Sub getMainlineText() As String
    
    With NagatoNetworkManager
        Return Subst$("&1 Mbps (&2%)", (.MaxBitrate * (.Strength / 100) / 1000), .Strength)
    End With
    
End

Private Sub drawText()
    
    With Paint
        .Brush = .Color(Color.White)
        .Font.Size = MikuruUX.FontSizeMedium
        .Text(NagatoNetworkManager.HwAddress, Me.OffsetLeft, Me.OffsetTop1st)
        .Font.Size = MikuruUX.FontSizeLarge
        .Text(getMainlineText(), Me.OffsetLeft, Me.OffsetTop2nd)
        .Fill()
    End With

End

Private Function getActivatedPicture() As Picture
    
    Dim yukiBaseImage As Image
    
    NagatoNetworkManager.Refresh()
    
    yukiBaseImage = Me._GetBaseImage(MikuruColor.GetConditionColor(100 - NagatoNetworkManager.Strength))
    
    Paint.Begin(yukiBaseImage)
        drawText()
        Me._DrawIconPicture(MikuruNetworkIcon.GetStrengthIcon(NagatoNetworkManager.Strength))
    Paint.End()
    
    Return yukiBaseImage.Picture
    
End

Private Function getNotActivatedPicture() As Picture
    
    Dim yukiBaseImage As Image = Me._GetBaseImage(Color.Red)
    
    With Paint
        .Begin(yukiBaseImage)
        .Font.Size = MikuruUX.FontSizeLarge
        .Text("NetworkManager is not activated.", Me.OffsetLeft, Me.OffsetTop2nd)
        Me._DrawIconPicture(MikuruNetworkIcon.GetWiredIcon(False))
        .End()
    End With
    
    Return yukiBaseImage.Picture
    
End

Public Sub _AddOtherMenu(argParentMenu As Menu) ' Override.
    
    addAccessPointsMenu(argParentMenu)
    MikuruMenuSeparator.Set(argParentMenu)
    
End

Public Sub _SetPicture() 'override
    
    If NagatoNetworkManager.Activated Then
        Me._$form.Picture = getActivatedPicture()
    Else
        Me._$form.Picture = getNotActivatedPicture()
    End If 
    
End

