' Gambas class file

Inherits NagatoOSD

Public _$uniqueName As String = "NagatoOSDClock"

Private Const CenterX As Integer = 72
Private Const CenterY As Integer = 72
Private Const ClockRadius As Integer = 40

Private Sub addSelectClockFaceMenu(argParentMenu As Menu)
    
    Dim yukiMenu As New Menu(argParentMenu) As "SelectClockFaceMenu"
    
    With yukiMenu
        .Text = ("select clock face")
        .Picture = MikuruMonoIconBlue.Get("alarm-clock", 2 * MikuruUX.Grid)
    End With
    
End

Private Function getX(argDegree As Integer, argLength As Integer) As Float
    
    Return Cos(Rad(argDegree)) * argLength + CenterX
    
End

Private Function getY(argDegree As Integer, argLength As Integer) As Float
    
    Return Sin(Rad(argDegree)) * argLength + CenterY
    
End

Private Sub drawClockHand(argDegree As Integer, argLength As Integer, argWidth As Integer, Optional argColor As Integer)
    
    With Paint
        .Brush = Paint.Color(IIf(argColor, argColor, Color.Black))
        .LineWidth = argWidth
        .LineCap = .LineCapRound
        .MoveTo(CenterX, CenterY)
        .LineTo(getX(argDegree, argLength), getY(argDegree, argLength))
        .Stroke()
    End With
    
End

Private Sub drawClock(argNow As Date)
    
    With MikuruUX
        Paint.DrawImage(MikuruClockFace.GetImage(NagatoSettingsOSD.ClockFace, 14 * .Grid), 2 * .Grid, 2 * .Grid)
        drawClockHand(Second(argNow) * 6 - 90, ClockRadius * 1, 1, Color.Red)                               'second
        drawClockHand(Minute(argNow) * 6 - 90, ClockRadius * 0.91, 3)                                              'minute
        drawClockHand(Hour(argNow) * 30 + Minute(argNow) * 0.5 - 90, ClockRadius * 0.75, 5)     'hour
    End With
    
End

Private Sub setFontConfig()
    
    If NagatoOSD.FontName <> "" Then
        Paint.Font.Name = NagatoOSD.FontName
    Endif
    
End

Private Sub drawText(argNow As Date)
    
    setFontConfig()
    
    With Paint
        .Brush = .Color(Color.White)
        .Font.Size = MikuruUX.FontSizeMedium
        .RichText("<b>" & MikuruCalendarText.Get(argNow) & "</b>", Me.OffsetLeft, Me.OffsetTop1st)
        .Font.Size = MikuruUX.FontSizeLarge
        .RichText(Format$(argNow, ("hh:nn:ss AM/PM")), Me.OffsetLeft, Me.OffsetTop2nd)
        .Fill()
    End With

End

Public Sub _SetPicture()
    
    Dim yukiImage As Image = Me._GetBaseImage(NagatoSettingsOSD.GetColor(Me.UniqueName))
    Dim yukiNow As Date = Now
    
    Paint.Begin(yukiImage)
        Paint.AntiAlias = True
        drawClock(yukiNow)
        drawText(yukiNow)
    Paint.End()
    
    Me._$form.Picture = yukiImage.Picture
    
End

Public Sub _AddOtherMenu(argParentMenu As Menu) ' override
    
    addSelectClockFaceMenu(argParentMenu)
        MikuruMenuSeparator.Set(argParentMenu)
    Me._AddSelectLabelColorMenu(argParentMenu)
    
End

Public Sub SelectClockFaceMenu_Click()
    
    Dim yukiResult As Integer = HaruhiClockFaceSelect.ShowDialog()
    
    If yukiResult <> 0 Then NagatoSettingsOSD.ClockFace = yukiResult
    
End
