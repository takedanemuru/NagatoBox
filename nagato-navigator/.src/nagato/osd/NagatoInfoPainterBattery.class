' Gambas class file

Inherits NagatoInfoPainter

Private $batteryFont As New Font
Private $batteryText As String

Private Function getWarningText() As String
    
    Return Subst$("[ALERT!!] Battery rate is &1%", Format$(NagatoUPower.TotalBatteryPercentage, "##.#0"))
    
End

Public Sub Refresh()
    
    $batteryText = IIf(NagatoUPower.IsPlugged, ("[Plugged] "), ("[Unplugged] "))
    If NagatoUPower.TotalBatteryPercentage = 100 Then
        $batteryText &= ("Battery rate: 100%")
    Else
        $batteryText &= Subst$(("Battery rate: &1%"), Format$(NagatoUPower.TotalBatteryPercentage, "###.#0"))
    End If 
    $batteryFont.Size = 11
    $batteryFont.Bold = True

    Me._$drawingArea.Refresh()
    
End

Public Sub _InitializeDrawingArea()
    
    With Me._$drawingArea
        .Expand = True
        .Width = 0
        .Background = NagatoSettingsInfobar.LabelColor
    End With
    
End

Public Sub DrawingArea_Draw()
    
    With Paint
        .Begin(Me._$drawingArea)
        .Brush = .Color(Color.White)
        .Font = $batteryFont
        .RichText($batteryText, 0, 0, Me._$drawingArea.Width, 24, Align.Left)
        .Fill()
        .End()
    End With
    
End
