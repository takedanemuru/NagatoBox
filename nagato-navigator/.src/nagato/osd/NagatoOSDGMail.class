' Gambas class file

Inherits NagatoOSD

Public _$uniqueName As String = "NagatoOSDGMail"

Private Function getFullMailBoxIcon() As Image
    
    Return Image.Load("pictures/Full.png").Stretch(14 * MikuruUX.Grid, 14 * MikuruUX.Grid)
    
End

Private Function getEmptyMailBoxIcon() As Image
    
    Return Image.Load("pictures/Empty.png").Stretch(14 * MikuruUX.Grid, 14 * MikuruUX.Grid)
    
End

Private Sub drawSubLineText()
    
    With Paint
        .Brush = .Color(Color.White)
        .Font.Size = MikuruUX.FontSizeMedium
        .DrawText(("GMail"), NagatoOSD.OffsetLeft, NagatoOSD.OffsetTop1st)
    End With
    
End

Private Sub drawMainLineText()
    
    Dim yukiText As String = Subst$(("&1 Messages"), NagatoGMailPoP3.InboxMessage)
    
    With Paint
        .Brush = .Color(Color.White)
        .Font.Size = MikuruUX.FontSizeLarge
        .DrawText(yukiText, NagatoOSD.OffsetLeft, NagatoOSD.OffsetTop2nd)
    End With
    
End

Private Sub drawIcon()
    
    Dim yukiIcon As Image = IIf(NagatoGMailPoP3.InboxMessage = 0, getEmptyMailBoxIcon(), getFullMailBoxIcon())
    
    Paint.DrawImage(yukiIcon, 2 * MikuruUX.Grid, 2 * MikuruUX.Grid)
    
End

Public Sub _SetPicture() 'override
    
    Dim yukiBaseImage As Image = Me._GetBaseImage(Color.Red)
    
    Paint.Begin(yukiBaseImage)
        drawSubLineText()
        drawMainLineText()
        drawIcon()
    Paint.End()
    
    Me._$form.Picture = yukiBaseImage.Picture
    
End
