' Gambas class file

Inherits NagatoOSD

Public _$uniqueName As String = "NagatoOSDRss"

Private $feedIndex As Integer = 0
Private $itemIndex As Integer = 1
Private $feed As NagatoRssFeed
Private $feedSelectMenu As Menu
Private $mainLineText As NagatoScrollingText
Private $observer As Observer

Private Sub refreshFeedSelectMenu()
    
    Dim yukiFeedsMenu As NagatoSelectFeedMenu
    
    $feedSelectMenu.Children.Clear()
    
    yukiFeedsMenu = New NagatoSelectFeedMenu($feedSelectMenu) As "SelectFeedMenu"
    
    
End

Private Sub addSelectFeedMenu(argParentMenu As Menu)
    
    $feedSelectMenu = New Menu(argParentMenu)
    
    With $feedSelectMenu
        .Text = ("select feed")
        .Picture = MikuruMonoIconBlue.Get("feed", 16)
    End With
    
    $observer = New Observer(argParentMenu) As "Asakura"
    
End

Private Sub addOpenWithBrowserMenu(argParentMenu As Menu)
    
    Dim yukiMenu As New Menu(argParentMenu) As "OpenWithBrowserMenu"
    
    With yukiMenu
        .Text = ("open with browser")
        .Picture = MikuruMonoIconBlue.Get("web", 16)
    End With
    
End

Private Sub setItemIndex()
    
    Inc $itemIndex
    
    If $itemIndex > $feed.Max Then
        $itemIndex = 1
        setFeedIndex()
    Endif
    
End

Private Sub setFeedIndex()
    
    If $feedIndex > NagatoRss.Max Then $feedIndex = 0
    
    $feed = NagatoRss.GetFeed($feedIndex)
    
End

Private Sub drawIcon()
    
    Me._DrawIconImage(Image.Load("pictures/rss1.png"))
    
End

Private Sub drawHeaderText()
    
    With Paint
        .Brush = Paint.Color(Color.White)
        .Font.Size = MikuruUX.FontSizeMedium
        .DrawText($feed.GetTitle(0), Me.OffsetLeft, Me.OffsetTop1st)
    End With
    
End

Private Sub drawMainLineText()
    
    Dim yukiPosition As Integer
    
    With Paint
        .Brush = .Color(Color.White)
        .Font.Size = MikuruUX.FontSizeMediumLarge
        If Not Object.IsValid($mainLineText) Then
            $mainLineText = New NagatoScrollingText(.Font, 34, 2) As "MainLine"
        End If
        $mainLineText.SetText($feed.GetTitle($itemIndex))
        .DrawText($mainLineText.Text, Me.OffsetLeft + $mainLineText.Offset, Me.OffsetTop2nd)
    End With
    
End

Public Sub _SetPicture()
    
    Dim yukiImage As Image = Me._GetBaseImage(Color.Orange)
    
    If Not Object.IsValid($feed) Then $feed = NagatoRss.GetFeed($feedIndex)
    
    If NagatoRss.Ready Then
        With Paint
            .Begin(yukiImage)
            .AntiAlias = True
            drawIcon()
            Me._DrawClippingRectangle()
            drawHeaderText()
            drawMainLineText()
            .End()
        End With
    End If
    
    Me._$form.Picture = yukiImage.Picture
    
End

Public Sub _AddOtherMenu(argParentMenu As Menu) ' override
    
    argParentMenu.Children.Clear()
    
    addSelectFeedMenu(argParentMenu)
    addOpenWithBrowserMenu(argParentMenu)
    
    MikuruMenuSeparator.Set(argParentMenu)
    
End

Public Sub Asakura_Show()
    
    refreshFeedSelectMenu()
    
End

Public Sub SelectFeedMenu_Selected(argFeedIndex As Integer)
    
    If $feedIndex = argFeedIndex Then Return
    
    $feedIndex = argFeedIndex
    $itemIndex = 1
    $feed = NagatoRss.GetFeed(argFeedIndex)
    
End

Public Sub OpenWithBrowserMenu_Click()
    
    HaruhiMiniBrowser.SetUrlAndShowCenter($feed.GetLink($itemIndex))
    
End

Public Sub MainLine_Next()
    
    setItemIndex()
    
End
