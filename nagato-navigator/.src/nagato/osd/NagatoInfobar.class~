' Gambas class file

Create Static

Private $form As Form
Private $menuButton As MenuButton 
Private $mainInfo As Object
Private $clock As New NagatoInfoClock
Private $pager As New NagatoInfoPager

Private Sub switchMainInfo(argMode As Integer)
    
    Select Case argMode
        Case NagatoInfo.Rss
            $mainInfo = New NagatoInfoRss
        Case NagatoInfo.IconBox
            $mainInfo = New NagatoInfoIconBox
        Case NagatoInfo.Music
            $mainInfo = New NagatoInfoMusic
        Case NagatoInfo.System
            $mainInfo = New NagatoInfoSystem
    End Select
    
End

Private Sub setMenu()
    
    Dim yukiRootMenu As New Menu(Me, True)
    Dim yukiMenu As NagatoInfoBarSuperMenu
    
    yukiRootMenu.Name = "InfoBarRootMenu" ' This name is unique/global 
    $menuButton.Menu = yukiRootMenu.Name
    
    yukiMenu = New NagatoInfoBarSuperMenu(yukiRootMenu) As "Menu"
    
End

Private Function getPage() As Integer ' this function should return -1 when user click "non pager area".
    
    Dim yukiOrigin As Integer = Desktop.W - $pager.Width
    
    If yukiOrigin > Mouse.X Or If Mouse.X > (yukiOrigin + Desktop.Count * 26) Then Return -1
    
    Return (Mouse.X - yukiOrigin) Div 26 
    
End

Private Function getMousePosition() As Variant
    
    Dim yukiPage As Integer = getPage()
    
    If yukiPage = -1 Then
        If Mouse.X > (Desktop.W - $clock.Width) Then
            Return "clock"
        Else If (Me.Width - $pager.Width) > Mouse.X
            Return "main"
        Endif
    Else
        Return yukiPage
    Endif
    
End

Private Sub leftClick()
    
    Dim yukiPosition As Variant = getMousePosition()
    
    If yukiPosition = "main" Then
        $mainInfo.MouseDown(Mouse.X)
    Else If yukiPosition = "clock" Then
        HaruhiBalloon.SetText(Subst$("Today is &1.", MikuruCalendarText.Get(Now)))
    Else 
        Desktop.Current = yukiPosition 
        setPicture()
        Kyon.CallNagato()
    Endif
    
Catch
    Return
    
End

Private Sub rightClick()
    
    Dim yukiPosition As Variant = getMousePosition()
    
    If yukiPosition = "main" Then
        $mainInfo.PopUpMenu(Mouse.X)
    Else If yukiPosition = "clock" Then
        Debug "clock"
    Else 
        $pager.PopUpMenu(Mouse.X)
    Endif
    
Catch
    Return
    
End

Private Sub drawMainLine()
    
    With Paint
        .Brush = .Color(Color.White)
        .Font.Size = 10
        $mainInfo.DrawInfo($pager.Width)
        .Brush = Paint.Color(MikuruColor.NagatoPurple)
        .Fill()
    End With
    
End

Private Sub resetPosition()
    
    Me.Y = Screen.Height - 2 * MikuruUX.Grid
    Me.X = 0
    
End

Private Sub setPicture()
    
    Dim yukiImage As New Image(Screen.Width, 4 * MikuruUX.Grid, NagatoSettingsInfobar.LabelColor)
    
    Paint.Begin(yukiImage)
        Paint.Brush = Paint.Color(Color.White)
        $clock.DrawInfo(4 * MikuruUX.Grid)
        $pager.DrawInfo($clock.Width)
        drawMainLine()
    Paint.End()
    
    $form.Height = 4 * MikuruUX.Grid
    $form.Width = Screen.Width
    $form.Mask = True
    $form.Picture = yukiImage.Picture
    
    resetPosition()
    
Catch
    Return
    
End

Public Sub Refresh()
    
    setPicture()
    
End

Public Sub _new()
    
    $form = New Form
    $form.Height = 4 * MikuruUX.Grid
    $form.Width = Screen.Width
    $form.Mask = True
    $form.Sticky = True
    
    $menuButton = New MenuButton($form)
    With $menuButton
        .X = 0
        .Y = 0
        .Width = 2 * MikuruUX.Grid
        .Height = 2 * MikuruUX.Grid
        .Picture = MikuruMonoIconBlue.Get("star")
    End With
    
End

Public Sub Show()
    
    $form.Show()
    
End

Public Sub Form_Show()
    
    $menuButton.Picture = MikuruMonoIconBlue.Get("star")
    switchMainInfo(NagatoSettingsInfobar.Type)
    setMenu()

End

Public Sub Form_Enter()
    
    If Mouse.ScreenY > (Screen.Height - MikuruUX.Grid)
        $menuButton.Visible = True
        Kyon.CallNagato()
        HaruhiBalloon.TextAreaText = ("YUKI.N > click me.")
    End If
    
End

Public Sub Form_Leave()
    
    $menuButton.Visible = False
    HaruhiBalloon.Close()
    
End

Public Sub Form_MouseDown()
    
    If Mouse.Left Then
        leftClick()
    Else If Mouse.Right Then
        rightClick()
    Endif
    
End

Public Sub Form_Move()
    
    setPicture()
    
End

Public Sub Menu_Switch(argMode As Integer)
    
    switchMainInfo(argMode)
    NagatoSettingsInfobar.Type = argMode
    setPicture()
    
End
