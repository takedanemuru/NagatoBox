' Gambas class file

Inherits NagatoOSD 'NagatoOSD is a Static Class.

Public _$uniqueName As String = "NagatoOSDRadio"

Private $mainLineText As NagatoScrollingText

Private Sub drawSubLineText()
    
    With Paint
        .Brush = .Color(Color.White)
        .Font.Size = MikuruUX.FontSizeMedium
        .Text(MikuruWebRadio.Radio, Me.OffsetLeft, Me.OffsetTop1st)
        .Fill()
    End With
    
End

Private Sub drawMainLineText()
    
    With Paint
        .Brush = .Color(Color.White)
        .Font.Size = MikuruUX.FontSizeMediumLarge
        If Not Object.IsValid($mainLineText) Then $mainLineText = New NagatoScrollingText(.Font, 34, 2)
        $mainLineText.SetText(MikuruWebRadio.Title)
        .Text($mainLineText.Text, Me.OffsetLeft + $mainLineText.Offset, Me.OffsetTop2nd)
        .Fill()
    End With
    
End

Private Sub drawIcon()
    
    Dim yukiIcon As Image = Image.Load("pictures/signal.png").Stretch(14 * MikuruUX.Grid, 14 * MikuruUX.Grid)
    
    Paint.DrawImage(yukiIcon, 2 * MikuruUX.Grid, 2 * MikuruUX.Grid)
    
End

Private Sub addRadioTunesMenu(argParentMenu As Menu)
    
    Dim yukiMenu As Menu
    Dim yukiPls As NagatoPls
    
    For Each yukiPls In MikuruWebRadio.PlsList
        yukiMenu = New Menu(argParentMenu) As "RadioTuneMenu"
        With yukiMenu
            .Text = yukiPls.Title
            .Tag = yukiPls.Url
        End With
    Next
    
End

Private Sub addRadioMenu(argParentMenu As Menu)
    
    Dim yukiMenu As New Menu(argParentMenu)
    
    With yukiMenu
        .Text = ("Tunes")
        .Picture = MikuruMonoIconBlue.Get("wireless-router")
    End With
    
    addRadioTunesMenu(yukiMenu)
    
End

Public Sub _AddOtherMenu(argParentMenu As Menu) ' override
    
    addRadioMenu(argParentMenu)
        MikuruMenuSeparator.Set(argParentMenu)
    Me._AddSelectLabelColorMenu(argParentMenu)
    
End

Public Sub _SetPicture() 'override
    
    Dim yukiBaseImage As Image
    
    yukiBaseImage = Me._GetBaseImage(Color.Orange)
    
    Paint.Begin(yukiBaseImage)
        drawIcon()
        Me._DrawClippingRectangle()
        drawSubLineText()
        drawMainLineText()
    Paint.End()
    
    Me._$form.Picture = yukiBaseImage.Picture
    
End

Public Sub RadioTuneMenu_Click()
    
    MikuruWebRadio.SetUrlAndPlay(Last.Tag)
    
End
