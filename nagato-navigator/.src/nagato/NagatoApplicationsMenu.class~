' Gambas class file

Inherits NagatoMenu

Private Const ICON_SIZE As Integer = 16
Private Const MENU_TITLE_LENGTH As Integer = 30

Private Sub addDesktopFileMenu(argParentMenu As Menu, argCategoryArray As Object[])
    
    Dim yukiMenu As Menu
    Dim yukiDesktopFile As NagatoDesktopFile
    
    For Each yukiDesktopFile In argCategoryArray
        If Not Exist(yukiDesktopFile.Path) Then Continue
        yukiMenu = New Menu(argParentMenu) As "DesktopFileMenu"
        yukiMenu.Picture = yukiDesktopFile.GetIcon(ICON_SIZE)
        yukiMenu.Text = MikuruCollapsedText.Get(yukiDesktopFile.Name, MENU_TITLE_LENGTH)
        yukiMenu.Tag = yukiDesktopFile.Path
    Next
    
End

Private Sub addCategoryMenu(argCategoryName As String, argCategoryArray As Object[], argIcon As Picture)
    
    Dim yukiMenu As Menu
    
    If argCategoryArray.Count = 0 Then Return
    
    yukiMenu = New Menu(Me._$rootMenu)
    With yukiMenu
        .Picture = argIcon
        .Text = (argCategoryName)
    End With
    
    addDesktopFileMenu(yukiMenu, argCategoryArray)
    
End

Public Sub _InitializeMenu() 'override
    
    With NagatoMenuCache
        addCategoryMenu(("Nagato"), .NagatoSoftwareCompilationApplications, Picture["super-mono/basic/blue/star.png"])
            MikuruMenuSeparator.Set(Me._$rootMenu)
        addCategoryMenu(("Development"), .Developments, Picture["super-mono/basic/blue/wrench.png"])
        addCategoryMenu(("Education"), .Educations, Picture["super-mono/basic/blue/shapes.png"])
        addCategoryMenu(("Game"), .Games, Picture["super-mono/basic/blue/game-controller.png"])
        addCategoryMenu(("Graphics"), .Graphics, Picture["super-mono/basic/blue/image.png"])
        addCategoryMenu(("Internet"), .Networks, Picture["super-mono/basic/blue/web.png"])
        addCategoryMenu(("Media"), .Medias, Picture["super-mono/basic/blue/video-music.png"])
        addCategoryMenu(("Office"), .Offices, Picture["super-mono/basic/blue/document-edit.png"])
        addCategoryMenu(("Settings"), .Settings, Picture["super-mono/basic/blue/equalizer.png"])
        addCategoryMenu(("System"), .Systems, Picture["super-mono/basic/blue/computer.png"])
        addCategoryMenu(("Utility"), .Utilities, Picture["super-mono/basic/blue/ruler.png"])
        addCategoryMenu(("KDE4"), .Kde4, Picture["super-mono/basic/blue/gear.png"])
    End With
    
End

Public Sub DesktopFileMenu_Click()
    
    NagatoDesktopFile.Execute(Last.Tag)
    
End
