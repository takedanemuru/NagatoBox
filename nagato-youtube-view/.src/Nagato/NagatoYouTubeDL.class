' Gambas class file

Inherits NagatoObject

Public Sub DownloadAsMovie(argUrl As String)
    
    Dim yukiDirectory As String = MikuruDialog.GetDirectoryPath("MUSIC")
    
    If yukiDirectory = "" Then Return

    Exec ["youtube-dl", "-f", "webm", argUrl, "-o", yukiDirectory &/ "%(title)s.%(ext)s"] For Read As "YouTubeDL"
    
    Raise Signal(MikuruSignal.DownloaderStart, Null)
    
End

Public Sub DownloadAsMusic(argUrl As String)
    
    Dim yukiDirectory As String = MikuruDialog.GetDirectoryPath("MUSIC")
    
    If yukiDirectory = "" Then Return

    Exec ["youtube-dl", "-x", "--audio-format", "mp3", argUrl, "-o", yukiDirectory &/ "%(title)s.%(ext)s"] For Read As "YouTubeDL"
    
    Raise Signal(MikuruSignal.DownloaderStart, Null)

End


Public Sub YouTubeDL_Read()
    
    Dim yukiLine As String
    
    Read #Last, yukiLine, -256
    
    If yukiLine Begins "[download]" Then Raise Signal(MikuruSignal.DownloaderOngoing, Null)
    If yukiLine Begins "[ffmpeg]" Then Raise Signal(MikuruSignal.DownloaderEncoding, Null)
    
End

Public Sub YouTubeDL_Kill()
    
    Raise Signal(MikuruSignal.DownloaderFinished, Null)
    
End

Public Sub YouTubeDL_Error(argError As String)
    
    Raise Signal(MikuruSignal.DownloaderErrorOccurred, [MikuruString.Remove(argError, "\n")])
    
End
