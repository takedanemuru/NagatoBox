' Gambas module file

Public Enum Image, Music, Directory, None

Public Function IsValideMediaFilePath(argPath As String) As Boolean
    
    If Not Exist(argPath) Then Return False
    If MikuruMimeType.IsAudioFile(argPath) Then Return True
    
    Return False
    
End

Public Function Sanitize(argPaths As String[]) As String[]
    
    Dim yukiPath As String
    Dim yukiResult As New String[] 
    
    For Each yukiPath In argPaths
        If Not Exist(yukiPath) Then Continue
        If MikuruMimeType.IsAudioFile(yukiPath) Then yukiResult.Add(yukiPath)
    Next
    
    Return yukiResult
    
End

Public Function GetDragDataType(argData As String) As Integer
    
    Dim yukiUrl As String
    Dim yukiPath As String
    
    For Each yukiUrl In Split(argData, gb.CrLf, "", True)
        yukiPath = MikuruUrl.DecodeUrlToPath(yukiUrl)
        If IsDir(yukiPath) Then Return Me.Directory
        If DesktopMime.FromFile(yukiPath).Type Begins "image" Then Return Me.Image
        If DesktopMime.FromFile(yukiPath).Type Begins "audio" Then Return Me.Music
    Next
    
    Return Me.None
    
End

Public Function GetDirecoryPath(argData As String) As String
    
    Dim yukiUrl As String
    Dim yukiPath As String 
    
    For Each yukiUrl In Split(argData, gb.CrLf, "", True)
        yukiPath = MikuruUrl.DecodeUrlToPath(yukiUrl)
        If Not Exist(yukiPath) Or If Not IsDir(yukiPath) Then Continue
        Return yukiPath
    Next
    
Catch
    Return ""
    
End

Public Function GetImagePath(argData As String) As String
    
    Dim yukiUrl As String
    Dim yukiPath As String
    
    For Each yukiUrl In Split(argData, gb.CrLf, "", True)
        yukiPath = MikuruUrl.DecodeUrlToPath(yukiUrl)
        If DesktopMime.FromFile(yukiPath).Type Begins "image" Then Return yukiPath
    Next
    
End

Public Function GetMusicPaths(argData As String) As String[]
    
    Dim yukiUrl As String
    Dim yukiPath As String
    Dim yukiPaths As New String[]
    
    For Each yukiUrl In Split(argData, gb.CrLf, "", True)
        yukiPath = MikuruUrl.DecodeUrlToPath(yukiUrl)
        If DesktopMime.FromFile(yukiPath).Type Begins "audio" Then yukiPaths.Add(yukiPath)
    Next
    
    Return yukiPaths
    
End
