' Gambas module file

Property Read WebRadioDirectory As String
Property Read PlsList As NagatoPls[]

Private $plsList As New NagatoPls[]
Private $player As MediaPlayer

Private Sub initializeDirectory()
    
    Mkdir (Me.WebRadioDirectory) 
    File.Save(Me.WebRadioDirectory &/ "AnimeNfo.pls", File.Load("Radio/AnimeNfo.pls"))
    File.Save(Me.WebRadioDirectory &/ "LiveIreland.pls", File.Load("Radio/liveireland.pls"))
    File.Save(Me.WebRadioDirectory &/ "SkyFmSmoothJazz.pls", File.Load("Radio/skyfm_smoothjazz.pls"))
    
End

Private Sub initializePlsList()
    
    Dim yukiPath As String
    Dim yukiPls As NagatoPls
    
    For Each yukiPath In Dir(Me.WebRadioDirectory)
        yukiPls = New NagatoPls(Me.WebRadioDirectory &/ yukiPath)
        If yukiPls.Ready Then $plsList.Add(yukiPls)
    Next
    
End

Public Sub EnsureDefault()
    
    If Not Exist(Me.WebRadioDirectory) Then initializeDirectory()
    
    initializePlsList()
    $player = New MediaPlayer
    $player.Buffering = True
    
End

Public Sub Stop()
    
    Try $player.Stop()
    
End

Public Sub SetUrlAndPlay(argUrl As String)
    
    NagatoQueue.Pause()

    Try $player.Stop()
    $player.URL = argUrl
    If $player.State = MediaPlayer.Null Then
        If Not Error Then $player.Play()
    Else
        $player.State = MediaPlayer.Null
    End If

Catch
    Debug Error.Text
    
End

Private Function WebRadioDirectory_Read() As String

    Return User.Home &/ ".nagato-web-radio"

End

Private Function PlsList_Read() As NagatoPls[]

    Return $plsList

End
