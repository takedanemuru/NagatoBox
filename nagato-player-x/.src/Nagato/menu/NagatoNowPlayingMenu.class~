' Gambas class file

Inherits NagatoStarMenu

Private Enum MenuAddMusic, MenuAddDirectory, MenuSaveAsPlaylist, MenuClear

Event SavePlaylist

Private Sub addMusic()
    
    Dialog.Title = ("Select music")
    Dialog.Path = NagatoSettingsMusic.DialogLastPath
    
    If Not Dialog.OpenFile(True) Then
        NagatoQueue.AddMusics(Dialog.Paths)
    Endif
    
End

Private Sub addDirectory()
    
    Dialog.Title = ("Select directory")
    Dialog.Path = User.Home
    
    If Not Dialog.SelectDirectory() Then NagatoQueue.AddDirectory(Dialog.Path)
    
End

Private Sub saveAsPlaylist()
    
    If HaruhiSaveAsPlaylist.ShowDialog() = MikuruDialog.ResponseApply Then Raise SavePlaylist
    
End

Private Sub clear()
    
    NagatoQueue.ClearAll()
    
End

Public Sub _new()
    
    Me._InitializeRootMenu()
    Me._AddMenuItem(("Add music"), MenuAddMusic, "music")
    Me._AddMenuItem(("Add directory"), MenuAddDirectory, "folder")
    Me._AddMenuItem(("Save as playlist"), MenuSaveAsPlaylist, "hard-drive-download")
        Me._AddSpacer()
    Me._AddMenuItem(("Clear queue"), MenuClear, "bin")
        Me._AddSpacer()
    Me._AddAboutMenu()
        Me._AddSpacer()
    Me._AddClosePlaylistMenu()
    
End

Public Sub MenuItem_Click()
    
    Select Case Last.Tag
        Case MenuAddMusic
            addMusic()
        Case MenuAddDirectory
            addDirectory()
        Case MenuSaveAsPlaylist
            saveAsPlaylist()
        Case MenuClear
            clear()
    End Select
    
End

Private Function Name_Read() As String

    Return "NowPlayingMenu"

End
