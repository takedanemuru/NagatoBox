' Gambas class file

Private $drawingArea As DrawingArea
Private $coverArt As Image
Private $textRect As NagatoRichTextRect

Private Sub resetCoverArt()
    
    If NagatoDBusInterfaceProperty.CoverArtPath Then
        $coverArt = Image.Load(NagatoDBusInterfaceProperty.CoverArtPath)
        $coverArt = $coverArt.Stretch(200, 200)
    Else
        $coverArt = New Image(200, 200, Color.Lighter(Color.Violet))
    End If
    
End

Private Sub paintText()
    
    $textRect.SetText(NagatoQueue.GetProperty(MikuruProperty.CurrentTitle), Paint.Font)
    
    Paint.Brush = Paint.Color(Color.Gray + Color.Transparent * 128)
        With $textRect.ShadeRect
            Paint.Rectangle(.X, .Y, .W, .H)
        End With
    Paint.Fill()
    
    Paint.Brush = Paint.Color(Color.White)
        With $textRect.TextRect
            Paint.DrawRichText($textRect.Text, .X, .Y, .W, .H, Align.Center)
        End With
    Paint.Fill()
    
End

Public Sub _call(argSignal As Integer, argValues As Variant[])
    
    Select argSignal
        Case MikuruSignal.BroadcastIndexMoved
            resetCoverArt()
        Case MikuruSignal.BroadcastMessage
            '$painter.SetMessage(argValues[0], argValues[1])
    End Select
    
    $drawingArea.Refresh()
    
End

Public Sub _new(argContainer As Container)
    
    resetCoverArt()
    $textRect = New NagatoRichTextRect(200, 200, 10)
    $drawingArea = New DrawingArea(argContainer) As "DrawingArea"
    
End

Public Sub DrawingArea_MouseDown()
    
    If Mouse.Right Then 
        HaruhiPlayList.Show()
    Else If Mouse.X > 150 Then
        NagatoQueue(MikuruSignal.QueueNext, Null)
    Else If 50 > Mouse.X Then
        NagatoQueue(MikuruSignal.QueueBack, Null)
    Else
        If NagatoQueue.GetProperty(MikuruProperty.PlayState) <> Media.Playing Then
            NagatoQueue(MikuruSignal.QueuePlay, Null)
        Else
            NagatoQueue(MikuruSignal.QueuePause, Null)
        End If
    End If
    
End

Public Sub DrawingArea_Draw()
    
    Dim yukiRate As Float = NagatoQueue.GetProperty(MikuruProperty.CurrentDurationData).Rate
    
    Draw.Begin($drawingArea)
        Paint.Font.Size = 9
        Paint.DrawImage($coverArt, 0, 0)
        paintText()
        Paint.Brush = Paint.Color(Color.Orange)
        Paint.Rectangle(0, 198, 200 * yukiRate, 2)
        Paint.Fill()
    Draw.End()
    
End
