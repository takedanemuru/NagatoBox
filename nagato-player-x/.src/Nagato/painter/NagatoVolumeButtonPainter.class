' Gambas class file

Inherits NagatoWidgetPainter

Private Function getRate() As Float
    
    Dim yukiRate As Float
    
    With MikuruUx.Button.Volume
        yukiRate = (MikuruMousePosition.X - .X) / (.W - .X)
    End With
    
    If 0 > yukiRate Then yukiRate = 0
    If yukiRate > 1 Then yukiRate = 1
    
    Return yukiRate
    
End

Private Sub paintProgressBarOnMouse()
    
    Dim yukiRate As Float = getRate()
    Dim yukiBarRectangle As Rect = Me._GetBarRectangle(MikuruUx.Button.Volume.Rect, yukiRate)
    Dim yukiText As String = Subst$("<b>VOL : &1%</b>", Min(CInt(yukiRate * 100), 100))
    
    Me._PaintClipRectangle(MikuruUx.Button.Volume.Rect, 2 * MikuruUx.Grid)
    Me._PaintRectangle(MikuruUx.Button.Volume.Rect, MikuruColor.WidgetOnMouseLight) 'frame
    Me._PaintRectangle(yukiBarRectangle, MikuruColor.WidgetOnMouse, 2 * MikuruUx.Grid) 'bar
    Me._PaintRichText(MikuruUx.Button.Volume.Rect, yukiText)
    Paint.Restore() 'restore clip rectangle
    
End

Private Sub paintProgressBarNormal()
    
    Dim yukiVolume As Float = NagatoQueue.GetProperty(MikuruProperty.SoftwareVolume)
    Dim yukiBarRectangle As Rect = Me._GetBarRectangle(MikuruUx.Button.Volume.Rect, yukiVolume)
    
    Me._PaintClipRectangle(MikuruUx.Button.Volume.Rect, 2 * MikuruUx.Grid)
    Me._PaintRectangle(MikuruUx.Button.Volume.Rect, MikuruColor.WidgetLight) 'frame
    Me._PaintRectangle(yukiBarRectangle, MikuruColor.Widget, 2 * MikuruUx.Grid) 'bar
    Me._PaintRichText(MikuruUx.Button.Volume.Rect, Subst$("<b>VOL : &1%</b>", CInt(yukiVolume * 100)))
    Paint.Restore() 'restore clip rectangle
    
End

Public Sub Draw()
    
    If MikuruUx.Button.Volume.Contains(MikuruMousePosition.X, MikuruMousePosition.Y) Then
        paintProgressBarOnMouse()
    Else
        paintProgressBarNormal()
    Endif
    
End
