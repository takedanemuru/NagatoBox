' Gambas class file

Public Function _GetWidgetColor(argRectangle As Rect, Optional argFlag As Integer = MikuruState.None) As Integer
    
    If argRectangle.Contains(MikuruMousePosition.X, MikuruMousePosition.Y) Then
        Return MikuruColor.WidgetOnMouse
    Else If MikuruState.CheckState(argFlag) Then
        Return MikuruColor.WidgetToggled
    Else
        Return MikuruColor.Widget
    Endif
    
End

Public Sub _PaintClipRectangle(argRectangle As Rect, Optional argRadius As Integer = 0)
    
    With argRectangle
        Paint.Save()
        Paint.Rectangle(.X, .Y, .W, .H, argRadius)
        Paint.Clip()
    End With
    
End

Public Sub _PaintRectangle(argRectangle As Rect, argColor As Integer, Optional argRadius As Integer = 0)
    
    With argRectangle
        Paint.Brush = Paint.Color(argColor)
        Paint.Rectangle(.X, .Y, .W, .H, argRadius)
        Paint.Fill()
    End With
    
End

Public Function _GetBarRectangle(argBaseRectangle As Rect, argProgressRate As Float) As Rect
    
    Dim yukiRectangle As Rect
    
    With argBaseRectangle
        yukiRectangle = New Rect(.X, .Y, (.W) * argProgressRate, .H)
    End With
    
    Return yukiRectangle
    
End

Public Sub _PaintRichText(argRectangle As Rect, argText As String, Optional argAlign As Integer = Align.Center)
    
    With argRectangle
        Paint.Brush = Paint.Color(Color.White)
        Paint.DrawRichText(argText, .X, .Y, .W, .H, argAlign)
        Paint.Fill()
    End With 
    
End
