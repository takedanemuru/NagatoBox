' Gambas class file

Inherits NagatoTable

Private Sub deleteDuplicatedRecord(argPlaylistNameId As Integer)
    
    Try Me._$connection.Delete(NagatoDB.Table.PlaylistData, "playlist_name_id = &1", argPlaylistNameId)
    
End

Private Sub save(argPlaylistNameId As Integer, argPath As String)
    
    Dim yukiResult As Result
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Create(NagatoDB.Table.PlaylistData)
        yukiResult[NagatoDB.Column.PlaylistNameId] = argPlaylistNameId
        yukiResult[NagatoDB.Column.Path] = argPath
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Debug Error.Text
    Me._$connection.Rollback()
    
End

Private Sub savePaths(argPlaylistNameId As Integer, argPaths As String[])
    
    Dim yukiPath As String
    
    For Each yukiPath In argPaths
        save(argPlaylistNameId, yukiPath)
    Next
    
End

Public Sub _EnsureTable() 'override
    
    Dim yukiTable As Table
    
    If Me._$connection.Tables.Exist(NagatoDB.Table.PlaylistData) Then Return

    yukiTable = Me._$connection.Tables.Add(NagatoDB.Table.PlaylistData)
    yukiTable.Fields.Add(NagatoDB.Column.Id, db.Serial)
    yukiTable.Fields.Add(NagatoDB.Column.PlaylistNameId, db.Integer)
    yukiTable.Fields.Add(NagatoDB.Column.Path, db.String)
    yukiTable.PrimaryKey = [NagatoDB.Column.Id]
    yukiTable.Update()
    
End

Public Sub OverwritePlaylist(argPlaylistNameId As Integer, argPaths As String[])
    
    If 0 > argPlaylistNameId Then Return
    
    deleteDuplicatedRecord(argPlaylistNameId)
    savePaths(argPlaylistNameId, argPaths)
    
End
