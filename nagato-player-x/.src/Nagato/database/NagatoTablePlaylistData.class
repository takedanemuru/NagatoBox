' Gambas class file

Inherits NagatoTable

Private Sub save(argPlaylistNameId As Integer, argPath As String)
    
    Dim yukiResult As Result
    
    With NagatoDBMedia
        Me._$connection.Begin()
            yukiResult = Me._$connection.Create(.Table.PlaylistData)
            yukiResult[.ColumnPlaylistNameId] = argPlaylistNameId
            yukiResult[.ColumnPath] = argPath
            yukiResult.Update()
        Me._$connection.Commit()
    End With
    
Catch
    Debug Error.Text
    Me._$connection.Rollback()
    
End

Public Sub _EnsureTable() 'override
    
    Dim yukiTable As Table
    
    With NagatoDBMedia
        If Not Me._$connection.Tables.Exist(.Table.PlaylistData) Then
            yukiTable = Me._$connection.Tables.Add(.Table.PlaylistData)
            yukiTable.Fields.Add(.ColumnId, db.Serial)
            yukiTable.Fields.Add(.ColumnPlaylistNameId, db.Integer)
            yukiTable.Fields.Add(.ColumnPath, db.String)
            yukiTable.PrimaryKey = [.ColumnId]
            yukiTable.Update()
        Endif
    End With
    
End

Public Sub OverwritePlaylist(argPlaylistNameId As Integer, argPaths As String[])
    
    Dim yukiPath As String
    
    Try Me._$connection.Delete(NagatoDBMedia.Table.PlaylistData, "playlist_name_id = &1", argPlaylistNameId)
    
    For Each yukiPath In argPaths
        save(argPlaylistNameId, yukiPath)
    Next
    
End
