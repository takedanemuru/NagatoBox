' Gambas class file

Create Static

Property Read HostName As String 'act as const.

Property Read Column As NagatoDBColumns
Property Read Table As NagatoDBTables

Public _$uniqueName As String ' MUST BE OVERRIDDEN ! 
Public _$connection As Connection
Private $columns As New NagatoDBColumns
Private $tables As New NagatoDBTables

Private Sub addConnection()
    
    Me._$connection = New Connection
    
    With Me._$connection
        .Type = "sqlite3"
        .Host = Me.HostName
        .Name = ""
        .Login = ""
        .Password = ""
        .Port = ""
    End With
    
End

Private Sub checkAndCreateDatabase()
    
    Me._$connection.Open()
    
    If Not Me._$connection.Databases.Exist(Me._$uniqueName) Then
        Me._$connection.Databases.Add(Me._$uniqueName)
        Wait 0.5
    Endif
    
    Me._$connection.Close()
    
End

Private Sub moveToDatabase()
    
    Me._$connection.Host = Me.HostName
    Me._$connection.Name = Me._$uniqueName
    
End

Public Sub _SetConnection() 
    
    MikuruDirectory.Ensure(Me.HostName)
    addConnection()
    checkAndCreateDatabase()
    moveToDatabase()
    
End

Private Function HostName_Read() As String

    'Return User.Home &/ ".nagato_software_compilation"
    Return Desktop.DataDir &/ "NagatoBox/database"

End

Private Function Column_Read() As NagatoDBColumns

    Return $columns

End

Private Function Table_Read() As NagatoDBTables

    Return $tables

End
