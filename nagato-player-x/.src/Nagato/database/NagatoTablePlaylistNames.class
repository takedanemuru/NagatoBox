' Gambas class file

Inherits NagatoTable

Private Sub push(argPlaylistName As String, argDescription As String)
    
    Dim yukiResult As Result
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Create(NagatoDB.Table.PlaylistNames)
        yukiResult[NagatoDB.Column.PlaylistName] = argPlaylistName
        yukiResult[NagatoDB.Column.PlaylistDescription] = argDescription
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Me._$connection.Rollback()
    
End

Private Sub edit(argPlaylistName As String, argDescription As String)
    
    Dim yukiResult As Result
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Edit(NagatoDB.Table.PlaylistNames)
        yukiResult[NagatoDB.Column.PlaylistName] = argPlaylistName
        yukiResult[NagatoDB.Column.PlaylistDescription] = argDescription
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Me._$connection.Rollback()
    
End

Public Sub _EnsureTable() 'override
    
    Dim yukiTable As Table
    
    If Not Me._$connection.Opened Then Me._$connection.Open()
    
    If Me._$connection.Tables.Exist(NagatoDB.Table.PlaylistNames) Then Return
    
    yukiTable = Me._$connection.Tables.Add(NagatoDB.Table.PlaylistNames)
    yukiTable.Fields.Add(NagatoDB.Column.Id, db.Serial)
    yukiTable.Fields.Add(NagatoDB.Column.PlaylistName, db.String)
    yukiTable.Fields.Add(NagatoDB.Column.PlaylistDescription, db.String)
    yukiTable.PrimaryKey = [NagatoDB.Column.Id]
    yukiTable.Update()
    
End

Public Sub EnsurePlaylistName(argPlaylistName As String, argDescription As String)
    
    If Me._$connection.Find(NagatoDB.Table.PlaylistNames, "playlist_name = &1", argPlaylistName).Count > 0 Then
        edit(argPlaylistName, argDescription)
    Else
        push(argPlaylistName, argDescription)
    End If
    
End

Public Function GetPlaylistNameId(argPlaylistName As String) As Integer
    
    Dim yukiResult As Result
    
    yukiResult = Me._$connection.Find(NagatoDB.Table.PlaylistNames, "playlist_name = &1", argPlaylistName)
    
    Return yukiResult[NagatoDBMedia.Column.Id]
    
Catch
    Return -1
    
End

Public Function GetAllResult() As Result
    
    Return Me._$connection.Find(NagatoDB.Table.PlaylistNames)
    
End

Public Function HasResult() As Boolean
    
    If Me._$connection.Find(NagatoDB.Table.PlaylistNames).Count > 0 Then
        Return True
    Else
        Return False
    Endif
    
End
