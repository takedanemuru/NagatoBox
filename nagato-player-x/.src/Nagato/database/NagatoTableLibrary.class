' Gambas class file

Inherits NagatoTable

Public Sub _EnsureTable() 'override
    
    Dim yukiTable As Table
    
    Me._$connection.Open()
    
    If Not Me._$connection.Tables.Exist(NagatoDB.Table.Library) Then
        yukiTable = Me._$connection.Tables.Add(NagatoDB.Table.Library)
        yukiTable.Fields.Add(NagatoDB.Column.Id, db.Serial)
        yukiTable.Fields.Add(NagatoDB.Column.Title, db.String)
        yukiTable.Fields.Add(NagatoDB.Column.Artist, db.String)
        yukiTable.Fields.Add(NagatoDB.Column.Album, db.String)
        yukiTable.Fields.Add(NagatoDB.Column.Path, gb.String)
        yukiTable.Fields.Add(NagatoDB.Column.Duration, gb.Float)
        yukiTable.PrimaryKey = [NagatoDB.Column.Id]
        yukiTable.Update()
    Endif
    
End

Public Sub AddTagsByNagatoTags(argPath As String, argTags As NagatoId3TagInfo)
    
    Dim yukiResult As Result
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Edit(NagatoDB.Table.Library, "path = &1", argPath)
        If argTags.Title Then yukiResult[NagatoDB.Column.Title] = argTags.Title
        If argTags.Artist Then yukiResult[NagatoDB.Column.Artist] = argTags.Artist
        If argTags.Album Then yukiResult[NagatoDB.Column.Album] = argTags.Album
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Me._$connection.Rollback()
    
End

Public Sub FetchDuration(argPath As String, argDuration As Float) 
    
    Dim yukiResult As Result
    
    If argDuration = 0 Then Return
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Edit(NagatoDB.Table.Library, "path = &1", argPath)
        yukiResult[NagatoDB.Column.Duration] = argDuration
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Me._$connection.Rollback()
    
End

Public Sub EnsureLibrary(argPath As String) ' MUST be fullpath.
    
    Dim yukiResult As Result
    Dim yukiId3TagInfo As NagatoId3TagInfo
    
    If Me._$connection.Find(NagatoDB.Table.Library, "path = &1", argPath).Count > 0 Then Return
    
    yukiId3TagInfo = MikuruId3TagInfo(argPath)
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Create(NagatoDB.Table.Library)
        yukiResult[NagatoDB.Column.Title] = yukiId3TagInfo.Title
        yukiResult[NagatoDB.Column.Artist] = yukiId3TagInfo.Artist
        yukiResult[NagatoDB.Column.Album] = yukiId3TagInfo.Album
        yukiResult[NagatoDB.Column.Duration] = MikuruDuration.GetDuration(argPath)
        yukiResult[NagatoDB.Column.Path] = argPath
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Me._$connection.Rollback()
    
End
