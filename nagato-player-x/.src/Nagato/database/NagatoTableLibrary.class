' Gambas class file

Inherits NagatoTable

Public Sub _EnsureTable() 'override
    
    Dim yukiTable As Table
    
    Me._$connection.Open()
    
    If Not Me._$connection.Tables.Exist(NagatoDB.Table.Library) Then
        yukiTable = Me._$connection.Tables.Add(NagatoDB.Table.Library)
        yukiTable.Fields.Add(NagatoDBMedia.Column.Id, db.Serial)
        yukiTable.Fields.Add(NagatoDBMedia.Column.Title, db.String)
        yukiTable.Fields.Add(NagatoDBMedia.Column.Artist, db.String)
        yukiTable.Fields.Add(NagatoDBMedia.Column.Album, db.String)
        yukiTable.Fields.Add(NagatoDBMedia.Column.Path, gb.String)
        yukiTable.Fields.Add(NagatoDBMedia.Column.Duration, gb.Float)
        yukiTable.PrimaryKey = [NagatoDBMedia.Column.Id]
        yukiTable.Update()
    Endif
    
End

Public Sub AddTagsByNagatoTags(argPath As String, argTags As NagatoTags)
    
    Dim yukiResult As Result
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Edit(NagatoDB.Table.Library, "path = &1", argPath)
        If argTags.Title Then yukiResult[NagatoDBMedia.Column.Title] = argTags.Title
        If argTags.Artist Then yukiResult[NagatoDBMedia.Column.Artist] = argTags.Artist
        If argTags.Album Then yukiResult[NagatoDBMedia.Column.Album] = argTags.Album
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Me._$connection.Rollback()
    
End

Public Function FetchDuration(argPath As String, argDuration As Float) As Boolean
    
    Dim yukiResult As Result
    Dim yukiFlag As Boolean = False
    
    If argDuration = 0 Then Return
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Edit(NagatoDB.Table.Library, "path = &1", argPath)
        yukiFlag = (yukiResult[NagatoDBMedia.Column.Duration] = argDuration)
        If Not yukiFlag Then yukiResult[NagatoDBMedia.Column.Duration] = argDuration
        yukiResult.Update()
    Me._$connection.Commit()
    
    Return yukiFlag
    
Catch
    Me._$connection.Rollback()
    Return False
    
End

Public Sub EnsureLibrary(argPath As String) ' MUST be fullpath.
    
    Dim yukiResult As Result
    
    If Me._$connection.Find(NagatoDB.Table.Library, "path = &1", argPath).Count > 0 Then Return
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Create(NagatoDB.Table.Library)
        yukiResult[NagatoDBMedia.Column.Title] = MikuruId3TagInfo.GetTitle(argPath)
        yukiResult[NagatoDBMedia.Column.Artist] = MikuruId3TagInfo.GetArtist(argPath)
        yukiResult[NagatoDBMedia.Column.Album] = MikuruId3TagInfo.GetAlbum(argPath)
        yukiResult[NagatoDBMedia.Column.Duration] = MikuruDuration.GetDuration(argPath)
        yukiResult[NagatoDBMedia.Column.Path] = argPath
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Me._$connection.Rollback()
    
End
