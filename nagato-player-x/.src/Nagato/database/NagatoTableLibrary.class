' Gambas class file

Inherits NagatoTable

Public Sub _EnsureTable() 'override
    
    Dim yukiTable As Table
    
    With NagatoDBMedia
        If Not Me._$connection.Tables.Exist(.TableLibrary) Then
            yukiTable = Me._$connection.Tables.Add(.TableLibrary)
            yukiTable.Fields.Add(.ColumnId, db.Serial)
            yukiTable.Fields.Add(.ColumnTitle, db.String)
            yukiTable.Fields.Add(.ColumnArtist, db.String)
            yukiTable.Fields.Add(.ColumnAlbum, db.String)
            yukiTable.Fields.Add(.ColumnPath, gb.String)
            yukiTable.Fields.Add(.ColumnPlayTimes, gb.Integer)
            yukiTable.Fields.Add(.ColumnStars, gb.Integer)
            yukiTable.Fields.Add(.ColumnDuration, gb.Float)
            yukiTable.PrimaryKey = [.ColumnId]
            yukiTable.Update()
        Endif
    End With
    
End

Public Sub Add(argPath As String, argTitle As String, argArtist As String, argAlbum As String)
    
    Dim yukiResult As Result
    
    With NagatoDBMedia
        Me._$connection.Begin()
            yukiResult = Me._$connection.Create(.TableLibrary)
            yukiResult[.ColumnPath] = argPath
            yukiResult[.ColumnTitle] = argTitle
            yukiResult[.ColumnArtist] = argArtist
            yukiResult[.ColumnAlbum] = argAlbum
            yukiResult.Update()
        Me._$connection.Commit()
    End With
    
Catch
    Debug Error.Text
    Me._$connection.Rollback()
    
End

Public Sub EnsureLibrary(argPath As String) ' MUST be fullpath.
    
    Dim yukiResult As Result
    
    If Me._$connection.Find(NagatoDBMedia.TableLibrary, "path = &1", argPath).Count > 0 Then Return
    
    Me._$connection.Begin()
        yukiResult = Me._$connection.Create(NagatoDBMedia.TableLibrary)
        yukiResult[NagatoDBMedia.ColumnTitle] = MikuruId3TagInfo.GetTitle(argPath)
        yukiResult[NagatoDBMedia.ColumnArtist] = MikuruId3TagInfo.GetArtist(argPath)
        yukiResult[NagatoDBMedia.ColumnAlbum] = MikuruId3TagInfo.GetAlbum(argPath)
        yukiResult[NagatoDBMedia.ColumnDuration] = MikuruDuration.GetDuration(argPath)
        yukiResult[NagatoDBMedia.ColumnPath] = argPath
        yukiResult.Update()
    Me._$connection.Commit()
    
Catch
    Debug Error.Text
    Me._$connection.Rollback()
    
End