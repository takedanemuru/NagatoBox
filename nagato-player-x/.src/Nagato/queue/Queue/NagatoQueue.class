' Gambas class file

Create Static

Private $player As New NagatoPlayer As "TFEI"
Private $musicList As NagatoMusicList

Private $handlerPlayer As NagatoQueueAssistantPlayer
Private $handlerList As NagatoQueueAssistantList
Private $handlerProperties As NagatoQueueAssistantCurrent

Event Broadcast(argSignal As Integer, argValues As Variant[])

Public Sub _call(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.QueueHeader To MikuruSignal.QueueHeader + 999
            $handlerPlayer(argSignal, argValues)
        Case MikuruSignal.ListHeader To MikuruSignal.ListHeader + 999
            $handlerList(argSignal, argValues)
    End Select

End

Public Function GetProperty(argProperty As Integer) As Variant
    
    Return $handlerProperties.GetProperty(argProperty)
    
End

Public Sub ChangeCoverArtByIndexes(argIndexes As Integer[], argCoverArtPath As String)
    
    Dim yukiIndex As Integer
    
    If Not Exist(argCoverArtPath) Then Return
    Debug DesktopMime.FromFile(argCoverArtPath).Type
    
    For Each yukiIndex In argIndexes
        MikuruEyeD3.SetPicture($musicList[yukiIndex].Path, argCoverArtPath)
        If yukiIndex = $musicList.CurrentIndex Then $handlerProperties.Reload()
    Next
    
End

Public Sub Activate(Optional argPaths As String[] = [""])
    
    $musicList = New NagatoMusicList(argPaths) As "TFEI"
    $handlerList = New NagatoQueueAssistantList($player, $musicList) As "TFEI"
    $handlerPlayer = New NagatoQueueAssistantPlayer($player, $musicList) As "TFEI"
    $handlerProperties = New NagatoQueueAssistantCurrent($player, $musicList)
    
End

Public Sub _get(argIndex As Integer) As NagatoMusicElement
    
    Return $musicList[argIndex]
    
End

Public Sub TFEI_Signal(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.BroadcastIndexMoved
            $handlerProperties.Reload()
            HaruhiPlayer.Title = $handlerProperties.GetProperty(MikuruProperty.CurrentTitle)
        Case MikuruSignal.BroadcastQueueChanged
            $handlerList.SaveCurrentQueue()
    End Select
    
    Raise Broadcast(argSignal, argValues)
    
End
