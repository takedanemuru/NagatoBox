' Gambas class file

Inherits NagatoQueueAssistant

Public Sub SaveCurrentQueue()
    
    Dim yukiPaths As New String[]
    Dim yukiIndex As Integer
    
    For yukiIndex = 0 To Me._$musicList.Max
        yukiPaths.Add(Me._$musicList[yukiIndex].Path)
    Next
    
    NagatoSettingsMusic.SavePaths(yukiPaths)
    
End

Private Sub addDirectory(argPath As String)
    
    Dim yukiPaths As String[] = MikuruMusicFiles.GetFromDirectory(argPath)
    
    If yukiPaths.Count > 0 Then Me._$musicList.EnqueuePaths(yukiPaths)
    
End

Private Sub dequeueByIndexes(argIndexes As Integer[])
    
    Me._$player.Stop()
    If Me._$musicList.DequeueIndexes(argIndexes) Then 
        Raise Signal(MikuruSignal.BroadcastQueueChanged, Null)
    End If
    Me._$player.Play()
    
End

Private Sub editTagsByTagIndexes(argIndexes As Integer[], argTags As NagatoId3TagInfo)
    
    Dim yukiIndex As Integer
    
    For Each yukiIndex In argIndexes
        Me._$musicList[yukiIndex].EditTags(argTags)
    Next
    
    Raise Signal(MikuruSignal.BroadcastId3TagChanged, argIndexes)
    
End

Public Sub _call(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.ListClear
            Me._$musicList.ClearAll()
        Case MikuruSignal.ListAddMusic
            Me._$musicList.EnqueuePaths(argValues)
        Case MikuruSignal.ListAddDirectory
            addDirectory(argValues[0])
        Case MikuruSignal.ListUp
            If argValues.Count > 0 Then Me._$musicList.Up(argValues)
        Case MikuruSignal.ListDown
            If argValues.Count > 0 Then Me._$musicList.Down(argValues)
        Case MikuruSignal.ListSort
            Me._$musicList.Sort(argValues[0], argValues[1])
        Case MikuruSignal.ListSaveAs
            Me._$musicList.SaveAsPlayList(argValues[0])
        Case MikuruSignal.ListDeleteDuplicateMusic
            Me._$musicList.RemoveDuplicateMusic()
        Case MikuruSignal.ListDelete
            dequeueByIndexes(argValues)
        Case MikuruSignal.ListEdit
            editTagsByTagIndexes(argValues[0], argValues[1])
    End Select
    
End
