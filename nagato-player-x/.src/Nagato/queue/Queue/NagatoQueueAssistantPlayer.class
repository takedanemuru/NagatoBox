' Gambas class file

Inherits NagatoQueueAssistant

Property Read State As Integer
Property SoftwareVolume As Float

Private $observer As Observer

Public Sub Seek(argProgress As Float) ' argFloat must be between 0 to 1
    
    Me._$player.SetByRate(argProgress)
    
End

Public Sub Play()
    
    If Not Me._$musicList.HasElement Then Return
    If Not Me._$player.State <> Media.Playing Then Me._$player.Play()
    
End

Public Sub Pause()
    
    Me._$player.Pause()
    
End

Public Sub Next()
    
    If Not Me._$musicList.HasElement Then Return

    Me._$musicList.Next()

    If Me._$player.State = Media.Playing Then 
        Try Me._$player.Play(0)
    Else
        Me._$player.ResetUrl()
    End If
    
End

Public Sub Back()
    
    If Not Me._$musicList.HasElement Then Return
    
    If Me._$player.State = Media.Playing And If NagatoQueue.ChannelCurrent.DurationData.Rate > 0.05 Then
        Me._$player.SetByRate(0)
    Else If Me._$player.State = Media.Playing Then
        Me._$musicList.Back()
        Me._$player.Play(0)
    Else ' when Me._$player is stopped/paused
        Me._$musicList.Back()
        Me._$player.ResetUrl()
    End If
    
End

Public Sub SetIndexAndPlay(argIndex As Integer)
    
    Me._$player.SetIndexAndPlay(argIndex)
    
End

Public Sub _OptionalOnInitialize()
    
    $observer = New Observer(Me._$player) As "Asakura"
    
End

Public Sub Asakura_MusicEnd()
    
    If NagatoSettingsMusic.Repeat Then Me.Back()
    If Not NagatoSettingsMusic.Repeat Then Me.Next()
    
End

Private Function State_Read() As Integer

    Return Me._$player.State

End

Private Function SoftwareVolume_Read() As Float

    Return Me._$player.SoftwareVolume

End

Private Sub SoftwareVolume_Write(Value As Float)

    Me._$player.SoftwareVolume = Value

End
