' Gambas class file

Private $musicElements As New NagatoMusicElements
Private $position As NagatoMusicPosition 

Public Sub Sort(argType As Integer, argSortAscend As Boolean)
    
    Dim yukiBuffer As New NagatoSortBuffer(argType, argSortAscend)
    Dim yukiCount As Integer
    
    For yukiCount = 0 To $musicElements.Count - 1
        yukiBuffer.Set($musicElements[yukiCount], (yukiCount = $position.Current))
    Next
    
    $musicElements.Set(yukiBuffer.Elements)
    $position.ResetOnSort(yukiBuffer.Current, $musicElements.Count)
    
End

Public Function Up(argSelection As Integer[]) As Integer[]
    
    Dim yukiCount As Integer
    Dim yukiBuffer As New NagatoMusicElement[]
    Dim yukiInsertPosition As Integer = Max(0, argSelection.Sort()[0] - 1)
    Dim yukiNewCurrentPosition As Integer
    Dim yukiNewSelection As New Integer[]

    For yukiCount = 0 To $musicElements.Count - 1
        If argSelection.Exist(yukiCount) Then
            yukiBuffer.Insert([$musicElements[yukiCount]], yukiInsertPosition)
            If yukiCount = $position.Current Then yukiNewCurrentPosition = yukiInsertPosition
            yukiNewSelection.Add(yukiInsertPosition)
            Inc yukiInsertPosition
        Else
            yukiBuffer.Add($musicElements[yukiCount])
            If yukiCount = $position.Current Then yukiNewCurrentPosition = yukiCount
        Endif
    Next

    $musicElements.Set(yukiBuffer)
    $position.Current = yukiNewCurrentPosition

    Return yukiNewSelection
    
End

Public Function Down(argSelection As Integer[]) As Integer[]
    
    Dim yukiCount As Integer
    Dim yukiIndex As Integer
    Dim yukiBuffer As New NagatoMusicElement[]
    Dim yukiMax As Integer
    Dim yukiInsertPosition As Integer
    Dim yukiNewCurrentPosition As Integer
    Dim yukiNewSelection As New Integer[]
    
    yukiMax = argSelection.Sort()[argSelection.Max]
    
    For yukiCount = 0 To $musicElements.Count - 1
        If argSelection.Exist(yukiCount) Then Continue
        yukiBuffer.Add($musicElements[yukiCount])
        If yukiCount = (yukiMax + 1) Then yukiInsertPosition = yukiCount 
        If yukiCount = $position.Current Then yukiNewCurrentPosition = yukiCount
    Next
    
   For Each yukiIndex In argSelection
       yukiBuffer.Add($musicElements[yukiIndex], yukiInsertPosition)
       If yukiIndex = $position.Current Then
            yukiNewCurrentPosition = yukiInsertPosition
       Else If yukiNewCurrentPosition > yukiInsertPosition Then 
           Inc yukiNewCurrentPosition
       End If
       yukiNewSelection.Add(yukiInsertPosition)
       Inc yukiInsertPosition
   Next
    
    $musicElements.Set(yukiBuffer)
    $position.Current = yukiNewCurrentPosition
    
    Return yukiNewSelection
    
End

Public Sub _new(argMusicElements As NagatoMusicElements, argPosition As NagatoMusicPosition)
    
    $musicElements = argMusicElements
    $position = argPosition
    
End
