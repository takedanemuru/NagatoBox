' Gambas class file

Inherits NagatoObject

Property Read Count As Integer

Private $musicElements As New NagatoMusicElement[]
Private $handlerData As NagatoMusicElementsHandlerData

Private Function addPaths(argPaths As String[]) As Integer
    
    Dim yukiPath As String
    Dim yukiCount As Integer = 0
    
    For Each yukiPath In MikuruPath.Sanitize(argPaths)
        If Not MikuruMusicFormat.IsPlayable(yukiPath) Then Continue
        $musicElements.Add(Object.New("NagatoMusicElement", [yukiPath]))
        Inc yukiCount
    Next
    
    Raise Signal(MikuruSignal.BroadcastQueueChanged, Null)
    
    Return yukiCount
    
End

Public Function GetDuplicateMusics() As Integer[]
   
    Return $handlerData.DuplicateIndexes
   
End

Public Function EnqueueByPaths(argPaths As String[]) As Integer
    
    Return addPaths(argPaths)
    
End 

Public Sub DequeueByIndexes(argIndexes As Integer[])
    
    Dim yukiIndex As Integer
    
    For Each yukiIndex In argIndexes.Sort(gb.Descent)
        $musicElements.Remove(yukiIndex)
    Next
    
End

Public Sub Clear()
    
    $musicElements.Clear()
    
End

Public Sub Initialize(argPaths As String[])
    
    $handlerData = New NagatoMusicElementsHandlerData($musicElements)
    addPaths(NagatoSettingsMusic.LoadPaths())
    addPaths(argPaths)
    
End

Public Sub _get(argIndex As Integer) As NagatoMusicElement
    
    Return $musicElements[argIndex]
    
End

Public Sub SaveAsPlaylist(argPlaylistName As String, argDescription As String)
    
    If $musicElements.Count = 0 Then Return
    
    NagatoDBPlaylists.SaveAsPlaylist(argPlaylistName, argDescription, $handlerData.Paths)
    Raise Signal(MikuruSignal.BroadcastPlaylistsChanged, Null)
    
End

Public Sub Set(argMusicElements As NagatoMusicElement[])
    
    Dim yukiIndex As Integer
    
    For yukiIndex = 0 To $musicElements.Max
        $musicElements[yukiIndex] = argMusicElements[yukiIndex]
    Next
    
End

Public Function GetProperty(argProperty As Integer, argIndex As Integer) As Variant
    
    Return $handlerData.GetProperty(argProperty, argIndex)
    
End

Private Function Count_Read() As Integer

    Return $musicElements.Count

End
