' Gambas class file

Inherits NagatoObject

Property Read State As Integer
Property SoftwareVolume As Float

Private $player As New MediaPlayer As "Player"

Event MusicEnd

Public Sub SetByRate(argRate As Float) 
    
    $player.Position = $player.Duration * Min(argRate, 1)
    
End

Public Sub TryPlay()
    
    $player.Play()
    
Catch
    Me.ResetUrl(True)
    
End

Public Sub ResetUrl(Optional argPlay As Boolean = True)
    
    $player.URL = NagatoQueue.GetProperty(MikuruProperty.CurrentUrl)
    $player.Position = 0
    If argPlay Then $player.Play()
    
End

Public Sub Pause()
    
    Try $player.Pause()
   
End

Public Sub Stop(Optional argClearUrl As Boolean = False)
    
    Try $player.Stop()
    If argClearUrl Then $player.URL = ""
    
End

Public Function GetDurationData() As NagatoDurationData
    
    Return Object.New("NagatoDurationData", [$player.Position, $player.Duration])
    
End

Public Sub Player_End()
    
    Raise MusicEnd
    
End

Private Function State_Read() As Integer

    Return $player.State

End

Private Function SoftwareVolume_Read() As Float

    Return $player.Audio.Volume

End

Private Sub SoftwareVolume_Write(Value As Float)

    Try $player.Audio.Volume = Min(Value, 1)

End
