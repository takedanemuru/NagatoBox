' Gambas class file

Property Read Rate As Float
Property Read Text As String
Property Read State As Integer
Property SoftwareVolume As Float

Private $player As New MediaPlayer As "Player"
Private $durationFetcher As New NagatoDurationFetcher 

Event MusicEnd
Event FetchDuration(argDuration As Float)

Public Function GetTextByRate(argRate As Float) As String ' argRate MUST be between 0 to 1

    Return MikuruDurationText.GetDurationTextByRate(argRate, $player.Duration)

End

Public Sub SetByRate(argRate As Float) 
    
    $player.Position = $player.Duration * Min(argRate, 1)
    
End

Public Sub SetPathAndPlay(argPath As String)
    
    If Not NagatoQueue.ChannelList.SetPath(argPath) Then Return
    Try $player.Stop
    $player.URL = Media.URL(argPath)
    $player.Play()
    
Catch
    Return
    
End

Public Sub Play(Optional argPosition As Float)
    
    If $player.State <> Media.Paused Then
        Try $player.Stop
        Try $player.URL = NagatoQueue.ChannelCurrent.Url
    End If
    
    $player.Play()
    If argPosition Then $player.Position = argPosition
    
Catch
    Return
    
End

Public Sub ResetUrl()
    
    Try $player.Stop()
    $player.URL = NagatoQueue.ChannelCurrent.Url
    $player.Position = 0
    
End

Public Sub Pause()
    
    Try $player.Pause()
   
End

Public Sub Stop()
    
    Try $player.Stop()
    
End

Public Sub Player_End()
    
    Raise MusicEnd
    
End

Private Function Rate_Read() As Float

    If $durationFetcher($player.Duration) Then Raise FetchDuration($player.Duration)

    Return ($player.Position / $player.Duration)

Catch
    Return 0
    
End

Private Function Text_Read() As String

    Return Me.GetTextByRate($player.Position / $player.Duration)

Catch
    Return ""

End

Private Function State_Read() As Integer

    Return $player.State

Catch
    Return MediaPlayer.Null

End

Private Function SoftwareVolume_Read() As Float

    Return $player.Audio.Volume

Catch
    Return 0

End

Private Sub SoftwareVolume_Write(Value As Float)

    Try $player.Audio.Volume = Min(Value, 1)

End

