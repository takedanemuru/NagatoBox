' Gambas class file

Inherits NagatoObject

Private $musicElements As NagatoMusicElements 
Private $position As NagatoMusicPosition 

Private $up As NagatoMoveBufferUp2
Private $down As NagatoMoveBufferDown2

Private Sub sort(argType As Integer, argSortAscend As Boolean)
    
    Dim yukiBuffer As New NagatoSortBuffer(argType, argSortAscend)
    Dim yukiIndex As Integer
    
    If $musicElements.Count = 0 Then Return
    
    For yukiIndex = 0 To $musicElements.Count - 1
        yukiBuffer.Set($musicElements[yukiIndex], (yukiIndex = $position.Current))
    Next
    
    $musicElements.Set(yukiBuffer.Elements)
    $position.ResetOnSort(yukiBuffer.Current, $musicElements.Count)
    Raise Signal(MikuruSignal.BroadcastQueueChanged, [True])
    
End

Public Sub _call(argSignal As Integer, argValues As Variant[])
    
    Select argSignal
        Case MikuruSignal.ListSort
            sort(argValues[0], argValues[1])
        Case MikuruSignal.ListDown
            $down.Move(argValues)
        Case MikuruSignal.ListUp
            $up.Move(argValues)
    End Select
    
End

Public Sub _new(argElements As NagatoMusicElements, argPosition As NagatoMusicPosition)
    
    $musicElements = argElements
    $position = argPosition
    
    $up = New NagatoMoveBufferUp2($musicElements, $position) As "TFEI"
    $down = New NagatoMoveBufferDown2($musicElements, $position) As "TFEI"
    
End
