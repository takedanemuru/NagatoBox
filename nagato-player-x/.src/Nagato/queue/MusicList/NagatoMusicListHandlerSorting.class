' Gambas class file

Inherits NagatoObject

Private $musicElements As NagatoMusicElements 
Private $position As NagatoMusicPosition 

Private Sub sort(argType As Integer, argSortAscend As Boolean)
    
    Dim yukiBuffer As New NagatoSortBuffer(argType, argSortAscend)
    Dim yukiIndex As Integer
    
    If $musicElements.Count = 0 Then Return
    
    For yukiIndex = 0 To $musicElements.Count - 1
        yukiBuffer.Set($musicElements[yukiIndex], (yukiIndex = $position.Current))
    Next
    
    $musicElements.Set(yukiBuffer.Elements)
    $position.ResetOnSort(yukiBuffer.Current, $musicElements.Count)
    Raise Signal(MikuruSignal.BroadcastQueueChanged, Null)
    
End

Private Sub Up(argSelection As Integer[]) 
    
    Dim yukiBuffer As New NagatoMoveBufferUp(argSelection, $position.Current)
    Dim yukiIndex As Integer
    
    For yukiIndex = 0 To $musicElements.Count - 1
        If argSelection.Exist(yukiIndex) Then yukiBuffer.AddSelected(yukiIndex, $musicElements[yukiIndex])
        If Not argSelection.Exist(yukiIndex) Then yukiBuffer.AddUnselected(yukiIndex, $musicElements[yukiIndex])
    Next
    
    $musicElements.Set(yukiBuffer.Buffer)
    $position.Current = yukiBuffer.NewPosition
    Raise Signal(MikuruSignal.BroadcastSelectionChanged, yukiBuffer.NewSelection)
    
End

Private Sub down(argSelection As Integer[]) 
    
    Dim yukiBuffer As New NagatoMoveBufferDown($musicElements, $position.Current, argSelection)
    
    $musicElements.Set(yukiBuffer.Buffer)
    $position.Current = yukiBuffer.NewPosition
    Raise Signal(MikuruSignal.BroadcastSelectionChanged, yukiBuffer.NewSelection)
    
End

Public Sub _call(argSignal As Integer, argValues As Variant[])
    
    Select argSignal
        Case MikuruSignal.ListSort
            sort(argValues[0], argValues[1])
        Case MikuruSignal.ListDown
            down(argValues)
        Case MikuruSignal.ListUp
            up(argValues)
    End Select
    
End

Public Sub _new(argElements As NagatoMusicElements, argPosition As NagatoMusicPosition)
    
    $musicElements = argElements
    $position = argPosition
    
End
