' Gambas class file

Inherits NagatoObject

Private $musicElements As NagatoMusicElements 
Private $position As NagatoMusicPosition 

Private Sub removeDuplicateMusic()
    
    Dim yukiIndexes As Integer[] = $musicElements.GetDuplicateMusics()
    
    If yukiIndexes.Count > 0 Then dequeueIndexes(yukiIndexes)
    
End

Private Sub enqueuePaths(argPaths As String[])
    
    Dim yukiCount As Integer = $musicElements.EnqueueByPaths(argPaths)

    If yukiCount = 0 Then Return
    
    MikuruMessage.SetNewMusic(yukiCount)
    Raise Signal(MikuruSignal.BroadcastQueueChanged, Null)
    $position.Refresh($musicElements.Count)
    
End

Private Function getNextIndex(argIndexes As Integer[], argCurrentIndex As Integer) As Integer
    
    Dim yukiLowerIndexesCount As Integer
    Dim yukiIndex As Integer
    
    For yukiIndex = 0 To argIndexes.Max
        If argCurrentIndex > argIndexes[yukiIndex] Then Inc yukiLowerIndexesCount
    Next
    
    Return argCurrentIndex - yukiLowerIndexesCount
    
End

Private Sub dequeueIndexes(argIndexes As Integer[]) 
    
    Dim yukiCurrentIndex As Integer = NagatoQueue.GetProperty(MikuruProperty.CurrentIndex)
    Dim yukiIncluded As Boolean = argIndexes.Exist(yukiCurrentIndex)
    
    $musicElements.DequeueByIndexes(argIndexes)
    $position.Refresh($musicElements.Count)
    If yukiIncluded Then
        $position.SetIndex(Rand(0, $musicElements.Count - 1), $musicElements.Count - 1)
    Else
        $position.SetIndex(getNextIndex(argIndexes, yukiCurrentIndex), $musicElements.Count - 1)
    End If
    
    Raise Signal(MikuruSignal.BroadcastQueueChanged, Null)
    
End

Private Sub clearAll()
    
    $position.SetIndex(0)
    $musicElements.Clear()
    MikuruMessage.Cleared()
    Raise Signal(MikuruSignal.BroadcastQueueChanged, Null)
    
End

Public Sub _call(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.ListAddMusic
            enqueuePaths(argValues)
        Case MikuruSignal.ListClear
            clearAll()
        Case MikuruSignal.ListDeleteDuplicateMusic
            removeDuplicateMusic()
        Case MikuruSignal.ListDelete
            dequeueIndexes(argValues)
    End Select
    
End

Public Sub _new(argElements As NagatoMusicElements, argPosition As NagatoMusicPosition)
    
    $musicElements = argElements
    $position = argPosition
    
End
