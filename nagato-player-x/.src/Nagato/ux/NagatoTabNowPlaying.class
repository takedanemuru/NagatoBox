' Gambas class file

Inherits NagatoSignal

Private $menuBar As NagatoMenuBarNowPlaying
Private $grids As NagatoGridsNowPlaying
Private $queueObserver As Observer

Event NewPlaylist

Private Sub saveAsPlaylist()
    
    If $grids.GetPaths().Count = 0 Then Return
    
    NagatoDBMedia.SaveAsPlaylist(MikuruDialog.PlaylistName, $grids.GetPaths())
    
    Raise NewPlaylist
    
End

Public Sub _new(argContainer As Container, argGrids As GridView)
    
    $menuBar = New NagatoMenuBarNowPlaying(argContainer) As "MenuBar"
    $grids = New NagatoGridsNowPlaying(argGrids) As "Grids"
    $queueObserver = New Observer(NagatoQueue) As "Asakura"
    
End

Public Sub MenuBar_Signal(argSignal As Integer, argVariables As Variant[])
    
    Select Case argSignal
        Case MikuruSignal.ListSaveAs
            saveAsPlaylist()
        Case MikuruSignal.ListUp
            NagatoQueue.ListMoveUp($grids.Selection)
        Case MikuruSignal.ListDown
            NagatoQueue.ListMoveDown($grids.Selection)
    End Select
    
End

Public Sub Asakura_Changed()
    
    If Not Object.IsValid($grids) Then Return
    
    $grids.Refresh()
    
End

Public Sub Asakura_Moved()
    
    If Not Object.IsValid($grids) Then Return
    
    $grids.Refresh()
    
End

Public Sub Asakura_SelectionMoved(argSelection As Integer[])
    
    If Not Object.IsValid($grids) Then Return
    
    $grids.Refresh()
    $grids.RefreshSelection(argSelection)
    
End

Public Sub Asakura_TagChanged()
    
    $grids.Refresh()
    
End
