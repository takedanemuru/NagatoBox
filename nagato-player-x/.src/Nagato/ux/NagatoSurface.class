' Gambas class file

Event RequestRefresh

Public Sub Drop(argData As Variant)
    
    Select Case MikuruPath.GetDragDataType(argData)
        Case MikuruPath.Image
            NagatoSettingsForm.SaveBackgroundPath(MikuruPath.GetImagePath(argData))
            Raise RequestRefresh
        Case MikuruPath.Music
            NagatoQueue.AddMusics(MikuruPath.GetMusicPaths(argData))
        Case MikuruPath.Directory
            NagatoQueue.ChannelList.AddDirectory(MikuruPath.GetDirecoryPath(argData))
    End Select
    
End

Public Sub MouseDown(argMouseX As Integer, argMouseY As Integer)

    If MikuruUx.Button.Play.Contains(argMouseX, argMouseY) Then
        NagatoQueue.ChannelPlayer.Play()
    Else If MikuruUx.Button.Pause.Contains(argMouseX, argMouseY) Then
        NagatoQueue.ChannelPlayer.Pause()
    Else If MikuruUx.Button.Next.Contains(argMouseX, argMouseY) Then
        NagatoQueue.ChannelPlayer.Next()
    Else If MikuruUx.Button.Back.Contains(argMouseX, argMouseY) Then
        NagatoQueue.ChannelPlayer.Back()
    Else If MikuruUx.Button.Shuffle.Contains(argMouseX, argMouseY) Then
        NagatoSettingsMusic.Shuffle = Not NagatoSettingsMusic.Shuffle
    Else If MikuruUx.Button.Repeat.Contains(argMouseX, argMouseY) Then
        NagatoSettingsMusic.Repeat = Not NagatoSettingsMusic.Repeat
    Else If MikuruUx.Button.Playlist.Contains(argMouseX, argMouseY) Then
        HaruhiPlayList.EnsureVisible()
    Else If MikuruUx.Button.Volume.Contains(argMouseX, argMouseY) Then
        With MikuruUx.Button.Volume.Rect
            NagatoQueue.ChannelPlayer.SoftwareVolume = ((argMouseX - .X) / (.W - .X))
        End With
    Else If MikuruUx.InfoBox.Contains(argMouseX, argMouseY) Then
        HaruhiTagEdit.SetPath(NagatoQueue.ChannelCurrent.Url)
        HaruhiTagEdit.ShowDialog()
    Else If MikuruUx.ProgressBar.Contains(argMouseX, argMouseY) Then
        With MikuruUx.ProgressBar
            NagatoQueue.ChannelPlayer.SetProgress((argMouseX - .X) / (.W - .X))
        End With
    Endif

End
