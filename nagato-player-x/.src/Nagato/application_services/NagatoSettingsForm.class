' Gambas class file

Create Static

Inherits NagatoSettings

Public Enum HPlayer, HPlayList, HCoverArt

Property Main As Integer

Private Sub saveSticky(argForm As Form)
    
    Me._$settings[Subst$("&1/sticky", argForm.Name)] = argForm.Sticky
    Me._$settings.Save()
    
End

Private Sub loadSticky(argForm As Form)
    
    argForm.Sticky = Me._$settings[Subst$("&1/sticky", argForm.Name), True]
    
End

Private Sub saveSkipTaskbar(argForm As Form)
    
    Me._$settings[Subst$("&1/skip_taskbar", argForm.Name)] = argForm.SkipTaskbar
    Me._$settings.Save()
    
End

Private Sub loadSkipTaskbar(argForm As Form)
    
    argForm.SkipTaskbar = Me._$settings[Subst$("&1/skip_taskbar", argForm.Name), True] 
    
End

Public Sub Load(argForm As Form)
    
    Me.SetPosition(argForm)
    Me.SetSize(argForm)
    
End

Public Sub Save(argForm As Form)
    
    Me.SavePosition(argForm)
    Me.SaveSize(argForm)
    
End

Public Sub SetPosition(argForm As Form)
    
    argForm.X = Me._$settings[Subst$("&1/x", argForm.Name), argForm.X]
    argForm.Y = Me._$settings[Subst$("&1/y", argForm.Name), argForm.Y]
    
End

Public Sub SavePosition(argForm As Form)
    
    Me._$settings[Subst$("&1/x", argForm.Name)] = argForm.X
    Me._$settings[Subst$("&1/y", argForm.Name)] = argForm.Y
    Me._$settings.Save()
    
End

Public Sub SetSize(argForm As Form)
    
    argForm.W = Me._$settings[Subst$("&1/w", argForm.Name), argForm.W]
    argForm.H = Me._$settings[Subst$("&1/h", argForm.Name), argForm.H]
    
End

Public Sub SaveSize(argForm As Form)
    
    Me._$settings[Subst$("&1/h", argForm.Name)] = argForm.H
    Me._$settings[Subst$("&1/w", argForm.Name)] = argForm.W
    Me._$settings.Save()
    
End

Public Sub SetMainFormSize(argForm As Form)
    
    argForm.Height = 52 * MikuruUx.Grid
    argForm.Width = 36 * MikuruUx.Grid

End

Public Sub LoadGloobusForm(argForm As Form)
    
    loadSkipTaskbar(argForm)
    loadSticky(argForm)
    Me.SetPosition(argForm)
    
End

Public Sub SaveGloobusForm(argForm As Form)
    
    saveSkipTaskbar(argForm)
    saveSticky(argForm)
    Me.SavePosition(argForm)
    
End

Private Function Main_Read() As Integer

    Return Me._$settings["form/main", HPlayer]

End

Private Sub Main_Write(Value As Integer)

    Me._$settings["form/main"] = Value
    Me._$settings.Save()

    MikuruMainWindows.ChangeMain(Value)

End
