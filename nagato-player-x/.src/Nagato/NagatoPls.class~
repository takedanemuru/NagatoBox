' Gambas class file

Property Read State As Integer
Property Read Ready As Boolean
Property Read Title As String
Property Read Url As String
Property Read Path As String

Public Enum NullObject, PathNotFound, UrlNotFound, TitleNotFound, UrlAndTitleFound

Private $state As Integer = Me.NullObject
Private $title As String
Private $url As String
Private $path As String

Static Public Function GetUrlFromPath(argPath As String) As String
    
    Dim yukiStream As Stream
    Dim yukiLine As String
    Dim yukiResult As String = ""
    
    yukiStream = Open argPath For Input
    
    While Not Eof(yukiStream)
        Line Input #yukiStream, yukiLine
        If yukiLine Begins "File1=" Then yukiResult = Replace$(yukiLine, "File1=", "")
    Wend
    
    yukiStream.Close()
    
    Return yukiResult
    
End

Private Sub setState()
    
    If $url = "" Then
        $state = Me.UrlNotFound
    Else If $title = "" Then
        $state = Me.TitleNotFound
    Else
        $state = Me.UrlAndTitleFound
    Endif
    
End

Private Sub initialize(argPath As String)
    
    Dim yukiStream As Stream
    Dim yukiLine As String
    
    yukiStream = Open argPath For Input
    
    While Not Eof(yukiStream)
        Line Input #yukiStream, yukiLine
        If yukiLine Begins "Title1=" Then $title = Replace$(yukiLine, "Title1=", "")
        If yukiLine Begins "File1=" Then $url = Replace$(yukiLine, "File1=", "")
    Wend
    
    yukiStream.Close()
    
    setState()
    
End

Public Sub _new(argPath As String)
    
    If Not Exist(argPath) Then
        $state = Me.PathNotFound
    Else
        $path = argPath
        initialize(argPath)
    Endif
    
End

Private Function State_Read() As Integer

    Return $state

End

Private Function Ready_Read() As Boolean

    Select Case $state
        Case Me.NullObject, Me.PathNotFound, Me.UrlNotFound
            Return False
        Case Else
            Return True
    End Select

End

Private Function Title_Read() As String

    Return $title

End

Private Function Url_Read() As String

    Return $url

End

Private Function Path_Read() As String

    Return $path

End
