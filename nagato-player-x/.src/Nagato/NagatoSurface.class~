' Gambas class file

Event RequestRefresh

Public Sub Drop(argData As Variant)
    
    Select Case MikuruPath.GetDragDataType(argData)
        Case MikuruPath.Image
            NagatoSettingsForm.SaveBackgroundPath(MikuruPath.GetImagePath(argData))
            Raise RequestRefresh
        Case MikuruPath.Music
            NagatoQueue.AddMusics(MikuruPath.GetMusicPaths(argData))
        Case MikuruPath.Directory
            NagatoQueue.AddDirectory(MikuruPath.GetDirecoryPath(argData))
    End Select
    
End

Public Sub MouseDown(argMouseX As Integer, argMouseY As Integer)

    If MikuruUx.PlayButton.Contains(argMouseX, argMouseY) Then
        NagatoQueue.Play()
    Else If MikuruUx.PauseButton.Contains(argMouseX, argMouseY) Then
        NagatoQueue.Pause()
    Else If MikuruUx.NextButton.Contains(argMouseX, argMouseY) Then
        NagatoQueue.Next()
    Else If MikuruUx.BackButton.Contains(argMouseX, argMouseY) Then
        NagatoQueue.Back()
    Else If MikuruUx.ShuffleButton.Contains(argMouseX, argMouseY) Then
        NagatoSettingsMusic.Shuffle = Not NagatoSettingsMusic.Shuffle
    Else If MikuruUx.RepeatButton.Contains(argMouseX, argMouseY) Then
        NagatoSettingsMusic.Repeat = Not NagatoSettingsMusic.Repeat
    Else If MikuruUx.PlaylistButton.Contains(argMouseX, argMouseY) Then
        HaruhiPlayList.Show()
    Else If MikuruUx.VolumeButton.Contains(argMouseX, argMouseY) Then
        With MikuruUx.VolumeButton
            NagatoQueue.Player.SoftwareVolume = ((argMouseX - .X) / (.W - .X))
        End With
    Else If MikuruUx.InfoBox.Contains(argMouseX, argMouseY) Then
        HaruhiTagEdit.SetPath(NagatoQueue.GetCurrentPath(True))
        HaruhiTagEdit.ShowDialog()
    Else If MikuruUx.ProgressBar.Contains(argMouseX, argMouseY) Then
        With MikuruUx.ProgressBar
            NagatoQueue.SetProgress((argMouseX - .X) / (.W - .X))
        End With
    Endif

End
