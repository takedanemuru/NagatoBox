' Gambas class file

Private $albumartUnloaded As Boolean = True
Private $observer As Observer
Private $albumartPath As String

Private $paths As String[]

Private Sub initializeWidgets()
    
    Dim yukiId3TagInfo As NagatoId3TagInfo = MikuruId3TagInfo($paths[0])
    
    ArtistTextBox.Text = yukiId3TagInfo.Artist
    AlbumTextBox.Text = yukiId3TagInfo.Album
    
End

Private Sub startAlbumArtSelection()
    
    If $paths.Count = 0 Then Return
    
    HaruhiAlbumArtSelect.Show()
    $observer = New Observer(HaruhiAlbumArtSelect) As "Asakura"
    
End

Private Sub setNewTag()
    
    Dim yukiPath As String
    
    For Each yukiPath In $paths
        With MikuruId3v2TagEdit
            If ArtistTextBox.Text <> "" Then .NewArtist(yukiPath, ArtistTextBox.Text)
            If AlbumTextBox.Text <> "" Then .NewAlbum(yukiPath, AlbumTextBox.Text)
            If Not $albumartUnloaded Then MikuruEyeD3.SetPicture(yukiPath, $albumartPath)
        End With
    Next
    
End

Public Sub StartEdit(argPaths As String[])
    
    MikuruArrayTest(argPaths)
    
    $paths = argPaths.Copy()
    initializeWidgets()
    
End

Public Sub Form_Show()
    
    Me.Center()
    ApplyButton.Enabled = False
    
End

Public Sub CancelButton_Click()

    Me.Close(MikuruDialog.ResponseCancel)

End

Public Sub ApplyButton_Click()
    
    setNewTag()
    NagatoBroadCast(NagatoBroadCast.SignalTagChanged)
    Me.Close(MikuruDialog.ResponseApply)
    
End

Public Sub TextBox_Change()

    ApplyButton.Enabled = True

End

Public Sub AlbumArtPictureBox_MouseDown()

    startAlbumArtSelection()

End

Public Sub IconSelectButton_Click()

    startAlbumArtSelection()

End

Public Sub Asakura_Select(argFullPath As String)
    
    AlbumArtPictureBox.Picture = Image.Load(argFullPath).Picture
    $albumartUnloaded = False
    $albumartPath = argFullPath
    
Catch
    Return
    
End
