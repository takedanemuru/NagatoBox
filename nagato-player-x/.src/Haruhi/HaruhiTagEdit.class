' Gambas class file

Private $edited As Boolean
Private $albumartUnloaded As Boolean = True
Private $albumartPath As String
Private $targetPath As String
Private $observer As Observer


Event TagChanged

Private Sub setTags()
    
    TitleTextBox.Text = MikuruId3TagInfo.GetTitle($targetPath)
    ArtistTextBox.Text = MikuruId3TagInfo.GetArtist($targetPath)
    AlbumTextBox.Text = MikuruId3TagInfo.GetAlbum($targetPath)
    
End

Private Sub setPicture()
    
    Dim yukiPicture As Picture = MikuruEyeD3.GetPicture($targetPath, AlbumArtPictureBox.H)
    
    If yukiPicture Then
        AlbumArtPictureBox.Picture = yukiPicture
    Endif
    
End

Private Sub setNewTag()
    
    If Not $edited Then Return
    
    With MikuruId3v2TagEdit
        If TitleTextBox.Text <> "" Then .NewTitle($targetPath, TitleTextBox.Text)
        If ArtistTextBox.Text <> "" Then .NewArtist($targetPath, ArtistTextBox.Text)
        If AlbumTextBox.Text <> "" Then .NewAlbum($targetPath, AlbumTextBox.Text)
    End With
    
    NagatoQueue.RefreshTag()
    
End

Public Sub SetPath(argPath As String)
    
    $edited = False
    $targetPath = MikuruUrlDecode(argPath)
    setTags()
    setPicture()
    
End

Public Sub Form_Open()

    Me.Center()

End

Public Sub CancelButton_Click()

    Me.Close()

End

Public Sub ApplyButton_Click()
    
    If $targetPath = "" Then Return

    setNewTag()
    
    Me.Close(MikuruDialog.ResponseApply)
    
    If Not $albumartUnloaded Then MikuruEyeD3.SetPicture($targetPath, $albumartPath)
    
    Raise TagChanged
    
End

Public Sub TextBox_Change()

    $edited = True

End

Public Sub AlbumArtPictureBox_MouseDown()

    If $targetPath = "" Then Return

    HaruhiAlbumArtSelect.Show()
    $observer = New Observer(HaruhiAlbumArtSelect) As "Asakura"

End

Public Sub Asakura_Select(argFullPath As String)
    
    $albumartPath = argFullPath
    AlbumArtPictureBox.Picture = Image.Load(argFullPath).Stretch(14 * MikuruUX.Grid, 14 * MikuruUX.Grid).Picture
    $albumartUnloaded = False
    
Catch
    Debug Error.Text
    Return
    
End

Public Sub IconSelectButton_Click()

    If $targetPath = "" Then Return

    HaruhiAlbumArtSelect.Show()
    $observer = New Observer(HaruhiAlbumArtSelect) As "Asakura"

End
