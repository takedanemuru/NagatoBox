' Gambas class file

Private $edited As Boolean
Private $albumartUnloaded As Boolean = True
Private $targetPath As String
Private $observer As Observer

Private Sub setTags()
    
    With NagatoQueue
        If .Title <> "" Then TitleTextBox.Text = .Title
        If .Artist <> "" Then ArtistTextBox.Text = .Artist
        If .Album <> "" Then AlbumTextBox.Text = .Album
    End With
    
End

Private Sub setPicture()
    
    If ArtistTextBox.Text = "" Then Return
    If AlbumTextBox.Text = "" Then Return
    
    If NagatoDBMedia.HasAlbumArt(ArtistTextBox.Text, AlbumTextBox.Text) Then
        AlbumArtPictureBox.Picture = NagatoDBMedia.GetAlbumArt(ArtistTextBox.Text, AlbumTextBox.Text)
        $albumartUnloaded = False
    Endif
    
End

Private Sub setNewTag()
    
    If Not $edited Then Return
    
    With MikuruId3v2TagEdit
        If TitleTextBox.Text <> "" Then .NewTitle($targetPath, TitleTextBox.Text)
        If ArtistTextBox.Text <> "" Then .NewArtist($targetPath, ArtistTextBox.Text)
        If AlbumTextBox.Text <> "" Then .NewAlbum($targetPath, AlbumTextBox.Text)
    End With
    
    NagatoQueue.RefreshTag()
    
End

Private Sub setNewPicture()
    
    'If Not $pictureSelected And If Not $edited Then Return
    If ArtistTextBox.Text = "" Then Return
    If AlbumTextBox.Text = "" Then Return
    If $albumartUnloaded Then Return
    
    NagatoDBMedia.SetAlbumArt(ArtistTextBox.Text, AlbumTextBox.Text, AlbumArtPictureBox.Picture)
    
End

Public Sub Form_Open()

    Me.Center()

    $edited = False
    $targetPath = NagatoQueue.CurrentPath
    setTags()
    setPicture()

End

Public Sub CancelButton_Click()

    Me.Close()

End

Public Sub ApplyButton_Click()
    
    If $targetPath = "" Then Return

    setNewTag()
    setNewPicture()
    
    Me.Close()
    
End

Public Sub TextBox_Change()

    $edited = True

End

Public Sub AlbumArtPictureBox_MouseDown()

    If $targetPath = "" Then Return

    HaruhiAlbumArtSelect.Show()
    $observer = New Observer(HaruhiAlbumArtSelect) As "Asakura"

End

Public Sub Asakura_Select(argFullPath As String)
    
    AlbumArtPictureBox.Picture = Image.Load(argFullPath).Stretch(14 * MikuruUX.Grid, 14 * MikuruUX.Grid).Picture
    $albumartUnloaded = False
    
Catch
    Return
    
End

Public Sub IconSelectButton_Click()

    If $targetPath = "" Then Return

    HaruhiAlbumArtSelect.Show()
    $observer = New Observer(HaruhiAlbumArtSelect) As "Asakura"

End
