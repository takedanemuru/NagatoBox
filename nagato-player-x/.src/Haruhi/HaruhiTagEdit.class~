' Gambas class file

Private $albumartPath As String
Private $targetPath As String
Private $observer As Observer

Private Sub setTags()
    
    Dim yukiId3TagInfo As NagatoId3TagInfo = MikuruId3TagInfo($targetPath)
    
    TitleTextBox.Text = yukiId3TagInfo.Title
    ArtistTextBox.Text = yukiId3TagInfo.Artist
    AlbumTextBox.Text = yukiId3TagInfo.Album
    
End

Private Sub setPicture()
    
    ' Dim yukiPicture As Picture = MikuruEyeD3.GetPicture($targetPath, AlbumArtPictureBox.H)
    ' 
    ' If yukiPicture Then AlbumArtPictureBox.Picture = yukiPicture
    
    AlbumArtPictureBox.Picture = MikuruEyeD3.GetPicture($targetPath, AlbumArtPictureBox.H)
    
End

Private Sub setNewTag()
    
    Dim yukiTags As New NagatoId3TagInfo
    
    yukiTags.Title = TitleTextBox.Text
    yukiTags.Artist = ArtistTextBox.Text
    yukiTags.Album = AlbumTextBox.Text
    
    MikuruId3v2TagEdit.Set($targetPath, TitleTextBox.Text, ArtistTextBox.Text, AlbumTextBox.Text)
    NagatoDBMedia.AddTagsByNagatoTags($targetPath, yukiTags)
    NagatoQueue.ChannelCurrent.Reload()
    
End

Public Sub SetPath(argPath As String)
    
    $targetPath = MikuruUrl.DecodeUrlToPath(argPath)
    setTags()
    setPicture()
    
End

Public Sub Form_Open()

    Me.Center()
    ApplyButton.Enabled = False

End

Public Sub CancelButton_Click()

    Me.Close()

End

Public Sub ApplyButton_Click()
    
    If $targetPath = "" Then Return

    setNewTag()
    Me.Close(MikuruDialog.ResponseApply)
    If $albumartPath Then MikuruEyeD3.SetPicture($targetPath, $albumartPath)
    NagatoBroadCast(NagatoBroadCast.SignalTagChanged)
    
End

Public Sub TextBox_Change()

    ApplyButton.Enabled = True

End

Public Sub AlbumArtPictureBox_MouseDown()

    If $targetPath = "" Then Return

    HaruhiAlbumArtSelect.Show()
    $observer = New Observer(HaruhiAlbumArtSelect) As "Asakura"

End

Public Sub Asakura_Select(argFullPath As String)
    
    $albumartPath = argFullPath
    AlbumArtPictureBox.Picture = MikuruCroppedImage(argFullPath, AlbumArtPictureBox.H)
    ApplyButton.Enabled = True
    
Catch
    Return
    
End

Public Sub IconSelectButton_Click()

    AlbumArtPictureBox_MouseDown()

End
