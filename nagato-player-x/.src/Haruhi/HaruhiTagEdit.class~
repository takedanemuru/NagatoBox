' Gambas class file

Private $albumartPath As String
Private $targetPath As String
Private $observer As Observer

Event TagChanged

Private Sub setTags()
    
    TitleTextBox.Text = MikuruId3TagInfo.GetTitle($targetPath)
    ArtistTextBox.Text = MikuruId3TagInfo.GetArtist($targetPath)
    AlbumTextBox.Text = MikuruId3TagInfo.GetAlbum($targetPath)
    
End

Private Sub setPicture()
    
    Dim yukiPicture As Picture = MikuruEyeD3.GetPicture($targetPath, AlbumArtPictureBox.H)
    
    If yukiPicture Then AlbumArtPictureBox.Picture = yukiPicture
    
End

Private Sub setNewTag()
    
    With MikuruId3v2TagEdit
        If TitleTextBox.Text <> "" Then .NewTitle($targetPath, TitleTextBox.Text)
        If ArtistTextBox.Text <> "" Then .NewArtist($targetPath, ArtistTextBox.Text)
        If AlbumTextBox.Text <> "" Then .NewAlbum($targetPath, AlbumTextBox.Text)
    End With
    
    NagatoQueue.ChannelTags.Refresh(TitleTextBox.Text, ArtistTextBox.Text, AlbumTextBox.Text)
    
End

Public Sub SetPath(argPath As String)
    
    $targetPath = MikuruUrlDecode(argPath)
    setTags()
    setPicture()
    
End

Public Sub Form_Open()

    Me.Center()
    ApplyButton.Enabled = False

End

Public Sub CancelButton_Click()

    Me.Close()

End

Public Sub ApplyButton_Click()
    
    If $targetPath = "" Then Return

    setNewTag()
    Me.Close(MikuruDialog.ResponseApply)
    If $albumartPath Then MikuruEyeD3.SetPicture($targetPath, $albumartPath)
    NagatoBroadCast(NagatoBroadCast.SignalTagChanged)
    
End

Public Sub TextBox_Change()

    ApplyButton.Enabled = True

End

Public Sub AlbumArtPictureBox_MouseDown()

    If $targetPath = "" Then Return

    HaruhiAlbumArtSelect.Show()
    $observer = New Observer(HaruhiAlbumArtSelect) As "Asakura"

End

Public Sub Asakura_Select(argFullPath As String)
    
    $albumartPath = argFullPath
    AlbumArtPictureBox.Picture = Image.Load(argFullPath).Picture
    
Catch
    Return
    
End

Public Sub IconSelectButton_Click()

    AlbumArtPictureBox_MouseDown()

End
