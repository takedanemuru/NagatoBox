' Gambas class file

Private $edited As Boolean
Private $albumartUnloaded As Boolean = True
Private $observer As Observer

Private $paths As String[]

Private Sub setTitle()
    
    TitleTextLabel.Visible = True
    TitleTextBox.Visible = True
    
    If MikuruId3TagInfo.GetTitle($paths[0]) <> "" Then
        TitleTextBox.Text = MikuruId3TagInfo.GetTitle($paths[0])
    Else
        TitleTextBox.Text = File.BaseName($paths[0])
    End If
    
End

Private Sub hideTitle()
    
    TitleTextLabel.Visible = False
    TitleTextBox.Visible = False
    TitleTextBox.Text = ""
    
End

Private Sub initializeWidgets()
    
    If $paths.Count = 1 Then
        setTitle()
    Else
        hideTitle()
    End If 
    
    AlbumTextBox.Text = MikuruId3TagInfo.GetAlbum($paths[0])
    ArtistTextBox.Text = MikuruId3TagInfo.GetArtist($paths[0])
    
    If NagatoDBMedia.HasAlbumArt(ArtistTextBox.Text, AlbumTextBox.Text) Then
        AlbumArtPictureBox.Picture = NagatoDBMedia.GetAlbumArt(ArtistTextBox.Text, AlbumTextBox.Text)
    End If
    
End

Private Sub startAlbumArtSelection()
    
    If $paths.Count = 0 Then Return
    
    HaruhiAlbumArtSelect.Show()
    $observer = New Observer(HaruhiAlbumArtSelect) As "Asakura"
    
End

Private Sub setNewTag()
    
    Dim yukiPath As String
    
    For Each yukiPath In $paths
        With MikuruId3v2TagEdit
            If TitleTextBox.Text <> "" Then .NewTitle(yukiPath, TitleTextBox.Text)
            If ArtistTextBox.Text <> "" Then .NewArtist(yukiPath, ArtistTextBox.Text)
            If AlbumTextBox.Text <> "" Then .NewAlbum(yukiPath, AlbumTextBox.Text)
            NagatoDBMedia.SetLibrary(yukiPath, TitleTextBox.Text, ArtistTextBox.Text, AlbumTextBox.Text)
        End With
    Next
    
    NagatoPlayerLegacy.RefreshTag()
    Me.Close(MikuruDialog.ResponseApply)
    
End

Private Sub setNewPicture()
    
    If AlbumTextBox.Text = "" Then Return
    If ArtistTextBox.Text = "" Then Return
    If $albumartUnloaded Then Return
    
    NagatoDBMedia.SetAlbumArt(ArtistTextBox.Text, AlbumTextBox.Text, AlbumArtPictureBox.Picture)
    
End

Public Sub StartEdit(argPaths As String[])
    
    $paths = argPaths.Copy()
    
    Debug $paths.Count
    Debug $paths[0]
    
    initializeWidgets()
    
End

Public Sub Form_Show()
    
    Me.Center()
    Me.Show()
    
End

Public Sub CancelButton_Click()

    Me.Close(MikuruDialog.ResponseCancel)

End

Public Sub ApplyButton_Click()
    
    setNewTag()
    
End

Public Sub TextBox_Change()

    $edited = True

End

Public Sub AlbumArtPictureBox_MouseDown()

    startAlbumArtSelection()

End

Public Sub IconSelectButton_Click()

    startAlbumArtSelection()

End

Public Sub Asakura_Select(argFullPath As String)
    
    AlbumArtPictureBox.Picture = Image.Load(argFullPath).Stretch(14 * MikuruUX.Grid, 14 * MikuruUX.Grid).Picture
    $albumartUnloaded = False
    
Catch
    Return
    
End
