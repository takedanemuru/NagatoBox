' Gambas class file

Private Const MaxSize As Integer = 200

Private $screenshot As Picture

Private Sub initializeScreenshot(argOnlyActive As Boolean)
    
    Dim yukiWindow As DesktopWindow
    
    If argOnlyActive Then
        yukiWindow = New DesktopWindow(Desktop.ActiveWindow)
        $screenshot = Desktop.Screenshot(yukiWindow.X, yukiWindow.Y, yukiWindow.W, yukiWindow.H)
    Else
        $screenshot = Desktop.Screenshot()
    End If
    
Catch
    $screenshot = Desktop.Screenshot()
    
End

Private Function getPreview() As Picture
    
    Dim yukiRate As Float = Min(MaxSize / $screenshot.H, MaxSize / $screenshot.W)
    
    With $screenshot
        Return .Image.Stretch(.W * yukiRate, .H * yukiRate).Picture
    End With
    
End

Private Sub initializePreview()
    
    Dim yukiPreview As Picture = getPreview()
    
    With PreviewPictureBox
        .Picture = yukiPreview
        .H = yukiPreview.H
        .W = yukiPreview.W
    End With
    
End

Private Sub initializeDirectoryButtonBox()
    
    With DirectoryButtonBox
        .Text = NagatoSettings.Directory
        .Picture = Stock["small/home"]
    End With
    
End

Private Sub initializeFileNameTextBox()
    
    Dim yukiTimeStamp As String = Format$(Now, ("yyyy/mm/dd_hh:nn:mm"))
    
    FileNameTextBox.Text = Subst$("screenshot_&1", yukiTimeStamp)
    
End

Private Function getPath() As String
    
    Dim yukiPath As String = DirectoryButtonBox.Text &/ FileNameTextBox.Text 
    Dim yukiCount As Integer = 0
    
    If Not Exist(yukiPath & ".png") Then Return yukiPath & ".png"
    
    Do
        Inc yukiCount
        If Not Exist(Subst$("&1(&2).png", yukiPath, yukiCount)) Then Return Subst$("&1(&2).png", yukiPath, yukiCount)
    Loop
    
End

Private Sub playSound()
    
    Dim yukiSound As New Sound("sound/camera-snap.wav") 
    
    yukiSound.Play()
    
End

Public Sub Call(argOnlyActive As Boolean)
    
    playSound()
    initializeScreenshot(argOnlyActive)
    Me.Center()
    Me.Show()
    initializePreview()
    initializeDirectoryButtonBox()
    initializeFileNameTextBox()
    
End

Public Sub CancelButton_Click()

    Me.Close()

End

Public Sub DirectoryButtonBox_Click()

    Dialog.Path = DirectoryButtonBox.Text
    Dialog.Title = ("Select directory")

    If Not Dialog.SelectDirectory() Then
        With DirectoryButtonBox
            .Text = Dialog.Path
            .Picture = IIf(Dialog.Path = User.Home, Stock["small/home"], Stock["small/directory"])
            CheckBox1.Visible = True
        End With
    Endif

End

Public Sub SaveButton_Click()

    $screenshot.Save(getPath())
    If CheckBox1.Value Then NagatoSettings.Directory = DirectoryButtonBox.Text 
    Me.Close()

End
