' Gambas class file

Private Const UserDirectory As String = ".local/share/applications"
Private Const SystemDirectory As String = "/usr/share/applications"

Private Sub addIcons(argDirectory As String, Optional argQuery As String = "")
    
    Dim yukiPath As String
    Dim yukiDesktopEntry As DesktopFile
    Dim yukiIcon As Picture
    
    For Each yukiPath In Dir(argDirectory, "*.desktop").Sort(gb.IgnoreCase)
        If ApplicationsIconView.Exist(yukiPath) Then Continue
        If yukiPath Not Like Subst$("*&1*", argQuery) Then Continue 
        yukiDesktopEntry = New DesktopFile(argDirectory &/ yukiPath)
        yukiIcon = MikuruIcon.GetDesktopEntryIcon(yukiDesktopEntry.Path, 64)
        ApplicationsIconView.Add(argDirectory &/ yukiPath, yukiDesktopEntry.name, yukiIcon)
    Next
    
End

Private Sub refresh(argQuery As String)
    
    ApplicationsIconView.Clear()
    
    addIcons(User.Home &/ UserDirectory, argQuery)
    addIcons(SystemDirectory, argQuery)
    addIcons(SystemDirectory &/ "kde4", argQuery)
    
End

Public Sub Form_Open()
    
    refresh("")
    
End

Public Sub ApplicationsIconView_Activate()

    Try Copy Last.key To Desktop.Path &/ File.Name(Last.Key)

End

Public Sub ApplicationsIconView_MouseDrag()
    
    If ApplicationsIconView.Key = "" Then Return
    
    Drag.Icon = ApplicationsIconView.Current.Picture
    ApplicationsIconView.Drag(NagatoDrag2.GetUriList([ApplicationsIconView.Key]), MikuruMimeType.UriList)
    
End

Public Sub SearchButtonBox_Activate()

    refresh(SearchButtonBox.Text)

End

Public Sub SearchButtonBox_Click()

    SearchButtonBox_Activate()

End
