' Gambas class file

Private $leftPane As NagatoLeftPane
Private $rightPane As NagatoRightPane
Private $mainPane As NagatoMainPane
Private $observer As Observer

Private Sub setRootLabel()
    
    With RootLabel
        .Text = ("WARNING !!, you are using the ROOT account, you may harm your system.")
        .H = 48
        .Visible = True
    End With
    
End

Public Sub Form_Open()
    
    If MikuruAccountChecker.IsRoot() Then setRootLabel()
     
    $leftPane = New NagatoLeftPane(LeftPanel) As "LeftPane"
    $mainPane = New NagatoMainPane(MainTabStrip) As "MainPane"
    $rightPane = New NagatoRightPane(RightPanel) As "RightPane"
    
    $observer = New Observer(TFEI) As "Asakura"
    $mainPane.SetInitialPath(NagatoArguments.StartupPath)
    
    Application.MainWindow = Me
    NagatoSettingsForm.Load(Me)
    
End

Public Sub Form_Close()
    
    NagatoSettingsForm.Save(Me)
    
    Quit 0
    
End

Public Sub LeftPane_Signal(argSignal As Integer, argValues As Variant[])
    
    If argSignal = MikuruSignal.LeftSearch Then $mainPane.SearchQuery(argValues[0], argValues[1])
    If argSignal = MikuruSignal.LeftRefresh Then $mainPane.Refresh()
    
End

Public Sub Asakura_Signal(argSignal As Integer, argValues As Variant[])
    
    Select Case argSignal
        Case TFEISignal.AddNewDirectory
            $mainPane.AddNewDirectory(argValues[0])
        Case TFEISignal.SetNewTrashBin
            $mainPane.SetNewTrashBin()
        Case TFEISignal.SetNewDirectory
            $mainPane.SetNewDirectory(argValues[0])
        Case TFEISignal.RefreshAll
            $mainPane.RefreshAll()
        Case TFEISignal.SetMainTabStrip
            $mainPane.SetMainTabStrip(argValues[0], argValues[1])
        Case TFEISignal.SetSearch
            $leftPane.SetSearch(argValues[0])
        Case TFEISignal.SetTrashProperty
            $rightPane.SetTrashProperty(argValues[0])
        Case TFEISignal.ShowHiddenChanged
            $mainPane.RefreshAll()
            $leftPane.RefreshTreeView()
        Case TFEISignal.SetFileProperty
            $rightPane.SetProperty(argValues[0])
        Case TFEISignal.ShowFileProperty
            RightPanel.Hidden = False
    End Select
    
End

Public Sub RightPanel_Show()

    $mainPane.EnsureVisible()

End
