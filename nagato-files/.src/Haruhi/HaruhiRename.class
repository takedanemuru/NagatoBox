' Gambas class file

Private $sourcePath As NagatoSourcePath
Private $targetPath As NagatoTargetPathWigets

Private Sub reset(argPath As String, Optional argIcon As Picture)
    
    $sourcePath = New NagatoSourcePath(argPath)
    $targetPath.SetPath(argPath)
    CurrentNameTextLabel.Text = $sourcePath.Name
    
    If argIcon Then IconPictureBox.Picture = argIcon
    
End

Public Function SetPath(argPath As String, Optional argIcon As Picture = Null) As Boolean
    
    If Not Exist(argPath) Then
        Message.Error(("YUKI.N > File has been removed."), ("OK"))
        Return False
    Else If Not Access(argPath, gb.Write) Then
        Message.Error(("YUKI.N > You don't have permission to rename file or directory."), ("OK"))
        Return False
    Else If NagatoXdg.IsUserDir(argPath) Or If argPath = User.Home Then 
        Message.Error(("YUKI.N > You don't have permission to rename xdg-user-directories."), ("OK"))
        Return False
    Else
        reset(argPath, argIcon)
        Return True
    End If
    
End

Public Sub _new()
    
    $targetPath = New NagatoTargetPathWigets(DirectoryButtonBox, NewNameTextBox)
    
End

Public Sub Form_Open()

    NewNameTextBox.SetFocus()

End

Public Sub ApplyButton_Click()

    Dim yukiTargetPath As String 

    If NewNameTextBox.Text = "" Then
        Message.Warning(("YUKI.N > Input any name."), ("OK"))
    Else If MikuruMedia.ConversionRequired($sourcePath.FullPath, $targetPath.FullPath) Then
        MikuruMedia.ConvertFile($sourcePath.FullPath, $targetPath.FullPath)
        Me.Close(MikuruDialog.ResponseCancel)
    Else If Exist($targetPath.FullPath) Then
        Message.Warning(("YUKI.N > The file path has been existed. Please use another name."), ("OK"))
    Else
        yukiTargetPath = MikuruPath.GetSafePathName($targetPath.FullPath)
        Move $sourcePath.FullPath To yukiTargetPath
        MikuruLastRenamed.Path = yukiTargetPath
        Me.Close(MikuruDialog.ResponseApply)
    Endif

End

Public Sub NewNameTextBox_Activate()

    ApplyButton_Click()

End

Public Sub CancelButton_Click()

    Me.Close(MikuruDialog.ResponseCancel)

End

Public Sub DirectoryButtonBox_Click()

    $targetPath.ChangeDirectory($sourcePath.Directory)

End
