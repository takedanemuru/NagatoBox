' Gambas class file

Property Read Ready As Boolean
Property Read Second As Integer

Private $desktop As NagatoDesktop
Private $panel As NagatoDesktopPanel
Private $panel2 As NagatoPanel2
Private $timer As New Timer As "DesktopTimer"
Private $ready As Boolean = False
Private $second As Integer

Event Timer

Private Sub initializeForm()
    
    With Me
        .X = 0
        .Y = 0
        .W = Screen.W
        .H = Screen.H
    End With
    
End

Public Sub RealodDesktopWallpaper()
    
    $desktop.ReloadDesktopWallpaper()
    $panel2.ReloadDesktopWallpaper()
    
End

Public Sub ShowDesktop()
    
    Dim yukiWindow As DesktopWindow
    
    For Each yukiWindow In Desktop.Windows
        If yukiWindow.Desktop <> Desktop.Current Then Continue
        If yukiWindow.SkipTaskbar Then Continue
        If yukiWindow.Sticky Then Continue
        yukiWindow.Minimized = True
    Next
    
End

Public Sub Form_Open()

    Application.MainWindow = Me
    initializeForm()
    
    If MikuruX11.SetAsRoot(Me.Id) Then 
        NagatoDBus.Register()
        $desktop = New NagatoDesktop(Me)
        '$panel = New NagatoDesktopPanel(Me)
        $panel2 = New NagatoPanel2(Me)
        MikuruX11.ReservePanelHeightOnScreen(Me.Id)
        $timer.Enabled = True
        HaruhiPersona.Welcome()
        $ready = True
    Else
        Message.Error("YUKI.N > Fail to activate nagato-desktop", ("OK"))
        Me.Close()
    End If

End

Public Sub Form_Close()
    
    $timer.Enabled = False
    NagatoDBus.Unregister()
    
End

Public Sub DesktopTimer_Timer()
    
    $second = Second(Now())
    
    Raise Timer
    
End

Private Function Ready_Read() As Boolean

    Return $ready

End

Private Function Second_Read() As Integer

    Return $second

End
