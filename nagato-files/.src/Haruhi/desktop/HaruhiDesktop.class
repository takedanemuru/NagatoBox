' Gambas class file

Private Const Type As String = "_NET_WM_WINDOW_TYPE"
Private Const TypeDesktop As String = "_NET_WM_WINDOW_TYPE_DESKTOP"

Private Const Margin As Integer = 2

Private $desktop As NagatoDesktop
Private $panel As NagatoDesktopPanel
Private $timer As New Timer As "DesktopTimer"

Event Timer

Private Sub initializeForm()
    
    With Me
        .X = 0
        .Y = 0
        .W = Screen.W
        .H = Screen.H
    End With
    
End

Private Sub setX11Properties()
    
    Try Shell Subst("xprop -id &1 -f &2 32a -set &2 &3", Me.Id, Type, TypeDesktop)
    Try Shell "openbox --restart"
    If Not Error Then NagatoDBus.Register()
    
End

Public Sub RealodDesktopWallpaper()
    
    $desktop.ReloadDesktopWallpaper()
    
End

Public Sub Form_Open()

    Application.MainWindow = Me
    initializeForm()
    $desktop = New NagatoDesktop(Me)
    $panel = New NagatoDesktopPanel(Me)
    setX11Properties()
    $timer.Enabled = True

End

Public Sub Form_Close()
    
    $timer.Enabled = False
    NagatoDBus.Unregister()
    
End

Public Sub DesktopTimer_Timer()
    
    Raise Timer
    
End
