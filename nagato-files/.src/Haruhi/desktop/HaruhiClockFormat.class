' Gambas class file

Public Enum ResponseCancel, ResponseApply
Public Enum TargetDesktop, TargetPanel

Private $sampleDate As Date
Private $target As Integer = Me.TargetDesktop

Private Function readValue() As String
    
    If $target = Me.TargetDesktop Then
        Return Object.GetProperty(NagatoSettingsDesktopTile, "ClockFormat")
    Else
        Return Object.GetProperty(NagatoSettingsPanel, "ClockFormat")
    Endif
    
End

Private Sub setValue(argValue As String)
    
    If $target = Me.TargetDesktop Then
        Object.SetProperty(NagatoSettingsDesktopTile, "ClockFormat", argValue)
    Else
        Object.SetProperty(NagatoSettingsPanel, "ClockFormat", argValue)
    Endif
    
End

Public Sub SetTarget(argTarget As Integer)
    
    $target = argTarget
    
End

Public Sub Form_Open()
    
    Dim yukiImage As Image = Image.Load("pictures/yukichan_21.jpg").Stretch(Me.W, Me.H)
    
    Me.Picture = yukiImage.Picture
    Me.Text = ("Clock format")
    $sampleDate = Now()
    CurrentLabel.Text = Format$($sampleDate, readValue())
    UserLabel.Text = ""
    UserInputBox.Text = readValue()
    
    Me.Center()
    
End


Public Sub UserInputBox_Change()

    UserLabel.Text = Format$($sampleDate, UserInputBox.Text)

End

Public Sub CancelButton_Click()

    Me.Close(Me.ResponseCancel)

End

Public Sub ApplyButton_Click()

    setValue(UserInputBox.Text)

    Me.Close(Me.ResponseApply)

End
