' Gambas module file

Private Const TileSize As Integer = 128
Private Const TextWidth As Integer = 112
Private Const IconSize As Integer = 64
Private Const Margin As Integer = 8

Private Function getTilePicture(argBaseImage As Image, argIcon As Picture, argText As String, argSelected As Boolean) As Picture
    
    Dim yukiHeight As Integer
    
    With Paint
        .Begin(argBaseImage)
        .Brush = .Color(Color.White)
        .Font.Bold = True
        yukiHeight = .Font.RichTextHeight(argText, TextWidth)
        .DrawPicture(argIcon, (TileSize - argIcon.W) / 2, Margin)
        .RichText(argText, Margin, TileSize - yukiHeight - Margin, TextWidth, yukiHeight, Align.Center)
        .Fill()
        If argSelected Then drawSelectedLabel()
        .End()
    End With
    
    Return argBaseImage.Picture
    
End

Private Function getDesktopEntryIcon(argPath As String) As Picture
    
    Dim yukiDesktopFile As New DesktopFile(argPath)
    Dim yukiIcon As Image
    
    If Exist(yukiDesktopFile.Icon) Then
        yukiIcon = Image.Load(yukiDesktopFile.Icon)
        If IconSize > Max(yukiIcon.H, yukiIcon.W) Then
            Return yukiIcon.Picture
        Else
            Return yukiIcon.Stretch(IconSize, IconSize).Picture
        End If
    Else
        Return yukiDesktopFile.GetIcon(IconSize).Picture
    End If
    
End

Private Sub drawSelectedLabel()
    
    paint.Brush = paint.Color(Color.Red)
        Paint.Rectangle(0, 0, TileSize, 24)
    paint.Fill()
        Paint.DrawPicture(MikuruIcon.Get("button-check"), 4, 4)
    paint.Brush = paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.RichText("SELECTED", 24, 0, TileSize - 48, 24, Align.Center)
    paint.Fill()
        Paint.LineWidth = 2
        Paint.MoveTo(0, 24)
        Paint.LineTo(TileSize, 24)
    Paint.Stroke()
End

Private Function getSymbolizedTile(argPicture As Picture, argSymbol As String) As Picture
    
    Dim yukiImage As Image = argPicture.Image
    
    Paint.Begin(yukiImage)
        Paint.DrawImage(MikuruIcon.Get(argSymbol, 32).Image, 8, 8)
    Paint.End()
    
    Return yukiImage.Picture
    
End

Public Function GetApplicationTile(argPath As String, Optional argSize As Integer = TileSize) As Picture
    
    Dim yukiBaseImage As New Image(argSize, argSize, Color.Red + 16777216 * 192) 
    Dim yukiDesktopFile As New DesktopFile(argPath)
    Dim yukiIcon As Picture = getDesktopEntryIcon(argPath)
    Dim yukiText As String = yukiDesktopFile.Name
    
    Return getTilePicture(yukiBaseImage, yukiIcon, yukiText, False)
    
End

Public Function GetDirectoryTile(argPath As String, argSelected As Boolean, Optional argSize As Integer = TileSize) As Picture
    
    Dim yukiColor As Integer = (IIf(argSelected, Color.Red, Color.Blue) + 16777216 * 192)
    Dim yukiBaseImage As New Image(argSize, argSize, yukiColor) 
    Dim yukiIcon As Picture = MikuruIcon.GetDirectory(argPath, IconSize)
    Dim yukiFolders As String = Subst$("folder:&1", Dir(argPath, "*", gb.Directory).Count)
    Dim yukiFiles As String = Subst$("file:&1", Dir(argPath, "*", gb.File).Count)
    Dim yukiText As String = File.Name(argPath) & "<br>" & yukiFolders & " " & yukiFiles
    Dim yukiTile As Picture = getTilePicture(yukiBaseImage, yukiIcon, yukiText, argSelected)
    Dim yukiSymbol As String = NagatoDBSymbol.GetSymbolName(argPath)
    
    If yukiSymbol <> "" And If Not NagatoXdgUserDirs.IsXdgUserDir(argPath) Then
        Return getSymbolizedTile(yukiTile, yukiSymbol)
    Else
        Return yukiTile
    Endif
    
End

Public Function GetFileTile(argPath As String, argSelected As Boolean, Optional argSize As Integer = TileSize) As Picture
    
    Dim yukiColor As Integer = (IIf(argSelected, Color.Red, Color.Magenta) + 16777216 * 192)
    Dim yukiBaseImage As New Image(argSize, argSize, yukiColor) 
    Dim yukiIcon As Picture = Desktop.GetFileIcon(argPath, IconSize)
    Dim yukiText As String = File.Name(argPath)
    Dim yukiTile As Picture = getTilePicture(yukiBaseImage, yukiIcon, yukiText, argSelected)
    Dim yukiSymbol As String = NagatoDBSymbol.GetSymbolName(argPath)
    
    If yukiSymbol <> "" Then
        Return getSymbolizedTile(yukiTile, yukiSymbol)
    Else
        Return yukiTile
    Endif
    
End
