' Gambas module file

Property Read UriList As String
Property Read Plain As String

Public Enum TypeDirectory, TypeImage, TypeApplication, TypeElse

Public Function GetType(argFullPath As String) As Integer
    
    Dim yukiMimeType As String = DesktopMime.FromFile(argFullPath).Type
    
    If IsDir(argFullPath) Then
        Return Me.TypeDirectory
    Else If yukiMimeType Begins "image" Then
        Return Me.TypeImage
    Else If yukiMimeType = "application/x-desktop" Then
        Return Me.TypeApplication
    Else
        Return Me.TypeElse
    End If
    
Catch
    Return Me.TypeElse
    
End

Public Function GetDesktopFiles(argFullPath As String) As DesktopFile[] 
    
    Return DesktopMime.FromFile(argFullPath).GetApplications()
    
End

Public Function IsDesktopEntry(argFullPath As String) As Boolean
    
    Return Me.GetType(argFullPath) = Me.TypeApplication
    
Catch
    Return False
    
End

Public Function IsAudioFile(argFullPath As String) As Boolean
    
    Return (DesktopMime.FromFile(argFullPath).Type Begins "audio")
    
Catch
    Return False
    
End

Public Function IsMediaFile(argFullPath As String) As Boolean
    
    Dim yukiMimeType As String
    
    yukiMimeType = DesktopMime.FromFile(argFullPath).Type
    
    If yukiMimeType Begins "audio" Then Return True
    If yukiMimeType Begins "video" Then Return True
    
    Return False
    
End

Public Function IsImageFile(argFullPath As String) As Boolean
    
    Return (DesktopMime.FromFile(argFullPath).Type Begins "image")
    
Catch
    Return False
    
End

Public Function IsArchiveFile(argFullPath As String) As Boolean
    
    Dim yukiMimeType As String = DesktopMime.FromFile(argFullPath).Type
    
    If yukiMimeType Begins "application/x-compress" Then Return True
    If yukiMimeType = "application/zip" Then Return True
    If yukiMimeType = "x-zip-compressed" Then Return True
    If yukiMimeType = "application/lha" Then Return True
    If yukiMimeType = "application/x-rar-compressed" Then Return True
    If yukiMimeType = "application/x-rar" Then Return True
    If yukiMimeType = "application/x-commpress" Then Return True
    If yukiMimeType = "application/x-tar" Then Return True
    If yukiMimeType = "application/x-gzip" Then Return True
    
    Return False
    
Catch
    Return False
    
End

Private Function UriList_Read() As String

    Return "text/uri-list"

End

Private Function Plain_Read() As String

    Return "text/plain"

End
