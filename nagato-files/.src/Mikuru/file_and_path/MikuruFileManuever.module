' Gambas module file

Public Sub CutUriList(argTargetDirectory As String, argUriList As String)
    
    Dim yukiPath As String
    
    For Each yukiPath In NagatoDrag2.GetPaths(argUriList)
        If (argTargetDirectory &/ File.Name(yukiPath)) = yukiPath Then Continue
        If Not Exist(yukiPath) Then Continue
        Try Copy yukiPath To MikuruPath.GetSafePathName(argTargetDirectory &/ File.Name(yukiPath))
        If Not Error Then Try Kill yukiPath
    Next
    
    Clipboard.Clear()
    
End

Public Sub CopyUriList(argTargetDirectory As String, argUriList As String)
    
    Dim yukiPath As String
    
    For Each yukiPath In NagatoDrag2.GetPaths(argUriList)
        Try Copy yukiPath To MikuruPath.GetSafePathName(argTargetDirectory &/ File.Name(yukiPath))
    Next
    
End

Private Sub copyDir(argTargetDirectory As String, argSourceDirectory As String)
    
    Dim yukiPath As String
    Dim yukiTargetPath As String
    Dim yukiSourcePath As String
    
    Mkdir argTargetDirectory
    
    For Each yukiPath In Dir(argSourceDirectory)
        yukiTargetPath = argTargetDirectory &/ yukiPath
        yukiSourcePath = argSourceDirectory &/ yukiPath
        If IsDir(yukiSourcePath) Then
            copyDir(yukiTargetPath, yukiSourcePath)
        Else
            Copy yukiSourcePath To yukiTargetPath
        Endif
    Next
    
End

Private Function getTargetDirectory(argTitle As String, argPath As String) As String
    
    Dialog.Title = argTitle
    Dialog.Path = File.Dir(argPath)
    
    Return IIf(Dialog.SelectDirectory(), "", Dialog.Path)
    
End

Public Sub MoveToDirectory(argPaths As String[], argDirectory As String)
    
    Dim yukiPath As String
    Dim yukiTargetPath As String
    
    For Each yukiPath In argPaths
        yukiTargetPath = MikuruPath.GetSafePathName(argDirectory &/ File.Name(yukiPath))
        Try Move yukiPath To yukiTargetPath
    Next
    
End

Public Sub CopyToDirectory(argPaths As String[], argDirectory As String)
    
    Dim yukiPath As String
    Dim yukiTargetPath As String
    
    For Each yukiPath In argPaths
        yukiTargetPath = MikuruPath.GetSafePathName(argDirectory &/ File.Name(yukiPath))
        If Not IsDir(yukiPath) Then
            Try Copy yukiPath To yukiTargetPath
        Else
            copyDir(yukiTargetPath, yukiPath)
        End If
    Next
    
End

Public Sub MoveTo(argPaths As String[], Optional argDirectory As String = "")
    
    Dim yukiDirectory As String = argDirectory
    
    If Not Exist(yukiDirectory) Then yukiDirectory = getTargetDirectory(("Move To..."), argPaths[0])

    If yukiDirectory Then Me.MoveToDirectory(argPaths, yukiDirectory)
    
End

Public Sub CopyTo(argPaths As String[], Optional argDirectory As String = "")
    
    Dim yukiDirectory As String = argDirectory
    
    If Not Exist(yukiDirectory) Then yukiDirectory = getTargetDirectory(("Copy To..."), argPaths[0])
    
    If yukiDirectory Then Me.CopyToDirectory(argPaths, yukiDirectory)

End
