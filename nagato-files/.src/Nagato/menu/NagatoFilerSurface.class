' Gambas class file

Inherits NagatoObject

Property Read UserInput As NagatoFilerUserInput

Private $initializer As NagatoIconViewInitializer
Private $iconView As NagatoFilerIconView
Private $userInput As NagatoFilerUserInput

Private Sub surfaceClearSelection()
    
    Dim yukiSelectedPaths As String[] = $userInput.Selection.Copy()
    
    $userInput.ClearWithoutLastSelected()
    $iconView.ClearSelection(yukiSelectedPaths)
    
End

Public Sub SurfaceRefresh(argDirectory As String)
    
    $userInput.ClearAll()
    $iconView.Refresh(argDirectory, $userInput.Selection, $userInput.LastSelected)
    
End

Public Sub SurfaceDelete(argDirectory As String, argPath As String)
    
    If $iconView.Delete(argPath) Then $userInput.Remove(argPath)
    If Not MikuruDirectory.HasItem(argDirectory) Then surfaceRefresh(argDirectory)
    
End

Public Sub SetSeachResult(argSearchResult As String[])
    
    $iconView.RefreshWithSearchResult(argSearchResult, $userInput.Selection)
    
End

Public Sub EnsureVisible()
    
    $iconView.Ensure($userInput.LastSelected)
    
End

Public Sub ShowLostDirectory()

    $iconView.ShowLostDirectory()
    
End

Public Sub RefreshKeepQuery(argDirectory As String)
    
    $iconView.RefreshKeepQuery(argDirectory, $userInput.Selection, $userInput.LastSelected)
    
End

Public Sub NewSelection(argDirectory As String, argSelection As String[])
    
    NagatoSettings.MultipleSelection = True
    $userInput.SetPaths(argSelection)
    $iconView.Refresh(argDirectory, $userInput.Selection, $userInput.LastSelected)
    
End

Public Sub MouseDown(argPath As String, argMouseLeft As Boolean)
    
    $userInput.Push(argPath, argMouseLeft)
    If Not NagatoSettings.MultipleSelection Then surfaceClearSelection()
    $iconView.ResetIcon(argPath, $userInput.Exist(argPath))
    TFEI.SetFileProperty($userInput.LastSelected)
    
End

Public Function Exist(argPath As String) As Boolean
    
    Return $iconView.Exist(argPath)
    
End

Public Sub _new(argContainer As Container)
    
    $initializer = New NagatoIconViewInitializer(argContainer)
    $userInput = New NagatoFilerUserInput($initializer.IconView) As "TFEI"
    $iconView = New NagatoFilerIconView($initializer.IconView) 
    
End

Private Function UserInput_Read() As NagatoFilerUserInput

    Return $userInput

End
