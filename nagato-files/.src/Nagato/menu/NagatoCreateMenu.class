' Gambas class file

Inherits NagatoSubMenu

Private Sub addFiles(argParentMenu As Menu, argDirectory As String)
    
    Dim yukiMenu As Menu
    Dim yukiPath As String
    
    For Each yukiPath In Dir(argDirectory, "*", gb.File).Sort(gb.IgnoreCase)
        yukiMenu = New Menu(argParentMenu) As "CreateFileMenu"
        yukiMenu.Text = yukiPath
        yukiMenu.Tag = argDirectory &/ yukiPath
        yukiMenu.Picture = MikuruIconMono.GetFromPath(argDirectory &/ yukiPath)
    Next
    
End

Private Sub addCopyWholeDirectoryMenu(argParentMenu As Menu, argDirectory As String)
    
    Dim yukiMenu As Menu
    
    If argDirectory = NagatoXdg.UserDirs.Templates Then Return
    
    yukiMenu = New Menu(argParentMenu) As "CopyWholeDirectoryMenu"
    
    With yukiMenu
        .Text = ("Copy Whole Directory")
        .Picture = MikuruIcon.Get("folder")
        .Tag = argDirectory
    End With
    
    Object.New("NagatoMenuItemDummy", [argParentMenu, ""])
    
End

Private Sub addDirectories(argParentMenu As Menu, argDirectory As String)
    
    Dim yukiPath As String
    Dim yukiMenu As Menu
    
    'addCopyWholeDirectoryMenu(argParentMenu, argDirectory)
    
    For Each yukiPath In Dir(argDirectory, "*", gb.Directory).Sort(gb.IgnoreCase)
        yukiMenu = New Menu(argParentMenu)
        yukiMenu.Text = yukiPath
        yukiMenu.Picture = MikuruIcon.Get("folder")
        addDirectories(yukiMenu, argDirectory &/ yukiPath)
    Next

    addFiles(argParentMenu, argDirectory)
    
End

Public Sub _InitializeParentMenu(argRootMenu As Menu) ' override
    
    Me._$rootMenu = New Menu(argRootMenu)
    
    With Me._$rootMenu
        .Text = ("Create")
        .Picture = MikuruIcon.Get("magic-wand")
    End With
    
End

Public Sub _InitializeMenu() ' override
    
    Me._AddMenu(("New Directory"), NagatoObject.CreateDirectory, "folder")
    Me._AddMenu(("New Text File"), NagatoObject.CreateBlankText, "document")
        Object.New("NagatoMenuItemDummy", [Me._$rootMenu, ""])
    addDirectories(Me._$rootMenu, NagatoXdg.UserDirs.Templates)
    
End

Public Sub MenuItem_Click()
    
    Raise Signal(Last.Tag, Null)
    
End

Public Sub CopyWholeDirectoryMenu_Click()
    
    Debug Last.Tag
    
End

Public Sub CreateFileMenu_Click()
    
    Raise Signal(NagatoObject.FileCreate, [Last.Tag])
    
End
