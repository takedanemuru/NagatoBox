' Gambas class file

Inherits NagatoSubMenu

Private Sub addSelectApplicationMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu) As "SelectApplicationMenu"
    
    With yukiMenu
        .Text = ("Select application")
    End With
    
End

Private Sub addDummyMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu) 
    
    With yukiMenu
        .Text = ""
    End With
    
End

Private Sub addIndivisualOpenWithMenu(argDesktopFileName As String)
    
    Dim yukiMenu As Menu
    Dim yukiPath As String = "/usr/share/applications/" & argDesktopFileName
    Dim yukiDesktopFile As DesktopFile

    If Not Exist(yukiPath) Then Return
    
    yukiDesktopFile = New DesktopFile(yukiPath)

    yukiMenu = New Menu(Me._$rootMenu) As "OpenWithMenu"
    
    With yukiMenu
        .Picture = MikuruIcon.GetDesktopEntryIcon(yukiDesktopFile.Path, 32)
        .Text = yukiDesktopFile.Name
        .Tag = yukiDesktopFile.Path
    End With

Catch
    Return
    
End

Private Function getMimeType() As String
    
    If IsDir(Me._$path) Then
        Return "inode/directory"
    Else
        Return DesktopMime.FromFile(Me._$path).Type
    Endif
    
End

Private Function getDesktopFilesWithMimeType() As String[]
    
    Dim yukiReturn As String[]
    
    yukiReturn = MikuruConfFlie.GetItems(MikuruApplications.GlobalMimeList, getMimeType()).Sort(gb.Natural)
    
    Return yukiReturn
    
End

Public Sub _InitializeParentMenu(argRootMenu As Menu) ' this method is protected/virtual. MUST be overridden
    
    Me._$rootMenu = New Menu(argRootMenu)
    
    With Me._$rootMenu
        .Text = ("Open with ...")
    End With
    
End

Public Sub _InitializeMenu() ' this method is protected/virtual. MUST be overridden
    
    Dim yukiAdditionalDesktopFile As String
    Dim yukiChecker As New String[]
    Dim yukiMimeTypes As String[] = getDesktopFilesWithMimeType()
    Dim yukiDefaultApp As New NagatoDefaultApplication(Me._$path)
    Dim yukiDefaultAppName As String = File.Name(yukiDefaultApp.DesktopFile.Name) & ".desktop"
    
    addSelectApplicationMenu()
    
    If yukiMimeTypes.Count > 0 Then addDummyMenu()
    
    For Each yukiAdditionalDesktopFile In yukiMimeTypes
        If Not yukiChecker.Exist(yukiAdditionalDesktopFile) Then
            If Not yukiDefaultApp.NotFound Then
                If yukiAdditionalDesktopFile = yukiDefaultAppName Then Continue
            Endif
            addIndivisualOpenWithMenu(yukiAdditionalDesktopFile)
            yukiChecker.Add(yukiAdditionalDesktopFile)
        End If
    Next
    
Catch
    Debug Error.Text
    Return
    
End

Public Sub OpenWithMenu_Click()
    
    Dim yukiDesktopFile As New DesktopFile(Last.Tag)
    
    MikuruChangeDefaultApplication(yukiDesktopFile, Me._$path)

    yukiDesktopFile.Run(Me._$path)
    
End

Public Sub SelectApplicationMenu_Click()
    
    HaruhiSelectApplication.ShowAllApplications(Me._$path)
    
End
