' Gambas class file

Inherits NagatoSubMenu

Private Sub addSelectApplicationMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu) As "SelectApplicationMenu"
    
    With yukiMenu
        .Text = ("Select application")
    End With
    
End

Private Sub addDummyMenu()
    
    Dim yukiMenu As New Menu(Me._$rootMenu) 
    
    With yukiMenu
        .Text = ""
    End With
    
End

Private Sub addIndivisualOpenWithMenu(argDesktopFileName As String)
    
    Dim yukiMenu As Menu
    Dim yukiPath As String = "/usr/share/applications/" & argDesktopFileName
    Dim yukiDesktopFile As DesktopFile

    If Not Exist(yukiPath) Then Return
    
    yukiDesktopFile = New DesktopFile(yukiPath)

    yukiMenu = New Menu(Me._$rootMenu) As "OpenWithMenu"
    
    With yukiMenu
        .Picture = MikuruIcon.GetDesktopEntryIcon(yukiDesktopFile.Path, 32)
        .Text = yukiDesktopFile.Name
        .Tag = yukiDesktopFile.Path
    End With

Catch
    Return
    
End

Private Function getDesktopFilesWithMimeType() As String[]
    
    Dim yukiReturn As String[]
    
    yukiReturn = MikuruConfFlie.GetItems(MikuruApplications.GlobalMimeList, "text/plain").Sort(gb.Natural)
    
    Return yukiReturn
    
End

Public Sub _InitializeParentMenu(argRootMenu As Menu) ' this method is protected/virtual. MUST be overridden
    
    Me._$rootMenu = New Menu(argRootMenu)
    
    With Me._$rootMenu
        .Text = ("Open As Text")
    End With
    
End

Public Sub _InitializeMenu() ' this method is protected/virtual. MUST be overridden
    
    Dim yukiAdditionalDesktopFile As String
    Dim yukiChecker As New String[]
    Dim yukiMimeTypes As String[] = getDesktopFilesWithMimeType()
    
    addSelectApplicationMenu()
    
    If yukiMimeTypes.Count > 0 Then addDummyMenu()
    
    For Each yukiAdditionalDesktopFile In yukiMimeTypes
        If Not yukiChecker.Exist(yukiAdditionalDesktopFile) Then
            addIndivisualOpenWithMenu(yukiAdditionalDesktopFile)
            yukiChecker.Add(yukiAdditionalDesktopFile)
        End If
    Next
    
Catch
    Debug Error.Text
    Return
    
End

Public Sub OpenWithMenu_Click()
    
    Dim yukiDesktopFile As New DesktopFile(Last.Tag)
    
    MikuruChangeDefaultApplication(yukiDesktopFile, Me._$path)

    yukiDesktopFile.Run(Me._$path)
    
Catch
    Message.Error(("YUKI.N > Some error occured.\n This application may be removed."), ("OK"))
    
End

Public Sub SelectApplicationMenu_Click()
    
    HaruhiSelectApplication.ShowAllApplications(Me._$path)
    
End
