' Gambas class file

Inherits NagatoSubMenu

Private Const BusName As String = "org.gambas.nagato-player-x"
Private Const ObjectPath As String = "/org/nagato/player/x/player"
Private Const Interface As String = "org.gambas.nagato.player.x.nagatodbusinterface"

Private $proxy As DBusProxy

Private Sub initializeProxy()
    
    $proxy = DBus[BusName][ObjectPath, Interface]
    
End

Private Sub addMenu(argIndex As Integer, argTitle As String)
    
    Dim yukiMenu As Menu = New Menu(Me._$rootMenu) As "QueueMenu"
    
    With yukiMenu
        .Tag = argIndex
        .Text = MikuruCollapsedText.Get(argTitle, Application.Font, 300)
    End With
    
End

Public Sub _InitializeParentMenu(argRootMenu As Menu) ' override
    
    Me._$rootMenu = New Menu(argRootMenu) As "ParentMenu"
    
    With Me._$rootMenu
        .Text = ("Queue")
        .Picture = MikuruIcon.Get("music")
    End With
    
End

Public Sub _InitializeMenu() ' this method is protected/virtual. MUST be overridden
    
    Dim yukiIndex As Integer
    Dim yukiTitle As String
    
    initializeProxy()
    
    For Each yukiTitle In $proxy.GetTitles(30)
        addMenu(yukiIndex, yukiTitle)
        Inc yukiIndex
    Next
    
End

Public Sub QueueMenu_Click()
    
    $proxy.PlayIndex(Last.Tag)
    
End
