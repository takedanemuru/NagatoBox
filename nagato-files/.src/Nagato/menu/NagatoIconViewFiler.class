' Gambas class file

Inherits NagatoObject

Property Read Selection As String[]

Private $view As NagatoFilerSurface
Private $userInput As NagatoFilerUserInput

Public Sub SetFocus(argPath As String)
    
    $view.SetFocus(argPath)
    
End

Public Sub Refresh(argDirectory As String)
    
    $view.Refresh(argDirectory)
    
End

Public Sub RefreshWithSearchQuery(argPaths As String[])
    
    $view.RefreshWithSearchQuery(argPaths)
    
End

Public Sub Delete(argPath As String)
    
    $view.Delete(argPath)
    
End

Public Sub HasKey(argDirectory As String, argPath As String) As Boolean
    
    Return $view.HasKey(argDirectory &/ argPath)
    
End

Public Sub SelectItems(argTag As Integer)
    
    Select Case argTag
        Case NagatoObject.ViewSelectAll
            $view.SelectAll()
        Case NagatoObject.ViewUnselectAll
            $view.UnselectAll()
        Case NagatoObject.ViewInvertSelection
            $view.InvertSelection()
    End Select
    
End

Public Sub EnsureVisible()
    
    $view.EnsureVisibleLastSelected()
    
End

Public Sub ShowLostDirectory()
    
    $view.ShowLostDirectory()
    
End

Public Sub _new(argContainer As Container)
    
    Dim yukiIconViewInitializer As New NagatoIconViewInitializer(argContainer)
    
    $view = New NagatoFilerSurface(argContainer, yukiIconViewInitializer.IconView) As "TFEI"
    $userInput = New NagatoFilerUserInput(yukiIconViewInitializer.IconView) As "TFEI"

End

Public Sub TFEI_Signal(argEvent As Integer, argValues As Variant[])
    
    Select Case argEvent
        Case NagatoObject.ViewContextMenu
            $view.PopUpContextMenu()
        Case NagatoObject.UserInputMouseDown
            $view.MouseDown(argValues[0], argValues[1])
        Case NagatoObject.UserInputMouseDrag
            $userInput.Drag($view.Selection)
        Case NagatoObject.UserInputKeyPress
            keyPress(argValues[0])
        Case NagatoObject.UserInputKeyPressWithControl
            keyPressWithControl(argValues[0])
        Default
            Raise Signal(argEvent, argValues)
    End Select
    
End

Private Sub keyPressWithControl(argKeyCode As Integer)
    
    Select Case argKeyCode
        Case Key["x"]
            NagatoClipBoard.Cut($view.Selection)
        Case Key["c"]
            NagatoClipBoard.Copy($view.Selection)
    End Select
    
End

Private Sub keyPress(argKeyCode As Integer)
    
    Select Case argKeyCode
        Case Key.Enter, 16777220
            $view.Enter()
        Case Key.Esc, 16777216
            $view.UnselectAll()
        Case Key.Delete
            NagatoTrash.Delete($view.Selection)
    End Select
    
End

Private Function Selection_Read() As String[]

    Return $view.Selection

End
