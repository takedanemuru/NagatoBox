' Gambas class file

Inherits NagatoIconViewHandler

Private Sub pressKey(argKeyCode As Integer)
    
    Select Case argKeyCode
        Case Key.Enter, 16777220
            If Not IsDir(Me._$selection.LastSelected) Then Return
            Raise Signal(NagatoObject.DirOpen, [Me._$selection.LastSelected])
        Case Key.Esc, 16777216
            Raise Signal(NagatoObject.ViewUnselectAll, Null)
        Case Key.Delete
            NagatoTrash.Delete(Me._$selection.Selection)
    End Select
    
End

Private Sub pressKeyWithAlt(argKeyCode As Integer)
    
    Select Case argKeyCode
        Case Key["h"]
            Raise Signal(NagatoObject.MoveHome, Null)
        Case Key["u"]
            Raise Signal(NagatoObject.MoveUp, Null)
        Case Key["b"]
            Raise Signal(NagatoObject.MoveBack, Null)
        Case Key["f"]
            Raise Signal(NagatoObject.MoveForward, Null)
    End Select
    
End

Private Sub pressKeyWithCtrl(argKeyCode As Integer)
    
    Select Case argKeyCode
        Case Key["h"]
            NagatoSettings.ShowHidden = Not NagatoSettings.ShowHidden
            TFEI.RefreshAll()
        Case Key["n"]
            Raise Signal(NagatoObject.DirCreate, Null)
        Case Key["f"]
            TFEI.SetSearch(False)
        Case Key["x"]
            NagatoClipBoard.Cut(Me._$selection.Selection)
        Case Key["c"]
            NagatoClipBoard.Copy(Me._$selection.Selection)
        Case Key["v"]
            Raise Signal(NagatoObject.FilePasteFromClipboard, [""])
        Case Key["t"]
            Raise Signal(NagatoObject.DirOpenInNewTab, [User.Home])
    End Select
    
End

Public Sub Asakura_KeyPress()
    
    If Key.Control Then
        pressKeyWithCtrl(Key.Code)
    Else If Key.Alt Then
        pressKeyWithAlt(Key.Code)
    Else
        pressKey(Key.Code)
    End If
    
End
