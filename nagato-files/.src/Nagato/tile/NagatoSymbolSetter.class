' Gambas class file

Private Function getTriangleTaggedTile(argPicture As Picture, argText As String, argColor As Integer) As Picture
    
    Dim yukiImage As Image = argPicture.Image
    Dim yukiHeight As Integer
    Dim yukiWidth As Integer = Sqr(2 * MikuruUxTile.TileSize ^ 2)
    Dim yukiRect As New Rect
    
    Paint.Begin(yukiImage)
        Paint.Brush = Paint.Color(argColor)
        Paint.Polygon([0, 0, MikuruUxTile.TileSize * 0.5, 0, 0, MikuruUxTile.TileSize * 0.5, 0, 0])
        Paint.Fill()
        Paint.Rotate(Rad(45))
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        yukiHeight = Paint.Font.RichTextHeight(argText)
        yukiRect.X = -0.5 * yukiWidth
        yukiRect.Y = (MikuruUxTile.TileSize * 0.5) - 2 * yukiHeight
        yukiRect.W = yukiWidth
        yukiRect.H = yukiHeight
        Paint.RichText(argText, yukiRect.X, yukiRect.Y, yukiRect.W, yukiRect.H, Align.Center)
        Paint.Fill()
    Paint.End()
    
    Return yukiImage.Picture
    
End

Private Function getSymlinkTag(argPicture As Picture) As Picture
    
    Return getTriangleTaggedTile(argPicture, "symlink", MikuruColor.Transparent(Color.Orange, 128))
    
End

Private Function getXdgUserDirTag(argPicture As Picture) As Picture
    
    Return getTriangleTaggedTile(argPicture, "xdg-dir", MikuruColor.Transparent(Color.Violet, 128))
    
End

Private Function getSymbolizedTile(argPicture As Picture, argSymbol As String) As Picture
    
    Dim yukiImage As Image = argPicture.Image
    
    Paint.Begin(yukiImage)
        Paint.DrawImage(MikuruIcon.Get(argSymbol, 32).Image, 8, 8)
    Paint.End()
    
    Return yukiImage.Picture
    
End

Public Sub _call(argTile As Picture, argPath As String) As Picture
    
    Dim yukiSymbol As String = NagatoDBSymbol.GetSymbolName(argPath)
    
    If yukiSymbol <> "" Then argTile = getSymbolizedTile(argTile, yukiSymbol)
    If Stat(argPath).Link Then argTile = getSymlinkTag(argTile)
    If NagatoXdg.IsUserDir(argPath) Then argTile = getXdgUserDirTag(argTile)
    
    Return argTile
    
End

