' Gambas class file

Inherits NagatoMenuPipe

Private $menuBar As NagatoMenuPanel
Private $filterBar As NagatoFilterBar
Private $iconView As NagatoIconView
Private $log As NagatoDirectoryLog
Private $directoryObserver As NagatoDirectoryObserver

Event ShowProperty(argPath As String)
Event NewTab(argPath As String)
Event AppQuit

Private Sub initializeDirectoryObserver(argDirectory As String)
    
    $directoryObserver = New NagatoDirectoryObserver As "DirectoryObserver"
    
    With $directoryObserver
        .IntervalSecond = 1
        .Directory = argDirectory
    End With
    
End

Private Sub move(argDirectory As String)
    
    $filterBar.CurrentDirectory = argDirectory
    $filterBar.Filtered = False
    HaruhiMain.SetMainTabStrip(File.BaseName(argDirectory), MikuruIcon.GetDirectoryMono(argDirectory))
    $log.Move(argDirectory)
    $menuBar.SetEnable($log.HasBack, Not ($log.Current = User.Home), True, $log.HasForward)
    refresh(argDirectory)
    If Not Object.IsValid($directoryObserver) Then initializeDirectoryObserver(argDirectory)
    $directoryObserver.Directory = argDirectory
    
End

Private Sub rename(argArgument As Variant)
    
    HaruhiRename.SetPath(argArgument, Desktop.GetFileIcon(argArgument, 64))
    If HaruhiRename.ShowDialog() = MikuruDialog.ResponseApply Then
        $iconView.SetFocus(argArgument)
    Endif
    
End

Private Sub addTag()
    
    If HaruhiSelectSymbol.ShowDialog() = MikuruDialog.ResponseApply Then
        NagatoDBSymbol.PushPaths($iconView.Selection, MikuruDialog.LastSelectedSymbol)
        $iconView.Refresh($log.Current)
    Endif
    
End

Private Sub refresh(argDirectory As String)
    
    $menuBar.SetPath(argDirectory)
    If $filterBar.Filtered Then
        $iconView.RefreshFiltered(argDirectory, $filterBar.Paths)
    Else
        $iconView.Refresh(argDirectory)
    End If
    
Catch
    Return
    
End

Public Sub SetNewDirectory(argDirectory As String)
    
    If $log.Current <> argDirectory Then move(argDirectory)
    
End

Public Sub _new(argContainer As TabStrip, argDirectory As String)
    
    $log = New NagatoDirectoryLog
    $menuBar = New NagatoMenuPanel(argContainer) As "MenuBar"
    $filterBar = New NagatoFilterBar(argContainer) As "FilterBar"
    $iconView = New NagatoIconView(argContainer) As "IconView"
    
    move(IIf(argDirectory = "", User.Home, argDirectory))
    
End

Public Sub IconView_DirectoryClicked(argDirectory As String)
    
    move(argDirectory)
    
End

Public Sub IconView_MenuEventWithArg(argEvent As Integer, argArgument As Variant)
    
    With NagatoMenuPipe
        Select Case argEvent
            Case .PathCopyToClipboard
                Clipboard.Copy(argArgument, "text/plain")
            Case .DirOpen
                move(argArgument)
            Case .DirOpenInNewTab
                Raise NewTab(argArgument)
            Case .DirRename
                HaruhiRename.SetPath(argArgument, Stock["64/directory"])
                HaruhiRename.ShowDialog()
            Case .DirDelete
                MikuruFile.TrashDirectory(argArgument)
            Case .FileOpen
                MikuruFile.Exec(argArgument)
            Case .FileDelete
                MikuruFile.TrashFile(argArgument)
            Case .FileRename
                rename(argArgument)
            Case .FileMoveToCurrentDir
                If $log.Current = File.Dir(argArgument) Then Return
                Try Move argArgument To MikuruPath.GetSafePathName($log.Current &/ File.Name(argArgument)) 
             Case NagatoMenuPipe.FileCopy
                MikuruFileManuever.CopyUriList($log.Current, argArgument)
             Case .FileShowProperty
                 Raise ShowProperty(argArgument)
             Case .TagAdd
                 addTag()
             Case .TagRemove
                 NagatoDBSymbol.RemovePaths($iconView.Selection)
                 $iconView.Refresh($log.Current)
        End Select
    End With
    
End

Public Sub MenuBar_MenuEvent(argEvent As Integer)
    
    With NagatoMenuPipe
        Select Case argEvent
            Case .MoveBack
                If $log.HasBack Then move($log.Back)
            Case .MoveForward
                If $log.HasForward Then move($log.Forward)
            Case .MoveHome
                If $log.Current <> User.Home Then move(User.Home)
            Case .MoveUp
                If $log.Current <> User.Home Then move(File.Dir($log.Current))
            Case .ViewShowHidden
                NagatoSettings.ShowHidden = Not NagatoSettings.ShowHidden
                refresh($menuBar.Path)
            Case .ViewNewTab
                Raise NewTab(User.Home)
            Case .DirCreate
                HaruhiNewDirectory.SetPath($log.Current &/ ("New directory"))
                HaruhiNewDirectory.ShowDialog()
            Case .AppQuit
                Raise AppQuit
        End Select
    End With
    
End

Public Sub MenuBar_MenuEventWithArg(argEvent As Integer, argArgument As Variant)
    
    Select Case argEvent
        Case NagatoMenuPipe.PathNew
            If IsDir(argArgument) Then move(argArgument)
    End Select
    
End

Public Sub MenuBar_ToggleFilterBar(argShow As Boolean)
    
    $filterBar.Toggle(argShow)
    
End

Public Sub DirectoryObserver_Modefied(argFileName As String)
    
    If Not NagatoSettings.ShowHidden And If argFileName Begins "." Then Return
    If Not $iconView.HasKey($log.Current, argFileName) Then refresh($log.Current)
    
End

Public Sub DirectoryObserver_Deleted(argFileName As String)
    
    If Not NagatoSettings.ShowHidden And If argFileName Begins "." Then Return
    $iconView.Delete($log.Current &/ argFileName)
    
End

Public Sub FilterBar_Changed()
    
    refresh($log.Current)
    
End
