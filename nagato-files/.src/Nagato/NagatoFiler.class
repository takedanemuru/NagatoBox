' Gambas class file

Inherits NagatoMenuPipe

Private $menuBar As NagatoMenuPanel
Private $iconView As NagatoIconView
Private $log As NagatoDirectoryLog
Private $directoryObserver As NagatoFilerDirectoryObserver

Event ShowProperty(argPath As String)
Event NewTab(argPath As String)
Event AppQuit

Private Sub ensureObserver(argDirectory As String)
    
    If Object.IsValid($directoryObserver) Then Return
    $directoryObserver = New NagatoFilerDirectoryObserver(argDirectory) As "DirectoryObserver"
    
End

Private Sub move(argDirectory As String)
    
    HaruhiMain.SetMainTabStrip(File.BaseName(argDirectory), MikuruIcon.GetDirectoryMono(argDirectory))
    $log.Move(argDirectory)
    $menuBar.SetEnable($log.HasBack, Not ($log.Current = User.Home), True, $log.HasForward)
    refresh(argDirectory)
    ensureObserver(argDirectory)
    $directoryObserver.Directory = argDirectory
    
End

Private Sub refresh(argDirectory As String)
    
    $menuBar.SetPath(argDirectory)
    $iconView.Refresh(argDirectory)
    
Catch
    Return
    
End

Public Sub SetNewDirectory(argDirectory As String)
    
    If $log.Current <> argDirectory Then move(argDirectory)
    
End

Public Sub SetSeachResult(argQuery As String, argRecursive As Boolean)
    
    $iconView.RefreshWithSearchQuery(MikuruFileSearch.SeachQuery($log.Current, argQuery, argRecursive))
    
End

Public Sub _new(argContainer As TabStrip, argDirectory As String)
    
    $log = New NagatoDirectoryLog
    $menuBar = New NagatoMenuPanel(argContainer) As "MenuBar"
    $iconView = New NagatoIconView(argContainer) As "IconView"
    
    move(IIf(argDirectory = "", User.Home, argDirectory))
    
End

Public Sub IconView_MenuEvent(argEvent As Integer)
    
    Select Case argEvent
        Case NagatoMenuPipe.CreateArchive
            MikuruCreate.Archive($iconView.Selection)
        Case NagatoMenuPipe.CreateClone
            MikuruCreate.Clone($log.Current, $iconView.Selection)
    End Select
    
End

Public Sub IconView_MenuEventWithArg(argEvent As Integer, argArgument As Variant)
    
    Debug argEvent
    
    With NagatoMenuPipe
        Select Case argEvent
            Case .PathCopyToClipboard
                Clipboard.Copy(argArgument, "text/plain")
            Case .DirOpen
                move(argArgument)
            Case .DirOpenInNewTab
                Raise NewTab(argArgument)
            Case .DirRename
                HaruhiRename.SetPath(argArgument, Stock["64/directory"])
                HaruhiRename.ShowDialog()
            Case .DirDelete
                MikuruFile.TrashDirectory(argArgument)
            Case .FileOpen
                MikuruFile.Exec(argArgument)
            Case .FileDelete
                MikuruFile.TrashFile(argArgument)
            Case .FileRename
                If NagatoFilerDialogs.Rename(argArgument) Then $iconView.SetFocus(argArgument)
            Case .FileMoveToCurrentDir
                If $log.Current = File.Dir(argArgument) Then Return
                Try Move argArgument To MikuruPath.GetSafePathName($log.Current &/ File.Name(argArgument)) 
             Case NagatoMenuPipe.FileCopy
                MikuruFileManuever.CopyUriList($log.Current, argArgument)
             Case .FileShowProperty
                 Raise ShowProperty(argArgument)
             Case .TagAdd
                 If NagatoFilerDialogs.Tagging($iconView.Selection) Then $iconView.Refresh($log.Current)
             Case .TagRemove
                 NagatoDBSymbol.RemovePaths($iconView.Selection)
                 $iconView.Refresh($log.Current)
        End Select
    End With
    
End

Public Sub MenuBar_MenuEvent(argEvent As Integer)
    
    With NagatoMenuPipe
        Select Case argEvent
            Case .MoveBack
                If $log.HasBack Then move($log.Back)
            Case .MoveForward
                If $log.HasForward Then move($log.Forward)
            Case .MoveHome
                If $log.Current <> User.Home Then move(User.Home)
            Case .MoveUp
                If $log.Current <> "/" Then move(File.Dir($log.Current))
            Case .ViewShowHidden
                NagatoSettings.ShowHidden = Not NagatoSettings.ShowHidden
                refresh($menuBar.Path)
            Case .ViewNewTab
                Raise NewTab(User.Home)
            Case .DirCreate
                HaruhiNewDirectory.SetPath($log.Current &/ ("New directory"))
                HaruhiNewDirectory.ShowDialog()
            Case .AppQuit
                Raise AppQuit
        End Select
    End With
    
End

Public Sub MenuBar_MenuEventWithArg(argEvent As Integer, argArgument As Variant)
    
    Select Case argEvent
        Case NagatoMenuPipe.PathNew
            If IsDir(argArgument) Then move(argArgument)
    End Select
    
End

Public Sub DirectoryObserver_MenuEventWithArg(argEvent As Integer, argFileName As Variant)
    
    Select Case argEvent
        Case NagatoMenuPipe.ObserverModefy
            If Not $iconView.HasKey($log.Current, argFileName) Then refresh($log.Current)
        Case NagatoMenuPipe.ObserverDelete
            $iconView.Delete($log.Current &/ argFileName)
    End Select
    
End
