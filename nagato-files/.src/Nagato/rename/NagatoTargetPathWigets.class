' Gambas class file

Property Read FullPath As String

Private $directoryBox As ButtonBox
Private $nameBox As TextBox

Private Function getDirectoryIcon(argPath As String) As Picture
    
    Return IIf(argPath = NagatoXdg.UserDirs.Desktop, MikuruIcon.Get("desktop"), MikuruIcon.Get("folder"))
    
End

Private Sub setDirectoryButtonBox(argPath As String)
    
    With $directoryBox
        .Text = File.Dir(argPath)
        .Picture = getDirectoryIcon(argPath)
    End With
    
End

Private Sub setNewNameTextLabel(argPath As String)
    
    Dim yukiExt As String = File.Ext(argPath)
    
    With $nameBox
        .Text = File.Name(argPath)
        If yukiExt = "" Then
            .Select()
        Else
            .Select(0, String.Len(.Text) - String.Len(yukiExt) - 1)
        End If
    End With
    
End

Public Sub SetPath(argPath As String)
    
    setDirectoryButtonBox(argPath)
    setNewNameTextLabel(argPath)
    
End

Public Sub ChangeDirectory(argDirectory As String)
    
    Dialog.Title = ("Select directory")
    Dialog.Path = File.Dir(argDirectory)
    
    If Not Dialog.SelectDirectory() Then
        setDirectoryButtonBox(Dialog.Path)
    Endif
    
End

Public Sub _new(argDirectoryBox As ButtonBox, argNameBox As TextBox)
    
    $directoryBox = argDirectoryBox
    $nameBox = argNameBox
    
End

Private Function FullPath_Read() As String

    Return $directoryBox.Text &/ $nameBox.Text

End
