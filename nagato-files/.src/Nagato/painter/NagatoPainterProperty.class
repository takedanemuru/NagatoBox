' Gambas class file

Private Const IconSize As Integer = 128

Private $drawingArea As DrawingArea
Private $currentPath As String

Private Sub drawHeader(argY As Integer)
    
    Dim yukiTemplate As String = "Size:<br>Permission:<br>Access:<br>Modefy:<br>Change:<br>"
    Dim yukiRect As New Rect(16, argY + 16, IconSize + 32, $drawingArea.Font.RichTextHeight(yukiTemplate))
    
    Paint.Brush = Paint.Color(Color.White)
    Paint.RichText(yukiTemplate, yukiRect.X, yukiRect.Y, yukiRect.W, yukiRect.H, Align.Left)
    Paint.Fill()
    
End

Private Function getPropertyText(argTemplate As String) As String
    
    Dim yukiStat As Stat = Stat($currentPath)
    Dim yukiSize As String = MikuruFile.GetFileSizeText($currentPath)
    Dim yukiPermission As String = MikuruFilePermission.GetText($currentPath)
    Dim yukiLastAccess As String = MikuruFile.GetTimeDifferenceText(yukiStat.LastAccess)
    Dim yukiLastModefy As String = MikuruFile.GetTimeDifferenceText(yukiStat.LastModified)
    Dim yukiLastChange As String = MikuruFile.GetTimeDifferenceText(yukiStat.LastChange)
    
    Return Subst$(argTemplate, yukiSize, yukiPermission, yukiLastAccess, yukiLastModefy, yukiLastChange)
    
Catch
    Return ""
    
End

Private Function drawProperty(argY As Integer) As Integer
    
    Dim yukiTemplate As String = "&1<br>&2<br>&3<br>&4<br>&5<br>"
    Dim yukiRect As New Rect(16, argY + 16, IconSize + 32, $drawingArea.Font.RichTextHeight(yukiTemplate))
    
    Paint.Brush = Paint.Color(Color.White)
    Paint.RichText(getPropertyText(yukiTemplate), yukiRect.X, yukiRect.Y, yukiRect.W, yukiRect.H, Align.Right)
    Paint.Fill()
    
    Return yukiRect.H + yukiRect.Y
    
End

Private Function drawImageProperties(argTopOffset As Integer) As Integer
    
    Dim yukiImage As Image = Image.Load($currentPath)
    Dim yukiTemplate As String = "<br>Height:<br>Width:"
    Dim yukiRect As New Rect(16, argTopOffset, IconSize + 32, $drawingArea.Font.RichTextHeight(yukiTemplate))
    Dim yukiProperties As String = Subst$("<br>&1<br>&2", yukiImage.H, yukiImage.W)
    
    Paint.Brush = Paint.Color(Color.White)
    Paint.RichText(yukiTemplate, yukiRect.X, yukiRect.Y, yukiRect.W, yukiRect.H, Align.Left)
    Paint.RichText(yukiProperties, yukiRect.X, yukiRect.Y, yukiRect.W, yukiRect.H, Align.Right)
    Paint.Fill()
    
    Return yukiRect.Y + yukiRect.H
    
End

Private Function drawPageCount(argTopOffset As Integer, argPath As String) 
    
    Dim yukiTemplate As String = "<br>Page:<br>"
    Dim yukiDocument As New PdfDocument(argPath)
    Dim yukiRect As New Rect(16, argTopOffset, IconSize + 32, $drawingArea.Font.RichTextHeight(yukiTemplate))
    
    With yukiRect
        Paint.Brush = Paint.Color(Color.White)
        Paint.RichText(yukiTemplate, .X, .Y, .W, .H, Align.Left)
        Paint.RichText(yukiDocument.Count, .X, .Y, .W, .H, Align.Right)
        Paint.Fill()
    End With
    
End

Private Function drawTrashProperty(argTopOffset As Integer) As Integer
    
    Dim yukiTemplate As String = "<br><u>Path To Restore:</u><br>"
    Dim yukiRect As New Rect(16, argTopOffset, IconSize + 32, $drawingArea.Font.RichTextHeight(yukiTemplate))
    
    With yukiRect
        Paint.Brush = Paint.Color(Color.White)
        Paint.RichText(yukiTemplate, .X, .Y, .W, .H, Align.Center)
        'Paint.RichText(yukiProperties, .X, yukiRect.Y, yukiRect.W, yukiRect.H, Align.Right)
        Paint.RichText(NagatoTrash.GetPreviousPath($currentPath), .X, .H + .Y, .W,, Align.Center)
        Paint.Fill()
    End With
    
    Return yukiRect.Y + yukiRect.H
    
End

Public Sub Paint(argY As Integer, argPath As String, argIsTrash As Boolean)
    
    Dim yukiMimeType As String = DesktopMime.FromFile(argPath).Type
    Dim yukiTopOffset As Integer
    
    $currentPath = argPath
    
    drawHeader(argY)
    yukiTopOffset = drawProperty(argY)
    
    If yukiMimeType Begins "image" Then yukiTopOffset = drawImageProperties(yukiTopOffset)
    If yukiMimeType = "application/pdf" Then drawPageCount(yukiTopOffset, argPath)
    If argIsTrash Then drawTrashProperty(yukiTopOffset)
    
End

Public Sub _new(argDrawingArea As DrawingArea)
    
    $drawingArea = argDrawingArea
    
End
