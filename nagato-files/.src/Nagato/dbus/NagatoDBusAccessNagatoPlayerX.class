' Gambas class file

Property Read Enabled As Boolean

Public Enum ActionNone, ActionPlay, ActionPause, ActionNext, ActionBack, ActionAddMusics

Private Const ServiceName As String = "org.gambas.nagato.player.x"
Private Const ObjectPath As String = "/org/nagato/player/x"
Private Const InterfaceName As String = "org.gambas.nagato.player.x.nagatointerface"

Private $player As DBusProxy

Private Sub ensureProxy()
    
    If Not Object.IsValid($player) Then $player = DBus[ServiceName][ObjectPath, InterfaceName]
    
End

Private Sub addMusics()
    
    Dialog.Path = Desktop.GetDirectory("MUSIC")
    If Dialog.OpenFile(True) Then Return
    
    $player.AddMusics(Dialog.Paths)
    
End

Public Function GetText() As String
    
    Dim yukiText As String 
    
    If DBus.Session.Applications.Exist(ServiceName) Then
        ensureProxy()
        yukiText &= ($player.Title & Space$(4))
        yukiText &= ($player.Artist & Space$(4))
        yukiText &= ($player.Album & Space$(4))
        yukiText &= ($player.ProgressInText)
        Return yukiText
    Else
        Return "YUKI.N > nagato-player-x is not activated"
    End If
    
End


Public Sub Action(argAction As Integer)
    
    ensureProxy()
    
    Select Case argAction
        Case ActionPlay
            $player.Play()
        Case ActionPause
            $player.Pause()
        Case ActionNext
            $player.Next()
        Case ActionBack
            $player.Back()
        Case ActionAddMusics
            addMusics()
    End Select
    
End

Private Function Enabled_Read() As Boolean

    Return DBus.Session.Applications.Exist(ServiceName)

End
