' Gambas class file

Property Read Activated As Boolean
Property Read CpuUsage As Float
Property Read MemoryUsage As Float
Property Read SwapUsage As Float
Property Read ProcessCount As Integer
Property Read Thermal As Float

Private Const ServiceName As String = "org.gambas.nagato-systemmonitor-daemon"
Private Const ObjectPath As String = "/org/nagato/systemmonitor/daemon"
Private Const InterfaceName As String = "org.gambas.nagato.systemmonitor.daemon.tfei"
Private Const Template As String = (" Processor : &1   Memory : &2   Swap : &3   Processes : &4   Thermal : &5")

Private $proxy As DBusProxy

Public Function GetText() As String
    
    Dim yukiCpuUsage As String = Format$($proxy.CpuUsage, "#0.00%")
    Dim yukiMemoryUsage As String = Format$($proxy.MemoryUsage, "#0.00%")
    Dim yukiSwapUsage As String = Format$($proxy.SwapUsage, "#0.00%")
    Dim yukiProcessCount As String = $proxy.ProcessCount
    Dim yukiThermal As String = Format$(Me.Thermal, "##0.00Â°C")
    
    Return Subst$(Template, yukiCpuUsage, yukiMemoryUsage, yukiSwapUsage, yukiProcessCount, yukiThermal)
    
Catch
    Return "YUKI.N > Please wait ..."
    
End

Public Sub Activate(argId As String)
    
    Shell Subst$("nagato-systemmonitor-daemon --check-in &1", argId)

    If Not DBus.Session.Applications.Exist(ServiceName) Then Return
    If Object.IsValid($proxy) Then Return
    
    $proxy = DBus[ServiceName][ObjectPath, InterfaceName]
    
End

Private Function Activated_Read() As Boolean

    If Not DBus.Session.Applications.Exist(ServiceName) Then Return False
    If Not Object.IsValid($proxy) Then Return False 

    Return True

End

Private Function CpuUsage_Read() As Float

    Return $proxy.CpuUsage

End

Private Function MemoryUsage_Read() As Float

    Return $proxy.MemoryUsage

End

Private Function SwapUsage_Read() As Float

    Return $proxy.SwapUsage

End

Private Function ProcessCount_Read() As Integer

    Return $proxy.ProcessCount

End

Private Function Thermal_Read() As Float

    If $proxy.ThermalCount = 0 Then Return 0

    Return ($proxy.GetTemperature(0)) / 1000

End
