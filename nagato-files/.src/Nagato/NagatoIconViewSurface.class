' Gambas class file

Inherits NagatoIconViewHandler

Public Function getPattern() As String
    
    Return IIf(NagatoSettings.ShowHidden, "*", "[^.]*")
    
End

Private Sub setIcon(argDirectory As String, argSelection As String[], argType As Integer)
    
    Dim yukiPath As String
    Dim yukiFullPath As String
    
    For Each yukiPath In Dir(argDirectory, getPattern(), argType).Sort(gb.Natural + gb.IgnoreCase)
        yukiFullPath = argDirectory &/ yukiPath
        Me._$iconView.Add(yukiFullPath, "", MikuruTiles.Get(yukiFullPath, argSelection.Exist(yukiFullPath)))
    Next
    
End

Private Sub ensureVisible(argPath As String)
    
    If Me._$iconView.Exist(argPath) Then Me._$iconView[argPath].EnsureVisible()
    
End

Private Sub refreshIconView(argDirectory As String, argSelection As String[], argLastSelected As String)
    
    Me._$iconView.Clear()
    setIcon(argDirectory, argSelection, gb.Directory)
    setIcon(argDirectory, argSelection, gb.File)
    NagatoDBThumbnailer.CleanUp(argDirectory)
    ensureVisible(argLastSelected)
    
End

Public Sub ResetIcon(argPath As String, argSelected As Boolean)
    
    Me._$iconView[argPath].Picture = MikuruTiles.Get(argPath, argSelected)
    
End

Public Sub Refresh(argDirectory As String, argSelection As String[], argLastSelected As String)
    
    refreshIconView(argDirectory, argSelection, argLastSelected)
    
End

Public Sub RefreshWithSearchResult(argPaths As String[], argSelection As String[])
    
    Dim yukiPath As String
    
    Me._$iconView.Clear()
    
    For Each yukiPath In argPaths.Sort(gb.IgnoreCase + gb.Natural)
        If IsDir(yukiPath) Then 
            Me._$iconView.Add(yukiPath, "", MikuruTiles.Get(yukiPath, argSelection.Exist(yukiPath)))
        End If
    Next
    
    For Each yukiPath In argPaths.Sort(gb.IgnoreCase + gb.Natural)
        If Not IsDir(yukiPath) Then 
            Me._$iconView.Add(yukiPath, "", MikuruTiles.Get(yukiPath, argSelection.Exist(yukiPath)))
        End If
    Next
    
End
