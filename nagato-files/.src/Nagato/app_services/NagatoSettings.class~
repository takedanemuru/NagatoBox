' Gambas class file

Create Static

Property ShowHidden As Boolean
Property MultipleSelection As Boolean
Property SortBy As Integer
Property SortAscent As Boolean

Private $settings As New Settings

Event ShowHiddenChanged
Event MultipleSelectionChanged

Public Sub LoadFormSettings(argForm As Form)
    
    argForm.X = $settings["form/x", argForm.X]
    argForm.Y = $settings["form/y", argForm.Y]
    argForm.Height = $settings["form/height", argForm.Height]
    argForm.Width = $settings["form/width", argForm.Width]
    
End

Public Sub SaveFormSettings(argForm As Form)
    
    $settings["form/x"] = argForm.X
    $settings["form/y"] = argForm.Y
    $settings["form/height"] = argForm.Height
    $settings["form/width"] = argForm.Width
    
End

Private Function ShowHidden_Read() As Boolean

    Return $settings["view/hidden", False]

End

Private Sub ShowHidden_Write(Value As Boolean)

    If Value <> $settings["view/hidden"] Then
        $settings["view/hidden"] = Value
        Raise ShowHiddenChanged
    End If
    
End

Private Function MultipleSelection_Read() As Boolean

    Return $settings["view/multiple_selection", False]

End

Private Sub MultipleSelection_Write(Value As Boolean)

    If Value <> $settings["view/multiple_selection"] Then
        $settings["view/multiple_selection"] = Value
        Raise MultipleSelectionChanged
    End If

End

Private Function SortBy_Read() As Integer

    Return $settings["sort/by", MikuruSort.ByName]

End

Private Sub SortBy_Write(Value As Integer)

    $settings["sort/by"] = Value

End

Private Function SortAscent_Read() As Boolean

    Return $settings["sort/ascent", True] 

End

Private Sub SortAscent_Write(Value As Boolean)

    $settings["sort/ascent"] = Value

End
