' Gambas class file

Inherits NagatoDesktopTile2

Private $proxy As NagatoDBusAccessNagatoPlayerX
Private $descriptionPainter As NagatoDescriptionPainterStandard

Event RequestRefresh

Private Sub getBaseImage() As Image
    
    Dim yukiBaseImage As Image
    
    If Exist($proxy.GetCoverArtPath()) Then
        yukiBaseImage = Image.Load($proxy.GetCoverArtPath())
        yukiBaseImage = yukiBaseImage.Stretch(MikuruUxTile.TileSize, MikuruUxTile.TileSize)
    Else
        yukiBaseImage = New Image(MikuruUxTile.TileSize, MikuruUxTile.TileSize, Color.Red)
    Endif
    
    Return yukiBaseImage
    
Catch
    yukiBaseImage = New Image(MikuruUxTile.TileSize, MikuruUxTile.TileSize, Color.Red)
    Return yukiBaseImage
    
End

Private Sub paintProgress()
    
    Paint.Brush = Paint.Color(Color.Orange)
        Paint.Rectangle(0, MikuruUxTile.TileSize - 2, MikuruUxTile.TileSize * $proxy.GetRate(), 2)
    Paint.Fill()
    
End

Private Sub getThumbnail() As Image
    
    Dim yukiBaseImage As Image = getBaseImage()
    
    Paint.Begin(yukiBaseImage)
        NagatoLabelPainterFacade.Paint(MikuruTileMode.SpecialPlayerX, "")
        $descriptionPainter.Paint($proxy.GetTextTile())
        paintProgress()
    Paint.End()
    
    Return yukiBaseImage
    
End

Public Sub _OnReload()
    
    Try Me._$iconView.Remove(NagatoDesktopSpecialKey.Music)
    
    If Not NagatoSettingsDesktopTile.ShowMusic Then Return
    
    Me._$iconView.Add(NagatoDesktopSpecialKey.Music, "", getBaseImage().Picture)
    
End

Public Sub _OnTimer() ' override
    
    If Not DBus.Session.Applications.Exist("org.gambas.nagato-player-x") Then
        Try Me._$iconView.Remove(NagatoDesktopSpecialKey.Music)
        If Not Error Then Me._$iconView.Refresh()
    Else If Not Me._$iconView.Exist(NagatoDesktopSpecialKey.Music) Then
        Raise RequestRefresh
    Else
        If Not Object.IsValid($proxy) Then $proxy = New NagatoDBusAccessNagatoPlayerX
        Me._$iconView[NagatoDesktopSpecialKey.Music].Picture = getThumbnail().Picture
    End If
    
End

Public Sub _OptionalOnInitialize()
    
    $descriptionPainter = New NagatoDescriptionPainterStandard(Color.Gray + Color.Transparent * 64)
    
End
