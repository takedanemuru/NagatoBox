' Gambas class file

Inherits NagatoDesktopTile2

Private $proxy As NagatoDBusAccessNagatoPlayerX
Private $descriptionPainter As NagatoDescriptionPainterStandard
Private $coverArt As Image

Private Sub getBaseImage() As Image
    
    Dim yukiCoverArtPath As String = $proxy.GetCoverArtPath()
    
    If $coverArt = Null And If Exist(yukiCoverArtPath) Then
        $coverArt = Image.Load(yukiCoverArtPath).Stretch(MikuruUxTile.TileSize, MikuruUxTile.TileSize)
    Else If Not Exist(yukiCoverArtPath) Then
        $coverArt = New Image(MikuruUxTile.TileSize, MikuruUxTile.TileSize, Color.Pink + 192 * Color.Transparent)
    Else If 2 > DateDiff(Stat(yukiCoverArtPath).LastChange, Now(), gb.Second) Then
        $coverArt = Image.Load(yukiCoverArtPath).Stretch(MikuruUxTile.TileSize, MikuruUxTile.TileSize)
    Endif
    
    Return $coverArt.Copy()

Catch
    Return getNotActivatedImage()
    
End

Private Sub getNotActivatedImage() As Image
    
    Dim yukiBaseImage As Image = MikuruTileBaseImage[Color.Red, 192]
    
    Paint.Begin(yukiBaseImage)
        NagatoLabelPainterFacade.Paint(MikuruTileMode.SpecialPlayerXNotActivated)
        $descriptionPainter.Paint("not activated")
    Paint.End()
    
    Return yukiBaseImage
    
End

Private Sub paintProgress()
    
    Paint.Brush = Paint.Color(Color.Orange)
        Paint.Rectangle(0, MikuruUxTile.TileSize - 2, MikuruUxTile.TileSize * $proxy.GetRate(), 2)
    Paint.Fill()
    
End

Private Sub getThumbnail() As Image
    
    Dim yukiBaseImage As Image = getBaseImage()
    
    Paint.Begin(yukiBaseImage)
        NagatoLabelPainterFacade.Paint(MikuruTileMode.SpecialPlayerX, "")
        $descriptionPainter.Paint($proxy.GetTextTile())
        paintProgress()
    Paint.End()
    
    Return yukiBaseImage
    
End

Public Sub _OnReload()
    
    Try Me._$iconView.Remove(NagatoDesktopKey.Music)
    If Not NagatoSettingsDesktopTile.GetVisible(NagatoDesktopKey.Music) Then Return
    
    Me._$iconView.Add(NagatoDesktopKey.Music, "", getBaseImage().Picture)
    
End

Public Sub _OnTimer() ' override
    
    If Not Me._$iconView.Exist(NagatoDesktopKey.Music) Then
        $coverArt = Null
    Else If Not DBus.Session.Applications.Exist("org.gambas.nagato-player-x") Then
        Try Me._$iconView[NagatoDesktopKey.Music].Picture = getNotActivatedImage().Picture
   Else
        If Not Object.IsValid($proxy) Then $proxy = New NagatoDBusAccessNagatoPlayerX
        Try Me._$iconView[NagatoDesktopKey.Music].Picture = getThumbnail().Picture
    End If
    
End

Public Sub _OptionalOnInitialize()
    
    $descriptionPainter = New NagatoDescriptionPainterStandard(Color.Gray + Color.Transparent * 64)
    
End
