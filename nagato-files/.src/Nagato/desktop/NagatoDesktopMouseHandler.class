' Gambas class file

Inherits NagatoDesktopHandler

Private $iconView As IconView
Private $observer As Observer
Private $selection As NagatoSelection
Private $contextMenu As New NagatoContextMenuDesktop2 As "ContextMenu"

Public Sub _new(argIconView As IconView, argSelection As NagatoSelection)
    
    $iconView = argIconView
    $observer = New Observer($iconView) As "Asakura"
    $selection = argSelection
    
End

Public Sub Asakura_DragMove()
    
    Dim yukiScrollArea As ScrollArea = $iconView.Children[0]
    
    $iconView.UnselectAll()
    If $iconView.FindAt(Drag.X + yukiScrollArea.ScrollX, Drag.Y) Then Return
    If IsDir($iconView.Item.Key) Then $iconView.Item.Selected = True
    
End

Public Sub Asakura_MouseDrag()
    
    If NagatoDesktopSpecialKey.IsSpecialKey($iconView.Key) Then Return
    If $selection.Count = 0 Then Return
    Drag.Icon = $iconView.Current.Picture
    $iconView.Drag(NagatoDrag2.GetUriList($selection.Paths), MikuruMimeType.UriList)
    
Catch
    Return ' avoid error when some object unexpectedlly vanished.
    
End

Public Sub Asakura_MouseDown()
    
     If $iconView.Key = "" Then
        Return
    Else If $iconView.FindAt(Mouse.ScreenX, Mouse.ScreenY) Then
        $iconView.Current.Selected = False
    Else If Not NagatoDesktopSpecialKey.IsSpecialKey($iconView.Key) Then
        $selection.Push($iconView.Key, Mouse.Left)
        If $iconView.Key = MikuruLastRenamed.Path Then MikuruLastRenamed.Clear()
        $iconView[$iconView.Key].Picture = MikuruTile($iconView.Key, $selection.Exist($iconView.Key))
    End If
    
End

Public Sub Asakura_Menu()
    
    Dim yukiPointAtRoot As Boolean = $iconView.FindAt(Mouse.ScreenX, Mouse.ScreenY) 

    $contextMenu.PopUp($iconView.Key, yukiPointAtRoot, $selection.Count)

End

Public Sub ContextMenu_Signal(argEvent As Integer, argValues As Variant[])
    
    Select Case argEvent
        Case NagatoObject.ViewChangeThumbnail
            HaruhiThumbnailSelector.SetThumbnails($selection.LastPath)
            If HaruhiThumbnailSelector.ShowDialog() <> 1 Then Return
            Raise Signal(NagatoObject.ViewReload, [$selection.LastPath])
        Case NagatoObject.CreateArchive
            MikuruCreate.Archive($selection.Paths)
        Case NagatoObject.FileDelete
            NagatoTrash.Delete($selection.Paths)
        Case NagatoObject.FileCopyTo
            MikuruFileManuever.CopyTo($selection.Paths, argValues[0])
        Case NagatoObject.FileMoveTo
            MikuruFileManuever.MoveTo($selection.Paths, argValues[0])
        Case NagatoObject.TagAdd
            NagatoFilerDialogs.Tagging($selection.Paths) 
        Case NagatoObject.TagRemove
            NagatoDBSymbol.RemovePaths($selection.Paths)
        Default
            Raise Signal(argEvent, argValues)
    End Select
    
End
