' Gambas class file

Inherits NagatoDesktopHandler

Private $iconView As IconView
Private $clock As NagatoDesktopClock
Private $trashBin As NagatoDesktopTrashBin 

Private Sub setIcon(argDirectory As String, argSelection As String[], argType As Integer)
    
    Dim yukiPath As String

    For Each yukiPath In MikuruSort(argDirectory, argType)
        If Not Access(yukiPath, gb.Read) Then Continue
        $iconView.Add(yukiPath, "", MikuruTile(yukiPath, argSelection.Exist(yukiPath)))
    Next
    
End

Private Sub initializeIcons(argSelectedPaths As String[])
    
    $clock = New NagatoDesktopClock($iconView)
    setIcon(Desktop.Path, argSelectedPaths, gb.Directory)
    setIcon(Desktop.Path, argSelectedPaths, gb.File)
    $trashBin = New NagatoDesktopTrashBin($iconView)
    
End

Public Sub Refresh(argSelectedPaths As String[])
    
    $clock.Stop()
    $trashBin.Stop()
    
    Object.Detach($clock)
    Object.Detach($trashBin)
    
    $iconView.Clear()
    initializeIcons(argSelectedPaths)
    $iconView.Refresh()

End

Public Sub _new(argIconView As IconView)
    
    $iconView = argIconView
    
    initializeIcons([""])
        
End
