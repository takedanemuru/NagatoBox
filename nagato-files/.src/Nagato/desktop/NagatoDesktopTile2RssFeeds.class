' Gambas class file

Inherits NagatoDesktopTile2

Private $descriptionPainter As NagatoTilePainterDescription
Private $rssFeeds As NagatoDesktopTileContentRssFeeds

Private Sub setBackgroundColor()
    
    If $rssFeeds.HasThumbnail Then
        $descriptionPainter.BackgroundColor = MikuruColor.Transparent(Color.Gray, 192)
    Else
        $descriptionPainter.BackgroundColor = Color.Transparent
    Endif
    
End

Private Function getBaseImage(argTitle As String) As Image
    
    If $rssFeeds.HasThumbnail Then
        Return $rssFeeds.GetThumbnail(argTitle)
    Else
        Return MikuruTileBaseImage[Color.Orange, 192]
    Endif
    
End

Public Function getIcon() As Picture
    
    Dim yukiImage As Image = getBaseImage($rssFeeds.Title)
    
    Paint.Begin(yukiImage)
        setBackgroundColor()
        $descriptionPainter.Paint($rssFeeds.Title)
        NagatoLabelPainterFacade(MikuruTileMode.SpecialRssText)
    Paint.End()
    
    Return yukiImage.Picture
    
Catch
    Return MikuruTileBaseImage[Color.Orange, 128].Picture
    
End

Public Sub _onReload()
    
    Try Me._$iconView.Remove(NagatoDesktopSpecialKey.RssFeeder)
    
    If Not NagatoSettingsDesktopTile.ShowRss Then Return
    
    Me._$iconView.Add(NagatoDesktopSpecialKey.RssFeeder, "", getIcon())
    
End

Public Sub _OnTimer() ' override
    
    If Not Me._$iconView.Exist(NagatoDesktopSpecialKey.RssFeeder) Then Return
    If Not $rssFeeds.EnsureActivation() Then Return
    
    Me._$iconView[NagatoDesktopSpecialKey.RssFeeder].Picture = getIcon()
    
End

Public Sub _OptionalOnInitialize()
    
    $descriptionPainter = New NagatoTilePainterDescription(Color.Transparent)
    $rssFeeds = New NagatoDesktopTileContentRssFeeds
    $rssFeeds.ClearThumbnails()
    
End
