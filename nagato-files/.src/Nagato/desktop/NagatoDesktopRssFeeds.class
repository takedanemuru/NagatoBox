' Gambas class file

Inherits NagatoDesktopSpecialIcon

Private Const TextWidth As Integer = 112 ' TileSize - Margin * 2
Private Const Margin As Integer = 8

Private $iconView As IconView
Private $observer As Observer
Private $feeder As New NagatoDBusAccessRssFeeder
Private $thumbnailer As New NagatoDesktopRssThumbnail
Private $labelPainter As New NagatoPainterPathLabel

Private Function getBaseImage(argTitle As String) As Image
    
    If $thumbnailer.HasThumbnail(argTitle) Then
        Return $thumbnailer.GetThumbnail(argTitle)
    Else
        Return MikuruTileBaseImage[Color.Orange, 128]
    Endif
    
End

Private Sub paintLabel(argText As String)
    
    Dim yukiHeight As Integer = Paint.Font.RichTextHeight(argText, TextWidth) + Margin * 2
    
    Paint.Brush = Paint.Color(MikuruColor.Transparent(Color.Gray, 128))
    Paint.Rectangle(0, NagatoSettingsTile.Size - yukiHeight, NagatoSettingsTile.Size, yukiHeight)
    Paint.Fill()
    
End

Private Sub paintDescription(argText As String)
    
    Dim yukiHeight As Integer 
    Dim yukiY As Integer 
    
    Paint.Font.Size = Application.Font.Size
    Paint.Font.Bold = True
    yukiHeight = Paint.Font.RichTextHeight(argText, TextWidth)
    yukiY = NagatoSettingsTile.Size - yukiHeight - Margin
    Paint.Brush = Paint.Color(Color.White)
    Paint.RichText(argText, Margin, yukiY, TextWidth, yukiHeight, Align.Center)
    Paint.Fill()
    
End

Public Function getIcon() As Picture
    
    Dim yukiTitle As String = $feeder.GetFeedItem()[0]
    Dim yukiImage As Image = getBaseImage(yukiTitle)
    
    Paint.Begin(yukiImage)
        If $thumbnailer.HasThumbnail(yukiTitle) Then paintLabel(yukiTitle)
        paintDescription(yukiTitle)
        Paint.DrawPicture(MikuruIcon.Get("feed-alt", 24), 8, 8)
    Paint.End()
    
    Return yukiImage.Picture
    
Catch
    Return MikuruTileBaseImage[Color.Orange, 128].Picture
    
End

Public Sub Stop()
    
    'Try NagatoDesktopTimer.Stop()
    
End

Public Sub _new(argIconView As IconView)
    
    $iconView = argIconView
    $iconView.Add(NagatoDesktop.SpecialKeyRssFeeder, "", MikuruTileBaseImage[Color.Orange, 128].Picture)
    
    $observer = New Observer(HaruhiDesktop) As "Asakura"
    
End

Public Sub Asakura_Timer()
    
    If Not $iconView.Exist(NagatoDesktop.SpecialKeyRssFeeder) Then Return
    If Not $feeder.Activated Then $feeder.Activate()
    If Not $feeder.Activated Then Return
    
    If Second(Now()) Mod 15 = 0 Then 
        $feeder.Next()
        $thumbnailer.SetUrl($feeder.GetFeedItem())
    End If
    
    $iconView[NagatoDesktop.SpecialKeyRssFeeder].Picture = getIcon()
    
End
