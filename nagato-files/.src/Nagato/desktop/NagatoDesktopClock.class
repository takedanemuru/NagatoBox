' Gambas class file

Inherits NagatoDesktopSpecialIcon

Private Const IconSize As Integer = 128
Private Const FontSize As Integer = 16

Private $iconView As IconView
Private $timer As New NagatoDesktopTimer As "DesktopTimer"

Private Function getBaseImage() As Image
    
    Dim yukiImage As New Image(IconSize, IconSize, Color.Orange + 16777216 * 128)

    Return yukiImage
    
End

Public Function getIcon() As Picture
    
    Dim yukiImage As Image = getBaseImage()
    
    Paint.Begin(yukiImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Size = FontSize
        Paint.RichText(Format$(Now(), NagatoSettingsDesktop.ClockFormat), 0, 0, IconSize, IconSize, Align.Center)
        Paint.Fill()
    Paint.End()
    
    Return yukiImage.Picture
    
End

Public Sub Stop()
    
    Object.Lock($timer)
    
End

Public Sub _new(argIconView As IconView)
    
    $iconView = argIconView
    $iconView.Add(NagatoDesktop.SpecialKeyClock, "", getIcon())
    
End

Public Sub DesktopTimer_Timer()
    
    Try $iconView[NagatoDesktop.SpecialKeyClock].Picture = getIcon()
    
End
