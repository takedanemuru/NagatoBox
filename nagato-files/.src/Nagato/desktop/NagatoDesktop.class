' Gambas class file

Private $iconView As NagatoIconViewInitializer
Private $observer As NagatoDesktopObserverHandler
Private $wallpaper As NagatoDesktopWallpaperHandler
Private $icons As NagatoDesktopIconsHandler
Private $keyboard As NagatoDesktopKeyboardHandler
Private $mouse As NagatoDesktopMouseHandler
Private $drop As NagatoDesktopDropHandler
Private $activation As NagatoDesktopActivationHandler

Public Sub ReloadDesktopWallpaper()
    
    $wallpaper.ReloadDesktopWallpaper()
    
End

Public Sub _new(argContainer As Container)
    
    $iconView = New NagatoIconViewInitializer(argContainer)
    $wallpaper = New NagatoDesktopWallpaperHandler($iconView.IconView)
    $icons = New NagatoDesktopIconsHandler($iconView.IconView)
    $mouse = New NagatoDesktopMouseHandler($iconView.IconView) As "Handler"
    $keyboard = New NagatoDesktopKeyboardHandler($iconView.IconView) As "Handler"
    $drop = New NagatoDesktopDropHandler($iconView.IconView)
    $activation = New NagatoDesktopActivationHandler($iconView.IconView)
    $observer = New NagatoDesktopObserverHandler($iconView.IconView) As "Handler"

End

Public Sub Handler_RequestRefresh()
    
    $icons.Refresh($mouse.SelectedPaths)
    
End

Public Sub Handler_Signal(argEvent As Integer, argValue As Variant)
    
    Select Case argEvent
        Case NagatoObject.ObserverDelete
            $mouse.Delete(Desktop.Path &/ argValue)
            $icons.Refresh($mouse.SelectedPaths)
        Case NagatoObject.ObserverModefy, NagatoObject.ViewForceReload
            $mouse.ClearAll()
            $icons.Refresh($mouse.SelectedPaths)
    End Select
    
End

Public Sub Handler_KeyPressWithControl(argKeyCode As Integer)
    
    Select Case argKeyCode
        Case Key["x"]
            If $mouse.SelectedPaths.Count = 0 Then Return
            Clipboard.Copy(NagatoDrag2.GetUriListCopy($mouse.SelectedPaths), "text/nagato-selection-cut")
        Case Key["c"]
            If $mouse.SelectedPaths.Count = 0 Then Return
            Clipboard.Copy(NagatoDrag2.GetUriListCopy($mouse.SelectedPaths), "text/uri-list")
        Case Key["v"]
            NagatoClipBoard.Paste(Desktop.Path)
    End Select
    
End

Public Sub Handler_KeyPress(argKeyCode As Integer)
    
    Select Case argKeyCode
        Case Key.Enter, 16777220
            ' to do
        Case Key.Esc, 16777216
            Clipboard.Clear()
            $mouse.ClearAll()
            $icons.Refresh($mouse.SelectedPaths)
        Case Key.Delete
            NagatoTrash.Delete($mouse.SelectedPaths)
    End Select
    
End
