' Gambas class file

Private $iconView As NagatoIconViewInitializer
Private $observer As NagatoDesktopObserverHandler
Private $wallpaper As NagatoDesktopWallpaperHandler
Private $icons As NagatoDesktopIconsHandler
Private $keyboard As NagatoDesktopKeyboardHandler
Private $mouse As NagatoDesktopMouseHandler
Private $drop As NagatoDesktopDropHandler
Private $activation As NagatoDesktopActivationHandler

Public Sub ReloadDesktopWallpaper()
    
    $wallpaper.ReloadDesktopWallpaper()
    
End

Public Sub _new(argContainer As Container)
    
    $iconView = New NagatoIconViewInitializer(argContainer)
    $wallpaper = New NagatoDesktopWallpaperHandler($iconView.IconView)
    $icons = New NagatoDesktopIconsHandler($iconView.IconView)
    $mouse = New NagatoDesktopMouseHandler($iconView.IconView) As "Handler"
    $keyboard = New NagatoDesktopKeyboardHandler($iconView.IconView) As "Handler"
    $drop = New NagatoDesktopDropHandler($iconView.IconView)
    $activation = New NagatoDesktopActivationHandler($iconView.IconView)
    $observer = New NagatoDesktopObserverHandler($iconView.IconView) As "Handler"

End

Public Sub Handler_Signal(argEvent As Integer, argValues As Variant[])
    
    Select Case argEvent
        Case NagatoObject.ObserverDelete
            $mouse.Delete(Desktop.Path &/ argValues[0])
        Case NagatoObject.ObserverModefy, NagatoObject.ViewForceReload
            $mouse.ClearAll()
    End Select

    $icons.Refresh($mouse.SelectedPaths)
    
End

Public Sub Handler_KeyPressWithControl(argKeyCode As Integer)
    
    Select Case argKeyCode
        Case Key["x"]
            NagatoClipBoard.Cut($mouse.SelectedPaths)
        Case Key["c"]
            NagatoClipBoard.Copy($mouse.SelectedPaths)
        Case Key["v"]
            NagatoClipBoard.Paste(Desktop.Path)
    End Select
    
End

Public Sub Handler_KeyPress(argKeyCode As Integer)
    
    Select Case argKeyCode
        Case Key.Enter, Key.Space
            ' to do
        Case Key.Esc
            Clipboard.Clear()
            $mouse.ClearAll()
            $icons.Refresh($mouse.SelectedPaths)
        Case Key.Delete
            NagatoTrash.Delete($mouse.SelectedPaths)
    End Select
    
End
