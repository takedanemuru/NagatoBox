' Gambas class file

Inherits NagatoDesktopTile

Private $iconView As IconView
Private $observer As Observer
Private $proxy As New NagatoDBusAccessWeather

Private Function getBaseImage() As Image
    
    Return MikuruTileBaseImage[$proxy.BackgroundColor, 192]
    
End

Private Sub getThumbnail() As Image
    
    Dim yukiThumbnail As Image = MikuruWeatherImage[$proxy.IconId]
    
    Return yukiThumbnail.Stretch(128, 128)
    
End

Public Function getLabelText() As String
    
    Select Case Second(Now())
        Case 0 To 14
            Return $proxy.Condition
        Case 15 To 29
            Return Subst("Temp. : &1 Â°C", $proxy.Temperature)
        Case 30 To 44
            Return Subst("Wind : &1 &2 m", String.Upper($proxy.WindDirection), $proxy.WindSpeed)
        Case 45 To 59
            Return Format$($proxy.Precipitation, "Rain : ##0.0 mm")
    End Select
    
End

Private Sub paintLabel()
    
    Dim yukiLabelText As String = getLabelText()
    Dim yukiHeight As Integer = Paint.Font.RichTextHeight(yukiLabelText, 112) + 16
    Dim yukiRect As New Rect(0, 128 - (yukiHeight), 128, yukiHeight)
    
    Paint.Brush = Paint.Color(Color.White)
    Paint.DrawRichText(yukiLabelText, yukiRect.X, yukiRect.Y, yukiRect.W, yukiRect.H, Align.Center)
    Paint.Fill()
    
End

Private Function getIcon() As Picture
    
    Dim yukiImage As Image = IIf($proxy.Activated, getThumbnail(), getBaseImage())
    
    Paint.Begin(yukiImage)
        Paint.Font.Bold = True
        If $proxy.Activated Then paintLabel()
    Paint.End()

    Return yukiImage.Picture
    
End

Public Sub _new(argIconView As IconView)
    
    $proxy.Activate()

    $iconView = argIconView
    $iconView.Add(NagatoDesktopSpecialKey.Weather, "", getBaseImage().Picture)
    
    $observer = New Observer(HaruhiDesktop) As "Asakura"
    
End

Public Sub Asakura_Timer()
    
    If Not $iconView.Exist(NagatoDesktopSpecialKey.Weather) Then Return
    If Not $proxy.Activated Then Return
    
    $iconView[NagatoDesktopSpecialKey.Weather].Picture = getIcon()
    
End
