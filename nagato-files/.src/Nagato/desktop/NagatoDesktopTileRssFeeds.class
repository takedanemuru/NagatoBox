' Gambas class file

Inherits NagatoDesktopTile

Private Const TextWidth As Integer = 112 ' TileSize - Margin * 2
Private Const Margin As Integer = 8

Private $iconView As IconView
Private $observer As Observer
Private $labelPainter As New NagatoPainterPathLabel
Private $rssFeeds As New NagatoDesktopTileContentRssFeeds

Private Function getBaseImageText() As Image
    
    Dim yukiBaseImage As Image = MikuruTileBaseImage[Color.Orange, 192]
    
    Paint.Begin(yukiBaseImage)
        NagatoLabelPainterFacade.Paint(MikuruTileMode.SpecialRssText)
    Paint.End()
    
    Return yukiBaseImage
    
End

Private Function getBaseImage(argTitle As String) As Image
    
    Return IIf($rssFeeds.HasThumbnail, $rssFeeds.GetThumbnail(argTitle), getBaseImageText())
    
End

Private Sub paintDescriptionShade(argText As String)
    
    Dim yukiHeight As Integer = Paint.Font.RichTextHeight(argText, TextWidth) + Margin * 2
    
    Paint.Brush = Paint.Color(MikuruColor.Transparent(Color.Gray, 128))
    Paint.Rectangle(0, NagatoSettingsTile.Size - yukiHeight, NagatoSettingsTile.Size, yukiHeight)
    Paint.Fill()
    
    Paint.DrawPicture(MikuruIcon("feed-alt"), 4, 4)
    
End

Private Sub paintDescription(argText As String)
    
    Dim yukiHeight As Integer 
    Dim yukiY As Integer 
    
    Paint.Font.Size = Application.Font.Size
    Paint.Font.Bold = True
    yukiHeight = Paint.Font.RichTextHeight(argText, TextWidth)
    yukiY = NagatoSettingsTile.Size - yukiHeight - Margin
    Paint.Brush = Paint.Color(Color.White)
    Paint.RichText(argText, Margin, yukiY, TextWidth, yukiHeight, Align.Center)
    Paint.Fill()
    
End

Public Function getIcon() As Picture
    
    Dim yukiImage As Image = getBaseImage($rssFeeds.Title)
    
    Paint.Begin(yukiImage)
        If $rssFeeds.HasThumbnail Then paintDescriptionShade($rssFeeds.Title)
        paintDescription($rssFeeds.Title)
    Paint.End()
    
    Return yukiImage.Picture
    
Catch
    Return MikuruTileBaseImage[Color.Orange, 128].Picture
    
End

Public Sub Stop()
    
    
    
End

Public Sub _new(argIconView As IconView)
    
    $iconView = argIconView
    $iconView.Add(NagatoDesktopSpecialKey.RssFeeder, "", MikuruTileBaseImage[Color.Orange, 128].Picture)
    
    $observer = New Observer(HaruhiDesktop) As "Asakura"
    
End

Public Sub Asakura_Timer()
    
    If Not $iconView.Exist(NagatoDesktopSpecialKey.RssFeeder) Then Return
    If Not $rssFeeds.EnsureActivation() Then Return
    
    $iconView[NagatoDesktopSpecialKey.RssFeeder].Picture = getIcon()
    
End
