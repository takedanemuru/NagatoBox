' Gambas class file

Inherits NagatoDesktopTile

Private $iconView As IconView
Private $observer As Observer
Private $textRectangle As New Rect

Private $systemMonitor2 As New NagatoDesktopSystemMonitorContent

Private Function getBaseImage() As Image
    
    Dim yukiBaseImage As Image = MikuruTileBaseImage[Color.Blue, 192]
    
    Paint.Begin(yukiBaseImage)
        NagatoLabelPainterFacade.Paint(MikuruTileMode.SpecialSystems)
    Paint.End()
    
    Return yukiBaseImage
    
End

Private Sub setTextRectangle(argLabelText As String)
    
    Dim yukiHeight As Integer = Paint.Font.RichTextHeight(argLabelText, NagatoSettingsTile.Size)
    
    With $textRectangle
        .X = 8
        .Y = NagatoSettingsTile.Size - (yukiHeight + 8)
        .W = NagatoSettingsTile.Size - 16
        .H = yukiHeight
    End With
    
End

Private Sub paintText()
    
    setTextRectangle($systemMonitor2.Label)
    
    With $textRectangle
        Paint.RichText($systemMonitor2.Label, .X, .Y, .W, .H, Align.Left)
        Paint.RichText($systemMonitor2.Content, .X, .Y, .W, .H, Align.Right)
    End With
    
End

Public Function getIcon() As Picture
    
    Dim yukiImage As Image = getBaseImage()
    
    Paint.Begin(yukiImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.Font.Size = Application.Font.Size - 1
        paintText()
        Paint.Fill()
    Paint.End()
    
    Return yukiImage.Picture
    
End

Public Sub Stop()
    
    Object.Lock($observer)
    
End

Public Sub _new(argIconView As IconView)
    
    $iconView = argIconView
    $iconView.Add(NagatoDesktop.SpecialKeySystemMonitor, "", getBaseImage().Picture)
    
    $observer = New Observer(HaruhiDesktop) As "Asakura"
    
End

Public Sub Asakura_Timer()
    
    If Not $iconView.Exist(NagatoDesktop.SpecialKeySystemMonitor) Then Return
    If Not $systemMonitor2.EnsureActivation() Then Return
    
    $iconView[NagatoDesktop.SpecialKeySystemMonitor].Picture = getIcon()
    
End
