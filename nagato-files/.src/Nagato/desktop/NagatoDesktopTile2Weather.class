' Gambas class file

Inherits NagatoDesktopTile2

Private $proxy As NagatoDBusAccessWeather

Private Function getBaseImage() As Image
    
    Return MikuruTileBaseImage[$proxy.BackgroundColor, 192]
    
End

Private Sub getThumbnail() As Image
    
    Dim yukiThumbnail As Image = MikuruWeatherImage[$proxy.IconId]
    
    Return yukiThumbnail.Stretch(MikuruUxTile.TileSize, MikuruUxTile.TileSize)
    
End

Public Function getLabelText() As String
    
    Select Case Second(Now())
        Case 0 To 14
            Return $proxy.Condition
        Case 15 To 29
            Return Subst("Temp. : &1 Â°C", $proxy.Temperature)
        Case 30 To 44
            Return Subst("Wind : &1 &2 m", String.Upper($proxy.WindDirection), $proxy.WindSpeed)
        Case 45 To 59
            Return Format$($proxy.Precipitation, "Rainfall : ##0.0 mm")
    End Select
    
End

Private Sub paintLabel()
    
    Dim yukiLabelText As String = getLabelText()
    Dim yukiHeight As Integer = Paint.Font.RichTextHeight(yukiLabelText, 112) + 16
    Dim yukiRect As New Rect(0, MikuruUxTile.TileSize - (yukiHeight), MikuruUxTile.TileSize, yukiHeight)
    
    NagatoLabelPainterFacade.Paint(MikuruTileMode.SpecialWeather, "Weather")
    Paint.Brush = Paint.Color(MikuruColor.Transparent(Color.Gray, 192))
    Paint.Rectangle(yukiRect.X, yukiRect.Y, yukiRect.W, yukiRect.H)
    Paint.Fill()
    Paint.Brush = Paint.Color(Color.White)
    Paint.DrawRichText(yukiLabelText, yukiRect.X, yukiRect.Y, yukiRect.W, yukiRect.H, Align.Center)
    Paint.Fill()
    
End

Public Function getIcon() As Picture
    
    Dim yukiImage As Image = IIf($proxy.Activated, getThumbnail(), getBaseImage())
    
    Paint.Begin(yukiImage)
        Paint.Font.Bold = True
        If $proxy.Activated Then paintLabel()
    Paint.End()

    Return yukiImage.Picture
    
End

Public Sub _OnReload()
    
    Try Me._$iconView.Remove(NagatoDesktopSpecialKey.Weather)
    
    If Not NagatoSettingsDesktopTile.ShowWeather Then Return
    
    Me._$iconView.Add(NagatoDesktopSpecialKey.Weather, "", getBaseImage().Picture)
    
End

Public Sub _OnTimer() ' override
    
    If Not Me._$iconView.Exist(NagatoDesktopSpecialKey.Weather) Then Return
    If Not $proxy.Activated Then 
        $proxy.Activate()
    Else
        Me._$iconView[NagatoDesktopSpecialKey.Weather].Picture = getIcon()
    End If
    
End

Public Sub _OptionalOnInitialize()
    
    $proxy = New NagatoDBusAccessWeather
    
End
