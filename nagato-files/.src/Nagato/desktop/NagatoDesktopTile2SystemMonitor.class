' Gambas class file

Inherits NagatoDesktopTile2

Private $textRectangle As New Rect

Public Sub getStatus() As String
    
    Dim yukiText As String
    
    yukiText &= Format(NagatoSystemMonitor.CpuUsage, "##0.00%") & "<br>"
    yukiText &= Format(NagatoSystemMonitor.MemoryUsage, "##0.00%") & "<br>"
    yukiText &= Format(NagatoSystemMonitor.SwapUsage, "##0.00%") & "<br>"
    yukiText &= NagatoSystemMonitor.ProcessCount & "<br>"
    yukiText &= Format$(NagatoSystemMonitor.Thermal, "##0.00Â°C")
    
    Return yukiText
    
End

Private Function getLabel() As String
    
    Dim yukiText As String
    
    yukiText &= "CPU<br>"
    yukiText &= "Mem.<br>"
    yukiText &= "Swap<br>"
    yukiText &= "Proc.<br>"
    yukiText &= "Temp."
    
    Return yukiText
    
End

Private Function getBaseImage() As Image
    
    Dim yukiBaseImage As Image = MikuruTileBaseImage[Color.Blue, 192]
    
    Paint.Begin(yukiBaseImage)
        NagatoLabelPainterFacade.Paint(MikuruTileMode.SpecialSystems)
    Paint.End()
    
    Return yukiBaseImage
    
End

Private Sub setTextRectangle(argLabelText As String)
    
    Dim yukiHeight As Integer = Paint.Font.RichTextHeight(argLabelText, NagatoSettingsTile.Size)
    
    With $textRectangle
        .X = 8
        .Y = NagatoSettingsTile.Size - (yukiHeight + 8)
        .W = NagatoSettingsTile.Size - 16
        .H = yukiHeight
    End With
    
End

Private Sub paintText()
    
    Dim yukiLabel As String = getLabel()
    
    setTextRectangle(yukiLabel)
    
    With $textRectangle
        Paint.DrawRichText(yukiLabel, .X, .Y, .W, .H, Align.Left)
        Paint.DrawRichText(getStatus(), .X, .Y, .W, .H, Align.Right)
    End With
    
End

Public Function getIcon() As Picture
    
    Dim yukiImage As Image = getBaseImage()
    
    Paint.Begin(yukiImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.Font.Size = Application.Font.Size - 1
        paintText()
        Paint.Fill()
    Paint.End()
    
    Return yukiImage.Picture
    
End

Public Sub _OnReload()
    
    Try Me._$iconView.Remove(NagatoDesktopKey.SystemMonitor)
    If Not NagatoSettingsDesktopTile.GetVisible(NagatoDesktopKey.SystemMonitor) Then Return
    
    Me._$iconView.Add(NagatoDesktopKey.SystemMonitor, "", getBaseImage().Picture)
    
End

Public Sub _OnTimer() ' override
    
    If Not Me._$iconView.Exist(NagatoDesktopKey.SystemMonitor) Then Return

    Try Me._$iconView[NagatoDesktopKey.SystemMonitor].Picture = getIcon()
    
End
