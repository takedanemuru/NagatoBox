' Gambas class file

Inherits NagatoDesktopTile2

Private $textRectangle As New Rect

Private Function getBaseImage() As Image
    
    Dim yukiBaseImage As Image = MikuruTileBaseImage[Color.Blue, 192]
    
    Paint.Begin(yukiBaseImage)
        NagatoLabelPainterFacade.Paint(MikuruTileMode.SpecialSystems)
    Paint.End()
    
    Return yukiBaseImage
    
End

Private Sub setTextRectangle(argLabelText As String)
    
    Dim yukiHeight As Integer = Paint.Font.RichTextHeight(argLabelText, NagatoSettingsTile.Size)
    
    With $textRectangle
        .X = 8
        .Y = NagatoSettingsTile.Size - (yukiHeight + 8)
        .W = NagatoSettingsTile.Size - 16
        .H = yukiHeight
    End With
    
End

Private Sub paintText()
    
    setTextRectangle(NagatoSystemMonitorFacade.Label)
    
    With $textRectangle
        Paint.DrawRichText(NagatoSystemMonitorFacade.Label, .X, .Y, .W, .H, Align.Left)
        Paint.DrawRichText(NagatoSystemMonitorFacade.Content, .X, .Y, .W, .H, Align.Right)
    End With
    
End

Public Function getIcon() As Picture
    
    Dim yukiImage As Image = getBaseImage()
    
    Paint.Begin(yukiImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.Font.Size = Application.Font.Size - 1
        paintText()
        Paint.Fill()
    Paint.End()
    
    Return yukiImage.Picture
    
End

Public Sub _onReload()
    
    Try Me._$iconView.Remove(NagatoDesktopKey.SystemMonitor)
    If Not NagatoSettingsDesktopTile.GetVisible(NagatoDesktopKey.SystemMonitor) Then Return
    
    Me._$iconView.Add(NagatoDesktopKey.SystemMonitor, "", getBaseImage().Picture)
    
End

Public Sub _OnTimer() ' override
    
    If Not Me._$iconView.Exist(NagatoDesktopKey.SystemMonitor) Then Return
    If Not NagatoSystemMonitorFacade.EnsureActivation("nagato-desktop") Then Return

    Try Me._$iconView[NagatoDesktopKey.SystemMonitor].Picture = getIcon()
    
End
