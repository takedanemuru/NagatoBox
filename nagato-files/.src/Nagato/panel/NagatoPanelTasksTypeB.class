' Gambas class file

Inherits NagatoPanelTasks

Private $windows As NagatoDesktopWindowsTypeB

Public Sub _Refresh()
    
    $windows.Refresh(Me._$drawingArea.W)
    
    Me._$drawingArea.Refresh()
    
End

Public Sub _new(argContainer As Container, argExpand As Boolean)
    
    $windows = New NagatoDesktopWindowsTypeB
    
    Me._Initialize(argContainer, argExpand)
    
End

Private Function getTaskRect(argDesktopIndex As Integer, argWindowIndex As Integer, argWidthPerTask As Integer) As Rect
    
    Dim yukiRect As New Rect
    
    With yukiRect
        .X = $windows.WidthPerDesktop * argDesktopIndex + argWidthPerTask * argWIndowIndex
        .Y = 0
        .H = Me._$drawingArea.H
        .W = argWidthPerTask
    End With
    
    Return yukiRect
    
End

Private Sub paintTask(argDesktopIndex As Integer, argWindowIndex As Integer, argTaskWidth As Integer)
    
    Dim yukiDesktopWindow As DesktopWindow = $windows.GetDesktopWindow(argDesktopIndex, argWindowIndex)
    Dim yukiRect As Rect = getTaskRect(argDesktopIndex, argWindowIndex, argTaskWidth)

    Paint.Brush = Paint.Color(Color.White)
    Me._PaintIcon(yukiDesktopWindow, yukiRect)
    Me._PaintText(yukiDesktopWindow, yukiRect)
    Paint.Fill()
    
End

Private Sub paintDesktop(argDesktopIndex As Integer, argWindowsCount As Integer)
    
    Dim yukiWindowIndex As Integer
    Dim yukiWidthPerTask As Integer = $windows.GetTaskWidth(argDesktopIndex)
    
    If argWindowsCount = 0 Then Return
    
    For yukiWindowIndex = 0 To argWindowsCount - 1
        paintTask(argDesktopIndex, yukiWindowIndex, yukiWidthPerTask)
    Next
    
End

Public Sub ClockDrawingArea_Draw()

    Dim yukiIndex As Integer

    Paint.Begin(Me._$drawingArea)
        Paint.Font.Bold = True
        For yukiIndex = 0 To Desktop.Count - 1
            paintDesktop(yukiIndex, $windows.GetWindowsCountPerDesktop(yukiIndex))
        Next
    Paint.End()
    
End

Public Sub ClockDrawingArea_MouseDown()
    
    $windows.ActivateWindowByMouseX(Mouse.X)
    
End
