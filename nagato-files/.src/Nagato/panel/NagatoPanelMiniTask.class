' Gambas class file

Property Visible As Boolean

Private $container As HBox
Private $observer As Observer
Private $windows As New NagatoDesktopWindowsTypeC

Private Function setIcon(argDesktopWindow As DesktopWindow) As Boolean
    
    Dim yukiIcon As New MenuButton($container) As "TaskIcon"
    
    With yukiIcon
        .Border = False
        If argDesktopWindow.Icon Then .Picture = argDesktopWindow.Icon.Stretch(16, 16).Picture
        If Not argDesktopWindow.Icon Then .Picture = MikuruIcon.Get("question-balloon")
        .Arrow = False
        .W = $container.H
        .Tooltip = argDesktopWindow.Name
        .Tag = argDesktopWindow.Id
    End With
    
Finally
    Return Not Error
    
End

Private Function setIcons() As Integer
    
    Dim yukiDesktopWindow As DesktopWindow
    Dim yukiIndex As Integer
    Dim yukiWidth As Integer = $container.H
    
    For yukiIndex = 0 To NagatoDesktopWatcher2.Max
        yukiDesktopWindow = NagatoDesktopWatcher2[yukiIndex]
        If yukiDesktopWindow.SkipTaskbar Then Continue
        If setIcon(yukiDesktopWindow) Then yukiWidth += $container.H
    Next
    
    Return yukiWidth
    
End

Private Sub refresh()
    
    $container.Children.Clear()
    $container.W = setIcons()
    
End

Public Sub _new(argContainer As Container)
    
    $observer = New Observer(NagatoDesktopWatcher2) As "Asakura"
    
    $container = New HBox(argContainer)
    $container.Visible = False
    
End

Public Sub Asakura_ListChanged()
    
    If $container.Visible Then refresh()
    
Catch ' if $container is invalid object
    Return
    
End

Public Sub TaskIcon_Click()
    
    Dim yukiDesktopWindow As New DesktopWindow(Last.Tag)
    
    Desktop.Current = yukiDesktopWindow.Desktop
    yukiDesktopWindow.Activate()
    
Catch
    Return 
    
End

Private Function Visible_Read() As Boolean

    Return $container.Visible

End

Private Sub Visible_Write(Value As Boolean)

    $container.Visible = Value

    Me.Asakura_ListChanged()

End
