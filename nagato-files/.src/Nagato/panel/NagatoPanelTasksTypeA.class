' Gambas class file

Inherits NagatoPanelTasks

Private $windows As NagatoDesktopWindowsTypeA

Public Sub activateWindow(argX As Integer)
    
    Dim yukiIndex As Integer = argX Div $windows.Width
    
    Try Desktop.Current = $windows[yukiIndex].Desktop
    Try $windows[yukiIndex].Activate()
    
End

Private Function getTaskRect(argWindowIndex As Integer) As Rect
    
    Dim yukiRect As New Rect
    
    With yukiRect
        .X = $windows.Width * argWindowIndex
        .Y = 0
        .H = Me._$drawingArea.H
        .W = $windows.Width
    End With
    
    Return yukiRect
    
End

Private Sub paintTask(argWindowIndex As Integer)
    
    Dim yukiRect As Rect = getTaskRect(argWindowIndex)
    Dim yukiDesktopWindow As DesktopWindow = $windows[argWindowIndex]

    Paint.Brush = Paint.Color(NagatoSettingsPanel.Foreground)
    Me._PaintIcon(yukiDesktopWindow, yukiRect)
    Me._PaintText(yukiDesktopWindow, yukiRect)
    Paint.Fill()
    
End

Public Sub _Refresh()
    
    $windows.Refresh(Me._$drawingArea.W)
    
    Me._$drawingArea.Refresh()
    
End

Public Sub _Show()
    
    Me._Refresh()
    
End

Public Sub _new(argContainer As Container, argExpand As Boolean)
    
    $windows = New NagatoDesktopWindowsTypeA
    
    Me._Initialize(argContainer, argExpand)
    
End

Public Sub ClockDrawingArea_Draw()

    Dim yukiIndex As Integer

    Paint.Begin(Me._$drawingArea)
    Paint.Font.Bold = True
    For yukiIndex = 0 To $windows.Max
        paintTask(yukiIndex)
    Next
    Paint.End()
    
End

Public Sub ClockDrawingArea_MouseDown()
    
    Dim yukiDesktopWindow As DesktopWindow
    
    If Mouse.Right Then 
        yukiDesktopWindow = $windows.GetDesktopWindowByMouseX(Mouse.X)
        Me._PopUpContextMenu(yukiDesktopWindow)
    End If
    
    If Mouse.Left Then activateWindow(Mouse.X)
    
End
