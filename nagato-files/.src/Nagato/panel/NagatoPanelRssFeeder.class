' Gambas class file

Inherits NagatoDesktopPanelContent

Private $rssFeeder As NagatoDBusAccessRssFeeder
Private $cursorCount As NagatoRssText
Private $text As String
Private $timer As Timer 

Private Sub setNewText()
    
    $text = $rssFeeder.GetDescription()
    $cursorCount.NewText($text, $rssFeeder.TitleLength)
    
End

Private Sub setFeedNext()
    
    $rssFeeder.Next()
    setNewText()
    
End

Private Function getText() As String
    
    If $text = "" Then setNewText()
    If $cursorCount.SetCountNext() Then setFeedNext()

    Return $cursorCount.Text
    
End

Private Sub initializeRssTimer()
    
    $timer = New Timer As "RssTimer"
    
    With $timer
        .Delay = 100
        .Enabled = True
    End With
    
End

Public Sub _new(argContainer As Container, argExpand As Boolean)
    
    $cursorCount = New NagatoRssText
    $rssFeeder = New NagatoDBusAccessRssFeeder
    Me._Initialize(argContainer, argExpand)
    initializeRssTimer()
    
End

Public Sub ClockDrawingArea_Draw()

    Paint.Begin(Me._$drawingArea)
        Paint.Brush = Paint.Color(NagatoSettingsPanel.Foreground)
        Paint.Font.Bold = True
        Paint.Text(getText(), 0, 0, Me._$drawingArea.W, Me._$drawingArea.H, Align.Left)
        Paint.Fill()
    Paint.End()
    
End

Public Sub ClockDrawingArea_MouseDown()
    
    
    
End

Public Sub RssTimer_Timer()
    
    If Not Me.Visible Then Return
    
    $rssFeeder.Activate()
    
    Me._$drawingArea.Refresh()
    
End
