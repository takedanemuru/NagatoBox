' Gambas class file

Inherits NagatoPanelContentCore

Private Const BoxSize As Integer = 22

Private $drawingArea As DrawingArea
Private $watcher As New NagatoDesktopWatcher As "DesktopWatcher"
Private $painter As New NagatoPagerPainter

Private Function getPagerWidth() As Integer
    
    Return BoxSize * Desktop.Count + 4
    
End

Private Sub popUpContextMenu()
    
    Dim yukiMenu As New NagatoContextMenuPanelPager(HaruhiDesktop, "")
    
    yukiMenu.Popup()
    
End

Private Sub refreshAll()
    
    If Not Object.IsValid($drawingArea) Then Return
    
    $drawingArea.W = getPagerWidth()
    $drawingArea.Refresh()
    
End

Public Sub ForceReload()
    
    $drawingArea.Background = NagatoSettingsPanel.Background
    refreshAll()
    
End

Public Sub _new(argContainer As Container, argExpand As Boolean)
    
    $drawingArea = New DrawingArea(argContainer) As "ClockDrawingArea"
    
    With $drawingArea
        .Expand = argExpand
        .Background = NagatoSettingsPanel.Background
        .Width = getPagerWidth()
    End With
    
End

Public Sub ClockDrawingArea_Draw()

    Paint.Begin($drawingArea)
        $painter.Paint()
    Paint.End()
    
End

Public Sub ClockDrawingArea_MouseDown()
    
    If Mouse.Left Then Desktop.Current = (Mouse.X Div BoxSize)
    If Mouse.Right Then popUpContextMenu()
    
End

Public Sub DesktopWatcher_Changed()
    
    refreshAll()
    
End
