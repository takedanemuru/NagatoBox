' Gambas class file

Inherits NagatoMenuPipe

Property Read Path As String
Property Read DirectoryPath As String
Property Read FilePath As String

Private $pathBar As ButtonBox

Event Changed

Private Sub initializeButtonBox(argContainer As Container)
    
    $pathBar = New ButtonBox(argContainer) As "PathBar"
    
    With $pathBar
        .Expand = True
        .Picture = MikuruIcon.Get("button-rotate-ccw")
    End With
    
End

Public Sub SetPath(argPath As String)
    
    If Not Exist(argPath) Then Return
    
    Object.Lock($pathBar)
        $pathBar.Text = argPath
    Object.Unlock($pathBar)
    
End

Public Sub SetUnexistPath(argPath As String)
    
    Object.Lock($pathBar)
        $pathBar.Text = argPath
    Object.Unlock($pathBar)
    
End

Public Sub _new(argContainer As Container)
    
    initializeButtonBox(argContainer)
    
End

Public Sub PathBar_Change()
    
    If NagatoSettings.PathbarAutomaticSearch Then Raise Changed
    
End

Public Sub PathBar_Activate()
    
    Raise Changed
    
End

Public Sub PathBar_Click()
    
    PathBar_Activate()
    
End

Private Function Path_Read() As String

    Return $pathBar.Text

End

Private Function DirectoryPath_Read() As String

    Dim yukiPath As String = $pathBar.Text

    If Exist(yukiPath) And If IsDir(yukiPath) Then Return yukiPath
    If Exist(File.Dir(yukiPath)) And If IsDir(File.Dir(yukiPath)) Then Return File.Dir(yukiPath)

    Return ""

End

Private Function FilePath_Read() As String

    If Me.DirectoryPath = "" Then Return
    If Exist($pathBar.Text) And If IsDir($pathBar.Text) Then Return ""

    Return File.Name($pathBar.Text)

End
