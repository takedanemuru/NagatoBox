' Gambas class file

Inherits NagatoIconViewHandler

Public Function getPattern() As String
    
    Return IIf(NagatoSettings.ShowHidden, "*", "[^.]*")
    
End

Private Sub setIcon(argDirectory As String, argSelection As String[], argType As Integer)
    
    Dim yukiPath As String

    For Each yukiPath In MikuruSort(argDirectory, argType)
        If Not Access(yukiPath, gb.Read) Then Continue
        Me._$iconView.Add(yukiPath, "", MikuruTiles.Get(yukiPath, argSelection.Exist(yukiPath)))
    Next
    
End

Private Sub ensureVisible(argPath As String)
    
    If Me._$iconView.Exist(argPath) Then Me._$iconView[argPath].EnsureVisible()
    
End

Private Sub getNotFoundTile() As Picture
    
    Dim yukiImage As New Image(128, 128, Color.DarkGreen + 16777216 * 128)
    Dim yukiMessage As String = ("File and Directory not found")
    Dim yukiHeight As Integer
    
    Paint.Begin(yukiImage)
        Paint.Brush = Paint.Color(Color.White)
        Paint.DrawPicture(MikuruIcon.Get("exclamation", 48), 40, 16)
        Paint.Font.Bold = True
        yukiHeight = Paint.Font.RichTextHeight(yukiMessage, 112)
        Paint.RichText(yukiMessage, 0, 128 - yukiHeight - 8, 128, yukiHeight + 8, Align.Center)
        Paint.Fill()
    Paint.End()
    
    Return yukiImage.Picture
    
End

Private Sub refreshIconView(argDirectory As String, argSelection As String[], argLastSelected As String)
    
    Me._$iconView.Clear()
    setIcon(argDirectory, argSelection, gb.Directory)
    setIcon(argDirectory, argSelection, gb.File)
    If Me._$iconView.Count = 0 Then Me._$iconView.Add("NOTFOUND", "", getNotFoundTile())
    NagatoDBThumbnailer.CleanUp(argDirectory)
    ensureVisible(argLastSelected)
    
End

Private Sub setIcons(argPaths As String[], argSelection As String[], argOnlyDirectory As Boolean)
    
    Dim yukiPath As String
    
    For Each yukiPath In argPaths.Sort(gb.IgnoreCase + gb.Natural)
        If IsDir(yukiPath) = argOnlyDirectory Then 
            Me._$iconView.Add(yukiPath, "", MikuruTiles.Get(yukiPath, argSelection.Exist(yukiPath)))
        End If
    Next
    
End

Public Sub ResetIcon(argPath As String, argSelected As Boolean)
    
    Me._$iconView[argPath].Picture = MikuruTiles.Get(argPath, argSelected)
    
End

Public Sub Refresh(argDirectory As String, argSelection As String[], argLastSelected As String)
    
    refreshIconView(argDirectory, argSelection, argLastSelected)
    
End

Public Sub RefreshWithSearchResult(argPaths As String[], argSelection As String[])
    
    Me._$iconView.Clear()
    
    setIcons(argPaths, argSelection, True)
    setIcons(argPaths, argSelection, False)
    
End
