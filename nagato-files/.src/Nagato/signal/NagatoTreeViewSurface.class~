' Gambas class file

Inherits NagatoTreeViewHandler

Private Const DummyKey As String = "dummy"
Private Const TreeViewIconSize As Integer = 24

Private Sub setFile(argDirectory As String)
    
    Dim yukiPath As String
    Dim yukiFullPath As String
    
    For Each yukiPath In MikuruPath.GetChildrenFile(argDirectory, NagatoSettings.ShowHidden)
        yukiFullPath = argDirectory &/ yukiPath
        Try Me._$treeView.Add(yukiFullPath, yukiPath, MikuruIconMono.GetFromPath(yukiFullPath), argDirectory)
    Next
    
End

Private Function getTreeviewIconSymbol(argDirectory As String) As Picture
    
    Dim yukiSymbolName As String = NagatoDBSymbol.GetSymbolName(argDirectory)
    
    Return MikuruIcon.Get(IIf(yukiSymbolName = "", "folder", yukiSymbolName), TreeViewIconSize)
    
    ' If yukiSymbolName = "" Then
    '     Return MikuruIcon.Get("folder", TreeViewIconSize)
    ' Else
    '     Return MikuruIcon.Get(yukiSymbolName, TreeViewIconSize)
    ' End If
    
End

Private Function getDirectoryIcon(argDirectory As String) As Picture
    
    If NagatoXdg.IsUserDir(argDirectory) Then
        Return MikuruIcon.GetDirectoryMono(argDirectory, TreeViewIconSize)
    Else
        Return getTreeviewIconSymbol(argDirectory)
    Endif
    
End

Private Sub setDummyDirectory(argDirectory As String, argParent As String)
    
    Try Me._$treeView.Add(argDirectory, File.Name(argDirectory), getDirectoryIcon(argDirectory), argParent)
    
    If Dir(argDirectory, "*").Count > 0 Then
        Try Me._$treeView.Add(argDirectory & DummyKey, DummyKey,, argDirectory)
    Endif
    
End

Private Sub setHomeDirectory(argDirectory As String)
    
    Dim yukiDirectoryName As String = File.Name(argDirectory)
    
    If Not Me._$treeView.Exist(argDirectory) Then
        Me._$treeView.Add(argDirectory, yukiDirectoryName, MikuruIcon.Get("home", TreeViewIconSize))
    Endif
    
End

Private Sub setSubDirectory(argDirectory As String)
    
    Dim yukiPath As String
    
    For Each yukiPath In MikuruPath.GetChildrenDirectory(argDirectory, NagatoSettings.ShowHidden)
        setDummyDirectory(argDirectory &/ yukiPath, argDirectory)
    Next
    
End

Public Sub SetDirectory(argDirectory As String)
    
    setHomeDirectory(argDirectory)
    setSubDirectory(argDirectory)
    setFile(argDirectory)
    
End

Public Sub SetChildDirectory(argDirectory As String)
    
    Try Me._$treeView.Remove(argDirectory & DummyKey)
    
    setSubDirectory(argDirectory)
    setFile(argDirectory)
    
End
