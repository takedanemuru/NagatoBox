' Gambas class file

Inherits NagatoSignalWidget

Private $hBox As HBox
Private $pathBox As ButtonBox
Private $name As String
Private $userSettingPath As String

Event Changed

Private Sub initializeHBox(argContainer As Container)
    
    $hBox = New HBox(argContainer)
    
    With $hBox
        .Height = 48
        .Padding = 6
    End With
    
End

Private Sub setLabel(argPath As String)
    
    Dim yukiLabel As New Label($hBox)
    
    With yukiLabel
        .Expand = True
        .Text = NagatoXdg.GetName(argPath)
        .Font.Bold = True
    End With
    
    $name = yukiLabel.Text
    
End

Private Sub setButtonBox(argPath As String)
    
    $pathBox = New ButtonBox($hBox) As "ButtonBox"
    
    With $pathBox
        .W = HaruhiXdgConfig.W / 2 
        .Picture = NagatoXdg.GetIcon(argPath)
        .Text = Replace$(argPath, User.Home, "~")
        .ReadOnly = True
        .Tooltip = ("Click to change")
    End With
    
End

Public Sub _InitializeWidgets()
    
    initializeHBox(Me._$container)
    setLabel(Me._$path)
    setButtonBox(Me._$path)
    
End

Public Sub Apply()
    
    Debug "try apply change"
    
    If Me._$path = $userSettingPath Then Return
    
    Exec ["xdg-user-dirs-update", "--set", $name, $userSettingPath]
    
End

Public Sub Reset()
    
    $userSettingPath = Me._$path
    
End

Public Sub ButtonBox_Click()
    
    Dialog.Path = User.Home
    
    If Dialog.SelectDirectory() Then Return
    If Dialog.Path = Me._$path Then Return
    
    $pathBox.Text = Replace$(Dialog.Path, User.Home, "~")
    $userSettingPath = Dialog.Path
    
    Raise Signal(MikuruSignal.DialogChanged, Null)
    
End
