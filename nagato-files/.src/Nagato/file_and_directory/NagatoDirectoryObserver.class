' Gambas class file

Property Directory As String

Private $directory As String
Private $timer As New Timer As "Timer"
Private $lastObserved As Date
Private $previousPaths As String[]

Event Deleted(argFileName As String)
Event Modefied(argFileName As String)
Event Lost

Private Sub checkDeletedPath(argCurrentPaths As String[])
    
    Dim yukiPath As String
    
    If Not Object.IsValid($previousPaths) Then Return

    For Each yukiPath In $previousPaths
         If Not argCurrentPaths.Exist(yukiPath) Then Raise Deleted(yukiPath)
    Next
    
End

Private Sub withinLastObserved(argPath As String) As Boolean
    
    Dim yukiLastChanged As Date = Stat($directory &/ argPath).LastChange
    
    Return DateDiff(yukiLastChanged, $lastObserved, gb.Second) > ($timer.Delay / 1000)
    
End

Private Sub checkModefiedPath(argCurrentPaths As String[])
    
    Dim yukiPath As String
    
    For Each yukiPath In argCurrentPaths
        If Not Exist($directory &/ yukiPath) Then Continue
        If Not withinLastObserved(yukiPath) Then Raise Modefied(yukiPath)
    Next
    
End

Public Sub _new(Optional argInterval As Integer = 1000)

    $directory = User.Home
    $lastObserved = Now
    $timer.Delay = argInterval
    $timer.Start()

End

Public Sub Timer_Timer()
    
    Dim yukiCurrentPaths As String[] 
    
    If Not Exist($directory) Then
        Raise Lost
    Else
        yukiCurrentPaths = Dir($directory)
        checkDeletedPath(yukiCurrentPaths)
        checkModefiedPath(yukiCurrentPaths)
        $lastObserved = Now
        $previousPaths = yukiCurrentPaths
    Endif
    
End

Private Function Directory_Read() As String

    Return $directory

End

Private Sub Directory_Write(Value As String)

    $directory = Value

End
