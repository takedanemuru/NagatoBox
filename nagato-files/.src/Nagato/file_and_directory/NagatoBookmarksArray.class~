' Gambas class file

Create Static

Property Read PathGtk3Bookmarks As String
Property Read PathGtk2Bookmarks As String
Property Read List As String[]

Private $bookmarkList As New String[]

Private Sub addBookmarkList(argPath As String)
    
    Dim yukiFile As File
    Dim yukiLine As String
    
    If Not Exist(argPath) Then Return
    
    yukiFile = Open argPath For Input

    While Not Eof(yukiFile)
        Line Input #yukiFile, yukiLine
        yukiLine = MikuruPath.UrlToPath(yukiLine)
        If Exist(yukiLine) And If Not $bookmarkList.Exist(yukiLine) Then $bookmarkList.Add(yukiLine)
    Wend
    
    Close #yukiFile
    
End

Public Sub AddBookmark(argPath As String)
    
    Dim yukiBookmarks As String = File.Load(Me.PathGtk3Bookmarks)
    Dim yukiNewBookmark As String = MikuruPath.PathToUri(argPath)
    
    Try $bookmarkList.Add(argPath)
    If Not Error Then File.Save(Me.PathGtk3Bookmarks, yukiBookmarks & yukiNewBookmark)
    
End

Public Sub RemovePath(argPath As String)
    
    Try $bookmarkList.Remove($bookmarkList.Find(argPath))
    
End

Public Sub _new()
    
    addBookmarkList(Me.PathGtk2Bookmarks) 'gtk-2.0 bookmarks
    addBookmarkList(Me.PathGtk3Bookmarks)
    
End

Private Function List_Read() As String[]

    Return $bookmarkList.Sort(gb.IgnoreCase)

End

Private Function PathGtk3Bookmarks_Read() As String

    Return Desktop.ConfigDir &/ "gtk-3.0/bookmarks"

End

Private Function PathGtk2Bookmarks_Read() As String

    Return User.Home &/ ".gtk-bookmarks"

End
