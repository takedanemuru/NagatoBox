' Gambas class file

Create Static

Public Struct Directories 
    All As String[]
    Desktop As String
    Documents As String
    Download As String
    Music As String
    Pictures As String
    PublicShare As String
    Templetes As String
    Videos As String
End Struct

Public UserDirs As New Directories

Private Function getDir(argType As String) As String
    
    Dim yukiResult As String
    
    Shell Subst$("xdg-user-dir &1", argType) To yukiResult
    
    Return Replace$(yukiResult, "\n", "")
    
End

Private Sub initializeDirData()
    
    With UserDirs
        .Desktop = getDir("DESKTOP")
        .Documents = getDir("DOCUMENTS")
        .Download = getDir("DOWNLOAD")
        .Music = getDir("MUSIC")
        .Pictures = getDir("PICTURES")
        .PublicShare = getDir("PUBLICSHARE")
        .Templetes = getDir("TEMPLATES")
        .Videos = getDir("VIDEOS")
    End With
    
End

Public Sub initializeDirArray()
    
    With UserDirs
        .All = [.Desktop, .Documents, .Download, .Music, .Pictures, .PublicShare, .Templetes, .Videos]
    End With
    
End

Public Sub _new()
    
    initializeDirData()
    initializeDirArray()
    
End

Public Function GetIcon(argPath As String) As Picture
    
    Select Case argPath
        Case NagatoXdg.UserDirs.Desktop
            Return MikuruIcon("desktop")
        Case NagatoXdg.UserDirs.Documents
            Return MikuruIcon("document-edit")
        Case NagatoXdg.UserDirs.Download
            Return MikuruIcon("hard-drive-download")
        Case NagatoXdg.UserDirs.Music
            Return MikuruIcon("music")
        Case NagatoXdg.UserDirs.Pictures
            Return MikuruIcon("image")
        Case NagatoXdg.UserDirs.PublicShare
            Return MikuruIcon("users")
        Case NagatoXdg.UserDirs.Templetes
            Return MikuruIcon("ruler")
        Case NagatoXdg.UserDirs.Videos
            Return MikuruIcon("video")
        Default
            Return Null
    End Select
    
End

Public Function GetName(argPath As String) As String
    
    Select Case argPath
        Case NagatoXdg.UserDirs.Desktop
            Return "DESKTOP"
        Case NagatoXdg.UserDirs.Documents
            Return "DOCUMENTS"
        Case NagatoXdg.UserDirs.Download
            Return "DOWNLOAD"
        Case NagatoXdg.UserDirs.Music
            Return "MUSIC"
        Case NagatoXdg.UserDirs.Pictures
            Return "PICTURES"
        Case NagatoXdg.UserDirs.PublicShare
            Return "PUBLICSHARE"
        Case NagatoXdg.UserDirs.Templetes
            Return "TEMPLATES"
        Case NagatoXdg.UserDirs.Videos
            Return "VIDEOS"
        Default
            Return ""
    End Select
    
End

Public Function IsUserDir(argPath As String) As Boolean
    
    Return UserDirs.All.Exist(argPath)
    
End
