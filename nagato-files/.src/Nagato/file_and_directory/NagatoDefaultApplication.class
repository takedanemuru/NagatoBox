' Gambas class file

Property Read NotFound As Boolean
Property Read DesktopFile As DesktopFile
Property Read Name As String

Private $notFound As Boolean = True
Private $desktopFile As DesktopFile

Private Function getDesktopFilePath(argDesktopFileName As String) As String
    
    Return MikuruApplications.GlobalApplicationDirectory &/ argDesktopFileName

End

Private Sub setDesktopFile(argFilePath As String) As Boolean
    
    Dim yukiDesktopFiles As String[] = MikuruApplications.GetDefaultDesktopFiles(argFilePath)
    Dim yukiDesktopFile As DesktopFile
    
    If yukiDesktopFiles.Count = 0 Then Return False

    Try yukiDesktopFile = New DesktopFile(getDesktopFilePath(yukiDesktopFiles[0]))
    If Error Then Return False
    
    $desktopFile = yukiDesktopFile
    Return True
    
End

Public Sub _new(argFilePath As String)
    
    If Not setDesktopFile(argFilePath) Then Return
    
    $notFound = False
    
End

Private Function NotFound_Read() As Boolean

    Return $notFound

End

Private Function DesktopFile_Read() As DesktopFile

    Return $desktopFile

End

Private Function Name_Read() As String

    If $notFound Then
        Return ""
    Else
        Return File.Name($desktopFile.Name) & ".desktop"
    Endif

End
