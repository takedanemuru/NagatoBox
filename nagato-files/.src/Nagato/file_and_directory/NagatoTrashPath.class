' Gambas class file

Property Read Files As String
Property Read Info As String

Private Function getDecodedPath(argPath As String) As String
    
    Return MikuruPath.GetSafePathName(URL.Decode(Replace(argPath, "Path=", "")))
    
End

Public Function GetTrashInfoPath(argPath As String) As String
    
    Return Quote(Replace$(argPath, Me.Files, Me.Info) & ".trashinfo")
    
End

Public Function GetPathToRestore(argPath As String) As String
    
    Dim yukiInfoPath As String = UnQuote(Me.GetTrashInfoPath(argPath))
    Dim yukiStream As Stream
    Dim yukiLine As String
    Dim yukiResult As String = ""
    
    If Not Exist(yukiInfoPath) Then Return ""
    
    yukiStream = Open yukiInfoPath For Input

    While Not Eof(yukiStream)
        Line Input #yukiStream, yukiLine
        If yukiLine Begins "Path=" Then
            yukiResult = getDecodedPath(yukiLine)
        End If
    Wend
    
    Close #yukiStream
    
    Return yukiResult
    
End

Private Function Files_Read() As String

    Return User.Home &/ ".local/share/Trash/files"

End

Private Function Info_Read() As String

    Return User.Home &/ ".local/share/Trash/info"

End
