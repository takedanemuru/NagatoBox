' Gambas class file

Create Private

Public _$grids As GridView

Public Sub _SetBackgroundColor(argColor As Integer)
    
    Dim yukiIndex As Integer
    
    For yukiIndex = 0 To Me._$grids.Columns.Max
        Me._$grids[Me._$grids.Rows.Max, yukiIndex].Background = argColor
    Next
    
End

Public Sub _SetColumnTitles(...)
    
    Dim yukiIndex As Integer
    
    For yukiIndex = 0 To Param.Max
        Me._$grids.Columns[yukiIndex].Text = Param[yukiIndex]
    Next
    
End

Public Sub _AbstractOnInitializeGridView()
    
    ' virtual, MUST be overridden.
    
End

Public Sub _SetColumnWidth(argColumnsIndex As Integer, Optional argMargin As Integer = 16)
    
    Dim yukiColumnHeaderWidth As Integer
    Dim yukiRowValueWidth As Integer
    
    With Me._$grids
        yukiColumnHeaderWidth = Application.Font.RichTextWidth(.Columns[argColumnsIndex].Text) + argMargin
        yukiRowValueWidth = Application.Font.RichTextWidth(.[.Rows.Max, argColumnsIndex].Text) + argMargin
        .Columns[argColumnsIndex].W = Max(yukiColumnHeaderWidth, yukiRowValueWidth)
    End With
    
End

Public Sub _SetColumn(argColumn As Integer, argAlign As Integer, argData As String)
    
    With Me._$grids
        .[.Rows.Max, argColumn].Text = argData
        .[.Rows.Max, argColumn].Alignment = argAlign
    End With
    
    Me._SetColumnWidth(argColumn)
    
End

Public Sub _SetPicture(argColumn As Integer, argPicture As Picture, argMargin As Integer)
    
    Me._$grids[Me._$grids.Rows.Max, argColumn].Picture = argPicture
    Me._SetColumnWidth(argColumn, argMargin)
    
End

Public Sub _new(argContainer As Container)
    
    Me._$grids = New GridView(argContainer) As "GridView"
    Me._AbstractOnInitializeGridView()
    
End
