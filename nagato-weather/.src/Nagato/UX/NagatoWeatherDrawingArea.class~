' Gambas class file

Property Longitude As Float

Private $drawingArea As DrawingArea
Private $observer As Observer
Private $currentWeather As NagatoWeatherElement
Private $textPainter As NagatoWeatherPainterText
Private $longitude As Float

Private Function getWeatherIcon() As Image
    
    Debug $currentWeather.IconId
    Debug $longitude
    
    If MikuruTime.IsDay($longitude) Then
        Return NagatoWeatherIcon.GetIconImageDay($currentWeather.IconId).Stretch(96, 96)
    Else
        Return NagatoWeatherIcon.GetIconImageNight($currentWeather.IconId).Stretch(96, 96)
    End If
    
End

Private Sub setWeatherInfomation(argBackground As Integer)
    
    $drawingArea.Background = argBackground
    
    Paint.Begin($drawingArea)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.DrawImage(getWeatherIcon(), ($drawingArea.W - 96) / 2, 16)
        $textPainter.Draw($currentWeather)
    Paint.End()
    
Catch
    Debug Error.Text
    Return
    
End

Private Sub setPleaseWait(argBackground As Integer)
    
    $drawingArea.Background = argBackground
    
    Paint.Begin($drawingArea)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.DrawImage(MikuruIcon.Get("question", 48).Image, 40, 8)
        Paint.DrawRichText("Please wait...", 0, 0, $drawingArea.W, $drawingArea.H, Align.Center)
    Paint.End()
    
Catch
    Debug Error.Text
    Return
    
End

Public Sub Refresh()
    
    $drawingArea.Refresh()
    
End

Public Sub _new(argDrawingArea As DrawingArea)
    
    $drawingArea = argDrawingArea
    $textPainter = New NagatoWeatherPainterText($drawingArea)
    
    $observer = New Observer($drawingArea) As "Asakura"
    
End

Public Sub Asakura_Draw()
    
    Dim yukiBackground As Integer
    
     If NagatoWeatherCache.IsReady
        $currentWeather = NagatoWeatherCache.GetWeatherElementMostRecent()
        Debug $currentWeather.IconId
        yukiBackground = NagatoWeatherCondition.GetColor($currentWeather.IconId, MikuruTime.IsDay($longitude))
        setWeatherInfomation(yukiBackground)
    Else
        setPleaseWait(Color.DarkBlue)
    End If
    
'Catch
    ' Debug Error.Text
    ' Return
    
End

Private Function Longitude_Read() As Float

    Return $longitude

End

Private Sub Longitude_Write(Value As Float)

    $longitude = Value

End
