' Gambas class file

Property Longitude As Float

Private Const IconSize As Integer = 96

Private $drawingArea As DrawingArea
Private $observer As Observer
Private $currentWeather As NagatoWeatherElement
Private $textPainter As NagatoWeatherPainterText
Private $longitude As Float

Event BackgroundColor(argColor As Integer)

Private Function getWeatherIcon() As Image
    
    Return MikuruWeatherIcon(MikuruTime.IsDay($longitude), $currentWeather[MikuruWeatherKey.IconId], IconSize)
    
End

Private Sub setWeatherInfomation(argBackground As Integer)
    
    $drawingArea.Background = argBackground
    
    Paint.Begin($drawingArea)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.DrawImage(getWeatherIcon(), ($drawingArea.W - IconSize) / 2, 16)
        $textPainter.Draw($currentWeather)
    Paint.End()
    
' Catch
'     Debug Error.Text
'     Return
    
End

Private Sub setPleaseWait(argBackground As Integer)
    
    $drawingArea.Background = argBackground
    
    Paint.Begin($drawingArea)
        Paint.Brush = Paint.Color(Color.White)
        Paint.Font.Bold = True
        Paint.DrawImage(MikuruIcon.Get("question", 48).Image, ($drawingArea.W - 48) / 2, 16)
        Paint.DrawRichText("Please wait...", 0, 0, $drawingArea.W, $drawingArea.H, Align.Center)
    Paint.End()
    
Catch
    Debug Error.Text
    Return
    
End

Public Sub Refresh()
    
    $drawingArea.Refresh()
    
End

Public Sub _new(argDrawingArea As DrawingArea)
    
    $drawingArea = argDrawingArea
    $textPainter = New NagatoWeatherPainterText($drawingArea)
    
    $observer = New Observer($drawingArea) As "Asakura"
    
End

Public Sub Asakura_Draw()
    
    Dim yukiBackground As Integer
    
     If NagatoWeatherCache.IsReady
        $currentWeather = NagatoWeatherCache.GetWeatherElementMostRecent()
        yukiBackground = MikuruWeatherColor($currentWeather[MikuruWeatherKey.IconId], MikuruTime.IsDay($longitude))
        Raise BackgroundColor(yukiBackground)
        setWeatherInfomation(yukiBackground)
    Else
        Raise BackgroundColor(Color.DarkBlue)
        setPleaseWait(Color.DarkBlue)
    End If
    
End

Private Function Longitude_Read() As Float

    Return $longitude

End

Private Sub Longitude_Write(Value As Float)

    $longitude = Value

End
