' Gambas class file

Inherits NagatoGridsHandler

Private $conditons As New NagatoWeatherCondition

Private Sub getIcon(argCount As Integer, argElement As NagatoWeatherElement) As Picture
    
    Dim yukiIconId As Integer = argElement[MikuruWeatherKey.IconId]
    
    Return MikuruWeatherIcon((argCount >= 6 And 18 > argCount), yukiIconId, 64).Picture
    
End

Private Sub setGridColor(argCount As Integer)
    
    If 6 > argCount Or If argCount >= 18 Then
         Me._SetBackgroundColor(MikuruBackgroundColor.Night)
    Else
        Me._SetBackgroundColor(MikuruBackgroundColor.Day)
    End If
    
End

Private Sub setRowData(argHour As Integer, argTime As String)
    
    Dim yukiElement As NagatoWeatherElement = NagatoWeatherCache.GetElement(argTime)

    With MikuruWeatherKey
        Me._SetColumn(0, Align.Left, MikuruReadableTime(argTime))
        Me._SetColumn(1, Align.Left, $conditons[yukiElement[.IconId]])
        Me._SetPicture(1, getIcon(argHour, yukiElement), 96)
        Me._SetColumn(2, Align.Right, Subst$("&1Â°C", yukiElement[.Temperature]))
        Me._SetColumn(3, Align.Right, Subst$("&1mm", yukiElement[.Precipitation]))
        Me._SetColumn(4, Align.Right, Subst$("&1, &2m", yukiElement[.WindDirection], yukiElement[.WindSpeed]))
    End With
    
End

Public Sub SetData(argDate As String)
    
    Dim yukiHour As Integer
    Dim yukiTime As String
    
    For yukiHour = 0 To 23
        yukiTime = Subst$("&1T&2:00:00Z", argDate, Format$(yukiHour, "0#"))
        If Not NagatoWeatherCache.HasElement(yukiTime) Then Continue
        Inc Me._$grids.Rows.Count
        setGridColor(yukiHour)
        setRowData(yukiHour, yukiTime)
    Next
    
End

Public Sub _AbstractOnInitializeGridView() ' override
    
    With Me._$grids
        .Grid = False
        .Header = GridView.Horizontal
        .Columns.Count = 5
        Me._$grids.AutoResize = True
        Me._SetColumnTitles(("Time"), ("Condition"), ("Temperature"), ("Precipitation"), ("Wind"))
        .Rows.Height = 64
        .ScrollBar = Scroll.Vertical
        .Expand = True
    End With
    
    NagatoSettingsGrids.Load(Me._$grids)
    
End
