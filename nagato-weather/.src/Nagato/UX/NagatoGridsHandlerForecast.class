' Gambas class file

Inherits NagatoGridsHandler

Private Sub getIcon(argCount As Integer, argElement As NagatoWeatherElement) As Picture
    
    Return MikuruWeatherIcon((argCount >= 6 And 18 > argCount), argElement[MikuruWeatherKey.IconId], 64).Picture
    
End

Private Sub setGridColor(argCount As Integer)
    
    If 6 > argCount Or If argCount >= 18 Then
         Me._SetBackgroundColor(MikuruBackgroundColor.Night)
    Else
        Me._SetBackgroundColor(MikuruBackgroundColor.Day)
    End If
    
End

Private Sub setColumnWidth(argColumnsIndex As Integer, Optional argMargin As Integer = 16)
    
    Dim yukiColumnHeaderWidth As Integer
    Dim yukiRowValueWidth As Integer
    
    With Me._$grids
        yukiColumnHeaderWidth = Application.Font.RichTextWidth(.Columns[argColumnsIndex].Text) + argMargin
        yukiRowValueWidth = Application.Font.RichTextWidth(.[.Rows.Max, argColumnsIndex].Text) + argMargin
        .Columns[argColumnsIndex].W = Max(yukiColumnHeaderWidth, yukiRowValueWidth)
    End With
    
End

Public Sub SetData(argDate As String)
    
    Dim yukiCount As Integer
    Dim yukiTime As String
    Dim yukiElement As NagatoWeatherElement
    
    For yukiCount = 0 To 23
        yukiTime = Subst$("&1T&2:00:00Z", argDate, Format$(yukiCount, "0#"))
        If NagatoWeatherCache.HasElement(yukiTime) Then
            With Me._$grids
                Inc .Rows.Count
                yukiElement = NagatoWeatherCache.GetElement(yukiTime)
                setGridColor(yukiCount)
                .[.Rows.Max, 0].Text = MikuruReadableTime(yukiTime)
                .[.Rows.Max, 0].Alignment = Align.Right
                setColumnWidth(0)
                .[.Rows.Max, 1].Picture = getIcon(yukiCount, yukiElement)
                .[.Rows.Max, 1].Text = yukiElement[MikuruWeatherKey.IconName]
                setColumnWidth(1, 96)
                .[.Rows.Max, 2].Text = Subst$("&1Â°C", yukiElement[MikuruWeatherKey.Temperature])
                .[.Rows.Max, 2].Alignment = Align.Right
                setColumnWidth(2)
                .[.Rows.Max, 3].Text = Subst$("&1mm", yukiElement[MikuruWeatherKey.Precipitation])
                .[.Rows.Max, 3].Alignment = Align.Right
                setColumnWidth(3)
            End With
        Endif
    Next
    
End

Public Sub _InitializeGridView() ' override
    
    With Me._$grids
        .Grid = False
        .Header = GridView.Horizontal
        .Columns.Count = 4
        Me._$grids.AutoResize = True
        Me._SetColumnTitles("Time", "Condition", "Temperature", "Precipitation")
        .Rows.Height = 64
        .ScrollBar = Scroll.Vertical
    End With
    
    NagatoSettingsGrids.Load(Me._$grids)
    
End
