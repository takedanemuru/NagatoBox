' Gambas class file

Private $textRect As New Rect
Private $drawingArea As DrawingArea
Private $currentWeather As NagatoWeatherElement

Private Sub setTextRect()
    
    With $textRect
        .X = 8
        .Y = 8
        .W = $drawingArea.W - 16
        .H = $drawingArea.H - 16
    End With
    
End

Private Function getTitle() As String
    
    Dim yukiCondition As String = Upper$(NagatoWeatherCondition.GetText($currentWeather[MikuruWeatherKey.IconId]))
    
    Return Subst$("<h2>&1</h2>&2", yukiCondition, String$(8, "<br>"))
    
End

Private Function getHeaders() As String
    
    Dim yukiReturn As String = "<h2> </h2><br>"
    
    yukiReturn &= "Temperature:<br>"
    yukiReturn &= "Wind Speed:<br>"
    yukiReturn &= "Wind Direction:<br>"
    yukiReturn &= "Hummidity:<br>"
    yukiReturn &= "Cloudiness:<br>"
    yukiReturn &= "Pressure:<br>"
    yukiReturn &= "Precipitation:<br>"
    
    Return yukiReturn
    
End

Private Function getStatus() As String
    
    Dim yukiReturn As String = "<h2> </h2><br>"
    
    With $currentWeather
        yukiReturn &= Subst$("&1Â°C<br>", .[MikuruWeatherKey.Temperature])
        yukiReturn &= Subst$("&1m<br>", .[MikuruWeatherKey.WindSpeed])
        yukiReturn &= Subst$("&1<br>", .[MikuruWeatherKey.WindDirection])
        yukiReturn &= Subst$("&1%<br>", .[MikuruWeatherKey.Humidity])
        yukiReturn &= Subst$("&1%<br>", .[MikuruWeatherKey.Cloudiness])
        yukiReturn &= Subst$("&1HPa<br>", .[MikuruWeatherKey.Pressure])
        yukiReturn &= Subst$("&1mm<br>", .[MikuruWeatherKey.Precipitation])
    End With
    
    Return yukiReturn
    
End

Public Sub Draw(argWeatherElement As NagatoWeatherElement)
    
    $currentWeather = argWeatherElement
    
    setTextRect()

    With $textRect
        Paint.DrawRichText(getTitle(), .X, .Y, .W, .H, Align.Center)
        Paint.DrawRichText(getHeaders(), .X, .Y, .W, .H, Align.Left)
        Paint.DrawRichText(getStatus(), .X, .Y, .W, .H, Align.Right)
    End With
    
End

Public Sub _new(argDrawingArea As DrawingArea)
    
    $drawingArea = argDrawingArea
    
End

