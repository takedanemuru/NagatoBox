' Gambas class file

Private $textRect As New NagatoTextRectangle
Private $drawingArea As DrawingArea
Private $currentWeather As NagatoWeatherElement
Private $conditions As New NagatoWeatherCondition

Private Function getTitle() As String
    
    Dim yukiCondition As String = $conditions[$currentWeather[MikuruWeatherKey.IconId]]
    
    Return Subst$("<h2>&1</h2>&2", yukiCondition, String$(8, "<br>"))
    
End

Private Function getStatus() As String
    
    Dim yukiReturn As String = "<h2> </h2><br>"
    
    With $currentWeather
        yukiReturn &= Subst$("&1Â°C<br>", .[MikuruWeatherKey.Temperature])
        yukiReturn &= Subst$("&1m<br>", .[MikuruWeatherKey.WindSpeed])
        yukiReturn &= Subst$("&1<br>", .[MikuruWeatherKey.WindDirection])
        yukiReturn &= Subst$("&1%<br>", .[MikuruWeatherKey.Humidity])
        yukiReturn &= Subst$("&1%<br>", .[MikuruWeatherKey.Cloudiness])
        yukiReturn &= Subst$("&1HPa<br>", .[MikuruWeatherKey.Pressure])
        yukiReturn &= Subst$("&1mm<br>", .[MikuruWeatherKey.Precipitation])
    End With
    
    Return yukiReturn
    
End

Public Sub Draw(argWeatherElement As NagatoWeatherElement)
    
    $currentWeather = argWeatherElement
    $textRect.Reset($drawingArea)

    Paint.Brush = Paint.Color(Color.White)
    Paint.Font.Bold = True
        With $textRect.Rect
            Paint.DrawRichText(getTitle(), .X, .Y, .W, .H, Align.Center)
            Paint.DrawRichText(File.Load("Text/ConditionHeaders"), .X, .Y, .W, .H, Align.Left)
            Paint.DrawRichText(getStatus(), .X, .Y, .W, .H, Align.Right)
        End With
    Paint.Fill()
    
End

Public Sub _new(argDrawingArea As DrawingArea)
    
    $drawingArea = argDrawingArea
    
End
