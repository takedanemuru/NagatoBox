' Gambas class file

Private $drawingArea As DrawingArea
Private $painterCurrentWeather As NagatoWeatherPainterCurrentWeather
Private $painterPleaseWait As NagatoWeatherPainterPleaseWait

Event BackgroundColor(argColor As Integer)
Event MoveFormTo(argX As Integer, argY As Integer)
Event ChangeStarButtonVisible(argVisible As Boolean)

Public Sub Refresh()
    
    $drawingArea.Refresh()
    
End

Public Sub _new(argContainer As Container)
    
    $drawingArea = New DrawingArea(argContainer) As "MainDrawingArea"
    $drawingArea.Expand = True

    $painterPleaseWait = New NagatoWeatherPainterPleaseWait($drawingArea)
    $painterCurrentWeather = New NagatoWeatherPainterCurrentWeather($drawingArea)
    
End

Public Sub ChangeLongitude(argLongitude As Float)
    
    $painterCurrentWeather.ChangeLongitude(argLongitude)
    
End

Public Sub MainDrawingArea_Draw()
    
    Dim yukiBackground As Integer
    
     If NagatoWeatherCache.IsReady
        yukiBackground = $painterCurrentWeather.Draw()
    Else
        yukiBackground = $painterPleaseWait.Draw()
    End If

    Raise BackgroundColor(yukiBackground)
    
End

Public Sub MainDrawingArea_MouseDrag()
    
    Dim yukiX As Integer = Mouse.ScreenX - Mouse.StartX
    Dim yukiY As Integer = Mouse.ScreenY - Mouse.StartY
    
    Raise MoveFormTo(yukiX, yukiY)
    
End

Public Sub MainDrawingArea_Enter()

    Raise ChangeStarButtonVisible(True)

End

Public Sub MainDrawingArea_Leave()

    Raise ChangeStarButtonVisible(False)

End
