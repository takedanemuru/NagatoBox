' Gambas class file

Inherits NagatoContainer

Private $backButton As Button
Private $applyButton As Button

Private Sub addBackButton()
    
    $backButton = New Button(Me._$hbox) As "BackButton"
    
    With $backButton
        .Text = ("Back To Main")
        .AutoResize = True
    End With
    
End

Private Sub addApplyButton()
    
    $applyButton = New Button(Me._$hbox) As "ApplyButton"
    
    With $applyButton
        .Text = ("Apply")
        .AutoResize = True
        .Enabled = False
    End With
    
End

Public Sub _OnInitializeWidgets()
    
    addBackButton()
    Object.New("NagatoTextLabel", [Me._$hbox, Me._OnGetTextAlignment(), Me._OnGetLabelText()])
    addApplyButton()
    
End

Public Function _OnGetBackgroundColor() As Integer
    
    Return Color.White + Color.Transparent * 192
    
End

Public Sub Enable()
    
    $applyButton.Enabled = True
    
End

Public Sub ApplyButton_Click()
    
    $applyButton.Enabled = False
    
    Raise Signal(MikuruSignal.PropertyApplyChanges, Null)
    
End

Public Sub BackButton_Click()
    
    Dim yukiMessage As String = "YUKI.N > Your changes are not applied."
    
    If $applyButton.Enabled Then
        If Message.Warning(yukiMessage, ("Cancel"), ("Discard")) = 1 Then Return
    Endif
    
    Raise Signal(MikuruSignal.GoToMain, Null)
    
End
