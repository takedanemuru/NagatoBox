' Gambas class file

Private $settings As New Settings
Private $observer As Observer
Private $draw As New NagatoDraw
Private $timer As New Timer As "Timer"
Private $block As New NagatoBlockMover
Private $sound As New NagatoSoundEffects
Private $notStarted As Boolean = True
Private $score As Integer = 0
Private $tick As Integer
Private $isDead As Boolean = False

Private Sub checkGameOver()
        
        If $block.IsGameOver() Then 
                endSequence()
        Else
                $block.Next()
        Endif
        
End

Private Sub setBlockToBoard()
        
        Dim yukiCounter As Integer
        
        For yukiCounter = 0 To 7 Step 2
                With $block
                        NagatoBoard.SetBlock(.CurrentPosition[yukiCounter], .CurrentPosition[yukiCounter + 1], .Color)
                End With
        Next
        
        NagatoBoard.CheckLine()
        checkGameOver()
                
End

Private Sub endSequence()
        
        $sound.EndBell()
        NagatoBoard.TurnToBlack()
        $block.TurnToBlack()
        $timer.Stop()
        DrawingArea1.Refresh()

End

Private Sub reactive()
        
        If $notStarted Then $notStarted = False
        
        NagatoBoard.Activate()
        $block = New NagatoBlockMover
        $score = 0
        $isDead = False
        $timer.Start()
        
End

Public Sub _new()

        $observer = New Observer(NagatoBoard) As "Observer"

End

Public Sub Form_Open()

        Me.X = $settings["form/x", Me.X]
        Me.Y = $settings["form/y", Me.Y]

        NagatoBoard.Activate()
        $draw.SetMainImage()
        $timer.Delay = 200
        $timer.Start()
        
End

Public Sub Form_Close()
        
        $settings["form/x"] = Me.X
        $settings["form/y"] = Me.Y
        
End

Public Sub Timer_Timer()
        
        DrawingArea1.SetFocus()
        
        If $notStarted Then Return
        
        If Not $isDead Then
                $block.MoveDown()
        Else
                $sound.Kick()
                setBlockToBoard()
                $isDead = False
        Endif
        
        DrawingArea1.Refresh()
        
        If $block.IsDead() Then $isDead = True
        
End

Public Sub DrawingArea1_MouseDown()
        
        If $notStarted Then reactive()
        If Not $timer.Enabled Then reactive()
        
End

Public Sub DrawingArea1_Draw()
        
        $draw.Initialize()

        If $notStarted Then
                $draw.StartUp()
        Else
                $draw.OnGoing($block)
                If Not $timer.Enabled Then $draw.End($block, $score) 
        Endif
        
End

Public Sub DrawingArea1_KeyPress()

        If $notStarted Then 
                reactive()
                Return
        Endif

        '$timer.Stop()

        Select Case Key.Code
                Case Key.Space
                        $block.MoveRotate()
                Case Key.Left
                        $block.MoveLeft()
                Case Key.Right
                        $block.MoveRight()
                Case Key.Up
                        $block.MoveRotate()
                Case Key.Down
                        If $isDead Then Return
                        $block.MoveDown()
                Default
                        Return
        End Select

        DrawingArea1.Refresh()

        $isDead = False
        '$timer.Start()

End

Public Sub Observer_Score()
        
        DrawingArea1.Refresh()
        Inc $score
        $timer.Delay -= 2
        Me.Title = Subst$("nagato-tetris score: &1", $score)
        $sound.Success()
        
End
