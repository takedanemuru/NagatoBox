' Gambas class file

Property Read BeforeImage As Image
Property Read AfterImage As Image

Private $before As ImageView
Private $after As ImageView
Private $previousZoom As Float

Private Sub reserveZoom()
    
    $previousZoom = $before.Zoom
    
End

Private Sub restoreZoom()
    
    $before.Zoom = $previousZoom
    $after.Zoom = $previousZoom
    
End

Public Sub Rotate(argAngle As Float)
    
    $previousZoom = $before.Zoom
    
    $before.Image = $before.Image.Rotate(Rad(argAngle))
    $after.Image = $after.Image.Rotate(Rad(argAngle))
    
    restoreZoom()
    
End

Public Sub SetZoom(argZoom As Float)
    
    $before.Zoom *= argZoom
    $after.Zoom *= argZoom
    
End

Public Sub SetZoomNormal()
    
    $before.Zoom = 1
    $after.Zoom = 1
    
End

Public Sub SetZoomFit()
    
    $before.ZoomFit()
    $after.ZoomFit()

End

Public Sub Toggle()
    
    $after.Visible = Not $after.Visible
    
End

Public Sub SetPath(argPath As String)
    
    Try $before.Image = Image.Load(argPath)
    If Not Error Then $after.Image = $before.Image
    
End

Public Sub ApplyEffect()
    
    $before.Image = $after.Image
    
End

Private Sub setBalance(argValues As Float[])
    
    Dim yukiImage As Image = $before.Image.Copy()
    
    yukiImage.BeginBalance()
        yukiImage.Brightness(argValues[0])
        yukiImage.Saturation(argValues[1])
        yukiImage.Hue(argValues[2])
        yukiImage.Contrast(argValues[3])
        yukiImage.Lightness(argValues[4])
        yukiImage.Gamma(argValues[5])
    $after.Image = yukiImage.EndBalance()
    
End

Public Sub PreviewEffect(argEffect As Integer, argValues As Variant[])
    
    $after.Image = $before.Image
    
    reserveZoom()
    Select Case argEffect
        Case MikuruEffect.OilPaint
            $after.Image = $before.Image.OilPaint(argValues[0])
        Case MikuruEffect.Charcoal
            $after.Image = $before.Image.Gray()
        Case MikuruEffect.Resize
            $after.Image = $before.Image.Stretch(argValues[1], argValues[0])
        Case MikuruEffect.Balance
            setBalance(argValues)
    End Select
    restoreZoom()
    
End

Public Sub _new(argViewBefore As ImageView, argViewAfter As ImageView)
    
    $before = argViewBefore
    $after = argViewAfter
    
End

Private Function BeforeImage_Read() As Image

    Return $before.Image

End

Private Function AfterImage_Read() As Image

    Return $after.Image

End
