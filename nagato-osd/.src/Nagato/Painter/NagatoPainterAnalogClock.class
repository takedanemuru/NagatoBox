' Gambas class file

Private $offset As New Point

Private Function getX(argDegree As Integer, argLength As Integer) As Float
    
    Return Cos(Rad(argDegree)) * argLength + $offset.X
    
End

Private Function getY(argDegree As Integer, argLength As Integer) As Float
    
    Return Sin(Rad(argDegree)) * argLength + $offset.Y
    
End

Private Sub paintClockHand(argDegree As Integer, argLength As Integer, argWidth As Integer)
    
    Paint.Brush = Paint.Color(NagatoSettingsFonts.ShadeColor + Color.Transparent * 250)
    Paint.LineWidth = argWidth
    Paint.LineCap = Paint.LineCapRound
    Paint.MoveTo(getX(argDegree, 10), getY(argDegree, 10))
    Paint.LineTo(getX(argDegree, argLength), getY(argDegree, argLength))
    Paint.Stroke()
    
End

Private Sub paintClockFace()
    
    Paint.Brush = Paint.Color(Color.Lighter(NagatoSettingsFonts.BaseColor) + Color.Transparent * 250)
        Paint.Ellipse($offset.X - 90, $offset.Y - 90, 180, 180)
    Paint.Fill()
    
End

Private Sub paintPoints()
  
    Dim yukiPoint As Integer
    
    Paint.Brush = Paint.Color(NagatoSettingsFonts.ShadeColor + Color.Transparent * 250)
        Paint.Ellipse($offset.X - 2, $offset.Y - 2, 4, 4)
        For yukiPoint = 0 To 11
            Paint.Ellipse(getX(yukiPoint * 30, 80) - 2, getY(yukiPoint * 30, 80) - 2, 4, 4)
        Next
    Paint.Fill()
    
End

Private Sub paintClockHands()
    
    Dim yukiNow As Date = Now()
    
    paintClockHand(Hour(yukiNow) * 30 + Minute(yukiNow) * 0.5 - 90, 65, 6)
    paintClockHand(Minute(yukiNow) * 6 - 90, 75, 3)
    If Not NagatoSettingsClock.ShowSeconds Then Return
    paintClockHand(Second(yukiNow) * 6 - 90, 80, 1)
    
End

Private Sub setOffset(argY As Integer)
    
    $offset.X = NagatoSettingsForm.Width / 2
    $offset.Y = NagatoSettingsForm.Width / 2 + argY
    
End

Public Sub _call(argY As Integer) As Integer
    
    setOffset(argY)
    
    paintClockFace()
    paintPoints()
    paintClockHands()
    
    Return argY + 200
    
End
