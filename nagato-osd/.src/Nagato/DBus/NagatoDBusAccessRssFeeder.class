' Gambas class file

Property Read Activated As Boolean

Private Const ServiceName As String = "org.gambas.nagato-rss-daemon"
Private Const ObjectPath As String = "/org/nagato/rss/daemon"
Private Const InterfaceName As String = "org.gambas.nagato.rss.daemon.tfei"

Private $proxy As DBusProxy
Private $feedIndex As Integer = 0
Private $itemIndex As Integer = 0

Public Enum RssTitle, RssDescription, RssThumbnailUrl, RssUrl

Private Sub getFeedItemOffset(argOffset As Integer) As String[]
    
    Dim yukiFeedIndex As Integer = $feedIndex
    Dim yukiItemIndex As Integer = $itemIndex
    
    Do While argOffset > 0 
        Dec argOffset
        yukiItemIndex = IIf($proxy.GetFeedItemsCount(yukiFeedIndex) = yukiItemIndex + 1, 0, yukiItemIndex + 1)
        If yukiItemIndex > 0 Then Continue
        yukiFeedIndex = IIf($proxy.FeedCount = yukiFeedIndex + 1, 0, yukiFeedIndex + 1)
    Loop
    
    Return $proxy.GetFeedItem(yukiFeedIndex, yukiItemIndex)
    
End

Public Sub Next()
    
    ' FOR TESTING
    ' $itemIndex = 0
    ' $feedIndex = IIf($proxy.FeedCount = $feedIndex + 1, 0, $feedIndex + 1)
    
    $itemIndex = IIf($proxy.GetFeedItemsCount($feedIndex) = $itemIndex + 1, 0, $itemIndex + 1)
    If $itemIndex > 0 Then Return
    $feedIndex = IIf($proxy.FeedCount = $feedIndex + 1, 0, $feedIndex + 1)
    
End

Public Function GetFeedItem(argOffset As Integer) As String[]
    
    If Not Object.IsValid($proxy) Then Return Null
    
    Return getFeedItemOffset(argOffset)
    
End

Public Sub _get(argOffset As Integer, Optional argItemType As Integer = 1) As String
    
    If Not Object.IsValid($proxy) Then Return ""
    
    Return getFeedItemOffset(argOffset)[argItemType]
    
Catch
    Return ""
    
End

Public Sub Activate()
    
    Shell "nagato-rss-daemon --check-in nagato-osd"
    
    If Not DBus.Session.Applications.Exist(ServiceName) Then Return
    If Not Object.IsValid($proxy) Then $proxy = DBus[ServiceName][ObjectPath, InterfaceName]
    
End

Private Function Activated_Read() As Boolean

    If Not DBus.Session.Applications.Exist(ServiceName) Then Return False
    If Not Object.IsValid($proxy) Then Return False 
    If $proxy.FeedCount = 0 Then Return False

    Return True

End
