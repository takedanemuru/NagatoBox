' Gambas class file

Property Read Activated As Boolean
Property Read TitleLength As Integer

Private Const ServiceName As String = "org.gambas.dataovermind-rssfeeder"
Private Const ObjectPath As String = "/org/dataovermind/rssfeeder"
Private Const InterfaceName As String = "org.gambas.dataovermind.rssfeeder.tfei"

Private $proxy As DBusProxy
Private $feedTitle As String
Private $feedIndex As Integer = 0
Private $itemIndex As Integer = 0

Private Sub ensureTitle()
    
    If $feedTitle = "" Then $feedTitle = $proxy.GetFeedTitle($feedIndex) & " : "
    
Catch
    Return
    
End

Public Sub Next()
    
    $itemIndex = IIf($proxy.GetFeedItemsCount($feedIndex) = $itemIndex + 1, 0, $itemIndex + 1)

    If $itemIndex = 0 Then 
        $feedIndex = IIf($proxy.FeedCount = $feedIndex + 1, 0, $feedIndex + 1)
        $proxy.GetFeedTitle($feedIndex)
    End If
    
End

Public Function GetDescription() As String
    
    Dim yukiItem As String[] = $proxy.GetFeedItem($feedIndex, $itemIndex)
    
    ensureTitle()
    
    Return $feedTitle & yukiItem[0]
   
Catch
    Debug Error.Text
    Return "YUKI.N > Please wait ..."
    
End

Public Function GetFeedItem() As String[]
    
    If Not Object.IsValid($proxy) Then Return ["", "", "", ""]
    
    Return $proxy.GetFeedItem($feedIndex, $itemIndex)
    
End

Public Function GetNextFeedItem() As String[]
    
    Dim yukiFeedIndex As Integer = $feedIndex
    Dim yukiItemIndex As Integer = $itemIndex
    
    yukiItemIndex = IIf($proxy.GetFeedItemsCount($feedIndex) = yukiItemIndex + 1, 0, yukiItemIndex + 1)
    If yukiItemIndex = 0 Then
        yukiFeedIndex = IIf($proxy.FeedCount = yukiFeedIndex + 1, 0, yukiFeedIndex + 1)
    Endif
    
    Return $proxy.GetFeedItem(yukiFeedIndex, yukiItemIndex)
    
Catch
    Return ["", "", "", ""]
    
End

Public Function GetAfterNextFeedItem() As String[]
    
    Dim yukiFeedIndex As Integer = $feedIndex
    Dim yukiItemIndex As Integer = $itemIndex
    Dim yukiFeedItemsCount As Integer = $proxy.GetFeedItemsCount($feedIndex)
    
    If yukiFeedItemsCount = yukiItemIndex + 2 Then
        Inc yukiFeedIndex
        yukiItemIndex = 0
    Else If yukiFeedItemsCount = yukiItemIndex + 1 Then
        Inc yukiFeedIndex
        yukiItemIndex = 1
    Else
        yukiItemIndex = yukiItemIndex + 2
    Endif
    
    Return $proxy.GetFeedItem(yukiFeedIndex, yukiItemIndex)
    
Catch
    Return ["", "", "", ""]
    
End

Public Sub Activate()
    
    If Not DBus.Session.Applications.Exist(ServiceName) Then Shell "dataovermind-rssfeeder"
    
    If DBus.Session.Applications.Exist(ServiceName) Then 
        If Not Object.IsValid($proxy) Then $proxy = DBus[ServiceName][ObjectPath, InterfaceName]
    End If
    
End

Private Function Activated_Read() As Boolean

    If Not DBus.Session.Applications.Exist(ServiceName) Then Return False
    If Not Object.IsValid($proxy) Then Return False 

    Return True

End

Private Function TitleLength_Read() As Integer

    Return String.Len($feedTitle)

End
