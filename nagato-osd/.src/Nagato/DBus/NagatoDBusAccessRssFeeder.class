' Gambas class file

Property Read Activated As Boolean
Property Read TitleLength As Integer

Private Const ServiceName As String = "org.gambas.dataovermind-rssfeeder"
Private Const ObjectPath As String = "/org/dataovermind/rssfeeder"
Private Const InterfaceName As String = "org.gambas.dataovermind.rssfeeder.tfei"

Private $proxy As DBusProxy
Private $feedTitle As String
Private $feedIndex As Integer = 0
Private $itemIndex As Integer = 0

Public Enum RssTitle, RssDescription, RssThumbnailUrl, RssUrl

Private Sub ensureTitle()
    
    If $feedTitle = "" Then $feedTitle = $proxy.GetFeedTitle($feedIndex) & " : "
    
Catch
    Return
    
End

Public Sub Next()
    
    $itemIndex = IIf($proxy.GetFeedItemsCount($feedIndex) = $itemIndex + 1, 0, $itemIndex + 1)

    If $itemIndex = 0 Then 
        $feedIndex = IIf($proxy.FeedCount = $feedIndex + 1, 0, $feedIndex + 1)
        $proxy.GetFeedTitle($feedIndex)
    End If
    
End

Public Sub _get(argOffset As Integer, Optional argItemType As Integer = 1) As String
    
    Dim yukiOffset As Integer = argOffset
    Dim yukiFeedIndex As Integer = $feedIndex
    Dim yukiItemIndex As Integer = $itemIndex
    
    If Not Object.IsValid($proxy) Then Return ["", "", "", ""]
    
    Do
        If yukiOffset = 0 Then Break
        Inc yukiItemIndex
        If yukiItemIndex >= $proxy.GetFeedItemsCount(yukiFeedIndex) Then
            yukiItemIndex = 0
            Inc yukiFeedIndex
        Endif
        If yukiFeedIndex >= $proxy.FeedCount Then yukiFeedIndex = 0
        Dec yukiOffset
    Loop
    
    Return $proxy.GetFeedItem(yukiFeedIndex, yukiItemIndex)[argItemType]
    
End

Public Sub Activate()
    
    If Not DBus.Session.Applications.Exist(ServiceName) Then Shell "dataovermind-rssfeeder"
    
    If DBus.Session.Applications.Exist(ServiceName) Then 
        If Not Object.IsValid($proxy) Then $proxy = DBus[ServiceName][ObjectPath, InterfaceName]
    End If
    
End

Private Function Activated_Read() As Boolean

    If Not DBus.Session.Applications.Exist(ServiceName) Then Return False
    If Not Object.IsValid($proxy) Then Return False 

    Return True

End

Private Function TitleLength_Read() As Integer

    Return String.Len($feedTitle)

End
