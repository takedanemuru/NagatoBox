' Gambas class file

Private $player As New MediaPlayer As "Player"
Private $source As New NagatoMediaSource

Private Function play(argStreamPath As String) 

    If argStreamPath Not Begins "mms://" Then
        TextLabel1.Text = ("YUKI.N > Some error caused.")
    Else
        $player.URL = argStreamPath
        $player.Play()
        TextLabel1.Text = Subst$(("now playing: &1"), argStreamPath)
    End If

End

Public Sub Form_Open()

    If Not $source.SetXmlDocument() Then
        Me.Close()
    Else
        NagatoSettings.LoadFormSettings(Me)
        play($source.Path)
    End If

End

Public Sub Form_Close()

    Try $player.Stop()

    NagatoSettings.SaveFormSettings(Me)

End

Public Sub Player_Tag(argMediaTagList As MediaTagList)
    
    Dim yukiTag As String
    
    For Each yukiTag In argMediaTagList.Tags
        If "title" = LCase(yukiTag) Then Me.Title = argMediaTagList[yukiTag]
        Debug yukiTag;; CString(argMediaTagList[yukiTag])
    Next
    
End

Public Sub TextLabel1_MouseDown()

    If $player.State = Media.Playing Then
        $player.Pause()
    Else
        Try $player.Play()
    Endif

End

Public Sub Player_State()
    
    If $player.State = Media.Playing Then
         TextLabel1.Tooltip = "Click to pause"
    Else
        TextLabel1.Tooltip = "Click to play"
    Endif
    
End
