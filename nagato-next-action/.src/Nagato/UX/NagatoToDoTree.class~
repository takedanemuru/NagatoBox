' Gambas class file

Inherits NagatoMenuPipe

Private $tab As TabStrip
Private $tree As TreeView

Private Sub expandAll()
    
    Dim yukiId As Integer
    
    For Each yukiId In NagatoDBToDo.GetIds()
        If NagatoDBToDo.IsDone(yukiId) Then Continue
        Try $tree[yukiId].Expanded = True
    Next
    
End

Private Sub refreshTree()
    
    Dim yukiResult As Result = NagatoDBToDo.GetAll()
    Dim yukiIcon As Picture
    Dim yukiParent As Integer
    
    $tree.Clear()
    
    Do 
        For Each yukiResult
            With NagatoDBToDo
                Try yukiIcon = MikuruStatus.GetIcon(yukiResult[.ColumnStatus])
                If Error Then yukiIcon = Null
                If yukiResult[.ColumnId] = yukiResult[.ColumnProject] Then
                    Try $tree.Add(yukiResult[.ColumnId], yukiResult[.ColumnName], yukiIcon)
                Else
                    Try $tree.Add(yukiResult[.ColumnId], yukiResult[.ColumnName], yukiIcon, yukiResult[.ColumnProject])
                End If
            End With
        Next
        If yukiResult.Count = $tree.Count Then Break
    Loop
    
    expandAll()
    
End

Public Sub Refresh()
    
    refreshTree()
    
End

Public Function GetCurrentId() As Integer
    
    Return $tree.Current.Key
    
End

Public Sub _new(argTab As TabStrip)
    
    $tab = argTab
    
    $tree = New TreeView($tab) As "ToDoTree"
    $tree.Expand = True
    refreshTree()
    
End

Public Sub ToDoTree_Menu()
    
    Dim yukiMenu As New NagatoContextMenuToDoGrid(HaruhiMain) As "Menu"
    
    yukiMenu.PopUp()
    
End
