' Gambas module file

Public Enum TypeExit, TypeReboot, TypeShutdown, TypeLockScreen

Private Const LogindBus As String = "system://org.freedesktop.login1"
Private Const LogindManagerObjectPath As String = "/org/freedesktop/login1"
Private Const LogindManagerInterface As String = "org.freedesktop.login1.Manager"

Private $logindManager As DBusProxy

Private Sub initializeLogindManager()
    
    $logindManager = DBus[LogindBus][LogindManagerObjectPath, LogindManagerInterface]
    
End

Public Function IsOpenbox() As Boolean
    
    Dim yukiOutput As String
    
    Shell "printenv | grep DESKTOP_SESSION" To yukiOutput
    
    If (yukiOutput Ends "openbox\n") Then
        Return True
    Else If (yukiOutput Ends "NagatoBox\n") Then
        Return True
    Else
        Return False
    Endif
    
End

Public Function IsNagatoBox() As Boolean
    
    Dim yukiOutput As String
    
    Shell "printenv | grep DESKTOP_SESSION" To yukiOutput
    
    If (yukiOutput Ends "NagatoBox\n") Then
        Return True
    Else
        Return False
    Endif
    
End

Public Sub Logout(argType As Integer)
    
    If Not Object.IsValid($logindManager) Then initializeLogindManager()
    
    Select Case argType
        Case TypeExit
            Shell Subst$("openbox --exit && loginctl kill-user &1", User.Id)
        Case TypeReboot
            $logindManager.Reboot(False)
        Case TypeShutdown
            $logindManager.PowerOff(True)
        Case TypeLockScreen
            Try Shell "nagato-config-screenlocker -l"
    End Select
    
End
