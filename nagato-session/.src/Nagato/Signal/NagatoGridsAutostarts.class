' Gambas class file

Inherits NagatoGrids

Private $overriddens As New String[]
Private $headerTitles As New NagatoGridsHeadersAutostarts

Private Sub initializeGrid(argGrid As GridView)
    
    Me._$grids = argGrid
    Me._ClearGrids()
    Me._SetGridsColumns($headerTitles.Titles)
    Me._SetGridsProperties()
        
End

Private Sub setApplicationName(argBaseName As String)
    
    With NagatoGridsHeadersAutostarts
        Me._AdjustColumnWidth(argBaseName, .ColumnName)
        Me._$grids[Me._$grids.Rows.Max, .ColumnName].Text = argBaseName
    End With
    
End

Private Sub setType(argType As String, argColor As Integer, argIconName As String)
    
    With NagatoGridsHeadersAutostarts
        Me._SetProperty(.ColumnType, argType, argIconName)
        Me._SetBackgroundColor(argColor)
        Me._AdjustColumnWidth(argType, .ColumnType)
    End With
    
End

Private Sub setTypeColumn(argAutostartFile As NagatoAutostartFile)
    
    If argAutostartFile.Hide Then
        setType(("BLOCKER"), Color.Orange, "traffic-cone")
    Else If Not argAutostartFile.Required Then 
        setType(("DO NOT EXECUTE"), Color.Gray, "exclamation")
    Else If $overriddens.Exist(argAutostartFile.Name) Then 
        setType(("BLOCKED BY USER"), Color.Blue, "construction")
    End If
    
End

Private Sub setData(argData As NagatoAutostarts)
    
    Dim yukiAutostartFile As NagatoAutostartFile
    
    For Each yukiAutostartFile In argData.All
        With NagatoGridsHeadersAutostarts
            Inc Me._$grids.Rows.Count
            setApplicationName(yukiAutostartFile.Name)
            Me._SetProperty(.ColumnOnlyShowIn, yukiAutostartFile.OnlyShowInByText)
            Me._SetProperty(.ColumnNotShowIn, yukiAutostartFile.NotShowInByText)
            If Not yukiAutostartFile.Hide Then
                Me._SetProperty(.ColumnExec, yukiAutostartFile.Exec)
                Me._SetProperty(.ColumnTryExec, yukiAutostartFile.TryExec)
            End If
            setTypeColumn(yukiAutostartFile)
        End With
    Next
End

Public Sub _new(argGridView As GridView, argAutostarts As NagatoAutostarts, Optional argOverriddens As String[])
    
    If argOverriddens Then $overriddens = argOverriddens.Copy()
    
    initializeGrid(argGridView)
    setData(argAutostarts)
    
End

Public Sub Refresh()
    
    'refresh?
    
End
