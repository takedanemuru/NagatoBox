' Gambas class file

Inherits NagatoSelectApplicationWidget

Private Const ApplicationsDirectory As String = "/usr/share/applications"

Private $iconView As IconView

Private Sub getIconPicture(argDesktopFile As DesktopFile, argSize As Integer) As Picture
    
    If argDesktopFile.Icon Begins "/" Then
        Return Image.Load(argDesktopFile.Icon).Stretch(argSize, argSize).Picture
    Else
        Return argDesktopFile.GetIcon(argSize).Picture
    Endif
    
End

Private Sub addIcon(argFullPath As String)
    
    Dim yukiDesktopFile As New DesktopFile(argFullPath)
    Dim yukiName As String
    
    With yukiDesktopFile
        yukiName = IIf(.Name, .Name, File.BaseName(.Path))
        $iconView.Add(.Path, yukiName, getIconPicture(yukiDesktopFile, 48))
    End With
    
End

Private Sub initializeIconView()
    
    Dim yukiPath As String
    Dim yukiFullPath As String
    
    For Each yukiPath In RDir(ApplicationsDirectory).Sort()
        If yukiPath Begins "screensavers" Then Continue
        yukiFullPath = ApplicationsDirectory &/ yukiPath
        If IsDir(yukiFullPath) Then Continue
        If File.Ext(yukiFullPath) <> "desktop" Then Continue
        addIcon(yukiFullPath)
    Next
    
End

Public Sub _Initialize() ' virtual
    
    $iconView = New IconView(Me._$container) As "IconView"
    $iconView.Expand = True
    
    initializeIconView()
    
End

Public Sub IconView_Click()
    
    Debug Last.Tag
    
End
