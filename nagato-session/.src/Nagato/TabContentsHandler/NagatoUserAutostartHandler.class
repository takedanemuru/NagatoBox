' Gambas class file

Inherits NagatoTabContentsHandler

Private $menuButton As MenuButton = Kyon.UserAutostartMenuButton
Private $gridView As GridView = Kyon.UserAutoStartGrid

Private Sub ShowDetail(argRowIndex As Integer)
    
    If argRowIndex <> $gridView.Rows.Max Then
        Kyon.RefreshTabStrip($gridView[argRowIndex, 1].Text, Stock["small/pen"])
        NagatoEditorHandler.SetText(MikuruDesktopEntryFile.Get(argRowIndex, $gridView), False)
    End If
    
End

Private Sub delete(argRowIndex As Integer)
    
    Dim yukiPath As String = MikuruAutostartDirectory.User &/ $gridView[argRowIndex, 1].Text & ".desktop"
    
    If Not Exist(yukiPath) Then Return
    
    Kill yukiPath
    
    NagatoGridsHandler.Refresh()
    
End

Public Sub _InitializeGearMenu()
    
    $menuButton.Menu = "UserAutostartMenu"
    Me._$gearMenu = New NagatoUserAutostartMenu($menuButton.Menu) As "Menu"
    
End

Public Sub _InitializeObserver()
    
    Me._$observer = New Observer($gridView) As "Asakura"
    
End

Public Sub Asakura_Activate()
    
    ShowDetail($gridView.RowAt(Mouse.Y))
    
End

Public Sub Menu_Edit()
    
    If $gridView.Rows.Selection.Count = 0 Then Return
    
    ShowDetail($gridView.Rows.Selection[0])
    
End

Public Sub Menu_Delete()
    
    If $gridView.Rows.Selection.Count = 0 Then Return
    
    If HaruhiDialogDelete.ShowDialog() = MikuruDialog.ResponseDelete Then
        delete($gridView.Rows.Selection[0])
    Endif
    
End
