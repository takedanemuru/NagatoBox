' Gambas class file

Private Const UserDirectory As String = ".local/share/applications"
Private Const SystemDirectory As String = "/usr/share/applications"

Private Sub addIcon(argPath As String)
    
    Dim yukiDesktopEntry As New DesktopFile(argPath)
    Dim yukiIcon As Picture = MikuruIconDesktopEntry[argPath, 64]
    Dim yukiName As String = yukiDesktopEntry.Name

    If Not yukiName Then yukiName = File.BaseName(yukiDesktopEntry.Path)
    ApplicationsIconView.Add(argPath, yukiName, yukiIcon)
    
End

Private Sub addIcons(argDirectory As String, Optional argQuery As String = "")
    
    Dim yukiPath As String
    
    For Each yukiPath In Dir(argDirectory, "*.desktop").Sort(gb.IgnoreCase)
        If ApplicationsIconView.Exist(yukiPath) Then Continue
        If yukiPath Not Like Subst$("*&1*", argQuery) Then Continue 
        addIcon(argDirectory &/ yukiPath)
    Next
    
End

Private Sub refresh(argQuery As String)
    
    ApplicationsIconView.Clear()
    
    addIcons(User.Home &/ UserDirectory, argQuery)
    addIcons(SystemDirectory, argQuery)
    addIcons(SystemDirectory &/ "kde4", argQuery)
    
End

Public Sub Form_Open()
    
    refresh("")
    
End

Public Sub ApplicationsIconView_Activate()

    Try Copy Last.key To Desktop.Path &/ File.Name(Last.Key)

End

Public Sub SearchButtonBox_Activate()

    refresh(SearchButtonBox.Text)

End

Public Sub SearchButtonBox_Click()

    SearchButtonBox_Activate()

End
