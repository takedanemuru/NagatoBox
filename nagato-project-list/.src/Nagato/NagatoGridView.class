' Gambas class file

Private $gridview As GridView

Private Sub setColumn(argColumn As Integer, argText As String, argWidth As Integer, argAlign As Integer)
    
    With $gridView.Columns[argColumn]
       .Text = argText
       .W = argWidth
       .Alignment = argAlign
    End With
    
End

Private Sub setColumns()
    
    With $gridView
        setColumn(0, "#", .W / 24, Align.Right)
        setColumn(1, "", 24, Align.Center)
        setColumn(2, "Name", .w / 3, Align.Left)
        setColumn(3, "Version", .w / 12, Align.Right)
        setColumn(4, "Last Modefied", .w / 6, Align.Right)
        setColumn(5, "Description", .w / 3, Align.Left)
    End With
    
End

Public Sub initializeGridView(argContainer As Container)
    
    $gridview = New GridView(argContainer)
    
    With $gridview
        .Expand = True
        .Rows.Count = 0
        .Header = GridView.Horizontal
        .Columns.Count = 6
    End With
    
    setColumns()
    
End

Private Sub setRowData(argProject As NagatoProject)
    
    $gridView[$gridView.Rows.Max, 0].Text = $gridview.Rows.Count
    Try $gridView[$gridView.Rows.Max, 1].Picture = argProject.Icon
    $gridView[$gridView.Rows.Max, 2].Text = argProject.Title
    $gridView[$gridView.Rows.Max, 3].Text = argProject.Version
    $gridView[$gridView.Rows.Max, 4].Text = Format(argProject.LastModified, "yyyy-mm-dd")
    $gridView[$gridView.Rows.Max, 5].Text = UnQuote(argProject.Description)
    
End

Private Sub paintGrids(argStatus As Integer)
    
    Dim yukiColor As Integer
    Dim yukiColumn As Integer
    
    If argStatus = MikuruDate.CurrentCours Then Return
    If argStatus = MikuruDate.OldCours Then yukiColor = Color.Lighter(Color.Pink)
    If argStatus = MikuruDate.Unmaintained Then yukiColor = Color.Red
    
    For yukiColumn = 0 To $gridView.Columns.Max
        $gridView[$gridView.Rows.Max, yukiColumn].Background = yukiColor
    Next
    
End

Private Sub setData()
    
    Dim yukiProject As NagatoProject
    
    For Each yukiProject In NagatoProjectList.All
        Inc $gridview.Rows.Count
        setRowData(yukiProject)
        paintGrids(yukiProject.Status)
    Next
    
End

Public Sub _new(argContainer As Container)
    
    initializeGridView(argContainer)
    setData()
    
End
